///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые не рекомендуется редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив из Строка
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("КонтактнаяИнформация.*");
	Возврат Результат;

КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.Печать

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЭтоГруппа
	|	ИЛИ ЗначениеРазрешено(Ссылка)";

	Ограничение.ТекстДляВнешнихПользователей =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК _ДемоПартнеры
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиПартнеры
	|	ПО ВнешниеПользователиПартнеры.ОбъектАвторизации = _ДемоПартнеры.Ссылка
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник._ДемоКонтактныеЛицаПартнеров КАК _ДемоКонтактныеЛицаПартнеров
	|	ПО _ДемоКонтактныеЛицаПартнеров.Владелец = _ДемоПартнеры.Ссылка
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиКонтактныеЛица
	|	ПО ВнешниеПользователиКонтактныеЛица.ОбъектАвторизации = _ДемоКонтактныеЛицаПартнеров.Ссылка
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЭтоГруппа
	|	ИЛИ ЗначениеРазрешено(ВнешниеПользователиПартнеры.Ссылка)
	|	ИЛИ ЗначениеРазрешено(ВнешниеПользователиКонтактныеЛица.Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

// СтандартныеПодсистемы.ЗащитаПерсональныхДанных

#Область СлужебныйПрограммныйИнтерфейс

// АПК:530-выкл Для расчета сроков хранения персональных данных партнеров необходимо изменять данные в таблице другой
// подсистемы - регистр сведений СубъектыДляРасчетаСроковХранения.
//
Процедура ЗарегистрироватьДляРасчетаСроковХраненияПерсональныхДанных(ПараметрыОбновления) Экспорт

	ПараметрыОбновления.ОбработкаЗавершена = Ложь;
	ЗащитаПерсональныхДанных.ЗаполнитьСубъектыДляРасчетаСроковХраненияПерсональныхДанных();
	ПараметрыОбновления.ОбработкаЗавершена = Истина;

КонецПроцедуры
// АПК:530-вкл

Процедура ЗарегистрироватьДанныеКОбработкеДляРасчетаСроковХраненияПерсональныхДанных(ПараметрыОбновления) Экспорт

	ЗащитаПерсональныхДанных.ЗаполнитьСубъектыДляРасчетаСроковХраненияПерсональныхДанных();

КонецПроцедуры

// См. ЗащитаПерсональныхДанныхПереопределяемый.ПриРасчетеСроковХраненияПерсональныхДанных.
Процедура ПриРасчетеСроковХраненияПерсональныхДанных(ДанныеСубъектов, СрокиХранения) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Субъекты", ДанныеСубъектов.ВыгрузитьКолонку("Субъект"));

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	_ДемоПартнеры.Ссылка
	|ИЗ
	|	Справочник._ДемоПартнеры КАК _ДемоПартнеры
	|ГДЕ
	|	_ДемоПартнеры.ВидПартнера = ЗНАЧЕНИЕ(Перечисление._ДемоЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|	И _ДемоПартнеры.Ссылка В (&Субъекты)";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СрокиХранения.Добавить();
		НоваяСтрока.Субъект = Выборка.Ссылка;
		НоваяСтрока.СрокХранения = Дата(3999, 12, 31);
		НоваяСтрока.Комментарий = НСтр("ru = 'Партнер не является физическим лицом'",
			ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

// Конец СтандартныеПодсистемы.ЗащитаПерсональныхДанных

#КонецЕсли