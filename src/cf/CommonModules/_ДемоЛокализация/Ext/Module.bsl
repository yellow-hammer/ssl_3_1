///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет корректность номера счета и сообщает об ошибке.
//
// Параметры:
//  НомерСчета - Строка
//  БИК - Строка
//  Отказ - Булево - необходимо установить значение Истина, если проверка не пройдена.
//
Процедура ПриПроверкеНомераСчета(Знач НомерСчета, Знач БИК, Отказ) Экспорт
	
	// Локализация
	Если ЗначениеЗаполнено(НомерСчета) И ЗначениеЗаполнено(БИК)
		И Не РегламентированныеДанныеКлиентСервер.КонтрольныйКлючЛицевогоСчетаСоответствуетТребованиям(
			НомерСчета, БИК, Истина) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Контрольное число счета не совпадает с рассчитанным с учетом БИК'"),,, 
			"Объект.НомерСчета", Отказ);
	КонецЕсли;
	// Конец Локализация

КонецПроцедуры

// Получает необходимые сведения об организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры,
//                  которые требуется получить в параметре Сведения.
//  Дата - Дата
//       - Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
//  Сведения - Структура - содержит значения затребованных полей.
//  
Процедура ПриПолученииСведенийОбОрганизации(Знач Организация, Знач Поля, Знач Дата, Сведения) Экспорт 
	
	// Локализация
	Сведения = ОрганизацииСервер.СведенияОбОрганизации(Организация, Поля, Дата);
	// Конец Локализация

КонецПроцедуры

// Позволяет переопределить поля контактной информации перед вывод на печать в _ДемоПечатьКарточкиОрганизации
// 
// Параметры:
//  КонтактнаяИнформацияПоОрганизации - Массив из СтрокаТаблицыЗначений:
//    * Объект                            - ЛюбаяСсылка - владелец контактной информации.
//    * ВидКонтактнойИнформации           - СправочникСсылка.ВидыКонтактнойИнформации - вид контактной информации.
//    * Тип                               - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации.
//    * Значение                          - Строка - контактная информация во внутреннем формате JSON.
//    * ПредставлениеКонтактнойИнформации - Строка - представление контактной информации.
//    * Дата                              - Дата - дата, с которой действует запись контактной информации.
//    * ИдентификаторСтрокиТабличнойЧасти - Число - идентификатор строки этой табличной части
//    * ЗначенияПолей                     - Строка - устаревший XML, соответствующий XDTO пакетам КонтактнаяИнформация
//                                          или Адрес. Для обратной совместимости.
//
Процедура ПриПодготовкеКонтактнойИнформацииПередВыводомНаПечать(КонтактнаяИнформацияПоОрганизации) Экспорт

	// Локализация
	Для Каждого СтрокаКонтактнойИнформации Из КонтактнаяИнформацияПоОрганизации  Цикл
		Если СтрокаКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
			ЗначениеКИ = ?(ЗначениеЗаполнено(СтрокаКонтактнойИнформации.Значение), СтрокаКонтактнойИнформации.Значение,
				СтрокаКонтактнойИнформации.ЗначенияПолей);
			НомерЧасовойЗоны = АдресныйКлассификатор.НомерЧасовойЗоны(ЗначениеКИ);
			Если ЗначениеЗаполнено(НомерЧасовойЗоны) Тогда
				ПредставлениеАдреса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru = '%1. %2 Часовая зона: %3.'"), СтрокаКонтактнойИнформации.ПредставлениеКонтактнойИнформации,
					Символы.ПС, НомерЧасовойЗоны);
				СтрокаКонтактнойИнформации.ПредставлениеКонтактнойИнформации = ПредставлениеАдреса;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	// Конец Локализация

КонецПроцедуры

// Позволяет задать дополнительные настройки команд печати.
//
// Параметры:
//   ПолноеИмяОбъектаМетаданных   - ОбъектМетаданных - к которому подключены источники команд
//   КомандыПечати 		- см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ПриПолученииКомандПечати(Знач ПолноеИмяОбъектаМетаданных, КомандыПечати) Экспорт

	// Локализация
	Если ПолноеИмяОбъектаМетаданных = "Документ._ДемоСчетНаОплатуПокупателю" Тогда
		КомандаПечатиКвитанции = КомандыПечати.Найти("Квитанция", "Идентификатор");
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечатиКвитанции, "ВалютаДокумента", Справочники.Валюты.НайтиПоКоду(643));
	КонецЕсли;
	// Конец Локализация
	
КонецПроцедуры

#КонецОбласти
