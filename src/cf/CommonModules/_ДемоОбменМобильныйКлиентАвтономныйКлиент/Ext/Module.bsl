///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Проверяет завершение синхронизации основного и автономного серверов.
//
// Возвращаемое значение:
//  Булево - Истина, если синхронизация не завершена.
//
Функция ВыполняетсяСинхронизация()
	ТекстОшибки = "";
	ИдентификаторФоновогоЗадания = ПараметрыПриложения.Получить("ИдентификаторФоновогоЗадания");
	Возврат ИдентификаторФоновогоЗадания <> Неопределено
		И НЕ _ДемоОбменМобильныйКлиентАвтономныйВызовСервера.ОбменДаннымиЗакончен(ИдентификаторФоновогоЗадания, ТекстОшибки);
КонецФункции

// Выполняет синхронизацию данных
// - запрашивает у пользователя пароль доступа к основной ИБ
// - запускает фоновый процесс синхронизации
// - если не удалось запустить фоновое задание, получает текст ошибки и сообщает пользователю/
//
Процедура НачатьСинхронизацию() Экспорт
	
#Если МобильныйКлиент Тогда
	Если ВыполняетсяСинхронизация() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОсновнойСерверДоступен() <> Истина Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Синхронизация не выполняется из-за отсутствия связи с сервером и возобновится после восстановления связи.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;

	ИдентификаторФоновогоЗадания = _ДемоОбменМобильныйКлиентАвтономныйВызовСервера.ВыполнитьОбменДаннымиВФоне();
	ПараметрыПриложения.Вставить("ИдентификаторФоновогоЗадания", ИдентификаторФоновогоЗадания);
	Оповестить("НачаласьСинхронизация");
	ПодключитьОбработчикОжидания("ОжидатьЗавершениеСинхронизации", 2);
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти