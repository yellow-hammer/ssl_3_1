///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
// ОбщийМодуль.ИнтернетПоддержкаПользователей.
//
// Серверные процедуры и функции Интернет-поддержки пользователей:
//  - определения настроек программы и подключения к сервисам;
//  - обработка аутентификации в сервисах Интернет-поддержки пользователей;
//  - настройка аутентификации в сервисах Интернет-поддержки пользователей;
//  - переход на страницы интегрированных сайтов.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщегоНазначения

// Возвращает имя, по которому программа идентифицируется в
// сервисах Интернет-поддержки.
//
// Возвращаемое значение:
//  Строка - имя программы. <Пустая строка>, если имя программы не заполнено.
//
Функция ИмяПрограммы() Экспорт
	
	Результат = СлужебнаяИмяПрограммы();
	Возврат ?(Результат = "Unknown", "", Результат);
	
КонецФункции

// Возвращает настройки соединения с серверами Интернет-поддержки.
//
// Возвращаемое значение:
//  Структура - настройки соединения. Поля структуры:
//    * УстанавливатьПодключениеНаСервере - Булево - Истина, если подключение
//      устанавливается на сервере 1С:Предприятие;
//    * ТаймаутПодключения - Число - таймаут подключения к серверам в секундах;
//    * ДоменРасположенияСерверовИПП - Число - если 0, устанавливать подключение
//      к серверам ИПП в доменной зоне 1c.ru, если 1 - в доменной зоне 1c.eu.
//
Функция НастройкиСоединенияССерверами() Экспорт
	
	Возврат ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
	
КонецФункции

#КонецОбласти

#Область АутентификацияВСервисахИнтернетПоддержки

// Возвращает логин и пароль пользователя Интернет-поддержки, сохраненные в информационной базе.
// Перед вызовом вызывающий код должен устанавливать привилегированный режим.
//
// Возвращаемое значение:
//  Структура - Структура - логин и пароль пользователя Интернет-поддержки:
//    * Логин - Строка - логин пользователя Интернет-поддержки;
//    * Пароль - Строка - пароль пользователя Интернет-поддержки.
//  Неопределено - при отсутствии сохраненных данных аутентификации.
//
Функция ДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт

	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИдентификаторПодсистемы(),
		"login,password");

	Если ДанныеВБезопасномХранилище.login <> Неопределено
		И ДанныеВБезопасномХранилище.password <> Неопределено Тогда
		Возврат Новый Структура(
			"Логин, Пароль",
			ДанныеВБезопасномХранилище.login,
			ДанныеВБезопасномХранилище.password);
	КонецЕсли;

КонецФункции

// Возвращает тикет аутентификации пользователя на портале поддержки.
// Возвращенный тикет может быть проверен вызовом операции check
// сервиса https://login.1c.ru или https://login.1c.eu
//
// Подробнее см. https://login.1c.ru/rest/public/swagger-ui.html.
//
// Получение тикета выполняется в соответствии с настройками
// библиотеки:
//  - доменная зона серверов (1c.ru или 1c.eu);
// Перед вызовом вызывающий код должен устанавливать привилегированный режим.
//
// Параметры:
//  ВладелецТикета - Строка - произвольное имя сервиса, для которого
//      выполняется аутентификация пользователя. Это же имя должно
//      использоваться при вызове операции checkTicket;
//      Не допускается незаполненное значение параметра.
//
// Возвращаемое значение:
//  Структура - результат получения тикета. Поля структуры:
//        * Тикет - Строка - полученный тикет аутентификации. Если при получении
//          тикета произошла ошибка (неверный логин или пароль или другая ошибка),
//          значение поля - пустая строка.
//        * КодОшибки - Строка - строковый код возникшей ошибки, который
//          может быть обработан вызывающим функционалом:
//              - <Пустая строка> - получение тикета выполнено успешно;
//              - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//              - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                 получения тикета с некорректным логином и паролем;
//              - "ОшибкаПодключения" - ошибка при подключении к сервису;
//              - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//              - "НеизвестнаяОшибка" - при получении тикета возникла
//                 неизвестная (не обрабатываемая) ошибка;
//              - "ОперацияНеПоддерживается" - сервис не интегрирован с Порталом 1С:ИТС.
//                Ошибка может возникнуть при работе в модели сервиса.
//        * СообщениеОбОшибке - Строка - краткое описание ошибки, которое
//          может быть отображено пользователю;
//        * ИнформацияОбОшибке - Строка - подробное описание ошибки, которое
//          может быть записано в журнал регистрации.
//
Функция ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета) Экспорт

	Если Не ЗначениеЗаполнено(ВладелецТикета) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнено значение параметра ""ВладелецТикета""'");
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		// Работа в модели сервиса.
		МодульИнтернетПоддержкаПользователейВМоделиСервиса =
			ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейВМоделиСервиса");
		Результат = МодульИнтернетПоддержкаПользователейВМоделиСервиса.ТикетАутентификацииНаПорталеПоддержки(
			ВладелецТикета);
		
	Иначе
		
		Результат = СлужебнаяТикетАутентификации(
			"",
			"",
			ВладелецТикета,
			НастройкиСоединенияССерверами());
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет заполнение данных аутентификации пользователя
// Интернет-поддержки.
//
// Возвращаемое значение:
//  Булево - признак заполнения данных аутентификации.
//      Истина - данные аутентификации заполнены,
//      Ложь - в противном случае.
//
Функция ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат (ДанныеАутентификацииПользователяИнтернетПоддержки() <> Неопределено);
	
КонецФункции

#КонецОбласти

#Область НастройкаАутентификации

// Проверяет данные аутентификации пользователя
// Интернет-поддержки.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки;
//  Пароль - Строка - пароль пользователя Интернет-поддержки.
//
// Возвращаемое значение:
//  Структура - результат проверки данных аутентификации:
//   *Результат - Булево - результат проверки, если Истина, логин и пароль ведены верно,
//   *КодОшибки - Строка - идентификатор ошибки в случае если логин и пароль ведены верно
//                или в процессе проверки возникли ошибки;
//   *СообщениеОбОшибке - Строка - описание ошибки проверки данных аутентификации.
//
Функция ПроверитьЛогинИПароль(Логин, Пароль) Экспорт
	
	Результат = Новый Структура("КодОшибки, СообщениеОбОшибке, Результат", "", "", Ложь);
	
	ДанныеАутентификации = Новый Структура;
	ДанныеАутентификации.Вставить("Логин", Логин);
	ДанныеАутентификации.Вставить("Пароль", Пароль);
	
	РезультатПроверки = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
		ДанныеАутентификации);
	Если РезультатПроверки.Отказ Тогда
		Результат.КодОшибки = "НеверныйЛогинИлиПароль";
		Результат.СообщениеОбОшибке = РезультатПроверки.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	НастройкиСоединенияССерверами = НастройкиСоединенияССерверами();
	URLСервиса = URLСервисаПроверкиПаролей(НастройкиСоединенияССерверами.ДоменРасположенияСерверовИПП);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ДопПараметрыЗапроса = Новый Структура;
	ДопПараметрыЗапроса.Вставить("Метод"                   , "POST");
	ДопПараметрыЗапроса.Вставить("Заголовки"               , Заголовки);
	ДопПараметрыЗапроса.Вставить("ФорматОтвета"            , 1);
	ДопПараметрыЗапроса.Вставить("ДанныеДляОбработки"      , ПараметрыAuthJSON(Логин, Пароль));
	ДопПараметрыЗапроса.Вставить("ФорматДанныхДляОбработки", 1);
	ДопПараметрыЗапроса.Вставить("Таймаут"                 , 30);
	
	РезультатПолученияФайла = ЗагрузитьСодержимоеИзИнтернет(
		URLСервиса,
		,
		,
		ДопПараметрыЗапроса);
	
	Если РезультатПолученияФайла.КодСостояния = 200 Тогда
		
		Результат.Результат = Истина;
		
	ИначеЕсли РезультатПолученияФайла.КодСостояния = 403 Тогда
		
		Результат.КодОшибки = "НеверныйЛогинИлиПароль";
		Результат.СообщениеОбОшибке = НСтр("ru = 'Неверный логин или пароль.'");
		
	ИначеЕсли РезультатПолученияФайла.КодСостояния = 429 Тогда
		
		Результат.КодОшибки = "ПревышеноКоличествоПопыток";
		Результат.СообщениеОбОшибке = НСтр("ru = 'Превышено количество попыток ввода логина и пароля.
			|Проверьте правильность указанных данных и повторите
			|попытку через 30 минут.'");
		
	Иначе
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось проверить логин и пароль в сервисе %1. %2'"),
				URLСервиса,
				РезультатПолученияФайла.ИнформацияОбОшибке));
		
		Результат.КодОшибки         = РезультатПолученияФайла.КодОшибки;
		Результат.СообщениеОбОшибке = РезультатПолученияФайла.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет логин и пароль пользователя в подсистеме Интернет-поддержки
// в безопасном хранилище. В вызывающем коде перед записью данных необходимо
// выполнить:
//   - Проверку доступности подключения Интернет-поддержки, вызвав
//     метод ИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки;
//   - Вызвать метод ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации
//     или ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль для проверки наличия
//     не корректных символов в данных аутентификации;
//   - Установить привилегированный режим.
//
// В случае реализации собственной формы подключения Интернет-поддержки, необходимо
// после сохранения данных аутентификации, удалить значения введенные пользователем
// для того, чтобы минимизировать время хранения секретных данных на клиенте.
//
// Параметры:
//  ДанныеАутентификации - Структура, Неопределено - структура, содержащая логин 
//  и пароль пользователя и пароль пользователя Интернет-поддержки.
//  Если передано значение Неопределено, данные аутентификации удаляются.
//   * Логин - Строка - логин пользователя Интернет-поддержки;
//   * Пароль - Строка - пароль пользователя Интернет-поддержки.
//
Процедура СохранитьДанныеАутентификации(ДанныеАутентификации) Экспорт
	
	Если Не ДоступноПодключениеИнтернетПоддержки() Или Не ПривилегированныйРежим() Тогда
		ВызватьИсключение НСтр("ru = 'Подключение Интернет-поддержки недоступно.'");
	КонецЕсли;
	
	Если ДанныеАутентификации <> Неопределено Тогда
		РезультатПроверки = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
			ДанныеАутентификации);
		Если РезультатПроверки.Отказ Тогда
			ВызватьИсключение РезультатПроверки.СообщениеОбОшибке;
		КонецЕсли;
	КонецЕсли;
	
	СлужебнаяСохранитьДанныеАутентификации(ДанныеАутентификации);
	
КонецПроцедуры

// Определяет, доступно ли текущему пользователю выполнение интерактивного
// подключения Интернет-поддержки в соответствии с текущим режимом работы
// и правами пользователя.
//
// Возвращаемое значение:
//  Булево - Истина - интерактивное подключение доступно,
//           Ложь - в противном случае.
//
Функция ДоступноПодключениеИнтернетПоддержки() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Пользователи.РолиДоступны("ПодключениеИнтернетПоддержки", , Ложь) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Подключение1СТакском") Тогда
		МодульПодключение1СТакскомВызовСервера = ОбщегоНазначения.ОбщийМодуль("Подключение1СТакскомВызовСервера");
		Если МодульПодключение1СТакскомВызовСервера.ДоступноИспользованиеСервиса1СТакском() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ПереходПоСсылкам

// Возвращает URL для перехода на страницу сайта, система аутентификации
// которого интегрирована с Порталом 1С:ИТС.
// В зависимости от текущего режима работы информационной базы и наличия у
// текущего пользователя информационной базы соответствующих прав
// переданный URL страницы сайта трансформируется таким образом, что открытие
// страницы происходит с учетными данными пользователя Портала 1С:ИТС.
// При отсутствии прав, несоответствии режима работы или возникновении ошибок
// переданный URL возвращается без изменений.
//
// Важно. Полученный URL необходимо использовать сразу после получения, т.к.
// URL действителен ограниченное время (исчисляется секундами).
//
// Параметры:
//  URLСтраницыСайта - Строка - URL страницы сайта;
//
// Возвращаемое значение:
//  Строка - URL для перехода на страницу сайта.
//
Функция URLДляПереходаНаСтраницуИнтегрированногоСайта(URLСтраницыСайта) Экспорт
	
	РезультатПолученияURL = СлужебнаяURLДляПереходаНаСтраницуИнтегрированногоСайта(URLСтраницыСайта);
	Возврат РезультатПолученияURL.URL;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Аутентификация

// Формирует заголовок для способа аутентификации basic authentication.
//
// Параметры:
//  Логин - Строка - логин в сервисе участника СБП;
//  Пароль - Строка - пароль в сервисе участника СБП.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокБазовойСхемыАутентификации(Логин, Пароль) Экспорт
	
	Заголовок = "Basic "
		+ ПолучитьBase64СтрокуИзДвоичныхДанных(
			ПолучитьДвоичныеДанныеИзСтроки(
				Логин + ":" + Пароль));
	
	Возврат СтрЗаменить(
		СтрЗаменить(
			Заголовок,
			Символы.ВК,
			""),
		Символы.ПС,
		"");
	
КонецФункции

// Формирует заголовок для способа аутентификации bearer authentication.
//
// Параметры:
//  Токен - Строка - токен в сервисе участника СБП.
//
// Возвращаемое значение:
//  Строка - заголовок аутентификации.
//
Функция ЗаголовокBearerАутентификации(Токен) Экспорт
	
	Возврат "Bearer " + Токен;
	
КонецФункции

#КонецОбласти

#Область СлужебныйОбщегоНазначения

// Сохраняет логин и пароль пользователя в подсистеме Интернет-поддержки
// в безопасном хранилище. В вызывающем коде перед записью данных необходимо
// проверить права и установить привилегированный режим.
//
// Параметры:
// ДанныеАутентификации - Структура, Неопределено - структура, содержащая логин 
//                        и пароль пользователя и пароль пользователя Интернет-поддержки.
//                        Если передано значение Неопределено, данные аутентификации удаляются.
//   * Логин - Строка - логин пользователя Интернет-поддержки;
//   * Пароль - Строка - пароль пользователя Интернет-поддержки.
//
Процедура СлужебнаяСохранитьДанныеАутентификации(ДанныеАутентификации) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		
		// Удалить все данные для логина из безопасного хранилища.
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИдентификаторПодсистемы());
		
		ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Очищены данные аутентификации.'"),
			Неопределено,
			Ложь);
		
		ПриИзмененииДанныхАутентификации(
			"",
			"");
		
	Иначе
	
		// Запись данных в безопасное хранилище
		ИДПодсистемы = ИдентификаторПодсистемы();
		НачатьТранзакцию();
		Попытка
			ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИДПодсистемы);
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ИДПодсистемы,
				ДанныеАутентификации.Логин,
				"login");

			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ИДПодсистемы,
				ДанныеАутентификации.Пароль,
				"password");
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписатьИнформациюВЖурналРегистрации(
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение;
		КонецПопытки;
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Записаны данные аутентификации, логин ""%1"".'"),
				ДанныеАутентификации.Логин),
			Неопределено,
			Ложь);
		
		ПриИзмененииДанныхАутентификации(
			ДанныеАутентификации.Логин,
			ДанныеАутентификации.Пароль);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку, что переданный объект имеет тип СправочникОбъект.ИдентификаторыОбъектовМетаданных.
//
// Параметры:
//  Объект - Произвольный - объект для проверки.
//
// Возвращаемое значение:
//  Булево - если Истина, тип совпадает с СправочникОбъект.ИдентификаторыОбъектовМетаданных.
//
Функция ЭтоИдентификаторОбъектаМетаданных(Объект) Экспорт
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.ИдентификаторыОбъектовМетаданных");
	
КонецФункции

// Возвращает признак наличия в конфигурации общих реквизитов-разделителей.
//
// Возвращаемое значение:
//   Булево - Истина, если это разделенная конфигурация.
//
Функция ЭтоРазделеннаяКонфигурация() Экспорт
	
	Возврат ИнтернетПоддержкаПользователейСлужебныйПовтИсп.ЭтоРазделеннаяКонфигурация();
	
КонецФункции

// Определяет, сеанс запущен с разделителями или без.
//
// Возвращаемое значение:
//   Булево - Истина, если сеанс запущен без разделителей.
//
Функция СеансЗапущенБезРазделителей() Экспорт
	
	Возврат ИнтернетПоддержкаПользователейСлужебныйПовтИсп.СеансЗапущенБезРазделителей();
	
КонецФункции

// Возвращает значение разделителя сеанса.
//
// Возвращаемое значение:
//  Число - значение разделителя.
//
Функция ЗначениеРазделителяСеанса() Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат 0;
	КонецЕсли;
	
	МодульРаботаВМоделиСервисаБИП = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервисаБИП");
	Возврат МодульРаботаВМоделиСервисаБИП.ЗначениеРазделителяСеанса();
	
КонецФункции

// Возвращает имя программы в сервисах Интернет-поддержки.
//
// Возвращаемое значение:
//  Строка - имя программы.
//
Функция СлужебнаяИмяПрограммы() Экспорт
	
	Результат = ОбщегоНазначения.ИдентификаторИнтернетПоддержкиКонфигурации();
	Если Результат = Неопределено Или ПустаяСтрока(Результат) Тогда
		Результат = "Unknown";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает синоним конфигурации для использования в других подсистемах.
//
// Возвращаемое значение:
//  Строка - синоним программы.
//
Функция СинонимКонфигурации() Экспорт
	
	Возврат Метаданные.Синоним;
	
КонецФункции

// Проверяет доступность изменения параметров Интернет-поддержки пользователей.
//
// Возвращаемое значение:
//  Булево - Истина, если у текущего пользователя есть право записи параметров ИПП.
//           Ложь - в противном случае.
//
Функция ПравоЗаписиПараметровИПП() Экспорт

	Возврат Пользователи.ЭтоПолноправныйПользователь(, , Ложь)
		Или Пользователи.РолиДоступны("ПодключениеИнтернетПоддержки", , Ложь)
		Или ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Подключение1СТакском")
		И Пользователи.РолиДоступны("ИспользованиеСервиса1СТакском", , Ложь);

КонецФункции

// Реализует инкрементальный расчет хеш-суммы и кодировку в формат base64.
// Способ расчета и тип вычисляемого значения определяются типом хеш-функции.
//
// Параметры:
//  Данные  - Строка, ДвоичныеДанные - данные для расчета хеш-суммы.
//
// Возвращаемое значение:
//  Строка - рассчитанная хеш-сумма закодированная по алгоритму base64.
//
Функция КонтрольнаяСуммаФайла(Данные) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
		ХешированиеДанных.Добавить(Данные);
	ИначеЕсли ТипЗнч(Данные) = Тип("Строка") Тогда
		ХешированиеДанных.ДобавитьФайл(Данные);
	Иначе
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректное значение параметра Данные (%1)'"),
			Строка(Данные));
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
		
	Возврат Base64Строка(ХешированиеДанных.ХешСумма);
	
КонецФункции

// Возвращает идентификатор подсистемы в в справочнике объектов
// метаданных.
//
// Возвращаемое значение:
//  Строка - идентификатор подсистемы.
//
Функция ИдентификаторПодсистемы() Экспорт
	
	Возврат "ИнтернетПоддержкаПользователей.БазоваяФункциональностьБИП";
	
КонецФункции

// Возвращает значение свойства Метаданные.Имя
//
// Возвращаемое значение:
//  Строка - имя конфигурации.
//
Функция ИмяКонфигурации() Экспорт

	Возврат Метаданные.Имя;

КонецФункции

// Возвращает значение свойства Метаданные.Версия
//
// Возвращаемое значение:
//  Строка - версия конфигурации.
//
Функция ВерсияКонфигурации() Экспорт
	
	ВерсияПрограммы = Метаданные.Версия;
	ИнтеграцияПодсистемБИП.ПриОпределенииНомераВерсииПрограммы(
		ВерсияПрограммы);
	ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииНомераВерсииПрограммы(
		ВерсияПрограммы);
	
	Возврат ВерсияПрограммы;
	
КонецФункции

// Проверяет доступность переданного URL по критерию:
//  - Код ответа 200;
//  - Таймаут ответа - 10 секунд.
//
// Параметры:
//  URL - Строка - URL для проверки;
//  НастройкиПроксиСервера - Структура, Неопределено - настройки прокси;
//  Метод - Строка - http метод проверки.
//
// Возвращаемое значение:
//  Булево - Истина елис URL доступен.
//
Функция ПроверитьURLДоступен(
		URL,
		НастройкиПроксиСервера = Неопределено,
		Метод = Неопределено) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ИмяОшибки"         , "");
	РезультатПроверки.Вставить("СообщениеОбОшибке" , "");
	РезультатПроверки.Вставить("ИнформацияОбОшибке", "");
	
	// Проверка разрешения работы с сервисом
	Если Не РазрешенаРаботаСХостом(URL) Тогда 
		
		ЕстьПравоАдминистрирования          = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
		РезультатПроверки.ИмяОшибки         = "0";
		РезультатПроверки.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено. %1'"),
			?(ЕстьПравоАдминистрирования,
				НСтр("ru = 'См. Журнал регистрации'"),
				НСтр("ru = 'Обратитесь к администратору.'")));
		РезультатПроверки.ИнформацияОбОшибке = НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено.
			|Для разрешения работы с сервисами откройте раздел Интернет-поддержка и сервисы и нажмите ""Настроить"".'");
		
		Возврат РезультатПроверки;
		
	КонецЕсли;
		
	Если НастройкиПроксиСервера = Неопределено Тогда
		Попытка
			ИнтернетПоддержкаПользователейСлужебныйПовтИсп.ПроверитьURLДоступен(
				URL,
				Метод,
				РезультатПроверки.ИмяОшибки,
				РезультатПроверки.СообщениеОбОшибке,
				РезультатПроверки.ИнформацияОбОшибке);
		Исключение
			Возврат РезультатПроверки;
		КонецПопытки;
	Иначе
		СлужебнаяПроверитьURLДоступен(
			URL,
			Метод,
			РезультатПроверки.ИмяОшибки,
			РезультатПроверки.СообщениеОбОшибке,
			РезультатПроверки.ИнформацияОбОшибке,
			НастройкиПроксиСервера);
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Определяет значение таймауту в зависимости от размера файла.
// Стандарт "748 Таймауты при работе с внешними ресурсами".
// Если известен размер файла, то размер в мегабайтах * 128, иначе
// предельное время загрузки, но не более 43200.
//
// Параметры:
//  Размер - Число - размер файла в байтах.
//
// Возвращаемое значение:
//  Число - время ожидания загрузки в секундах.
//
Функция ТаймаутЗагрузкиФайла(Размер) Экспорт
	
	Таймаут = Размер / 1024 /1024 *128;
	Если Таймаут > 43200 Тогда
		Таймаут = 43200;
	ИначеЕсли Таймаут < 30 Тогда
		Таймаут = 30; // Необходимо учитывать время на установку соединения.
	КонецЕсли;
	
	Возврат Таймаут;
	
КонецФункции

// Возвращает номер версии платформы 1С:Предприятия.
//
// Возвращаемое значение:
//  Строка - версия платформы.
//
Функция ТекущаяВерсияПлатформы1СПредприятие() Экспорт
	
	СистИнфо = Новый СистемнаяИнформация;
	Возврат СистИнфо.ВерсияПриложения;
	
КонецФункции

// Производит проверку хоста для URL полученного из внешних источников.
//
// Параметры:
//  URL - Строка - URL из внешнего источника.
//
Процедура ПроверитьURL(URL) Экспорт
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	ДоменХоста = Прав(НРег(СокрЛП(СтруктураURI.Хост)), 6);
	Если ДоменХоста <> ".1c.ru" И ДоменХоста <> ".1c.eu" Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестный хост (%1)'"),
			СтруктураURI.Хост);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецПроцедуры

// Дополняет запись JSON дополнительными параметрами.
//
// Параметры:
//  ДопПараметрыЗапроса - Структура - дополнительные параметры запроса;
//  ЗаписьДанныхСообщения - ЗаписьJSON - поток записи запроса к сервису.
//
Процедура ЗаписатьДополнительныеПараметрыЗапроса(ДопПараметрыЗапроса, ЗаписьДанныхСообщения) Экспорт
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("additionalParameters");
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	Для каждого КлючЗначение Из ДопПараметрыЗапроса Цикл
		ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("key");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(КлючЗначение.Ключ);
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("value");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(Строка(КлючЗначение.Значение));
		ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	КонецЦикла;
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
КонецПроцедуры

// Останавливает выполнение кода на заданное время.
//
// Параметры:
//  Секунд - Число - время ожидания в секундах.
//
Процедура Пауза(Секунд) Экспорт
	
	ТекущийСеансИнформационнойБазы = ПолучитьТекущийСеансИнформационнойБазы();
	ФоновоеЗадание = ТекущийСеансИнформационнойБазы.ПолучитьФоновоеЗадание();
	
	Если ФоновоеЗадание = Неопределено Тогда
		Параметры = Новый Массив;
		Параметры.Добавить(Секунд);
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("ИнтернетПоддержкаПользователей.Пауза", Параметры);
	КонецЕсли;
	
	ФоновоеЗадание.ОжидатьЗавершенияВыполнения(Секунд);
	
КонецПроцедуры

// Записывает в журнал регистрации сообщение информации
// с именем события "Интернет-поддержка пользователей.Ошибка".
//
// Параметры:
//  Сообщение - Строка - строковое представление ошибки.
//  Данные - Произвольный - данные, к которым относится сообщение об ошибке;
//  Ошибка - Булево - определяет уровень журнала регистрации.
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		Сообщение,
		Данные = Неопределено,
		Ошибка = Истина) Экспорт
	
	УровеньЖР = ?(Ошибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		,
		Данные,
		Сообщение);
	
КонецПроцедуры

// Подставляет в текст домен серверов ИПП в соответствии
// с текущими настройками подключения к серверам.
//
// Параметры:
//  Текст - Строка
//  ДоменнаяЗона - Неопределено, Число - числовой код доменной зоны, Неопределено
//   если передано домен не корректируется.
//
// Возвращаемое значение:
//  Строка - преобразованная строка
//
Функция ПодставитьДомен(Текст, Знач ДоменнаяЗона = Неопределено) Экспорт

	Если ДоменнаяЗона = 1 Тогда
		Результат = СтрЗаменить(Текст, "webits-info@1c.ru", "webits-info@1c.ua");
		Возврат СтрЗаменить(Результат, ".1c.ru", ".1c.eu");
	Иначе
		Возврат Текст;
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область Тарификация

// Определяет доступность услуги по переданному идентификатору
//
// Параметры:
//  ИдентификаторУслуги  - Строка - идентификатор услуги в сервисе;
//  ЗначениеРазделителя - Число - идентификатор области данных.
//
// Возвращаемое значение:
//  Булево - если Истина, услуга доступна.
//
Функция УслугаПодключена(ИдентификаторУслуги, ЗначениеРазделителя = Неопределено) Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		// В локальном режиме нет функциональности
		// для проверки по данным ИБ.
		Возврат Истина;
	Иначе
		
		ТребуетсяРазделение = Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();
		Если ТребуетсяРазделение Тогда
			Если ЗначениеРазделителя = Неопределено Тогда
				ВызватьИсключение НСтр("ru = 'Не заполнено значение параметра ""ЗначениеРазделителя"".'");
			КонецЕсли;
			МодульРаботаВМоделиСервисаБИП = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервисаБИП");
			МодульРаботаВМоделиСервисаБИП.УстановитьРазделениеСеанса(Истина, ЗначениеРазделителя);
		КонецЕсли;
		
		МодульТарификация = ОбщегоНазначения.ОбщийМодуль("Тарификация");
		Результат = МодульТарификация.ЗарегистрированаЛицензияБезлимитнойУслуги(
			ИнтернетПоддержкаПользователейКлиентСервер.ИдентификаторПоставщикаУслугПортал1СИТС(),
			ИдентификаторУслуги);
		
		Если ТребуетсяРазделение Тогда
			МодульРаботаВМоделиСервисаБИП.УстановитьРазделениеСеанса(Ложь);
		КонецЕсли;
		
		Если Не Результат Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Услуга с идентификатором %1 не подключена.'"),
					ИдентификаторУслуги),
				Неопределено,
				Ложь);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПолучениеСодержимогоИзИнтернет

// Загружает содержимое из Интернет по протоколу HTTP(S)
// с использованием методов GET, POST или PUT. Поддерживает
// обработку перенаправлений.
//
// Параметры:
//  URL - Строка - URL вызываемой операции;
//  Логин - Строка - логин basic authentication;
//  Пароль - Строка - пароль basic authentication.
//  ПараметрыЗапроса - Структура, Неопределено - уточняющие параметры запроса:
//   *ФорматОтвета - Число - определяет формат возврата содержимого ответа операции:
//      - 0 - имя файла ответа (формат ответа по умолчанию);
//      - 1 - как строка;
//      - 2 - как двоичные данные;
//   *Метод - Строка - http метод: "GET", "POST" или "PUT". По умолчанию используется GET;
//   *ДанныеДляОбработки - Строка, ДвоичныеДанные - данные для отправки методов
//                         передаваемые методом POST или PUT;
//   *ФорматДанныхДляОбработки - Число - формат отправки данных:
//      - 0 - имя файла (формат ответа по умолчанию);
//      - 1 - как строка;
//      - 2 - как двоичные данные;
//   *Заголовки - Соответствие - заголовки запроса;
//   *ИмяФайлаОтвета - Строка - путь к файлу для записи результата;
//   *Таймаут - Число - время ожидания завершения операции
//   *НастройкиПрокси - Структура - настройки подключения к прокси серверу.
//
// Возвращаемое значение:
//   Структура - содержит результат операции:
//    *КодОшибки - Строка - идентификатор ошибки;
//    *СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//    *ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//    *Содержимое - Строка, ДвоичныеДанные, Неопределено - тело ответа;
//    *КодСостояния - Число - код состояния http, который вернул сервер;
//    *ФорматОтвета - Число - определяет формат возврата содержимого ответа операции:
//      - 0 - имя файла ответа;
//      - 1 - как строка;
//      - 2 - как двоичные данные;
//   *Заголовки - Соответствие - заголовки ответа.
//
Функция ЗагрузитьСодержимоеИзИнтернет(
		Знач URL,
		Знач Логин = Неопределено,
		Знач Пароль = Неопределено,
		ПараметрыЗапроса = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибки"         , "");
	Результат.Вставить("СообщениеОбОшибке" , "");
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Содержимое"        , Неопределено);
	Результат.Вставить("КодСостояния"      , 0);
	Результат.Вставить("ФорматОтвета"      , 0);
	Результат.Вставить("Заголовки"         , Новый Соответствие);
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ФорматОтвета"            , 0);
	ПараметрыПолучения.Вставить("Метод"                   , "GET");
	ПараметрыПолучения.Вставить("ДанныеДляОбработки"      , Неопределено);
	ПараметрыПолучения.Вставить("ФорматДанныхДляОбработки", 0);
	ПараметрыПолучения.Вставить("Заголовки"               , Неопределено);
	ПараметрыПолучения.Вставить("ИмяФайлаОтвета"          , Неопределено);
	ПараметрыПолучения.Вставить("Таймаут"                 , -1);
	ПараметрыПолучения.Вставить("НастройкиПрокси"         , Неопределено);
	
	Если ПараметрыЗапроса <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыПолучения, ПараметрыЗапроса);
	КонецЕсли;
	 
	Если ПараметрыПолучения.Таймаут = -1 Тогда
		// Таймаут по умолчанию.
		ПараметрыПолучения.Таймаут = 30;
	КонецЕсли;
	
	Результат.ФорматОтвета = ПараметрыПолучения.ФорматОтвета;
	
	КоличествоПеренаправлений  = 0;
	МаксКолвоПеренаправлений   = 7;
	Перенаправления            = Новый Массив;
	ВыполненныеПеренаправления = Новый Соответствие;
	ПроксиПоСхемам             = Новый Соответствие;
	ЗащищенноеСоединениеКэш    = Неопределено;
	
	Если Не РазрешенаРаботаСХостом(URL) Тогда
		
		ЕстьПравоАдминистрирования = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено. %1'"),
			?(ЕстьПравоАдминистрирования,
				НСтр("ru = 'См. Журнал регистрации'"),
				НСтр("ru = 'Обратитесь к администратору.'")));
		ИнформацияОбОшибке = НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено.
			|Для разрешения работы с сервисами откройте раздел Интернет-поддержка и сервисы и нажмите ""Настроить"".'");
			
		УстановитьОписаниеОшибки(
			Результат,
			"0",
			СообщениеОбОшибке,
			ИнформацияОбОшибке,
			Перенаправления);
		
		Возврат Результат;
	КонецЕсли;
	
	URLДляПолучения = URL;
	HTTPЗапрос = Новый HTTPЗапрос;
	Если ПараметрыПолучения.Заголовки <> Неопределено Тогда
		HTTPЗапрос.Заголовки = ПараметрыПолучения.Заголовки;
	КонецЕсли;
	ТелоУстановлено = Ложь;
	Ответ = Неопределено;
	Пока КоличествоПеренаправлений < МаксКолвоПеренаправлений Цикл

		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLДляПолучения);
		Если СтруктураURI.Схема <> "https" Тогда
			ЗащищенноеСоединение = Неопределено;
		Иначе
			Если ЗащищенноеСоединениеКэш = Неопределено Тогда
				ЗащищенноеСоединениеКэш = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(
					Неопределено,
					Новый СертификатыУдостоверяющихЦентровОС);
			КонецЕсли;
			ЗащищенноеСоединение = ЗащищенноеСоединениеКэш;
		КонецЕсли;

		Если НЕ ПустаяСтрока(СтруктураURI.Логин) Тогда
			ЛогинДляПолучения  = СтруктураURI.Логин;
			ПарольДляПолучения = СтруктураURI.Пароль;
		Иначе
			ЛогинДляПолучения  = Логин;
			ПарольДляПолучения = Пароль;
		КонецЕсли;

		Если СтруктураURI.Порт = Неопределено ИЛИ ПустаяСтрока(СтруктураURI.Порт) Тогда
			Порт = ?(ЗащищенноеСоединение = Неопределено, 80, 443);
		Иначе
			Порт = Число(СтруктураURI.Порт);
		КонецЕсли;

		Прокси = ПроксиПоСхемам.Получить(СтруктураURI.Схема);
		Если Прокси = Неопределено Тогда
			Если ПараметрыПолучения.НастройкиПрокси = Неопределено Тогда
				Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
			Иначе
				Прокси = СформироватьИнтернетПрокси(ПараметрыПолучения.НастройкиПрокси, СтруктураURI.Схема);
			КонецЕсли;
			ПроксиПоСхемам.Вставить(СтруктураURI.Схема, Прокси);
		КонецЕсли;

		Соединение = Новый HTTPСоединение(
			СтруктураURI.Хост,
			Порт,
			ЛогинДляПолучения,
			ПарольДляПолучения,
			Прокси,
			ПараметрыПолучения.Таймаут,
			ЗащищенноеСоединение);

		Попытка

			HTTPЗапрос.АдресРесурса = СтруктураURI.ПутьНаСервере;

			Если ПараметрыПолучения.Метод = "GET" Тогда
				Ответ = Соединение.Получить(HTTPЗапрос, ПараметрыПолучения.ИмяФайлаОтвета);
			ИначеЕсли ПараметрыПолучения.Метод = "HEAD" Тогда
				Ответ = Соединение.ПолучитьЗаголовки(HTTPЗапрос);
			Иначе
			
				Если НЕ ТелоУстановлено Тогда

					Если ПараметрыПолучения.ДанныеДляОбработки <> Неопределено Тогда

						Если ПараметрыПолучения.ФорматДанныхДляОбработки = 0 Тогда

							HTTPЗапрос.УстановитьИмяФайлаТела(ПараметрыПолучения.ДанныеДляОбработки);

						ИначеЕсли ПараметрыПолучения.ФорматДанныхДляОбработки = 1 Тогда

							HTTPЗапрос.УстановитьТелоИзСтроки(ПараметрыПолучения.ДанныеДляОбработки);

						Иначе

							HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ПараметрыПолучения.ДанныеДляОбработки);

						КонецЕсли;

					КонецЕсли;

					ТелоУстановлено = Истина;

				КонецЕсли;

				Если ПараметрыПолучения.Метод = "PUT" Тогда
					Ответ = Соединение.Записать(HTTPЗапрос);
				Иначе
					// POST
					Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос, ПараметрыПолучения.ИмяФайлаОтвета);
				КонецЕсли;

			КонецЕсли;

		Исключение
			
			ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(
				ИнформацияОбОшибке());
			ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось загрузить содержимое (%1). %2'"),
				URL,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			// Диагностика соединения с ресурсом.
			Попытка
				
				РезультатДиагностики = ПолучениеФайловИзИнтернета.ДиагностикаСоединения(URL);
				ОписаниеРезультатаДиагностики = НСтр("ru = 'Результаты диагностики соединения:'")
					+ Символы.ПС + РезультатДиагностики.ОписаниеОшибки;
				
			Исключение
				
				ОписаниеРезультатаДиагностики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось выполнить диагностику соединения. %1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			КонецПопытки;
			
			УстановитьОписаниеОшибки(
				Результат,
				"ConnectError",
				ПредставлениеОшибки,
				ПодробноеОписаниеОшибки + Символы.ПС + ОписаниеРезультатаДиагностики,
				Перенаправления);
			Возврат Результат;
			
		КонецПопытки;

		Результат.КодСостояния = Ответ.КодСостояния;

		Если Ответ.КодСостояния = 301 // 301 Moved Permanently
			ИЛИ Ответ.КодСостояния = 302 // 302 Found, 302 Moved Temporarily
			ИЛИ Ответ.КодСостояния = 303 // 303 See Other by GET
			ИЛИ Ответ.КодСостояния = 307 Тогда // 307 Temporary Redirect

			КоличествоПеренаправлений = КоличествоПеренаправлений + 1;

			Если КоличествоПеренаправлений > МаксКолвоПеренаправлений Тогда
				УстановитьОписаниеОшибки(
					Результат,
					"ServerError",
					НСтр("ru = 'Превышено количество перенаправлений.'"),
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка сервера при получении файла (%1). Превышено количество перенаправлений (%2).'"),
						URL,
						МаксКолвоПеренаправлений),
					Перенаправления);
				Возврат Результат;
			Иначе
				Location = Ответ.Заголовки.Получить("Location");
				Если Location = Неопределено Тогда
					УстановитьОписаниеОшибки(
						Результат,
						"ServerError",
						НСтр("ru = 'Некорректное перенаправление.'"),
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Ошибка сервера (%1) при получении файла (%2). Некорректное перенаправление, отсутствует HTTP-заголовок ответа ""Location"".'"),
							Ответ.КодСостояния,
							URL),
						Перенаправления);
					Возврат Результат;
				Иначе
					Location = СокрЛП(Location);
					Если ПустаяСтрока(Location) Тогда
						УстановитьОписаниеОшибки(
							Результат,
							"ServerError",
							НСтр("ru = 'Некорректное перенаправление.'"),
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Ошибка сервера (%1) при получении файла (%2). Некорректное перенаправление, пустой HTTP-заголовок ответа ""Location"".'"),
								Ответ.КодСостояния,
								URL),
							Перенаправления);
						Возврат Результат;
					КонецЕсли;

					Если ВыполненныеПеренаправления.Получить(Location) <> Неопределено Тогда
						УстановитьОписаниеОшибки(
							Результат,
							"ServerError",
							НСтр("ru = 'Циклическое перенаправление.'"),
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Ошибка сервера (%1) при получении файла (%2). Циклическое перенаправление (%3).'"),
								Ответ.КодСостояния,
								URL,
								Location),
							Перенаправления);
						Возврат Результат;
					КонецЕсли;

					ВыполненныеПеренаправления.Вставить(Location, Истина);
					URLДляПолучения = Location;

					Перенаправления.Добавить(Строка(Ответ.КодСостояния) + ": " + Location);

				КонецЕсли;
				
			КонецЕсли;

		Иначе

			Прервать;

		КонецЕсли;

	КонецЦикла;

	Если ПараметрыПолучения.ФорматОтвета = 0 Тогда
		Результат.Содержимое = Ответ.ПолучитьИмяФайлаТела();
	ИначеЕсли ПараметрыПолучения.ФорматОтвета = 1 Тогда
		Результат.Содержимое = Ответ.ПолучитьТелоКакСтроку();
	ИначеЕсли ПараметрыПолучения.ФорматОтвета = 2 Тогда
		Результат.Содержимое = Ответ.ПолучитьТелоКакДвоичныеДанные();
	Иначе
		Результат.Содержимое = Ответ;
	КонецЕсли;
	
	Если Ответ <> Неопределено Тогда
		Результат.Заголовки = Ответ.Заголовки;
	КонецЕсли;
	
	// Обработка ответа
	Если Ответ.КодСостояния < 200 Или Ответ.КодСостояния >= 300 Тогда

		// Анализ ошибки
		Если Ответ.КодСостояния = 407 Тогда

			// Ошибка подключения - не пройдена аутентификация на прокси-сервере.
			УстановитьОписаниеОшибки(
				Результат,
				"ConnectError",
				НСтр("ru = 'Ошибка аутентификации на прокси-сервере.'"),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка клиента (%1) при выполнении запроса к ресурсу (%2).
						|Тело ответа: %3'"),
					Ответ.КодСостояния,
					URL,
					Лев(Ответ.ПолучитьТелоКакСтроку(), 5120)),
				Перенаправления);

		ИначеЕсли Ответ.КодСостояния < 200
			ИЛИ Ответ.КодСостояния >= 300
			И Ответ.КодСостояния < 400 Тогда

			// Формат ответа сервера не поддерживается.
			УстановитьОписаниеОшибки(
				Результат,
				"ServerError",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Некорректный ответ сервера (%1).'"),
					Ответ.КодСостояния),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка сервера при получении файла (%1). Некорректный (неподдерживаемый) ответ (%2).
						|Тело ответа: %3'"),
					URL,
					Ответ.КодСостояния,
					Лев(Ответ.ПолучитьТелоКакСтроку(), 5120)),
				Перенаправления);

		ИначеЕсли Ответ.КодСостояния >= 400 И Ответ.КодСостояния < 500 Тогда

			// Ошибка клиентской части - некорректный запрос.
			УстановитьОписаниеОшибки(
				Результат,
				"ClientError",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка (%1) при выполнении запроса к ресурсу.'"),
					Строка(Ответ.КодСостояния)),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка клиента (%1) при выполнении запроса к ресурсу (%2).
						|Тело ответа: %3'"),
					Ответ.КодСостояния,
					URL,
					Лев(Ответ.ПолучитьТелоКакСтроку(), 5120)),
				Перенаправления);

		Иначе

			// Ошибка сервера - 5хх
			УстановитьОписаниеОшибки(
				Результат,
				"ServerError",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Код ошибки: %1.'"),
					Строка(Ответ.КодСостояния)),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка сервера (%1) при обработке запроса к ресурсу (%2).
						|Тело ответа: %3'"),
					Ответ.КодСостояния,
					URL,
					Лев(Ответ.ПолучитьТелоКакСтроку(), 5120)),
				Перенаправления);

		КонецЕсли;

		ДобавитьСписокПеренаправленийКИнформацииОбОшибке(
			Результат.ИнформацияОбОшибке,
			Перенаправления);

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает WSПрокси после проверки на запрет работы с ресурсом.
//
// Параметры:
//  ПараметрыПодключения - Структура - см. ОбщегоНазначения.ПараметрыПодключенияWSПрокси.
//
// Возвращаемое значение:
//  WSПрокси - см. ОбщегоНазначения.СоздатьWSПрокси.
//
Функция СоздатьWSПрокси(ПараметрыПодключения) Экспорт
	
	Если Не РазрешенаРаботаСХостом(ПараметрыПодключения.АдресWSDL) Тогда
		
		ЕстьПравоАдминистрирования = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено. %1'"),
			?(ЕстьПравоАдминистрирования,
				НСтр("ru = 'См. Журнал регистрации'"),
				НСтр("ru = 'Обратитесь к администратору.'")));
			
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	
КонецФункции

// Возвращает HTTPСоединение после проверки на запрет работы с ресурсом.
//
// Параметры:
//  ПараметрыПодключения - Структура - свойства HTTPСоединения.
//
// Возвращаемое значение:
//  HTTPСоединение - HTTPСоединение, созданное на основании параметров.
//
Функция СоздатьHTTPСоединение(ПараметрыПодключения) Экспорт
	
	Если Не РазрешенаРаботаСХостом(ПараметрыПодключения.Сервер) Тогда
		
		ЕстьПравоАдминистрирования = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено. %1'"),
			?(ЕстьПравоАдминистрирования,
				НСтр("ru = 'См. Журнал регистрации'"),
				НСтр("ru = 'Обратитесь к администратору.'")));
			
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Результат = Новый HTTPСоединение(
		ПараметрыПодключения.Сервер,
		ПараметрыПодключения.Порт,
		ПараметрыПодключения.Пользователь,
		ПараметрыПодключения.Пароль,
		ПараметрыПодключения.Прокси,
		ПараметрыПодключения.Таймаут,
		ПараметрыПодключения.ЗащищенноеСоединение);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийСервисов

// Формирует структуру дополнительных параметров для передачи в сервисы.
//
// Параметры:
//  ФорматЗначенийСтрока - Булево - формат смещения.
//
// Возвращаемое значение:
//  Соответствие - дополнительные параметры.
//
Функция ДополнительныеПараметрыВызоваОперацииСервиса(ФорматЗначенийСтрока = Истина) Экспорт
	
	Результат = Новый Соответствие;
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И Не ОбщегоНазначения.ЭтоВебКлиент() Тогда
		
		Результат.Вставить("ClientPlatformType",
			ОбщегоНазначенияКлиентСервер.ИмяТипаПлатформы(
				СистемнаяИнформация.ТипПлатформы));
		Результат.Вставить("ClientOSVersion", СистемнаяИнформация.ВерсияОС);
		
	Иначе
		
		СистемнаяИнформацияКлиент = ОбщегоНазначения.СистемнаяИнформацияКлиента();
		Если СистемнаяИнформацияКлиент <> Неопределено Тогда
			Результат.Вставить("ClientPlatformType", СистемнаяИнформацияКлиент.ТипПлатформы);
			Результат.Вставить("ClientOSVersion", СистемнаяИнформацияКлиент.ВерсияОС);
		КонецЕсли;
		
		Результат.Вставить("ServerPlatformType",
			ОбщегоНазначенияКлиентСервер.ИмяТипаПлатформы(
				СистемнаяИнформация.ТипПлатформы));
		Результат.Вставить("ServerOSVersion", СистемнаяИнформация.ВерсияОС);
		
	КонецЕсли;
	
	Результат.Вставить("PlatformVersion", СистемнаяИнформация.ВерсияПриложения);
	
	Результат.Вставить("LibraryVersion",
		ИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки());
	Результат.Вставить("ConfigName", ИмяКонфигурации());
	Результат.Вставить("ConfigVersion", ВерсияКонфигурации());
	Результат.Вставить("Vendor", Метаданные.Поставщик);
	Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Результат.Вставить("IBID",
			СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	КонецЕсли;
	Результат.Вставить("ConfigLanguage", КодЯзыкаИнтерфейсаКонфигурации());
	Результат.Вставить("ConfigMainLanguage", КодОсновногоЯзыкаИнтерфейсаКонфигурации());
	Результат.Вставить("CurLocalizationCode", ТекущийКодЛокализации());
	Результат.Вставить("SystemLanguage", ТекущийЯзыкСистемы());
	Результат.Вставить("ClientTimeOffsetGMT",
		?(ФорматЗначенийСтрока,
		  Формат((ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата()), "ЧГ=0"),
		  (ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата())));
	
	Результат.Вставить("countryId", "");
	
	Результат.Вставить(
		"IBIsSeparated",
		?(ФорматЗначенийСтрока,
		  ?(ОбщегоНазначения.РазделениеВключено(), "true", "false"),
		  ОбщегоНазначения.РазделениеВключено()));
	Результат.Вставить("IBUserName", Строка(ИмяПользователя()));
	
	НастройкиСоединения = ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
	Если НастройкиСоединения.ДоменРасположенияСерверовИПП = 0 Тогда
		Результат.Вставить("DomainZone", "ru");
	ИначеЕсли НастройкиСоединения.ДоменРасположенияСерверовИПП = 1 Тогда
		Результат.Вставить("DomainZone", "eu");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НастройкиКлиентаЛицензирования

// Возвращает признак возможности работы с настройками клиента лицензирования
// в БИП.
//
// Возвращаемое значение:
//  Булево - Истина если версия платформы 1С:Предприятие 8.3.7 или выше и не работа в модели сервиса.
//
Функция ДоступнаРаботаСНастройкамиКлиентаЛицензирования() Экспорт

	Возврат Не ОбщегоНазначения.РазделениеВключено();

КонецФункции

#КонецОбласти

#Область ШаблоныСообщений

// См. ШаблоныСообщенийПереопределяемый.ПриПодготовкеШаблонаСообщения.
//
Процедура ПриПодготовкеШаблонаСообщения(
		Реквизиты,
		Вложения,
		НазначениеШаблона,
		ДополнительныеПараметры) Экспорт
		
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСлужебный");
		МодульОнлайнЗаказыСлужебный.ПриПодготовкеШаблонаСообщения(
			Реквизиты,
			Вложения,
			НазначениеШаблона,
			ДополнительныеПараметры);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнОплаты") Тогда
		МодульОнлайнОплаты = ОбщегоНазначения.ОбщийМодуль("ОнлайнОплаты");
		МодульОнлайнОплаты.ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСобытия
			= ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
		МодульСистемаБыстрыхПлатежейСобытия.ПриПодготовкеШаблонаСообщения(
			Реквизиты,
			Вложения,
			НазначениеШаблона,
			ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// См. ШаблоныСообщенийПереопределяемый.ПриФормированииСообщения.
//
Процедура ПриФормированииСообщения(
		Сообщение,
		НазначениеШаблона,
		ПредметСообщения,
		ПараметрыШаблона) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСлужебный");
		МодульОнлайнЗаказыСлужебный.ПриФормированииСообщения(
			Сообщение,
			НазначениеШаблона,
			ПредметСообщения,
			ПараметрыШаблона);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнОплаты") Тогда
		МодульОнлайнОплаты = ОбщегоНазначения.ОбщийМодуль("ОнлайнОплаты");
		МодульОнлайнОплаты.ПриФормированииСообщения(Сообщение, НазначениеШаблона, ПредметСообщения, ПараметрыШаблона);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСобытия
			= ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
		МодульСистемаБыстрыхПлатежейСобытия.ПриФормированииСообщения(
			Сообщение,
			НазначениеШаблона,
			ПредметСообщения,
			ПараметрыШаблона);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФункциональныеОпцииБСП

// Проверяет наличие подсистемы "Взаимодействия" и производит включение функциональной опции, отвечающей за SMS.
//
Процедура УстановитьИспользованиеSMS() Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		Возврат;
	КонецЕсли;
	
	МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("Взаимодействия");
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не МодульВзаимодействия.ИспользуютсяПрочиеВзаимодействия() Тогда
		МодульВзаимодействия.УстановитьИспользованиеПрочегоВзаимодействия(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяет наличие подсистемы "Шаблоны сообщений" и производит включение соответствующей функциональной опции.
//
Процедура УстановитьИспользованиеШаблоновСообщений() Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	МодульШаблоныСообщений = ОбщегоНазначения.ОбщийМодуль("ШаблоныСообщений");
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не МодульШаблоныСообщений.ИспользуютсяШаблоныСообщений() Тогда
		МодульШаблоныСообщений.УстановитьИспользованиеШаблоновСообщений(Истина);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяет наличие подсистемы "Взаимодействия" и производит включение функциональной опции, отвечающей за ЭП.
//
Процедура УстановитьИспользованиеЭлектроннойПочты() Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		Возврат;
	КонецЕсли;
	
	МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("Взаимодействия");
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не МодульВзаимодействия.ИспользуетсяПочтовыйКлиент() Тогда
		МодульВзаимодействия.УстановитьИспользованиеПочтовогоКлиента(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяет наличие подсистемы "Взаимодействия" и производит включение функциональной опции,
// отвечающей за отправку писем в формате HTML.
//
Процедура УстановитьОтправкуПисемВФорматеHTML() Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		Возврат;
	КонецЕсли;
	
	МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("Взаимодействия");
	
	УстановитьПривилегированныйРежим(Истина);

	Если Не МодульВзаимодействия.ИспользуетсяОтправкаПисемВФорматеHTML() Тогда
		МодульВзаимодействия.УстановитьИспользованиеОтправкиПисемВФорматеHTML(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Возвращает значение функциональной опции "ОтправлятьПисьмаВФорматеHTML"
//
// Возвращаемое значение:
//  Булево - признак использования функциональной опции "ОтправлятьПисьмаВФорматеHTML".
//
Функция ОтправлятьПисьмаВФорматеHTML() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("Взаимодействия");
		Возврат МодульВзаимодействия.ИспользуетсяОтправкаПисемВФорматеHTML();
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Аутентификация

// Определяет URL операции получения тикета в зависимости от переданного домена.
//
// Параметры:
//  Домен - Число - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - URL операции получения тикета.
//
Функция URLОперацииПолучитьТикет(Домен)

	Возврат "https://"
		+ ИнтернетПоддержкаПользователейКлиентСервер.ХостСервисаLogin(Домен)
		+ "/rest/public/ticket/get";

КонецФункции

// Определяет URL операции проверки данных аутентификации в зависимости от переданного домена.
//
// Параметры:
//  Домен - Число - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - URL операции проверки данных аутентификации.
//
Функция URLСервисаПроверкиПаролей(Домен)

	Возврат "https://"
		+ ИнтернетПоддержкаПользователейКлиентСервер.ХостСервисаLogin(Домен)
		+ "/rest/public/user/auth";

КонецФункции

// Внутренняя функция для получения тикетов аутентификации.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки;
//  Пароль - Строка - пароль пользователя Интернет-поддержки.
//  ВладелецТикета - Строка - произвольное имя сервиса, для которого
//      выполняется аутентификация пользователя. Это же имя должно
//      использоваться при вызове операции checkTicket;
//      Не допускается незаполненное значение параметра.
//  НастройкиСоединения - Структура - см. НастройкиСоединенияССерверами;
//
// Возвращаемое значение:
//  Структура - результат получения тикета. Поля структуры:
//        * Тикет - Строка - полученный тикет аутентификации. Если при получении
//          тикета произошла ошибка (неверный логин или пароль или другая ошибка),
//          значение поля - пустая строка.
//        * КодОшибки - Строка - строковый код возникшей ошибки, который
//          может быть обработан вызывающим функционалом:
//              - <Пустая строка> - получение тикета выполнено успешно;
//              - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//              - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                 получения тикета с некорректным логином и паролем;
//              - "ОшибкаПодключения" - ошибка при подключении к сервису;
//              - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//              - "НеизвестнаяОшибка" - при получении тикета возникла
//                 неизвестная (не обрабатываемая) ошибка;
//        * СообщениеОбОшибке - Строка - краткое описание ошибки, которое
//          может быть отображено пользователю;
//        * ИнформацияОбОшибке - Строка - подробное описание ошибки, которое
//          может быть записано в журнал регистрации.
//
Функция СлужебнаяТикетАутентификации(
	Знач Логин,
	Знач Пароль,
	Знач ИмяСервиса,
	НастройкиСоединения)
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибки"         , "");
	Результат.Вставить("СообщениеОбОшибке" , "");
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Тикет"             , Неопределено);
	
	Если Не ЗначениеЗаполнено(Логин) Тогда
		ДанныеАутентификации = ДанныеАутентификацииПользователяИнтернетПоддержки();
		Если ДанныеАутентификации <> Неопределено Тогда
			Логин  = ДанныеАутентификации.Логин;
			Пароль = ДанныеАутентификации.Пароль;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Логин) Тогда
		Результат.КодОшибки = "НеверныйЛогинИлиПароль";
		Результат.СообщениеОбОшибке  = НСтр("ru = 'Неверный логин или пароль.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	Если НастройкиСоединения = Неопределено Тогда
		НастройкиСоединения = НастройкиСоединенияССерверами();
	КонецЕсли;
	
	URLСервиса = URLОперацииПолучитьТикет(
		НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ДопПараметрыЗапроса = Новый Структура;
	ДопПараметрыЗапроса.Вставить("Метод"                   , "POST");
	ДопПараметрыЗапроса.Вставить("Заголовки"               , Заголовки);
	ДопПараметрыЗапроса.Вставить("ФорматОтвета"            , 1);
	ДопПараметрыЗапроса.Вставить("ДанныеДляОбработки"      , ПараметрыTicketGetJSON(Логин, Пароль, ИмяСервиса));
	ДопПараметрыЗапроса.Вставить("ФорматДанныхДляОбработки", 1);
	ДопПараметрыЗапроса.Вставить("Таймаут"                 , 30);

	РезультатОперации = ЗагрузитьСодержимоеИзИнтернет(
		URLСервиса,
		,
		,
		ДопПараметрыЗапроса);

	Если РезультатОперации.КодСостояния = 200 Тогда
		
		Попытка
			
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(РезультатОперации.Содержимое);
			ОтветОбъект = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			Результат.Тикет = ОтветОбъект.ticket;
			
		Исключение
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Результат.КодОшибки = "ОшибкаСервиса";
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить тикет аутентификации в сервисе %1.
					|Некорректный ответ сервиса.
					|Ошибка при обработке ответа сервиса:
					|%2
					|Код состояния: %3;
					|Тело ответа: %4'"),
				URLСервиса,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
				РезультатОперации.КодСостояния,
				Лев(РезультатОперации.Содержимое, 5120));
			ЗаписатьИнформациюВЖурналРегистрации(
				Результат.ИнформацияОбОшибке);
			Результат.СообщениеОбОшибке =
				НСтр("ru = 'Ошибка аутентификации в сервисе.
					|Подробнее см. в журнале регистрации.'");
			
		КонецПопытки;
		
	ИначеЕсли РезультатОперации.КодСостояния = 403 Тогда
		
		Результат.КодОшибки = "НеверныйЛогинИлиПароль";
		Результат.СообщениеОбОшибке  = НСтр("ru = 'Неверный логин или пароль.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		
	ИначеЕсли РезультатОперации.КодСостояния = 429 Тогда
		
		Результат.КодОшибки = "ПревышеноКоличествоПопыток";
		Результат.СообщениеОбОшибке = НСтр("ru = 'Превышено количество попыток аутентификации.
			|Повторите попытку позже.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		
	ИначеЕсли РезультатОперации.КодСостояния = 500 Тогда
		
		Результат.КодОшибки          = "ОшибкаСервиса";
		Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить тикет аутентификации в сервисе %1.
				|Внутренняя ошибка сервиса.
				|Код состояния: %2;
				|Тело ответа: %3'"),
			URLСервиса,
			РезультатОперации.КодСостояния,
			Лев(РезультатОперации.Содержимое, 5120));
		ЗаписатьИнформациюВЖурналРегистрации(
			Результат.ИнформацияОбОшибке);
		
		Результат.СообщениеОбОшибке =
			НСтр("ru = 'Ошибка аутентификации. Внутренняя ошибка сервиса.
				|Подробнее см. в журнале регистрации.'");
		
	ИначеЕсли РезультатОперации.КодСостояния = 0 Тогда
		
		Результат.КодОшибки         = "ОшибкаПодключения";
		Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить тикет аутентификации в сервисе %1.
				|%2'"),
			URLСервиса,
			РезультатОперации.ИнформацияОбОшибке);
		ЗаписатьИнформациюВЖурналРегистрации(
			Результат.ИнформацияОбОшибке);
		Результат.СообщениеОбОшибке = НСтр("ru = 'Ошибка подключения к сервису.
			|Подробнее см. в журнале регистрации.'");
		
	Иначе
		
		Результат.КодОшибки = "НеизвестнаяОшибка";
		Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить тикет аутентификации в сервисе %1.
				|Неизвестный код состояния ответа сервиса.
				|Код состояния: %2;
				|Тело ответа: %3'"),
			URLСервиса,
			РезультатОперации.КодСостояния,
			Лев(РезультатОперации.Содержимое, 5120));
		ЗаписатьИнформациюВЖурналРегистрации(
			Результат.ИнформацияОбОшибке);
		Результат.СообщениеОбОшибке =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка аутентификации в сервисе (%1).
					|Подробнее см. в журнале регистрации.'"),
				РезультатОперации.КодСостояния);
		
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Формирует тело запроса на получение тикета аутентификации.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки;
//  Пароль - Строка - пароль пользователя Интернет-поддержки.
//  ВладелецТикета - Строка - произвольное имя сервиса, для которого
//      выполняется аутентификация пользователя. Это же имя должно
//      использоваться при вызове операции checkTicket;
//      Не допускается незаполненное значение параметра.
//
// Возвращаемое значение:
//  Строка - тело запроса
//
Функция ПараметрыTicketGetJSON(Логин, Пароль, ВладелецТикета)

	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();

	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Логин);

	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("serviceNick");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ВладелецТикета);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();

	Возврат ЗаписьДанныхСообщения.Закрыть();

КонецФункции

// Формирует тело запроса для проверки данных аутентификации.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки;
//  Пароль - Строка - пароль пользователя Интернет-поддержки.
//
// Возвращаемое значение:
//  Строка - тело запроса
//
Функция ПараметрыAuthJSON(Логин, Пароль)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// См. ИнтернетПоддержкаПользователей.URLДляПереходаНаСтраницуИнтегрированногоСайта
//
Функция СлужебнаяURLДляПереходаНаСтраницуИнтегрированногоСайта(URLСтраницыСайта) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибки", "");
	Результат.Вставить("URL"      , URLСтраницыСайта);
	
	РезультатПолученияТикета = Неопределено;
	Если ОбщегоНазначения.РазделениеВключено() Или Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь) Тогда
		УстановитьПривилегированныйРежим(Истина);
		РезультатПолученияТикета = ТикетАутентификацииНаПорталеПоддержки(URLСтраницыСайта);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если РезультатПолученияТикета <> Неопределено Тогда
		Если ПустаяСтрока(РезультатПолученияТикета.КодОшибки) Тогда
			Результат.URL =
				ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin(
					"/ticket/auth?token=" + РезультатПолученияТикета.Тикет,
					НастройкиСоединенияССерверами());
		Иначе
			Результат.КодОшибки = РезультатПолученияТикета.КодОшибки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработкаСобытийБиблиотеки

// Вызывается при изменении логина и пароля пользователя ИПП в
// информационной базе из всех контекстов использования библиотеки.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки;
//  Пароль - Строка - пароль пользователя Интернет-поддержки.
//
Процедура ПриИзмененииДанныхАутентификации(Логин, Пароль)
	
	Если ДоступнаРаботаСНастройкамиКлиентаЛицензирования() Тогда
		УстановитьПривилегированныйРежим(Истина);
		КлиентЛицензирования.ПриИзмененииДанныхАутентификации(Логин, Пароль);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	// МониторПортала1СИТС
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.МониторПортала1СИТС") Тогда
		МодульМониторПортала1СИТС = ОбщегоНазначения.ОбщийМодуль("МониторПортала1СИТС");
		МодульМониторПортала1СИТС.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец МониторПортала1СИТС
	
	// ОблачныеКассы
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныеКассы") Тогда
		МодульОблачныеКассыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассыСлужебный");
		МодульОблачныеКассыСлужебный.ПриИзмененииДанныхАутентификации(Логин);
	КонецЕсли;
	// Конец ОблачныеКассы
	
	// СПАРКРиски
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СПАРКРиски") Тогда
		МодульСПАРКРиски = ОбщегоНазначения.ОбщийМодуль("СПАРКРиски");
		МодульСПАРКРиски.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец СПАРКРиски
	
	// РаботаСКлассификаторами
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКлассификаторами") Тогда
		МодульРаботаСКлассификаторами = ОбщегоНазначения.ОбщийМодуль("РаботаСКлассификаторами");
		МодульРаботаСКлассификаторами.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец РаботаСКлассификаторами
	
	// ПолучениеВнешнихКомпонент
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент") Тогда
		МодульПолучениеВнешнихКомпонент = ОбщегоНазначения.ОбщийМодуль("ПолучениеВнешнихКомпонент");
		МодульПолучениеВнешнихКомпонент.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец ПолучениеВнешнихКомпонент
	
	// ПодключениеСервисовСопровождения
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПодключениеСервисовСопровождения") Тогда
		МодульПодключениеСервисовСопровождения = ОбщегоНазначения.ОбщийМодуль("ПодключениеСервисовСопровождения");
		МодульПодключениеСервисовСопровождения.ПриИзмененииДанныхАутентификации();
	КонецЕсли;
	// Конец ПодключениеСервисовСопровождения
	
	// ПолучениеОбновленийПрограммы
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		МодульПолучениеОбновленийПрограммы.ПриИзмененииДанныхАутентификации(Логин);
	КонецЕсли;
	// Конец ПолучениеОбновленийПрограммы
	
	// ОнлайнЗаказы
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСлужебный");
		МодульОнлайнЗаказыСлужебный.ПриИзмененииДанныхАутентификации(Логин);
	КонецЕсли;
	// Конец ОнлайнЗаказы
	
	// СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСобытия = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
		МодульСистемаБыстрыхПлатежейСобытия.ПриИзмененииДанныхАутентификации(Логин);
	КонецЕсли;
	// Конец СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП
	
	// ПолучениеРегламентированныхОтчетов
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеРегламентированныхОтчетов") Тогда
		МодульПолучениеРегламентированныхОтчетов = ОбщегоНазначения.ОбщийМодуль("ПолучениеРегламентированныхОтчетов");
		МодульПолучениеРегламентированныхОтчетов.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец ПолучениеРегламентированныхОтчетов
	
	// ПоставляемыеНастройки
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПоставляемыеНастройки") Тогда
		МодульПоставляемыеНастройкиСервер = ОбщегоНазначения.ОбщийМодуль("ПоставляемыеНастройкиСервер");
		МодульПоставляемыеНастройкиСервер.ПриИзмененииДанныхАутентификации(Логин, Пароль);
	КонецЕсли;
	// Конец ПоставляемыеНастройки
	
	// Переопределяемая обработка события.
	Если ЗначениеЗаполнено(Логин) Тогда 
		ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
		ИнтеграцияПодсистемБИП.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(
			ДанныеАутентификации);
		ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(
			ДанныеАутентификации);
	Иначе
		ИнтеграцияПодсистемБИП.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(
			Неопределено);
		ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(
			Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеСодержимогоИзИнтернет

// Устанавливает описание ошибки в результат выполнения операции.
//
// Параметры:
//  Результат - Структура - результат выполнения операции;
//  КодОшибки - Строка - Идентификатор ошибки
//  СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//  ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//  Перенаправления - Массив - данные перенаправлений.
//
Процедура УстановитьОписаниеОшибки(
		Результат,
		КодОшибки,
		СообщениеОбОшибке,
		ИнформацияОбОшибке,
		Перенаправления)
	
	Результат.КодОшибки          = КодОшибки;
	Результат.СообщениеОбОшибке  = СообщениеОбОшибке;
	Сообщение = "";
	Если КодОшибки = "ConnectError" Тогда
		Сообщение = НСтр("ru = 'Ошибка при подключении к серверу.'");
	ИначеЕсли КодОшибки = "ServerError" Тогда
		Сообщение = НСтр("ru = 'На сервере возникла внутренняя ошибка при обработке запроса.'");
	ИначеЕсли КодОшибки = "ClientError" Тогда
		Сообщение = НСтр("ru = 'Некорректный запрос.'");
	ИначеЕсли КодОшибки = "InternalError" Тогда
		Сообщение = НСтр("ru = 'Внутренняя ошибка.'");
	ИначеЕсли КодОшибки = "LoginError" Тогда
		Сообщение = НСтр("ru = 'Ошибка аутентификации на сервере.'");
	КонецЕсли;
	
	Результат.СообщениеОбОшибке =
		?(ПустаяСтрока(Сообщение), "", Сообщение + " ")
		+ СообщениеОбОшибке;
	
	Результат.ИнформацияОбОшибке = ИнформацияОбОшибке;
	
	Если Перенаправления.Количество() > 0 Тогда
		Результат.ИнформацияОбОшибке = Результат.ИнформацияОбОшибке + Символы.ПС
			+ НСтр("ru = 'Перенаправления:'") + Символы.ПС
			+ СтрСоединить(Перенаправления, ", " + Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает описание перенаправления при выполнения операции.
//
// Параметры:
//  ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//  Перенаправления - Массив - данные перенаправлений.
//
Процедура ДобавитьСписокПеренаправленийКИнформацииОбОшибке(
		ИнформацияОбОшибке,
		Перенаправления)
	
	Если Перенаправления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОбОшибке = ИнформацияОбОшибке + Символы.ПС
		+ НСтр("ru = 'Перенаправления:'") + Символы.ПС
		+ СтрСоединить(Перенаправления, ", " + Символы.ПС);
	
КонецПроцедуры

// Проверяет доступность переданного URL по критерию:
//  - Код ответа 200;
//  - Таймаут ответа - 10 секунд.
//
// Параметры:
//  URL - Строка - URL для проверки;
//  Метод - Строка - http метод проверки;
//  ИмяОшибки - Строка - идентификатор ошибки;
//  СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//  ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//  НастройкиПроксиСервера - Структура, Неопределено - настройки прокси;
//
Процедура СлужебнаяПроверитьURLДоступен(
		URL,
		Метод,
		ИмяОшибки,
		СообщениеОбОшибке,
		ИнформацияОбОшибке,
		НастройкиПроксиСервера = Неопределено) Экспорт
	
	ДопПараметрыПолученияФайла = Новый Структура("ФорматОтвета, Таймаут", 1, 10);
	ДопПараметрыПолученияФайла.Вставить("НастройкиПрокси", НастройкиПроксиСервера);
	
	Если Метод <> Неопределено Тогда
		ДопПараметрыПолученияФайла.Вставить("Метод", Метод);
	КонецЕсли;
	
	Попытка
		РезультатЗагрузки = ЗагрузитьСодержимоеИзИнтернет(
			URL,
			,
			,
			ДопПараметрыПолученияФайла);
	Исключение
		ИмяОшибки = "Unknown";
		СообщениеОбОшибке = НСтр("ru = 'Неизвестная ошибка. Подробнее см. в журнале регистрации.'");
		ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестная ошибка при проверке доступности URL.
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	Если Не ПустаяСтрока(РезультатЗагрузки.КодОшибки) Тогда
		ИмяОшибки          = РезультатЗагрузки.КодОшибки;
		СообщениеОбОшибке  = РезультатЗагрузки.СообщениеОбОшибке;
		ИнформацияОбОшибке = РезультатЗагрузки.ИнформацияОбОшибке;
	КонецЕсли;
	
КонецПроцедуры

// Формирует ИнтернетПрокси на основании переданных настроек.
//
// Параметры:
//  НастройкаПроксиСервера - Строка, Соответствие - параметры подключения к прокси;
//  Протокол - Строка - протокол подключения к прокси.
//
// Возвращаемое значение:
//  ИнтернетПрокси - подготовленный объект прокси.
//
Функция СформироватьИнтернетПрокси(
		НастройкаПроксиСервера,
		Протокол)
	
	Если НастройкаПроксиСервера = Неопределено
		Или НастройкаПроксиСервера = "<СистемныеУстановки>" Тогда
		// Системные установки прокси-сервера.
		Возврат Неопределено;
	КонецЕсли;	
	
	ИспользоватьПрокси = НастройкаПроксиСервера.Получить("ИспользоватьПрокси");
	Если Не ИспользоватьПрокси Тогда
		// Не использовать прокси-сервер.
		Возврат Новый ИнтернетПрокси(Ложь);
	КонецЕсли;
	
	ИспользоватьСистемныеНастройки = НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки");
	Если ИспользоватьСистемныеНастройки Тогда
		// Системные настройки прокси-сервера.
		Возврат Новый ИнтернетПрокси(Истина);
	КонецЕсли;
			
	// Настройки прокси-сервера, заданные вручную.
	Прокси = Новый ИнтернетПрокси;
	
	// Определение адреса и порта прокси-сервера.
	ДополнительныеНастройки = НастройкаПроксиСервера.Получить("ДополнительныеНастройкиПрокси");
	ПроксиПоПротоколу = Неопределено;
	Если ТипЗнч(ДополнительныеНастройки) = Тип("Соответствие") Тогда
		ПроксиПоПротоколу = ДополнительныеНастройки.Получить(Протокол);
	КонецЕсли;
	
	ИспользоватьАутентификациюОС = НастройкаПроксиСервера.Получить("ИспользоватьАутентификациюОС");
	ИспользоватьАутентификациюОС = ?(ИспользоватьАутентификациюОС = Истина, Истина, Ложь);
	
	Если ТипЗнч(ПроксиПоПротоколу) = Тип("Структура") Тогда
		Прокси.Установить(Протокол, ПроксиПоПротоколу.Адрес, ПроксиПоПротоколу.Порт,
			НастройкаПроксиСервера["Пользователь"], НастройкаПроксиСервера["Пароль"], ИспользоватьАутентификациюОС);
	Иначе
		Прокси.Установить(Протокол, НастройкаПроксиСервера["Сервер"], НастройкаПроксиСервера["Порт"], 
			НастройкаПроксиСервера["Пользователь"], НастройкаПроксиСервера["Пароль"], ИспользоватьАутентификациюОС);
	КонецЕсли;
	
	Прокси.НеИспользоватьПроксиДляЛокальныхАдресов = НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
	
	АдресаИсключений = НастройкаПроксиСервера.Получить("НеИспользоватьПроксиДляАдресов");
	Если ТипЗнч(АдресаИсключений) = Тип("Массив") Тогда
		Для каждого АдресИсключения Из АдресаИсключений Цикл
			Прокси.НеИспользоватьПроксиДляАдресов.Добавить(АдресИсключения);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Прокси;
	
КонецФункции

// Определяет идентификатор домена на основании
// режима использования внешних ресурсов.
//
// Возвращаемое значение:
//  Число - идентификатор домена.
//
Функция ДоменРасположенияСерверовИПП() Экспорт
	
	РежимИспользования = ПолучитьРежимВнешнихРесурсов();
	Если ВРег(РежимИспользования) = "D" Тогда
		Возврат 0;
	Иначе
		Возврат 1;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Локализация

// Возвращается код текущего языка интерфейса конфигурации
// в формате ISO-639-1.
//
// Возвращаемое значение:
//  Строка - код языка интерфейса конфигурации.
//
Функция КодЯзыкаИнтерфейсаКонфигурации()

	Язык = ТекущийЯзык();
	Если Язык = Неопределено Тогда
		// Для пользователя информационной базы не указан язык.
		Возврат КодОсновногоЯзыкаИнтерфейсаКонфигурации();
	КонецЕсли;

	КодЯзыкаВМетаданных = ?(ТипЗнч(Язык) = Тип("Строка"), Язык, Язык.КодЯзыка);
	КодЯзыкаВФорматеISO639_1 = Неопределено;
	
	ИнтеграцияПодсистемБИП.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(
		КодЯзыкаВМетаданных,
		КодЯзыкаВФорматеISO639_1);
	ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(
		КодЯзыкаВМетаданных,
		КодЯзыкаВФорматеISO639_1);

	Возврат ?(КодЯзыкаВФорматеISO639_1 = Неопределено, КодЯзыкаВМетаданных, КодЯзыкаВФорматеISO639_1);

КонецФункции

// Возвращается код основного языка интерфейса конфигурации
// в формате ISO-639-1.
//
// Возвращаемое значение:
//  Строка - код языка интерфейса конфигурации.
//
Функция КодОсновногоЯзыкаИнтерфейсаКонфигурации()

	КодЯзыкаВМетаданных = ОбщегоНазначения.КодОсновногоЯзыка();
	КодЯзыкаВФорматеISO639_1 = Неопределено;
	
	ИнтеграцияПодсистемБИП.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(
		КодЯзыкаВМетаданных,
		КодЯзыкаВФорматеISO639_1);
	ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(
		КодЯзыкаВМетаданных,
		КодЯзыкаВФорматеISO639_1);

	Возврат ?(КодЯзыкаВФорматеISO639_1 = Неопределено, КодЯзыкаВМетаданных, КодЯзыкаВФорматеISO639_1);

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "2.1.7.1";
	Обработчик.Процедура =
		"ИнтернетПоддержкаПользователей.ОбновлениеИнформационнойБазы_ПереместитьПараметрыИнтернетПоддержкиВБезопасноеХранилищеДанных_2_1_7_1";
	Обработчик.ОбщиеДанные         = Ложь;
	Обработчик.НачальноеЗаполнение = Ложь;
	Обработчик.РежимВыполнения     = "Оперативно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "2.1.8.14";
	Обработчик.Процедура =
		"ИнтернетПоддержкаПользователей.ОбновлениеИнформационнойБазы_ЗаменитьИдентификаторПодсистемыВБезопасномХранилищеДанных_2_1_8_14";
	Обработчик.ОбщиеДанные         = Ложь;
	Обработчик.НачальноеЗаполнение = Ложь;
	Обработчик.РежимВыполнения     = "Оперативно";
	
КонецПроцедуры

// Выполняет перенос данных из регистра сведений УдалитьПараметрыИнтернетПоддержкиПользователей
// в безопасное хранилище.
//
Процедура ОбновлениеИнформационнойБазы_ПереместитьПараметрыИнтернетПоддержкиВБезопасноеХранилищеДанных_2_1_7_1() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		// Не используется при работе в модели сервиса
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьПараметрыИнтернетПоддержкиПользователей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ЗапросПараметровИПП = Новый Запрос;
		ЗапросПараметровИПП.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя КАК ИмяПараметра,
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Значение КАК ЗначениеПараметра
		|ИЗ
		|	РегистрСведений.УдалитьПараметрыИнтернетПоддержкиПользователей КАК УдалитьПараметрыИнтернетПоддержкиПользователей
		|ГДЕ
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя = ""login""
		|	И УдалитьПараметрыИнтернетПоддержкиПользователей.Пользователь = &ПустойИдентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя,
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.УдалитьПараметрыИнтернетПоддержкиПользователей КАК УдалитьПараметрыИнтернетПоддержкиПользователей
		|ГДЕ
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя = ""password""
		|	И УдалитьПараметрыИнтернетПоддержкиПользователей.Пользователь = &ПустойИдентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя,
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.УдалитьПараметрыИнтернетПоддержкиПользователей КАК УдалитьПараметрыИнтернетПоддержкиПользователей
		|ГДЕ
		|	УдалитьПараметрыИнтернетПоддержкиПользователей.Имя = ""regnumber""
		|	И УдалитьПараметрыИнтернетПоддержкиПользователей.Пользователь = &ПустойИдентификатор";
		
		ЗапросПараметровИПП.УстановитьПараметр(
			"ПустойИдентификатор",
			Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаПараметров = ЗапросПараметровИПП.Выполнить().Выбрать();
		
		// Запись данных в безопасное хранилище
		ИдентификаторПодсистемыБИП = ИдентификаторПодсистемы();
		Пока ВыборкаПараметров.Следующий() Цикл
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ИдентификаторПодсистемыБИП,
				ВыборкаПараметров.ЗначениеПараметра,
				ВыборкаПараметров.ИмяПараметра);
		КонецЦикла;
		
		// Очистка неиспользуемого регистра параметров ИПП
		НаборЗаписей = РегистрыСведений.УдалитьПараметрыИнтернетПоддержкиПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИнформациюВЖурналРегистрации(ИнформацияОбОшибке);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
КонецПроцедуры

// Выполняет замену идентификатора подсистемы в безопасном хранилище.
//
Процедура ОбновлениеИнформационнойБазы_ЗаменитьИдентификаторПодсистемыВБезопасномХранилищеДанных_2_1_8_14() Экспорт

	Если ОбщегоНазначения.РазделениеВключено() Тогда
		// Не используется при работе в модели сервиса.
		Возврат;
	КонецЕсли;
	
	ИдентификаторПодсистемыБИПУстаревший =
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.БазоваяФункциональностьБИП");
	ДанныеВБезопасномХранилищеУстаревшие =
		ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			ИдентификаторПодсистемыБИПУстаревший,
			"login,password,regnumber");
	
	ИдентификаторПодсистемы = ИдентификаторПодсистемы();
	Для Каждого КлючЗначение Из ДанныеВБезопасномХранилищеУстаревшие Цикл
		Если КлючЗначение.Значение <> Неопределено Тогда
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ИдентификаторПодсистемы,
				КлючЗначение.Значение,
				КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	// Удалить устаревшие данные после переноса.
	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИдентификаторПодсистемыБИПУстаревший);
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаРаботыССервисамиИнтернетПоддержки

// Вызывается при установке блокировки работы с внешними ресурсами.
// Позволяет отключить произвольные механизмы, работа
// которых в копии информационной базы недопустима.
//
Процедура ПриЗапретеРаботыСВнешнимиРесурсами() Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ПараметрыБлокировки = НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки();
		ПараметрыБлокировки.РаботаССервисамиИнтернетПоддержкиЗаблокирована = Истина;
		СохранитьПараметрыБлокировки(ПараметрыБлокировки);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Вызывается при снятии блокировки работы с внешними ресурсами.
// Для включения работы механизмов, отключенных в процедуре ПриЗапретеРаботыСВнешнимиРесурсами.
//
Процедура ПриРазрешенииРаботыСВнешнимиРесурсами() Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ЗаблокироватьДанныеПараметровБлокировки();
		
		ПараметрыБлокировки = НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки();
		СохранитьПараметрыБлокировки(ПараметрыБлокировки);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Разблокирует работу с сервисами Интернет-поддержки, которые были разрешены при работе в копии.
//
// Параметры:
//  Сервисы - Массив из Структура - Список ресурсов, которые должны быть разрешены.
//
Процедура РазблокироватьРаботуССервисамиИнтернетПоддержки(Сервисы) Экспорт
	
	НачатьТранзакцию();
	Попытка
		ЗаблокироватьДанныеПараметровБлокировки();
		
		ПараметрыБлокировки = ПараметрыБлокировкиССервисамиИнтернетПоддержки();
		
		Для Каждого Сервис Из Сервисы Цикл
			
			СервисИнтернетПоддержки = ПараметрыБлокировки.СервисыИнтернетПоддержки.Получить(Сервис.Название);
			Если СервисИнтернетПоддержки <> Неопределено Тогда
				СервисИнтернетПоддержки.РазрешенаРабота = Сервис.РазрешенаРабота;
			КонецЕсли;
		КонецЦикла;
		
		СохранитьПараметрыБлокировки(ПараметрыБлокировки);
		ОбновитьПовторноИспользуемыеЗначения();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Проверяет разрешение на работу с сервисом по переданному URL.
// Работа с хостом разрешена, когда выполняется одно из условий:
//  1) Не установлена блокировка работы с внешними ресурсами.
//  2) Не установлена блокировка работы с сервисами Интернет-поддержки.
//  3) Разрешена работа с сервисом. Сервис определяется по URL.
//
// Параметры:
//  URL - Строка - URL вызываемой операции;
// 
// Возвращаемое значение:
//  Булево - признак разрешения с сервисом.
//
Функция РазрешенаРаботаСХостом(URL)
	
	Если Не РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПараметрыБлокировки = ПараметрыБлокировкиССервисамиИнтернетПоддержки();
	Если Не ПараметрыБлокировки.РаботаССервисамиИнтернетПоддержкиЗаблокирована Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	СервисИнтернетПоддержки = ПараметрыБлокировки.ХостыСервисовИнтернетПоддержки.Получить(СтруктураURI.ИмяСервера);
	Если СервисИнтернетПоддержки = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПараметрыСервиса = ПараметрыБлокировки.СервисыИнтернетПоддержки.Получить(СервисИнтернетПоддержки);
	РазрешенаРаботаСХостом = (ПараметрыСервиса = Неопределено
		Или ПараметрыСервиса.РазрешенаРабота);
	
	Если Не РазрешенаРаботаСХостом Тогда
		
		ИнформацияОбОшибке = НСтр("ru = 'Обращение к сервисам Интернет-поддержки запрещено.
			|Для разрешения работы с сервисами откройте раздел Интернет-поддержка и сервисы и нажмите ""Настроить"".'");
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,,,
			ИнформацияОбОшибке);
		
	КонецЕсли;
	
	Возврат РазрешенаРаботаСХостом;
	
КонецФункции

// Возвращает сохраненные параметры блокировки с сервисами Интернет-Поддержки.
// Если нет сохраненных, тогда инициализирует параметры по умолчанию.
// 
// Возвращаемое значение:
//  Структура - см. НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки.
//
Функция ПараметрыБлокировкиССервисамиИнтернетПоддержки() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Сохраненные = Константы.ПараметрыБлокировкиРаботыССервисамиИнтернетПоддержки.Получить().Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки();
	
	Если Сохраненные = Неопределено Тогда
		СохранитьПараметрыБлокировки(Результат);
	КонецЕсли;
	
	Если ТипЗнч(Сохраненные) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Результат, Сохраненные);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Инициирует параметры блокировки работы с сервисами интернет-поддержки.
// 
// Возвращаемое значение:
//  Структура - параметры блокировки по умолчанию:
//    * РаботаССервисамиИнтернетПоддержкиЗаблокирована - Булево - признак блокировки ресурсов;
//    * ХостыСервисовИнтернетПоддержки - ФиксированноеСоответствие - см. ХостыСервисовИнтернетПоддержки;
//    * СервисыИнтернетПоддержки - Соответствие - см. СервисыИнтернетПоддержки;
//
Функция НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки()
	
	ХостыСервисовИнтернетПоддержки = ХостыСервисовИнтернетПоддержки();
	СервисыИнтернетПоддержки = СервисыИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	
	Результат = Новый Структура;
	Результат.Вставить("РаботаССервисамиИнтернетПоддержкиЗаблокирована", Ложь);
	Результат.Вставить("ХостыСервисовИнтернетПоддержки", ХостыСервисовИнтернетПоддержки);
	Результат.Вставить("СервисыИнтернетПоддержки", СервисыИнтернетПоддержки);
	
	Возврат Результат;
	
КонецФункции

// Инициирует описание используемых ресурсов в библиотеке Интернет-поддержи.
// 
// Возвращаемое значение:
//  ФиксированноеСоответствие - описание используемых ресурсов.
//
Функция ХостыСервисовИнтернетПоддержки()
	
	ХостыСервисовИнтернетПоддержки = Новый Соответствие;
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		ХостыСервисовИнтернетПоддержки.Вставить(
			ИнтернетПоддержкаПользователейКлиентСервер.ХостСервисаLogin(0),
			НСтр("ru = 'Сервисы аутентификации'"));
		ХостыСервисовИнтернетПоддержки.Вставить(
			ИнтернетПоддержкаПользователейКлиентСервер.ХостСервисаLogin(1),
			НСтр("ru = 'Сервисы аутентификации'"));
	
		Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
			МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
			МодульПолучениеОбновленийПрограммы.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.МониторПортала1СИТС") Тогда
			МодульМониторПортала1СИТС = ОбщегоНазначения.ОбщийМодуль("МониторПортала1СИТС");
			МодульМониторПортала1СИТС.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
			МодульСообщенияВСлужбуТехническойПоддержки = ОбщегоНазначения.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержки");
			МодульСообщенияВСлужбуТехническойПоддержки.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПодключениеСервисовСопровождения") Тогда
		МодульПодключениеСервисовСопровождения = ОбщегоНазначения.ОбщийМодуль("ПодключениеСервисовСопровождения");
		МодульПодключениеСервисовСопровождения.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Подключение1СТакском") Тогда
		МодульПодключение1СТакском = ОбщегоНазначения.ОбщийМодуль("Подключение1СТакском");
		МодульПодключение1СТакском.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКонтрагентами") Тогда
		МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
		МодульРаботаСКонтрагентами.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СПАРКРиски") Тогда
		МодульСПАРКРискиСервис = ОбщегоНазначения.ОбщийМодуль("СПАРКРискиСервис");
		МодульСПАРКРискиСервис.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		МодульОбработкаНовостейСлужебный = ОбщегоНазначения.ОбщийМодуль("ОбработкаНовостейСлужебный");
		МодульОбработкаНовостейСлужебный.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКлассификаторами") Тогда
		МодульРаботаСКлассификаторами = ОбщегоНазначения.ОбщийМодуль("РаботаСКлассификаторами");
		МодульРаботаСКлассификаторами.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент") Тогда
		МодульПолучениеВнешнихКомпонент = ОбщегоНазначения.ОбщийМодуль("ПолучениеВнешнихКомпонент");
		МодульПолучениеВнешнихКомпонент.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей") Тогда
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСлужебный");
		МодульОнлайнЗаказыСлужебный.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнОплаты") Тогда
		МодульОнлайнОплатыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнОплатыСлужебный");
		МодульОнлайнОплатыСлужебный.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОФД") Тогда
		МодульОФДСлужебный = ОбщегоНазначения.ОбщийМодуль("ОФДСлужебный");
		МодульОФДСлужебный.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтеграцияСЦРПТ") Тогда
		МодульИнтеграцияСЦРПТ = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЦРПТ");
		МодульИнтеграцияСЦРПТ.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеРегламентированныхОтчетов") Тогда
		МодульПолучениеРегламентированныхОтчетов = ОбщегоНазначения.ОбщийМодуль("ПолучениеРегламентированныхОтчетов");
		МодульПолучениеРегламентированныхОтчетов.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныйАрхив20") Тогда
		МодульОблачныйАрхив20 = ОбщегоНазначения.ОбщийМодуль("ОблачныйАрхив20");
		МодульОблачныйАрхив20.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныеКассы") Тогда
		МодульОблачныеКассыСервис = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассыСервис");
		МодульОблачныеКассыСервис.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПоставляемыеНастройки") Тогда
		МодульПоставляемыеНастройкиСервер = ОбщегоНазначения.ОбщийМодуль("ПоставляемыеНастройкиСервер");
		МодульПоставляемыеНастройкиСервер.ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки);
	КонецЕсли;
	
	Результат = Новый ФиксированноеСоответствие(ХостыСервисовИнтернетПоддержки);
	
	Возврат Результат;
	
КонецФункции

// Инициирует описание используемых сервисов в библиотеке Интернет-поддержи.
//
// Параметры:
//  ХостыСервисовИнтернетПоддержки - ФиксированноеСоответствие - см. ХостыСервисовИнтернетПоддержки.
// 
// Возвращаемое значение:
//  Соответствие - описание используемых сервисов.
//
Функция СервисыИнтернетПоддержки(ХостыСервисовИнтернетПоддержки)
	
	Результат = Новый Соответствие;
	Для Каждого ХостСервиса Из ХостыСервисовИнтернетПоддержки Цикл
		Результат.Вставить(
			ХостСервиса.Значение,
			Новый Структура(
				"НазваниеСервиса, РазрешенаРабота",
				ХостСервиса.Значение,
				Ложь));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет переданные параметры блокировки сервисов Интернет-поддержки.
//
// Параметры:
//  ПараметрыБлокировки - Структура - см. НовыеПараметрыБлокировкиССервисамиИнтернетПоддержки.
//
Процедура СохранитьПараметрыБлокировки(ПараметрыБлокировки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХранилищеЗначения = Новый ХранилищеЗначения(ПараметрыБлокировки);
	Константы.ПараметрыБлокировкиРаботыССервисамиИнтернетПоддержки.Установить(ХранилищеЗначения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Блокировка хранимых параметров работы с сервисами Интернет-поддержки.
//
Процедура ЗаблокироватьДанныеПараметровБлокировки()
	
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("Константа.ПараметрыБлокировкиРаботыССервисамиИнтернетПоддержки");
	Блокировка.Заблокировать();
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Возвращается имя события журнала регистрации для записи ошибок
// Интернет-поддержки пользователей.
//
// Возвращаемое значение:
//	Строка - имя события ошибки Интернет-поддержки.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интернет-поддержка пользователей'",
		ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает минимальную версию БСП,
// с которой возможна работа БИП.
//
// Возвращаемое значение:
//   Строка - номер версии БСП
//
Функция МинимальнаяВерсияБСП() Экспорт
	
	Возврат "3.1.9.230";
	
КонецФункции

#КонецОбласти

#КонецОбласти
