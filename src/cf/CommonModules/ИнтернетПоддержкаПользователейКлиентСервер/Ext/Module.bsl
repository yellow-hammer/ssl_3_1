///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
// ОбщийМодуль.ИнтернетПоддержкаПользователейКлиентСервер.
//
// Клиент-серверные процедуры и функции Интернет-поддержка пользователей:
//  - определение настроек библиотеки;
//  - общие проверки данных аутентификации.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает номер версии библиотеки.
//
// Возвращаемое значение:
//  Строка - номер версии библиотеки.
//
Функция ВерсияБиблиотеки() Экспорт
	
	Возврат "2.8.1.18";
	
КонецФункции

// Возвращает идентификатор поставщика услуг "Портал 1С:ИТС"
// для интеграции с подсистемой "Управление тарифами в модели
// сервиса" библиотеки "Технология сервиса".
//
// Возвращаемое значение:
//	Строка - идентификатор поставщика услуг.
//
Функция ИдентификаторПоставщикаУслугПортал1СИТС() Экспорт
	
	Возврат "Portal1CITS";
	
КонецФункции

// Выполняет проверку данных аутентификации Интернет-поддержки пользователей.
// Необходимо вызвать перед выполнением проверки логина и пароля в сервисе
// и сохранением данных в информационной базе.
//
// Параметры:
//  ДанныеАутентификации - Структура - структура, содержащая логин и пароль пользователя
//                         Интернет-поддержки:
//   * Логин - Строка - логин пользователя Интернет-поддержки;
//   * Пароль - Строка - пароль пользователя Интернет-поддержки.
//
// Возвращаемое значение:
//  Структура - результаты проверки данных аутентификации:
//   *Отказ - Булево - если Истина, при проверке обнаружены ошибки;
//   *СообщениеОбОшибке - Строка - сообщение для пользователя программы;
//   *Поле - Строка - идентификатор поля, в котором возникла ошибка:
//                      - "Логин" - ошибка возникла при проверке данных поля Логин;
//                      - "Пароль" - ошибка возникла при проверке данных поля Пароль;
//
Функция ПроверитьДанныеАутентификации(ДанныеАутентификации) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Отказ", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("Поле", "");
	
	Если ПустаяСтрока(ДанныеАутентификации.Логин) Тогда
		Результат.Отказ = Истина;
		Результат.СообщениеОбОшибке = НСтр("ru = 'Поле ""Логин"" не заполнено.'");
		Результат.Поле = "Логин";
	ИначеЕсли ПустаяСтрока(ДанныеАутентификации.Пароль) Тогда
		Результат.Отказ = Истина;
		Результат.СообщениеОбОшибке = НСтр("ru = 'Поле ""Пароль"" не заполнено.'");
		Результат.Поле = "Пароль";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбщегоНазначения

// Возвращает строковое представление размера файла.
//
Функция ПредставлениеРазмераФайла(Знач Размер) Экспорт

	Если Размер < 1024 Тогда
		Возврат Формат(Размер, "ЧДЦ=1") + " " + НСтр("ru = 'байт'");
	ИначеЕсли Размер < 1024 * 1024 Тогда
		Возврат Формат(Размер / 1024, "ЧДЦ=1") + " " + НСтр("ru = 'КБ'");
	ИначеЕсли Размер < 1024 * 1024 * 1024 Тогда
		Возврат Формат(Размер / (1024 * 1024), "ЧДЦ=1") + " " + НСтр("ru = 'МБ'");
	Иначе
		Возврат Формат(Размер / (1024 * 1024 * 1024), "ЧДЦ=1") + " " + НСтр("ru = 'ГБ'");
	КонецЕсли;

КонецФункции

// Определяет URL для вызова сервиса аутентификации.
//
// Параметры:
//  Операция - Строка - путь к ресурсу;
//  НастройкиСоединения - Структура, Неопределено - настройки подключения.
//
// Возвращаемое значение:
//  Строка - URL операции.
//
Функция URLСтраницыСервисаLogin(Путь = "", Знач НастройкиСоединения = Неопределено) Экспорт
	
	Если НастройкиСоединения = Неопределено Тогда
		Домен = 0;
	Иначе
		Домен = НастройкиСоединения.ДоменРасположенияСерверовИПП;
	КонецЕсли;
	Возврат "https://"
		+ ХостСервисаLogin(Домен)
		+ Путь;
	
КонецФункции

// Определяет URL для перехода на страницу Портала 1С:ИТС.
//
// Параметры:
//  Операция  - Строка - путь к ресурсу;
//  Домен     - Число, Неопределено  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - URL операции.
//
Функция URLСтраницыПорталаПоддержки(Путь = "", Знач Домен = Неопределено) Экспорт
	
	Если Домен = Неопределено Тогда
		Домен = 0;
	КонецЕсли;
	Возврат "https://"
		+ ХостПорталаПоддержки(Домен)
		+ Путь;
	
КонецФункции

// Формирует пользовательское представление расписания.
//
// Параметры:
//  Расписание - Структура из см. ОбщегоНазначенияКлиентСервер.СтруктураВРасписание
//             - РасписаниеРегламентногоЗадания
//
// Возвращаемое значение:
//  Строка - представление расписания.
//
Функция ПредставлениеРасписания(Расписание) Экспорт

	Если Расписание = Неопределено Тогда
		Возврат НСтр("ru = 'Настроить расписание'");
	Иначе
		Если ТипЗнч(Расписание) = Тип("Структура") Тогда
			Возврат Строка(ОбщегоНазначенияКлиентСервер.СтруктураВРасписание(Расписание));
		Иначе
			Возврат Строка(Расписание);
		КонецЕсли;
	КонецЕсли;

КонецФункции

// Определяет настройки Интернет-поддержки пользователей.
//
// Возвращаемое значение:
//  Настройки - Структура - настройки подсистемы на клиенте:
//   *ОтключитьАвторизацию - Булево - отключает авторизацию пользователей
//    при переходе на страницы интегрированных ресурсов.
//
Функция НастройкиИнтернетПоддержкиПользователей() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("ОтключитьАвторизацию", Ложь);
	
	ИнтернетПоддержкаПользователейКлиентСерверПереопределяемый.ПриОпределенииНастроекИнтернетПоддержкиПользователей(
		Настройки);
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет хост Портала 1С:ИТС.
//
// Параметры:
//  Домен - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - хост подключения.
//
Функция ХостПорталаПоддержки(Домен)


	Если Домен = 0 Тогда
		Возврат "portal.1c.ru";
	Иначе
		Возврат "portal.1c.eu";
	КонецЕсли;

КонецФункции

// Определяет хост сервиса аутентификации.
//
// Параметры:
//  Домен - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - хост подключения.
//
Функция ХостСервисаLogin(Домен) Экспорт


	Если Домен = 0 Тогда
		Возврат "login.1c.ru";
	Иначе
		Возврат "login.1c.eu";
	КонецЕсли;

КонецФункции

#КонецОбласти
