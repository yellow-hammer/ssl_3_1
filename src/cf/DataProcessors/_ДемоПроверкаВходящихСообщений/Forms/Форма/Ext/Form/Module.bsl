///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокВыбораУчетныхЗаписей();
	НачалоПериода = ТекущаяДатаСеанса();
	КонецПериода = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблица

&НаКлиенте
Процедура ТаблицаПриАктивизацииСтроки(Элемент)
	ТекстПисьма = "";
	Если Элементы.Таблица.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекстПисьма = Элементы.Таблица.ТекущиеДанные.Текст; 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьВходящиеВыполнить()
	
	Если НЕ ЗначениеЗаполнено(УчетнаяЗаписьВходящие) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Почта для получения входящих писем не указана.'"));
		Возврат;
	КонецЕсли;
	
	ПроверитьВходящиеВыполнитьЗавершение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВходящиеВыполнитьЗавершение()
	
	Состояние(НСтр("ru = 'Загрузка входящих сообщений.'"),,НСтр("ru = 'Пожалуйста, подождите...'"));
	Попытка
		ЗагрузитьВходящиеСообщения();
		НовыхПисем = ВходящиеСообщения.Количество();
		Если НовыхПисем > 0 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Получено новых писем:'") + " " + НовыхПисем);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Нет новых писем.'"));
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		РаботаСПочтовымиСообщениямиКлиент.СообщитьОбОшибкеПодключения(
			УчетнаяЗаписьВходящие, 
			НСтр("ru = 'Загрузка входящих сообщений'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВходящиеСообщения()
	
	ПоляИнтернетПочтовогоСообщения = РаботаСПочтовымиСообщениями.ПоляИнтернетПочтовогоСообщения();
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.ИмяОтправителя);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.Вложения);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.Тема);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.ДатаОтправления);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.ОбратныйАдрес);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.Отправитель);
	МассивКолонок.Добавить(ПоляИнтернетПочтовогоСообщения.Тексты);
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Колонки", МассивКолонок);

	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(НачалоПериода) Тогда
		Отбор.Вставить("ПослеДатыОтправления", НачалоПериода);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		Отбор.Вставить("ДоДатыОтправления", КонецДня(КонецПериода) + 1);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		ПараметрыЗагрузки.Вставить("Отбор", Отбор);
	КонецЕсли;
	
	Попытка
		ТаблицаВходящихСообщений = РаботаСПочтовымиСообщениями.ЗагрузитьПочтовыеСообщения(УчетнаяЗаписьВходящие, ПараметрыЗагрузки);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Проверка входящих сообщений'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, , УчетнаяЗаписьВходящие,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	ВходящиеСообщения.Очистить();
	
	Для Каждого ЭлементВходящееСообщение Из ТаблицаВходящихСообщений Цикл
		НоваяСтрока = ВходящиеСообщения.Добавить();
		НоваяСтрока.Отправитель     = ЭлементВходящееСообщение.ИмяОтправителя;
		Если ПустаяСтрока(ЭлементВходящееСообщение.ИмяОтправителя) Тогда
			НоваяСтрока.Отправитель     = ЭлементВходящееСообщение.Отправитель;
		КонецЕсли;
		НоваяСтрока.ОбратныйАдрес   = ЭлементВходящееСообщение.ОбратныйАдрес;
		НоваяСтрока.Тема            = ЭлементВходящееСообщение.Тема;
		НоваяСтрока.ДатаОтправления = ЭлементВходящееСообщение.ДатаОтправления;
		Если ЭлементВходящееСообщение.Вложения.Количество() > 0 Тогда
			НоваяСтрока.Вложение = Истина;
		Иначе
			НоваяСтрока.Вложение = Ложь;
		КонецЕсли;
		
		Для Каждого Текст Из ЭлементВходящееСообщение.Тексты Цикл
			Если Текст["ТипТекста"] = "ПростойТекст" Тогда
				НоваяСтрока.Текст = Текст["Текст"];
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораУчетныхЗаписей()
	
	Элементы.УчетнаяЗаписьВходящие.СписокВыбора.Очистить();
	
	ДоступныеУчетныеЗаписи = РаботаСПочтовымиСообщениями.ДоступныеУчетныеЗаписи(, Истина, Истина);
	
	ЕстьПолныеУчетныеЗаписи = Ложь;
	
	Для Каждого СтрУчЗапись Из ДоступныеУчетныеЗаписи Цикл
		ЕстьПолныеУчетныеЗаписи = Истина;
		Элементы.УчетнаяЗаписьВходящие.СписокВыбора.Добавить(СтрУчЗапись.Ссылка, СтрУчЗапись.Наименование);
	КонецЦикла;
	
	Если ЕстьПолныеУчетныеЗаписи Тогда
		УчетнаяЗаписьВходящие = Элементы.УчетнаяЗаписьВходящие.СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


