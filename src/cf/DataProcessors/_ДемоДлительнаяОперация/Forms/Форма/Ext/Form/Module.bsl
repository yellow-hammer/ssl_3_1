///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем НомерОперации; // порядковый номер длительной операции.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкоростьВыполнения = 0;
	ОжидаемыйРезультат = "Успешно";
	ФормаОжидания = "Показывать";
	ПрогрессВыполнения = Истина;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ОжидаемыйРезультат.ВысотаЗаголовка = 2;
		Элементы.СкоростьВыполнения.ВысотаЗаголовка = 2;
		Элементы.ФормаОжидания.ВысотаЗаголовка = 2;
	КонецЕсли;
	
	Если ОбщегоНазначения.РежимОтладки() Тогда
		Элементы.ПредупреждениеНадпись.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Элементы.ПредупреждениеНадпись.Заголовок, "РежимОтладки");
		Элементы.ГруппаПредупреждение.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	ПередВыполнением();
	
	ДлительнаяОперация = НачатьВыполнениеПроцедуры(НомерОперации);
	
	Контекст = Новый Структура("НомерОперации, ЭтоРасчетЗначения", НомерОперации, Ложь);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект, Контекст);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЗначение(Команда)
	
	ПередВыполнением();
	
	ДлительнаяОперация = НачатьВыполнениеФункции(НомерОперации);
	
	Контекст = Новый Структура("НомерОперации, ЭтоРасчетЗначения", НомерОперации, Истина);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект, Контекст);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередВыполнением()
	
	ОчиститьСообщения();
	Если НомерОперации = Неопределено Тогда
		НомерОперации = 0;
	КонецЕсли;
	НомерОперации = НомерОперации + 1;
	
	Если ФормаОжидания = "Показывать" Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУведомление;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
		ТекстУведомления = НСтр("ru = 'Пожалуйста, подождите...'");
		Если Не ПустаяСтрока(Уведомление) Тогда
			ТекстУведомления = Уведомление + Символы.ПС + ТекстУведомления;
		КонецЕсли;
		Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ТекстУведомления;
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.ИдентификаторНазначения = УникальныйИдентификатор;
	Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 Начало выполнения %2...'"),
		ОбщегоНазначенияКлиент.ДатаСеанса(),
		НомерОперации);
	Сообщение.Сообщить();
	
КонецПроцедуры 

&НаСервере
Функция НачатьВыполнениеФункции(НомерОперации)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Обработки._ДемоДлительнаяОперация.РассчитатьЗначение",
		НомерОперации, СкоростьВыполнения, ОжидаемыйРезультат = "Ошибка", ПрогрессВыполнения);
	
КонецФункции

&НаСервере
Функция НачатьВыполнениеПроцедуры(НомерОперации)
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(,
		"Обработки._ДемоДлительнаяОперация.ВыполнитьРасчет",
		НомерОперации, СкоростьВыполнения, ОжидаемыйРезультат = "Ошибка", ПрогрессВыполнения);
		
КонецФункции

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  Контекст - Структура:
//   * НомерОперации - Число
//   * ЭтоРасчетЗначения - Булево
//
&НаКлиенте
Процедура ОбработатьРезультат(Результат, Контекст) Экспорт
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУведомление;
	Если Результат = Неопределено Тогда // Отменено пользователем
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(
			Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ВывестиРезультат(Результат, Контекст.НомерОперации, Контекст.ЭтоРасчетЗначения);
	
КонецПроцедуры 

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовоеСостояниеДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ВыполнитьДействиеПрогрессВыполнения(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстУведомления = НСтр("ru = 'Пожалуйста, подождите...'");
	Если Не ПустаяСтрока(Уведомление) Тогда
		ТекстУведомления = Уведомление + Символы.ПС + ТекстУведомления;
	КонецЕсли;
	Если Результат.Прогресс <> Неопределено Тогда
		ТекстУведомления = ТекстУведомления + ПрогрессСтрокой(Результат.Прогресс);
		Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ТекстУведомления;
	КонецЕсли;
	Если Результат.Сообщения <> Неопределено Тогда
		Для Каждого СообщениеПользователю Из Результат.Сообщения Цикл
			СообщениеПользователю.ИдентификаторНазначения = УникальныйИдентификатор;
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура ВывестиРезультат(Результат, НомерВыполненнойОперации, ЭтоРасчетЗначения)
	
	Если ЭтоРасчетЗначения Тогда
		ТекстСообщения = Неопределено;
		Если Результат.Свойство("АдресРезультата") И ЗначениеЗаполнено(Результат.АдресРезультата) Тогда
			ТекстСообщения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		КонецЕсли;
		Если ТекстСообщения = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Из временного хранилища не получен результат (получено Неопределено)'");
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Действие %1 успешно выполнено'"), НомерВыполненнойОперации);
	КонецЕсли;
	
	Если Результат.Сообщения <> Неопределено Тогда
		Для Каждого СообщениеПользователю Из Результат.Сообщения Цикл
			СообщениеПользователю.ИдентификаторНазначения = УникальныйИдентификатор;
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.ИдентификаторНазначения = УникальныйИдентификатор;
	Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 %2'"), ТекущаяДатаСеанса(), ТекстСообщения);
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Функция ПрогрессСтрокой(Прогресс)
	
	Результат = "";
	Если Прогресс = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Процент = 0;
	Если Прогресс.Свойство("Процент", Процент) Тогда
		Результат = Строка(Процент) + "%";
	КонецЕсли;
	Текст = 0;
	Если Прогресс.Свойство("Текст", Текст) Тогда
		Если Не ПустаяСтрока(Результат) Тогда
			Результат = Результат + " (" + Текст + ")";
		Иначе
			Результат = Текст;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПараметрыОжидания()
	
	Перем ОповещениеОПрогрессеВыполнения, ПараметрыОжидания;
	
	Если (ПрогрессВыполнения Или ВыводитьСообщения) И (ФормаОжидания <> "Показывать") Тогда
		ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ВыполнитьДействиеПрогрессВыполнения", ЭтотОбъект);
	Иначе
		ОповещениеОПрогрессеВыполнения = Неопределено;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = Уведомление;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = ПрогрессВыполнения;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка._ДемоДлительнаяОперация";
	ПараметрыОжидания.ВыводитьОкноОжидания = (ФормаОжидания = "Показывать");
	ПараметрыОжидания.ВыводитьСообщения = ВыводитьСообщения;
	Возврат ПараметрыОжидания;

КонецФункции

#КонецОбласти
