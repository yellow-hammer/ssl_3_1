///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

// Пример функции, вызываемой в фоне.
Функция РассчитатьЗначение(НомерОперации, ДлительностьРасчета = 10, ЗавершитьСОшибкой = Ложь, ВыводитьПрогрессВыполнения = Ложь) Экспорт
	
	ИмитироватьДлительноеДействие(НомерОперации, ДлительностьРасчета, ВыводитьПрогрессВыполнения);
	
	Если ЗавершитьСОшибкой Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка: расчет %1 не выполнен'"), НомерОперации);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расчет %1 выполнен за %2 сек.'"), НомерОперации, ДлительностьРасчета);
	
КонецФункции

// Пример процедуры, вызываемой в фоне.
Процедура ВыполнитьРасчет(НомерОперации, ДлительностьРасчета = 10, ЗавершитьСОшибкой = Ложь, ВыводитьПрогрессВыполнения = Ложь) Экспорт
	
	ИмитироватьДлительноеДействие(НомерОперации, ДлительностьРасчета, ВыводитьПрогрессВыполнения);
	
	Если ЗавершитьСОшибкой Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка: действие %1 не выполнено'"), НомерОперации);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИмитироватьДлительноеДействие(НомерОперации, ДлительностьРасчета, ВыводитьПрогрессВыполнения)
	
	ФоновоеЗадание = ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание();
	
	Для Счетчик = 1 По ДлительностьРасчета Цикл
		Пауза(ФоновоеЗадание, 1);
		Процент = Цел(Счетчик / ДлительностьРасчета * 100);
		Этап = Мин(Цел(Счетчик / (ДлительностьРасчета / 3)) + 1, 3); // Имитация операции в 3 этапа.
		
		ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 операция %2 выполнена на %3 % (этап %4)'"),
			ТекущаяДатаСеанса(), НомерОперации, Процент, Этап));
		
		Если ВыводитьПрогрессВыполнения Тогда
			Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Этап %1'"), Этап);
			ДлительныеОперации.СообщитьПрогресс(Процент, Сообщение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура Пауза(ФоновоеЗадание, ИнтервалОжидания)
	
	Если ФоновоеЗадание <> Неопределено Тогда
		ФоновоеЗадание.ОжидатьЗавершенияВыполнения(ИнтервалОжидания);
	Иначе
		Конец = ТекущаяУниверсальнаяДатаВМиллисекундах() + ИнтервалОжидания * 1000;
		Пока ТекущаяУниверсальнаяДатаВМиллисекундах() < Конец Цикл
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
