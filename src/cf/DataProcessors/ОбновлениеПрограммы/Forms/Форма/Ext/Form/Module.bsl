///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
// Хранит структуру параметров администрирования для передачи их скрипту с целью обновления информационной базы
Перем ПараметрыАдминистрирования;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// По умолчанию работает сценарий рабочего обновления.
	Сценарий = ?(ПустаяСтрока(Параметры.Сценарий), "РабочееОбновление", Параметры.Сценарий);
	ЭтоПодчиненныйУзелРИБ = ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ();
	
	Элементы.ПанельПредупреждений.Видимость = Ложь;
	
	// После установки исправлений рекомендуется перезапустить программу.
	ПерезапуститьПрограмму = Истина;
	
	// Скрытие неактивных страниц помощника.
	// Используется для исключения эффекта "мерцания" при управлении
	// отображением элементов.
	СтраницаПустая = Элементы.СтраницаПустая;
	Для Каждого ТекСтраница Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		Если ТекСтраница <> СтраницаПустая Тогда
			ТекСтраница.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоПодчиненныйУзелРИБ
		И Не ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		И Не ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		
		// В подчиненном узле РИБ обновление программы выполняется через
		// механизмы распределенной информационной базы.
		СообщениеЖурнала =
			НСтр("ru = 'Обновление программы невозможно. В подчиненном узле информационной базы обновление получается из главного узла.'");
		ПолучениеОбновленийПрограммы.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьВнутреннююОшибку(
			НСтр("ru = '<b>Обновление программы невозможно</b>
				|В подчиненном узле информационной базы обновление получается из главного узла.'"),
			,
			СообщениеЖурнала);
		Элементы.КнопкаДалее.Видимость  = Ложь;
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Закрыть'");
		Возврат;
		
	ИначеЕсли ОбщегоНазначения.ЭтоВебКлиент()
		И Не ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		
		СообщениеЖурнала =
			НСтр("ru = 'Обновление программы невозможно. Обновление программы недоступно при работе в режиме веб-клиента.'");
		ПолучениеОбновленийПрограммы.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьВнутреннююОшибку(
			НСтр("ru = '<b>Обновление программы невозможно</b>
				|Обновление программы недоступно при работе в режиме веб-клиента.
				|Откройте программу в тонком или толстом клиенте и повторите попытку.'"),
			,
			СообщениеЖурнала);
		Элементы.КнопкаДалее.Видимость  = Ложь;
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Закрыть'");
		Возврат;
		
	ИначеЕсли ОбщегоНазначения.КлиентПодключенЧерезВебСервер()
		И Не ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		
		СообщениеЖурнала =
			НСтр("ru = 'Обновление программы невозможно. Обновление программы недоступно при работе в режиме подключения к веб серверу.'");
		ПолучениеОбновленийПрограммы.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьВнутреннююОшибку(
			НСтр("ru = '<b>Обновление программы невозможно</b>
				|Обновление программы недоступно при работе в режиме подключения к веб серверу.
				|Откройте программу в тонком или толстом клиенте и повторите попытку.'"),
			,
			СообщениеЖурнала);
		Элементы.КнопкаДалее.Видимость  = Ложь;
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Закрыть'");
		Возврат;
		
	КонецЕсли;
	
	// Проверка возможности использования механизма.
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		И Не ПолучениеОбновленийПрограммы.СлужебнаяДоступноИспользованиеПолученияОбновленийПлатформы() Тогда
		ТекстИсключения =
			НСтр("ru = 'Переход на новую версию платформы 1С:Предприятие недоступен в текущем режиме работы.'");
		ВызватьИсключение ТекстИсключения;
	ИначеЕсли (ЭтоСценарийРабочегоОбновления(ЭтотОбъект)
		И Не ПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Ложь, Ложь))
		Или (ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(ЭтотОбъект)
		И Не ПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы()) Тогда
		ТекстИсключения =
			НСтр("ru = 'Получение обновлений программы недоступно в текущем режиме работы.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЭтоАдминистраторСистемы = ПолучениеОбновленийПрограммы.ЭтоАдминистраторСистемы();
	ЭтоФайловаяИБ           = ПолучениеОбновленийПрограммы.ЭтоФайловаяИБ();
	ЭтоWindowsКлиент        = ОбщегоНазначения.ЭтоWindowsКлиент();
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		
		Если Не ЭтоАдминистраторСистемы Тогда
			// Переход на новую версию платформы выполняется только администратором системы.
			ВызватьИсключение НСтр("ru = 'Недостаточно прав для перехода на новую версию платформы.'");
		КонецЕсли;
		
		АвтоЗаголовок = Ложь;
		Заголовок     = НСтр("ru = 'Переход на новую версию платформы 1С:Предприятие'");
		
	ИначеЕсли ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(ЭтотОбъект) Тогда
		
		Если Не ПустаяСтрока(Параметры.ЗаголовокОкна) Тогда
			АвтоЗаголовок = Ложь;
			Заголовок     = Параметры.ЗаголовокОкна;
		КонецЕсли;
		ЗаголовокДоступноОбновление = Параметры.ЗаголовокДоступноОбновление;
		ЗаголовокНетОбновления      = Параметры.ЗаголовокНетОбновления;
		
		ИмяНовойПрограммы  = Параметры.ИмяНовойПрограммы;
		НомерНовойРедакции = Параметры.НомерНовойРедакции;
		
	ИначеЕсли Не ЭтоСценарийРабочегоОбновления(ЭтотОбъект) Тогда
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестный сценарий работы помощника обновления программы (""%1"").'"),
			Сценарий);
		
	КонецЕсли;
	
	ПараметрыПолученияОбновлений = Неопределено;
	
	Если ЭтоФайловаяИБ Тогда
		Если ПолучениеОбновленийПрограммы.ЭтоБазоваяВерсияКонфигурации() Тогда
			Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость = Ложь;
		Иначе
			ПараметрыПолученияОбновлений = ПолучениеОбновленийПрограммы.СлужебнаяПараметрыПолученияОбновлений();
			Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость =
				ПараметрыПолученияОбновлений.ВыбиратьКаталогСохраненияДистрибутиваПлатформы;
		КонецЕсли;
		Элементы.ГруппаКаталогСохраненияПлатформы.Видимость =
			Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость;
	КонецЕсли;
	
	Элементы.ДекорацияНаписатьВТехПоддержку.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		ИнтернетПоддержкаПользователей.ПодставитьДомен(
			НСтр("ru = 'При возникновении проблем напишите в <a href=""mailto:webits-info@1c.ru"">техподдержку</a>.'"),
			ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами().ДоменРасположенияСерверовИПП));
	
	Если Не ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(ЭтотОбъект) Тогда
		СоздатьРезервнуюКопию = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РезервноеКопированиеИБ");
		Элементы.СоздатьРезервнуюКопию.Видимость            = СоздатьРезервнуюКопию;
		Элементы.СоздатьРезервнуюКопиюУстановлено.Видимость = СоздатьРезервнуюКопию;
	КонецЕсли;
	
	Если ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		// Отображается перед началом работы системы, поэтому необходимо
		// зачитать настройки соединения с серверами.
		НастройкиСоединенияССерверами =
			ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
		РаботаВПрограммеЗапрещена = Параметры.РаботаВПрограммеЗапрещена;
		Элементы.ТекстСообщения.Заголовок = Параметры.ТекстСообщения;
		ОтобразитьНерекомендуемаяВерсияПлатформы();
	КонецЕсли;
	
	Если ЭтоСценарийРабочегоОбновления(ЭтотОбъект)
		Или ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(ЭтотОбъект) Тогда
		
		Если Не ПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Истина) Тогда
			
			Элементы.ГруппаУстановитьОбновлениеИзФайла1.Видимость = Ложь;
			Элементы.ГруппаУстановитьОбновлениеИзФайла2.Видимость = Ложь;
			
		Иначе
			
			Если ПараметрыПолученияОбновлений = Неопределено Тогда
				ПараметрыПолученияОбновлений = ПолучениеОбновленийПрограммы.СлужебнаяПараметрыПолученияОбновлений();
			КонецЕсли;
			
			Если Не ПараметрыПолученияОбновлений.ПолучатьОбновленияКонфигурации
				И Не ПараметрыПолученияОбновлений.ПолучатьИсправления Тогда
				
				Элементы.ГруппаУстановитьОбновлениеИзФайла1.Видимость = Ложь;
				Элементы.ГруппаУстановитьОбновлениеИзФайла2.Видимость = Ложь;
				
			Иначе
				
				Элементы.ГруппаУстановитьОбновлениеИзФайла1.Видимость = Истина;
				Элементы.ГруппаУстановитьОбновлениеИзФайла2.Видимость = Истина;
				Если ПараметрыПолученияОбновлений.ПолучатьОбновленияКонфигурации
					И ПараметрыПолученияОбновлений.ПолучатьИсправления Тогда
					
					УстановитьОбновлениеИзФайлаЗаголовок =
						НСтр("ru = 'Установить обновление конфигурации или исправления (патчи) из файла'");
					УстановитьОбновлениеИзФайлаПодсказка =
						НСтр("ru = 'Если у вас уже есть файл обновления конфигурации или файлы исправлений (патчи), перейдите по ссылке для установки обновления конфигурации из файла'");
					
				ИначеЕсли ПараметрыПолученияОбновлений.ПолучатьОбновленияКонфигурации
					И Не ПараметрыПолученияОбновлений.ПолучатьИсправления Тогда
					
					УстановитьОбновлениеИзФайлаЗаголовок =
						НСтр("ru = 'Установить обновление конфигурации из файла'");
					УстановитьОбновлениеИзФайлаПодсказка =
						НСтр("ru = 'Если у вас уже есть файл обновления конфигурации, перейдите по ссылке для установки обновления конфигурации из файла'");
						
				Иначе
					
					УстановитьОбновлениеИзФайлаЗаголовок =
						НСтр("ru = 'Установить исправления (патчи) из файла'");
					УстановитьОбновлениеИзФайлаПодсказка =
						НСтр("ru = 'Если у вас уже есть файлы исправлений (патчи), перейдите по ссылке для установки обновления конфигурации из файла'");
					
				КонецЕсли;
				
				Элементы.ДекорацияУстановитьОбновлениеИзФайла1.Заголовок = УстановитьОбновлениеИзФайлаЗаголовок;
				Элементы.ДекорацияУстановитьОбновлениеИзФайла2.Заголовок = УстановитьОбновлениеИзФайлаЗаголовок;
				Элементы.ДекорацияУстановитьОбновлениеИзФайла1.Подсказка = УстановитьОбновлениеИзФайлаПодсказка;
				Элементы.ДекорацияУстановитьОбновлениеИзФайла2.Подсказка = УстановитьОбновлениеИзФайлаПодсказка;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ВариантИнформированияОВыходеОбновлений      =
			ПолучениеОбновленийПрограммы.НастройкиАвтоматическогоОбновления().ВариантИнформированияОВыходеОбновлений;
		
		// Интеграция с подсистемой Центр мониторинга.
		НастроитьОтображениеИнтеграцииСЦентромМониторинга();
		
	КонецЕсли;
	
	ДоступнаОтправкаСообщенийТехПоддержке =
		ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки");
	Элементы.ДекорацияНаписатьВТехПоддержку.Видимость = ДоступнаОтправкаСообщенийТехПоддержке;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСообщениеСервиса Тогда
		// При создании на сервере было отображено сообщение об ошибке.
		Возврат;
	КонецЕсли;
	
	Если ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Является исключением из стандарта "Минимизация количества серверных вызовов", так как метод синхронный и
	// может выполняться некоторое время, а изменение его на асинхронный метод приведет к еще большему количеству
	// серверных вызовов.
	ПолучитьИОтобразитьИнформациюОДоступномОбновлении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// СтандартныеПодсистемы.ОбновлениеКонфигурации
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Обработка.АктивныеПользователи.Форма.АктивныеПользователи") Тогда
		
		ОтобразитьПолучениеОбновленийЗавершено();
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОбновлениеКонфигурации
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПолучениеОбновленийПрограммы_ПроверкаОткрытияФормы"
		И ТипЗнч(Параметр) = Тип("Структура") Тогда
		Параметр.Форма = ЭтотОбъект;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПолучениеИУстановка
		И Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаУстановкаТихий Тогда
		Отказ = Истина;
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Не завершена установка платформы 1С:Предприятие.'");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЗавершениеРаботы Тогда
		Если ЗначениеЗаполнено(ДлительнаяОперация) Тогда
			Отказ               = Истина;
			ТекстПредупреждения = НСтр("ru = 'Получение и установка обновлений не завершены.'");
		ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ВыборРежимаОбновленияФайловыйРежим
			Или Элементы.Страницы.ТекущаяСтраница = Элементы.ВыборРежимаОбновленияСерверныйРежим Тогда
			Отказ               = Истина;
			ТекстПредупреждения = НСтр("ru = 'Не установлено обновление программы.'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		// При завершении работы исключается вызов сервера.
		
		Если ЗначениеЗаполнено(ДлительнаяОперация) Тогда
			ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
		КонецЕсли;
		
		ОтключитьОбработчикОжидания("ИтерацияПолученияФайловОбновления");
		ОтключитьОбработчикОжидания("ЗагрузитьОбновлениеКонфигурации");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СтраницаВыбораВариантаОбновления

&НаКлиенте
Процедура ВариантИнформированияОВыходеОбновлений1ПриИзменении(Элемент)
	
	СохранитьВариантИнформированияОВыходеОбновленийНаСервере(ВариантИнформированияОВыходеОбновлений);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантИнформированияОВыходеОбновлений2ПриИзменении(Элемент)
	
	СохранитьВариантИнформированияОВыходеОбновленийНаСервере(ВариантИнформированияОВыходеОбновлений);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НадписьЕстьОшибкиУчетаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.КонтрольВеденияУчета") Тогда
		МодульКонтрольВеденияУчетаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КонтрольВеденияУчетаКлиент");
		МодульКонтрольВеденияУчетаКлиент.ОткрытьОтчетПоПроблемам("СистемныеПроверки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонфигурациюПриИзменении(Элемент)
	
	КонтекстОбновления       = Неопределено;
	ОписаниеФайловОбновлений = Неопределено;
	ПриИзмененииФлагаКомпонентаКонфигурацияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИсправленияПриИзменении(Элемент)
	
	КонтекстОбновления       = Неопределено;
	ОписаниеФайловОбновлений = Неопределено;
	ПриИзмененииИсправленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПлатформуПриИзменении(Элемент)
	
	КонтекстОбновления       = Неопределено;
	ОписаниеФайловОбновлений = Неопределено;
	ПриИзмененииФлагаПлатформыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогинПриИзменении(Элемент)
	
	ИзмененЛогинПароль = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ИзмененЛогинПароль = Истина;
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		"Пароль");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранятьДистрибутивыКомпонентаПлатформыВКаталог1ПриИзменении(Элемент)
	
	Элементы.КаталогСохраненияДистрибутиваПлатформы.Доступность = СохранятьДистрибутивыПлатформыВКаталог;
	Если СохранятьДистрибутивыПлатформыВКаталог Тогда
		КаталогСохраненияПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы();
	Иначе
		КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранятьДистрибутивыКомпонентаПлатформыВКаталогПриИзменении(Элемент)
	
	Элементы.КаталогСохраненияДистрибутиваКомпонентаПлатформы.Доступность = СохранятьДистрибутивыПлатформыВКаталог;
	Если СохранятьДистрибутивыПлатформыВКаталог Тогда
		КаталогСохраненияПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы();
	Иначе
		КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогСохраненияДистрибутиваКомпонентаПлатформыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьКаталогСохраненияПлатформы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогСохраненияДистрибутиваПлатформыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьКаталогСохраненияПлатформы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗарегистрироватьсяНаПортале1СИТСНажатие(Элемент)
	
	ОткрытьВебСтраницу(
		ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin(
			"/registration",
			НастройкиСоединения()),
		НСтр("ru = 'Регистрация'"));
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОсобенностиПереходаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьУстановленоДополнительноОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСообщениеОбОшибкеПолученияОбновленияОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРекомендацииПриОшибкеОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНаписатьВТехПоддержкуОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьЖурналРегистрацииОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСообщениеСервисаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнформацияУстановкаЗавершенаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтрУстановленаСоздатьРКОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтрУстановленаНеСоздаватьРКОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтрЗавершеноСоздатьРКОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтрЗавершеноНеСоздаватьРКОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРезервнуюКопиюУстановленоПриИзменении(Элемент)
	
	Если СоздатьРезервнуюКопию Тогда
		Элементы.СтраницыИнструкцияУстановкаУстановлена.ТекущаяСтраница = Элементы.СтрУстановленаСоздатьРК;
	Иначе
		Элементы.СтраницыИнструкцияУстановкаУстановлена.ТекущаяСтраница = Элементы.СтрУстановленаНеСоздаватьРК;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРезервнуюКопиюПриИзменении(Элемент)
	
	Если СоздатьРезервнуюКопию Тогда
		Элементы.СтраницыИнструкцияУстановкаЗавершена.ТекущаяСтраница = Элементы.СтрЗавершеноСоздатьРК;
	Иначе
		Элементы.СтраницыИнструкцияУстановкаЗавершена.ТекущаяСтраница = Элементы.СтрЗавершеноНеСоздаватьРК;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРезервнаяКопияФайлНажатие(Элемент)
	
	// СтандартныеПодсистемы.ОбновлениеКонфигурации
	ОбновлениеКонфигурацииКлиент.ПоказатьРезервноеКопирование(
		Новый Структура("СоздаватьРезервнуюКопию, ИмяКаталогаРезервнойКопииИБ, ВосстанавливатьИнформационнуюБазу",
			СоздаватьРезервнуюКопию,
			ИмяКаталогаРезервнойКопииИБ,
			ВосстанавливатьИнформационнуюБазу),
		Новый ОписаниеОповещения("ПриИзмененииПараметровРезервногоКопирования", ЭтотОбъект));
	// Конец СтандартныеПодсистемы.ОбновлениеКонфигурации
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОбновленияФайлПриИзменении(Элемент)
	
	ОтобразитьПолучениеОбновленийЗавершено();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОбновленияСерверПриИзменении(Элемент)
	
	ОтобразитьПолучениеОбновленийЗавершено();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОтложенныеОбработчикиОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбновлениеИнформационнойБазыКлиент.ПоказатьОтложенныеОбработчики();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьОтчетНаПочтуПриИзменении(Элемент)
	
	ОтобразитьПолучениеОбновленийЗавершено();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСписокДействий2Нажатие(Элемент)
	
	ПоказатьАктивныхПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСписокДействий4Нажатие(Элемент)
	
	ПоказатьАктивныхПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПатчиИсправляемыеОшибкиНажатие(Элемент)
	
	ИдентификаторВыбранногоОбновления = ?(ОбновитьКонфигурацию,
		ВариантОбновления,
		ИдентификаторТекущейВерсии());
	ВыбранноеОбновление               = ВариантыОбновлений[ИдентификаторВыбранногоОбновления];
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресОписаниеИсправлений", АдресИсправления);
	ПараметрыФормы.Вставить("ВыбранныеИсправления"    , ВыбранноеОбновление.ВыбранныеИсправления);
	ПараметрыФормы.Вставить("СписокИсправлений"       , ВыбранноеОбновление.СписокИсправлений);
	ПараметрыФормы.Вставить("ТолькоПросмотр"          , Элементы.УстановитьИсправления.ТолькоПросмотр);
	
	ОткрытьФорму("Обработка.ОбновлениеПрограммы.Форма.ВыборИсправлений",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ПриВыбореИсправлений", ЭтотОбъект, ИдентификаторВыбранногоОбновления));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияАктивныеПользователиОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьАктивныхПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановитьОбновлениеИзФайла1Нажатие(Элемент)
	
	УстановитьОбновлениеИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановитьОбновлениеИзФайла2Нажатие(Элемент)
	
	УстановитьОбновлениеИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружатьИУстанавливатьИсправленияАвтоматическиПриИзменении(Элемент)
	
	ПризнакСохранен = СохранитьПризнакАвтоматическойЗагрузкиИУстановкиИсправленийНаСервере(
		ЗагружатьИУстанавливатьИсправленияАвтоматически);
	Если ПризнакСохранен Тогда
		Элементы.ДекорацияЗагрузкаИсправленийРасписание.Доступность =
			ЗагружатьИУстанавливатьИсправленияАвтоматически;
	Иначе
			
		ЗагружатьИУстанавливатьИсправленияАвтоматически = Ложь;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Подключить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(
			Новый ОписаниеОповещения(
				"ПриОтветеНаВопросИсправленияПодключитьИнтернетПоддержкуПриВключенииУстановкиИсправлений",
				ЭтотОбъект),
			НСтр("ru = 'Для автоматического получения и установки исправлений (патчей)
				|необходимо подключить Интернет-поддержку пользователей.'"),
			Кнопки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗагрузкаИсправленийРасписаниеНажатие(Элемент)
	
	// АПК:574-выкл код не используется в мобильном клиенте
	Расписание = ПолучениеОбновленийПрограммыВызовСервера.РасписаниеЗаданияУстановкиИсправлений();
	ДиалогРасписание = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПриИзмененииРасписанияУстановкиИсправлений",
		ЭтотОбъект);
	ДиалогРасписание.Показать(ОписаниеОповещения);
	// АПК:574-вкл
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЕстьНеПримененныеИсправленияОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗавершитьРаботуСистемы(Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если (ТекСтраница = Элементы.СтраницаДоступнаяВерсияПлатформы
		Или ТекСтраница = Элементы.СтраницаПлатформаУжеУстановлена)
		И ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		ОтобразитьНерекомендуемаяВерсияПлатформы();
	Иначе
		// Информация об обновлении уже получена.
		ОтобразитьИнформациюОДоступномОбновлении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	ВыполнитьОбработкуКомандыДалее();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ПараметрЗакрытия = Неопределено;
	Если ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект)
		И Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНетНовойВерсииПлатформы
		И Не ВариантыОбновлений[ВариантОбновления].Платформа.ДоступноОбновление Тогда
		
		ПараметрЗакрытия = "Продолжить";
		
	КонецЕсли;
	
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьРаботуНаТекущейВерсии(Команда)
	
	ПолучениеОбновленийПрограммыВызовСервера.СохранитьНастройкиОповещенияОНерекомендуемойВерсииПлатформы();
	Закрыть("Продолжить");
	
КонецПроцедуры

&НаКлиенте
Процедура АктивныеПользователи(Команда)
	
	ПоказатьАктивныхПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДистрибутивПлатформы(Команда)
	
	ВыбранноеОбновление  = ВариантыОбновлений[ВариантОбновления];
	URLСтраницыПлатформы = ВыбранноеОбновление.Платформа.URLСтраницыПлатформы;
	
	Если ПустаяСтрока(URLСтраницыПлатформы) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Адрес страницы новой версии платформы не определен.'"));
		Возврат;
	КонецЕсли;
	
	Если ЭтоАдминистраторСистемы И СтрНайти(URLСтраницыПлатформы, "needAccessToken") = 0 Тогда
		URLСтраницыПлатформы = URLСтраницыПлатформы
			+ ?(СтрНайти(URLСтраницыПлатформы, "?") > 0, "&", "?")
			+ "needAccessToken=true";
	КонецЕсли;
	
	ОткрытьВебСтраницу(URLСтраницыПлатформы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Общего назначения.

// Используется для исключения одновременной видимости
// нескольких страниц.
&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьСтраницу(Страницы, Страница)
	
	Если Не Страница.Видимость Тогда
		Страница.Видимость = Истина;
	КонецЕсли;
	
	Страницы.ТекущаяСтраница = Страница;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылку(Ссылка)
	
	Если Ссылка = "open:RCInstruction" Тогда
		
		ОткрытьВнутреннийФайлИнструкции(
			"СозданиеРезервнойКопииИнформационнойБазы_ru",
			НСтр("ru = 'Создание резервной копии.htm'"));
		
	ИначеЕсли Ссылка = "open:V8Update" Тогда
		
		ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
		ОткрытьВебСтраницу(
			ВыбранноеОбновление.Платформа.URLОсобенностиПерехода,
			НСтр("ru = 'Особенности перехода на новую версию платформы 1С:Предприятие'"));
		
	ИначеЕсли Ссылка = "open:DistribFolder" Тогда
		
		ФайловаяСистемаКлиент.ОткрытьПроводник(КонтекстОбновления.КаталогДистрибутиваПлатформы);
		
	ИначеЕсли Ссылка = "open:ActiveUsers" Тогда
		
		ИмяОткрываемойФормы = "Обработка.АктивныеПользователи.Форма.АктивныеПользователи";
		ОткрытьФорму(ИмяОткрываемойФормы, Новый Структура("ОшибкаУстановкиМонопольногоРежима", Истина));
		
	ИначеЕсли Ссылка = "open:ProxySettings" Тогда
		
		ОткрытьФорму("ОбщаяФорма.ПараметрыПроксиСервера",
			Новый Структура("НастройкаПроксиНаКлиенте", Истина),
			ЭтотОбъект);
		
	ИначеЕсли НРег(Лев(Ссылка, 22)) = "mailto:webits-info@1c." Тогда
		
		ОтправитьСообщениеВТехПоддержку();
		
	ИначеЕсли НРег(Лев(Ссылка, 7)) = "mailto:" Тогда
		
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Ссылка);
		
	ИначеЕсли Ссылка = "open:log" Тогда
		
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", Новый Структура("Пользователь", ИмяПользователя()));
		
	ИначеЕсли Ссылка = "open:debuglog" Тогда
		
		ФайловаяСистемаКлиент.ОткрытьФайл(КонтекстОбновления.ПутьФайлаПротокола);
	ИначеЕсли Ссылка = "action:retruupdateplatfom" Тогда
		РежимУстановкиПлатформы = 1;
		ВыполнитьОбработкуКомандыДалее();
	ИначеЕсли НРег(Лев(Ссылка, 7)) = "http://"
		ИЛИ НРег(Лев(Ссылка, 8)) = "https://" Тогда
		
		ОткрытьВебСтраницу(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВебСтраницу(АдресСтраницы, ЗаголовокОкна = "")
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗаголовокОкна"              , ЗаголовокОкна);
	ПараметрыОткрытия.Вставить("Логин"                      , Логин);
	ПараметрыОткрытия.Вставить("Пароль"                     , Пароль);
	ПараметрыОткрытия.Вставить("ЭтоПолноправныйПользователь", ЭтоАдминистраторСистемы);
	ПараметрыОткрытия.Вставить("НастройкиСоединения"        , НастройкиСоединенияССерверами);
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьВебСтраницуСДополнительнымиПараметрами(
		АдресСтраницы,
		ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеВТехПоддержку()
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		Тема = НСтр("ru = 'Интернет-поддержка. Переход на новую версию Платформы 1С:Предприятие'");
	Иначе
		Тема = НСтр("ru = 'Интернет-поддержка. Обновление программы'");
	КонецЕсли;
	Вложения = Неопределено;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодключениеКПорталу Тогда
		
		Если Элементы.ПанельОшибкаПодключения.Видимость Тогда
			ОписаниеПричины = ИнтернетПоддержкаПользователейКлиент.ТекстФорматированногоЗаголовка(
				Элементы.ДекорацияСообщениеОбОшибкеПолученияОбновления.Заголовок);
		Иначе
			ОписаниеПричины = "";
		КонецЕсли;
		
		Сообщение = НСтр("ru = 'Не удалось подключиться к Порталу 1С:ИТС.'")
			+ ?(ПустаяСтрока(ОписаниеПричины),
				"",
				Символы.ПС + Символы.ПС + НСтр("ru = 'Описание:'") + Символы.ПС + ОписаниеПричины);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНеУдалосьПодключитьсяКСервису Тогда
		
		Сообщение = НСтр("ru = 'Ошибка при подключении сервиса автоматического обновления программы.'")
			+ ?(ПустаяСтрока(ПодробноеОписаниеОшибки),
				"",
				Символы.ПС + Символы.ПС + НСтр("ru = 'Описание:'") + Символы.ПС + ПодробноеОписаниеОшибки);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСообщениеСервиса Тогда
		
		Если ПустаяСтрока(ПодробноеОписаниеОшибки) Тогда
			Сообщение = НСтр("ru = 'Сообщение при подключении сервиса автоматического обновления программы:'")
				+ Символы.ПС
				+ ИнтернетПоддержкаПользователейКлиент.ТекстФорматированногоЗаголовка(
					Элементы.ДекорацияСообщениеСервиса.Заголовок);
		Иначе
			
			Если КонтекстОбновления <> Неопределено
				И Не ПустаяСтрока(КонтекстОбновления.ПутьФайлаПротокола) Тогда
				
				Вложения = Новый Массив;
				Если ПолучениеОбновленийПрограммыКлиентСервер.ФайлСуществует(КонтекстОбновления.ПутьФайлаПротокола) Тогда
					Вложения.Добавить(
						Новый Структура("Представление, ИмяФайла",
							НСтр("ru = 'Протокол установки платформы 1С_Предприятие.txt'"),
							КонтекстОбновления.ПутьФайлаПротокола));
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
				Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
				
				Сообщение = НСтр("ru = 'Ошибка при получении и установке платформы 1С:Предприятие:'")
					+ Символы.ПС
					+ ПодробноеОписаниеОшибки;
				
			Иначе
				
				Сообщение = НСтр("ru = 'Не удалось обновить программу.'")
					+ Символы.ПС
					+ НСтр("ru = 'Причина:'")
					+ Символы.ПС
					+ ПодробноеОписаниеОшибки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиентСервер");
		ДанныеСообщения = МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = "webIts";
		ДанныеСообщения.Тема       = Тема;
		ДанныеСообщения.Сообщение  = Сообщение;
		ДанныеСообщения.НастройкиТехническойИнформации.ОтправкаИдентификатораКонфигурации = Истина;
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиент");
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(
			ДанныеСообщения,
			ВложенияТехподдержке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСозданиеРезервнойКопииИБ()
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РезервноеКопированиеИБ") Тогда
		МодульРезервноеКопированиеИБКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РезервноеКопированиеИБКлиент");
		ПараметрыФормы = Новый Структура("КаталогПрограммы", КаталогУстановкиПлатформы);
		МодульРезервноеКопированиеИБКлиент.ОткрытьФормуРезервногоКопирования(ПараметрыФормы);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не встроена подсистема ""Резервное копирование ИБ"".'");
		ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(ТекстСообщения);
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнутреннийФайлИнструкции(ИмяМакета, ИмяФайла)
	
	#Если Не ВебКлиент Тогда
	ПутьФайлаИнструкции = ПодготовитьФайлМакетаИнструкции(ИмяМакета, ИмяФайла);
	
	// Открытие инструкции в Интернет-обозревателе по умолчанию.
	Попытка
		ФайловаяСистемаКлиент.ОткрытьФайл(ПутьФайлаИнструкции);
	Исключение
		ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при открытии внутреннего файла инструкции (%1).
					|%2'"),
				ИмяФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		ПоказатьПредупреждение(, НСтр("ru = 'Ошибка при открытии файла.'"));
		Возврат;
	КонецПопытки;
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОбновлениеИзФайла()
	
	// СтандартныеПодсистемы.ОбновлениеКонфигурации
	// Вызов программного интерфейса для установки обновления конфигурации из файла.
	Закрыть();
	ОбновлениеКонфигурацииКлиент.ПоказатьПоискИУстановкуОбновлений();
	// Конец СтандартныеПодсистемы.ОбновлениеКонфигурации
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьФайлМакетаИнструкции(Знач ИмяМакета, Знач ИмяФайла)
	
	Если Не ПолучениеОбновленийПрограммы.СлужебнаяДоступноИспользованиеПолученияОбновленийПлатформы() Тогда
		ВызватьИсключение
			НСтр("ru = 'Использование обновления платформы 1С:Предприятие недоступно в текущем режиме работы.'");
	КонецЕсли;
	
	// Файл записывается в контексте сервера на компьютере пользователя,
	// т.к. механизм используется только в файловом режиме работы.
	
	КаталогДляРаботыСОбновлениями =
		ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы();
	
	Если НЕ ПолучениеОбновленийПрограммыКлиентСервер.ФайлСуществует(КаталогДляРаботыСОбновлениями) Тогда
		Попытка
			СоздатьКаталог(КаталогДляРаботыСОбновлениями);
		Исключение
			ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при создании каталога для работы с обновлениями платформы 1С:Предприятие (%1).
						|%2'"),
					КаталогДляРаботыСОбновлениями,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			ВызватьИсключение НСтр("ru = 'Ошибка при создании временного каталога.'");
		КонецПопытки;
	КонецЕсли;
	
	ПутьФайлаИнструкции = КаталогДляРаботыСОбновлениями + ИмяФайла;
	Попытка
		ЗаписьТекста = Новый ЗаписьТекста(ПутьФайлаИнструкции);
		ЗаписьТекста.Записать(Обработки.ОбновлениеПрограммы.ПолучитьМакет(ИмяМакета).ПолучитьТекст());
		ЗаписьТекста.Закрыть();
	Исключение
		ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при записи данных макета инструкции в файл на диске (%1).
					|%2'"),
				ИмяФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		ВызватьИсключение НСтр("ru = 'Ошибка при создании временного файла.'");
	КонецПопытки;
	
	Возврат ПутьФайлаИнструкции;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОшибкаСоединения(КодОшибки)
	
	Возврат (КодОшибки = "ConnectError" ИЛИ КодОшибки = "ServerError" ИЛИ КодОшибки = "ClientError");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(Форма)
	
	Возврат (Форма.Сценарий = "ПереходНаНовуюВерсиюПлатформы");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(Форма)
	
	Возврат (Форма.Сценарий = "СообщениеОНерекомендуемойВерсииПлатформы");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(Форма)
	
	Возврат (Форма.Сценарий = "ПереходНаДругуюПрограммуИлиРедакцию");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСценарийРабочегоОбновления(Форма)
	
	Возврат (Форма.Сценарий = "РабочееОбновление");
	
КонецФункции

&НаКлиенте
Функция НастройкиСоединения()
	
	Если НастройкиСоединенияССерверами = Неопределено Тогда
		Возврат ИнтернетПоддержкаПользователейКлиент.НастройкиСоединенияССерверами();
	Иначе
		Возврат НастройкиСоединенияССерверами;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучениеОбновленияЗавершено(ФормаПомощника)
	
	Возврат (ФормаПомощника.КонтекстОбновления <> Неопределено
		И ФормаПомощника.КонтекстОбновления.Завершено);
	
КонецФункции

&НаКлиенте
Процедура УстановитьОбновлениеКонфигурации()
	
	ПомещаемыеФайлыИсправлений = Новый Массив;
	Если УстановитьИсправления Тогда
		Для Каждого ТекИсправление Из КонтекстОбновления.Исправления Цикл
			Если ПустаяСтрока(ТекИсправление.АдресФайла) Тогда
				ПомещаемыеФайлыИсправлений.Добавить(
					Новый ОписаниеПередаваемогоФайла(ТекИсправление.ИмяПолученногоФайла));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПомещаемыеФайлыИсправлений.Количество() = 0 Тогда
		НачатьУстановкуОбновления();
	Иначе
		Состояние(, , НСтр("ru = 'Пожалуйста, подождите...'"));
		
		ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
		ПараметрыЗагрузки.Интерактивно = Ложь;
		ПараметрыЗагрузки.ИдентификаторФормы = Новый УникальныйИдентификатор;
		
		ФайловаяСистемаКлиент.ЗагрузитьФайлы(
			Новый ОписаниеОповещения("НачатьУстановкуОбновления", ЭтотОбъект),
			ПараметрыЗагрузки,
			ПомещаемыеФайлыИсправлений);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьУстановкуОбновления(
	ПомещенныеФайлыИсправлений = Неопределено,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Состояние();
	Если ПомещенныеФайлыИсправлений <> Неопределено Тогда
		Для Каждого ТекФайлИсправления Из ПомещенныеФайлыИсправлений Цикл
			Для Каждого ТекИсправление Из КонтекстОбновления.Исправления Цикл
				Если ТекИсправление.ИмяПолученногоФайла = ТекФайлИсправления.ПолноеИмя Тогда
					ТекИсправление.АдресФайла = ТекФайлИсправления.Хранение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыУстановки = ПараметрыУстановкиОбновленияКонфигурации();
	
	СтрОписаниеФайловОбновлений = "";
	Для Каждого ТекФайл Из ПараметрыУстановки.ФайлыОбновления Цикл
		СтрОписаниеФайловОбновлений = СтрОписаниеФайловОбновлений + Символы.ПС
			+ "  " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Имя файла: %1, Выполнить обработчики обновления: %2;'"),
				ТекФайл.ПолноеИмяФайлаОбновления,
				ТекФайл.ВыполнитьОбработчикиОбновления);
	КонецЦикла;
	
	ИменаФайловИсправлений = Новый Массив;
	Для Каждого ТекИсправление Из КонтекстОбновления.Исправления Цикл
		ИменаФайловИсправлений.Добавить(ТекИсправление.ИмяПолученногоФайла);
	КонецЦикла;
	
	СообщениеЖурналаРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Вызов интерфейса установки обновления (СтандартныеПодсистемы.ОбновлениеКонфигурации).
			|Файлы обновления конфигурации: %1;
			|Файлы исправлений (патчей): %2;
			|Удалить исправления (патчи): %3;
			|Платформа 1С:Предприятие: %4'"),
			СтрОписаниеФайловОбновлений,
			СтрСоединить(ИменаФайловИсправлений, ","),
			СтрСоединить(ПараметрыУстановки.Исправления.Удалить),
			ПараметрыУстановки.КаталогПлатформы);
	ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(СообщениеЖурналаРегистрации);
	
	// Интеграция с подсистемой Центр мониторинга.
	Если ПередачаСведенийВЦентрМониторинга
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		ВключитьИспользованиеЦентраМониторинга();
	КонецЕсли;
	
	// Вызов программного интерфейса для установки обновления конфигурации.
	ОбновлениеКонфигурацииКлиент.УстановитьОбновление(
		ЭтотОбъект,
		ПараметрыУстановки,
		ПараметрыАдминистрирования);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыУстановкиОбновленияКонфигурации()
	
	Результат = Новый Структура;
	Результат.Вставить("РежимОбновления"                  , РежимОбновления);
	Результат.Вставить("ЗавершениеРаботыСистемы"          , Ложь);
	Результат.Вставить("ДатаВремяОбновления"              , ДатаВремяОбновления);
	Результат.Вставить("ВыслатьОтчетНаПочту"              , ВыслатьОтчетНаПочту);
	Результат.Вставить("АдресЭлектроннойПочты"            , АдресЭлектроннойПочты);
	Результат.Вставить("КодЗадачиПланировщика"            , КодЗадачиПланировщика);
	Результат.Вставить("ИмяФайлаОбновления"               , "");
	Результат.Вставить("СоздаватьРезервнуюКопию"          , СоздаватьРезервнуюКопию);
	Результат.Вставить("ИмяКаталогаРезервнойКопииИБ"      , ИмяКаталогаРезервнойКопииИБ);
	Результат.Вставить("ВосстанавливатьИнформационнуюБазу", ВосстанавливатьИнформационнуюБазу);
	Результат.Вставить("КаталогПлатформы"                 , "");
	
	Если ОбновитьПлатформу Тогда
		ВыбранноеОбновление        = ВариантыОбновлений[ВариантОбновления];
		Результат.КаталогПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогУстановкиПлатформы1СПредприятие(
			ВыбранноеОбновление.Платформа.Версия);
	Иначе
		
	КонецЕсли;
	
	ФайлыОбновления = Новый Массив;
	Для Каждого ТекФайл Из КонтекстОбновления.ОбновленияКонфигурации Цикл
		ФайлыОбновления.Добавить(
			Новый Структура("ПолноеИмяФайлаОбновления, ВыполнитьОбработчикиОбновления",
				ТекФайл.ПолноеИмяCFUФайлаВКаталогеДистрибутивов,
				ТекФайл.ПрименитьОбработчикиОбновления));
	КонецЦикла;
	Результат.Вставить("ФайлыОбновления", ФайлыОбновления);
	
	ИсправленияУстановить = Новый Массив;
	Для Каждого ТекИсправление Из КонтекстОбновления.Исправления Цикл
		ИсправленияУстановить.Добавить(ТекИсправление.АдресФайла);
	КонецЦикла;
	
	Результат.Вставить("Исправления",
		Новый Структура("Установить, Удалить",
			ИсправленияУстановить,
			КонтекстОбновления.ОтозванныеИсправления));
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьАктивныхПользователей()
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОповещатьОЗакрытии", Истина);
	СтандартныеПодсистемыКлиент.ОткрытьСписокАктивныхПользователей(ПараметрыФормы, ЭтотОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

&НаСервере
Функция ЗапрещеноУправлятьПризнакомОбновленияКонфигурации()
	
	Возврат ДоступноНесколькоВерсийДляОбновлений()
		И Не (ВариантОбновления = ИдентификаторТекущейВерсии()
		Или ВариантОбновления = ИдентификаторНовойСборкиТекущейВерсии());
	
КонецФункции

&НаСервере
Функция ДоступноНесколькоВерсийДляОбновлений()
	
	Если ВариантыОбновлений.Количество() > 2 Тогда
		Возврат Истина;
	ИначеЕсли ЗначениеЗаполнено(ИнформацияОДоступномОбновлении.ОкончаниеПоддержкиТекущейВерсии)
		И ИнформацияОДоступномОбновлении.ОкончаниеПоддержкиТекущейВерсии > ТекущаяДатаСеанса() Тогда
		
		ВариантАктуальнойВерсия = ВариантыОбновлений[ИдентификаторАктуальнойВерсии()];	// см. НовыйВариантОбновления
		Если ВариантАктуальнойВерсия.Конфигурация = Неопределено
			Или Не ВариантАктуальнойВерсия.Конфигурация.ДоступноОбновление Тогда
			
			Возврат Ложь;
			
		КонецЕсли;
		
		ТекущаяВерсия    = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(
			ИнтернетПоддержкаПользователей.ВерсияКонфигурации());
		АктуальнаяВерсия = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(
			ВариантАктуальнойВерсия.Конфигурация.Версия);
		
		Возврат ТекущаяВерсия <> АктуальнаяВерсия;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Новый объект варианта обновления.
//
// Параметры:
//  ИдентификаторВерсии - Строка
//  НомерВерсииБезСборки - Строка
//  ОкончаниеПоддержки - Неопределено - версия не находится на длительной поддержке.
//                     - Дата - дата (без времени) окончания поддержки текущей версии.
//
// Возвращаемое значение:
//  Структура:
//    * Конфигурация - Неопределено - обновление конфигурации не доступно.
//                   - Структура из КлючИЗначение - информация об обновлении конфигурации.
//    * Платформа - Неопределено - обновление платформы не доступно.
//                - Структура из КлючИЗначение - информация об обновлении платформы.
//    * ВыбранныеИсправления - СписокЗначений из Строка - список идентификаторов исправлений выбранных для установки.
//    * СписокИсправлений - СписокЗначений из Строка - список доступных исправлений для установки.
//    * ЗаголовокОбновления - Строка - описание варианта обновления.
//    * ПодсказкаОбновления - Строка - подсказка варианта обновления.
//
&НаСервереБезКонтекста
Функция НовыйВариантОбновления(ИдентификаторВерсии, НомерВерсииБезСборки, ОкончаниеПоддержки)
	
	Результат = Новый Структура();
	Результат.Вставить("Конфигурация"         , Неопределено);
	Результат.Вставить("Платформа"            , Неопределено);
	Результат.Вставить("ВыбранныеИсправления" , Новый СписокЗначений());
	Результат.Вставить("СписокИсправлений"    , Новый СписокЗначений());
	Результат.Вставить("ЗаголовокОбновления"  , "");
	Результат.Вставить("ПодсказкаОбновления"  , "");
	
	ПредставлениеОкончанияПоддержки = ?(ЗначениеЗаполнено(ОкончаниеПоддержки),
		Формат(ОкончаниеПоддержки, "ДЛФ=DD"),
		"");
	
	Если ИдентификаторВерсии = ИдентификаторТекущейВерсии()
		Или ИдентификаторВерсии = ИдентификаторНовойСборкиТекущейВерсии() Тогда
		
		Если ЗначениеЗаполнено(ОкончаниеПоддержки) Тогда
			Результат.ЗаголовокОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обновить текущую версию %1 (поддержка до %2)'"),
				НомерВерсииБезСборки,
				ПредставлениеОкончанияПоддержки);
			Результат.ПодсказкаОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'До %1 будут выпускаться версии с исправлением ошибок и поддержкой законодательных изменений.'"),
				НомерВерсииБезСборки);
		Иначе
			Результат.ЗаголовокОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обновить текущую версию %1'"),
				НомерВерсииБезСборки);
		КонецЕсли;
		
	ИначеЕсли ИдентификаторВерсии = ИдентификаторАктуальнойВерсии() Тогда
		
		Результат.ЗаголовокОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти на новую версию %1 (актуальная версия)'"),
			НомерВерсииБезСборки);
		
	Иначе
		
		Результат.ЗаголовокОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти на новую версию %1 (поддержка до %2)'"),
			НомерВерсииБезСборки,
			ПредставлениеОкончанияПоддержки);
		Результат.ПодсказкаОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'До %1 будут выпускаться версии с исправлением ошибок и поддержкой законодательных изменений.'"),
			ПредставлениеОкончанияПоддержки);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторТекущейВерсии()
	
	Возврат "ТекущаяВерсия";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторНовойСборкиТекущейВерсии()
	
	Возврат "НоваяСборкаТекущейВерсии";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторАктуальнойВерсии()
	
	Возврат "АктуальнаяВерсия";
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интеграция с подсистемой "Центр мониторинга".

&НаСервере
Процедура НастроитьОтображениеИнтеграцииСЦентромМониторинга()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга")Тогда
		МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
		Если МодульЦентрМониторинга.ЦентрМониторингаВключен() Или Не ОбщегоНазначения.РазрешенДоступКИнтернетСервисам() Тогда
			Элементы.ГруппаЦентрМониторингаСерверныйРежим.Видимость = Ложь;
			Элементы.ГруппаЦентрМониторингаФайловыйРежим.Видимость  = Ложь;
		Иначе
			ПередачаСведенийВЦентрМониторинга = Истина;
		КонецЕсли;
	Иначе
		Элементы.ГруппаЦентрМониторингаСерверныйРежим.Видимость = Ложь;
		Элементы.ГруппаЦентрМониторингаФайловыйРежим.Видимость  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьИспользованиеЦентраМониторинга()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
		МодульЦентрМониторинга.ВключитьПодсистему();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Шаг информации о доступном обновлении.

&НаКлиенте
Процедура ВыполнитьОбработкуКомандыДалее()
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	
	Если ТекСтраница = Элементы.СтраницаНетНовойВерсииПлатформы Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если ТекСтраница = Элементы.СтраницаПлатформаУжеУстановлена
		Или ТекСтраница = Элементы.СтраницаУстановкаПлатформыЗавершена Тогда
		
		Если СоздатьРезервнуюКопию Тогда
			Если ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
				// Если отображается в качестве нерекомендуемой версии платформы,
				// тогда нельзя выполнять закрытие, т.к. будет завершена работа системы.
				ОткрытьСозданиеРезервнойКопииИБ();
			Иначе
				Закрыть();
				ОткрытьСозданиеРезервнойКопииИБ();
			КонецЕсли;
		Иначе
			Закрыть();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ТекСтраница = Элементы.СтраницаНерекомендуемаяВерсияПлатформы Тогда
		
		ПолучитьИОтобразитьИнформациюОДоступномОбновлении();
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаДоступнаяВерсияПлатформы Тогда
		
		Если Не ЭтоФайловаяИБ Тогда
			// В Клиент-серверном варианте пользователю отображается только
			// ссылка для перехода на страницу загрузки дистрибутива
			// и кнопка "Далее" имеет заголовок "Готово".
			Закрыть();
			Возврат;
		КонецЕсли;
		
		// Обработчик для файлового режима работы.
		Если Элементы.ГруппаКаталогСохраненияПлатформы.Видимость
			И СохранятьДистрибутивыПлатформыВКаталог Тогда
			Если ПустаяСтрока(КаталогСохраненияПлатформы) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Не выбран каталог хранения дистрибутивов платформы.'"),
					,
					"КаталогСохраненияДистрибутиваПлатформы");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		НачатьПолучениеОбновлений();
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаВыбораВариантаОбновления Тогда
		
		Если ПустаяСтрока(ВариантОбновления) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не выбран вариант обновления.'"));
		Иначе
			ОтобразитьИнформациюОбОбновленииКомпонентов(Истина);
		КонецЕсли;
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаИнформацияОДоступномОбновленииКомпонентов Тогда
		
		// Обновление компонентов.
		Если Не ОбновитьКонфигурацию
			И Не УстановитьИсправления
			И Не ЭтоФайловаяИБ И ОбновитьПлатформу Тогда
			// Если кнопка "Далее" доступна и не обновляется конфигурация,
			// тогда выбрана только платформа и кнопка имеет заголовок "Готово".
			Закрыть();
			Возврат;
		КонецЕсли;
		
		// Обработчик для файлового режима работы.
		Если ОбновитьПлатформу
			И Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость
			И СохранятьДистрибутивыПлатформыВКаталог Тогда
			Если ПустаяСтрока(КаталогСохраненияПлатформы) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Не выбран каталог хранения дистрибутивов платформы.'"),
					,
					"КаталогСохраненияДистрибутиваКомпонентаПлатформы");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		НачатьПолучениеОбновлений();
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаПодключениеКПорталу Тогда
		
		ОчиститьСообщения();
		
		Результат = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
			Новый Структура("Логин, Пароль",
			Логин, Пароль));
	
		Если Результат.Отказ Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				Результат.СообщениеОбОшибке,
				,
				Результат.Поле);
		КонецЕсли;
		
		Если Результат.Отказ Тогда
			Возврат;
		КонецЕсли;
		
		НачатьПолучениеОбновлений();
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаНеУдалосьПодключитьсяКСервису
		Или ТекСтраница = Элементы.СтраницаСообщениеСервиса Тогда
		
		// Кнопка "Далее" имеет заголовок "Повторить" или
		// "Установить с ручными настройками" (при ошибке политик безопасности).
		Если Не ПустаяСтрока(ИнформацияОДоступномОбновлении.ИмяОшибки) Тогда
			
			// Повторить попытку получения информации об обновлении.
			ПолучитьИОтобразитьИнформациюОДоступномОбновлении();
			
		ИначеЕсли ОписаниеФайловОбновлений <> Неопределено
			И Не ПустаяСтрока(ОписаниеФайловОбновлений.ИмяОшибки)
			Или КонтекстОбновления <> Неопределено
			И Не ПустаяСтрока(КонтекстОбновления.ИмяОшибки) Тогда
			
			// 1) Повторить попытку получения информации о файлах обновлений
			// и начать получение файлов.
			// 2) Или Произошла ошибка получения файлов обновлений или ошибка установки платформы.
			// Повторить попытку получения файлов обновления или установки платформы.
			НачатьПолучениеОбновлений();
			
		КонецЕсли;
		
	ИначеЕсли ТекСтраница = Элементы.СтраницаУстановленыИсправления Тогда
		
		Если ПерезапуститьПрограмму Тогда
			ЗавершитьРаботуСистемы(Истина, Истина);
		Иначе
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли ТекСтраница = Элементы.ВыборРежимаОбновленияФайловыйРежим
		Или ТекСтраница = Элементы.ВыборРежимаОбновленияСерверныйРежим Тогда
		
		// Вызов программного интерфейса СтандартныеПодсистемы.ОбновлениеКонфигурации
		// для установки обновления.
		УстановитьОбновлениеКонфигурации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИОтобразитьИнформациюОДоступномОбновлении()
	
	Состояние(, , НСтр("ru = 'Получение информации о доступном обновлении'"));
	ПолучитьИОтобразитьИнформациюОДоступномОбновленииНаСервере();
	Состояние();
	
КонецПроцедуры

&НаСервере
Функция НовыйИнформацияОДоступномОбновлении()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкиСоединения",
		ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами());
	ДополнительныеПараметры.Вставить("ТолькоИсправления"  , Не ЭтоWindowsКлиент);
	Возврат ПолучениеОбновленийПрограммы.ИнформацияОДоступномОбновленииСлужебная(
		ИнтернетПоддержкаПользователей.ИмяПрограммы(),
		ИнтернетПоддержкаПользователей.ВерсияКонфигурации(),
		ИмяНовойПрограммы,
		НомерНовойРедакции,
		Сценарий,
		ДополнительныеПараметры);
	
КонецФункции

&НаСервере
Процедура ПолучитьИОтобразитьИнформациюОДоступномОбновленииНаСервере()
	
	ИнформацияОбОбновлении = НовыйИнформацияОДоступномОбновлении();
	ЗаполнитьИнформациюОбОбновлении(ИнформацияОбОбновлении);
	ОтобразитьИнформациюОДоступномОбновлении(Истина);
	
КонецПроцедуры

// Инициализирует работу мастера обновления программы.
//
// Параметры:
//  ИнформацияОбОбновлении - см. ПолучениеОбновленийПрограммы.ИнформацияОДоступномОбновленииСлужебная
//
&НаСервере
Процедура ЗаполнитьИнформациюОбОбновлении(ИнформацияОбОбновлении)
	
	Исправления = ИнформацияОбОбновлении.Исправления;
	Если Исправления = Неопределено Тогда
		АдресИсправления = "";
	ИначеЕсли ПустаяСтрока(АдресИсправления) Тогда
		АдресИсправления = ПоместитьВоВременноеХранилище(Исправления, УникальныйИдентификатор);
	Иначе
		ПоместитьВоВременноеХранилище(Исправления, АдресИсправления);
	КонецЕсли;
	
	ИнформацияОбОбновлении.Удалить("Исправления");
	ИнформацияОДоступномОбновлении = ИнформацияОбОбновлении;
	
	// Заполнение доступных вариантов обновления
	СписокВариантовОбновлений = Новый Соответствие();
	ВариантыОбновленийВерсий  = Новый Соответствие();
	ИмяТекущейПрограммы       = ИнтернетПоддержкаПользователей.ИмяПрограммы();
	ВерсияТекущейПрограммы    = ИнтернетПоддержкаПользователей.ВерсияКонфигурации();
	ТекущаяВерсияБезСборки    = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ВерсияТекущейПрограммы);
	
	// Добавление информации о текущей версии
	ИнформацияОВариантеОбновления = НовыйВариантОбновления(
		ИдентификаторТекущейВерсии(),
		ТекущаяВерсияБезСборки,
		ИнформацияОбОбновлении.ОкончаниеПоддержкиТекущейВерсии);
	Если ЗначениеЗаполнено(ИнформацияОбОбновлении.ОкончаниеПоддержкиТекущейВерсии)
		И ИнформацияОбОбновлении.ОкончаниеПоддержкиТекущейВерсии > ТекущаяДатаСеанса() Тогда
		
		ИнформацияОВариантеОбновления.Конфигурация = Новый Структура("ДоступноОбновление", Ложь);
		ИнформацияОВариантеОбновления.Платформа    = Новый Структура("ДоступноОбновление", Ложь);
		
	КонецЕсли;
	
	СписокВариантовОбновлений.Вставить(
		ИдентификаторТекущейВерсии(),
		ИнформацияОВариантеОбновления);
	ВариантыОбновленийВерсий.Вставить(
		ИмяТекущейПрограммы + "|" + ВерсияТекущейПрограммы,
		ИнформацияОВариантеОбновления);
	
	// Добавление информации о переходе на новые версии с длительной поддержкой
	Для Каждого ДополнительнаяВерсия Из ИнформацияОбОбновлении.ДополнительныеВерсии Цикл
		
		НомерВерсииБезСборки = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(
			ДополнительнаяВерсия.Конфигурация.Версия);
		Если ТекущаяВерсияБезСборки = НомерВерсииБезСборки Тогда
			Если ЗначениеЗаполнено(ДополнительнаяВерсия.Конфигурация.ОкончаниеПоддержки)
				И ДополнительнаяВерсия.Конфигурация.ОкончаниеПоддержки > ТекущаяДатаСеанса() Тогда
				
				ИдентификаторВерсии = ИдентификаторНовойСборкиТекущейВерсии();
				
			// Поддержка текущей версии завершена
			Иначе
				Продолжить;
			КонецЕсли;
		Иначе
			ИдентификаторВерсии = ИмяТекущейПрограммы + "|" + ДополнительнаяВерсия.Конфигурация.Версия;
		КонецЕсли;
		
		ИнформацияОВариантеОбновления = НовыйВариантОбновления(
			ИдентификаторВерсии,
			НомерВерсииБезСборки,
			ДополнительнаяВерсия.Конфигурация.ОкончаниеПоддержки);
		ЗаполнитьЗначенияСвойств(ИнформацияОВариантеОбновления, ДополнительнаяВерсия, "Конфигурация,Платформа");
		
		СписокВариантовОбновлений.Вставить(
			ИдентификаторВерсии,
			ИнформацияОВариантеОбновления);
		ВариантыОбновленийВерсий.Вставить(
			ИмяТекущейПрограммы + "|" + ДополнительнаяВерсия.Конфигурация.Версия,
			ИнформацияОВариантеОбновления);
		
	КонецЦикла;
	
	// Добавление информации о актуальной версии
	ИмяНовойВерсииПрограммы   = ?(ПустаяСтрока(ИмяНовойПрограммы), ИмяТекущейПрограммы, ИмяНовойПрограммы);
	НомерНовойВерсииПрограммы = "";
	ОкончаниеПоддержки        = Неопределено;
	Если ИнформацияОбОбновлении.Конфигурация <> Неопределено Тогда
		НомерНовойВерсииПрограммы = ИнформацияОбОбновлении.Конфигурация.Версия;
		ОкончаниеПоддержки        = ИнформацияОбОбновлении.Конфигурация.ОкончаниеПоддержки;
	КонецЕсли;
	
	ИнформацияОВариантеОбновления = НовыйВариантОбновления(
		ИдентификаторАктуальнойВерсии(),
		ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(НомерНовойВерсииПрограммы),
		ОкончаниеПоддержки);
	ЗаполнитьЗначенияСвойств(ИнформацияОВариантеОбновления, ИнформацияОбОбновлении, "Конфигурация,Платформа");
	
	СписокВариантовОбновлений.Вставить(
		ИдентификаторАктуальнойВерсии(),
		ИнформацияОВариантеОбновления);
	ВариантыОбновленийВерсий.Вставить(
		ИмяНовойВерсииПрограммы + "|" + НомерНовойВерсииПрограммы,
		ИнформацияОВариантеОбновления);
	
	// Заполнение информации о исправлениях
	Если Исправления <> Неопределено Тогда
		Для Каждого СтрокаИсправления Из Исправления Цикл
			// Исправление дополнительных подсистем применимо ко всем версиям
			Если СтрокаИсправления.ДляТекущейВерсии И СтрокаИсправления.ДляНовойВерсии Тогда
				Для Каждого ТекВариантОбновления Из СписокВариантовОбновлений Цикл
					ТекВариантОбновления.Значение.ВыбранныеИсправления.Добавить(СтрокаИсправления.Идентификатор);
					ТекВариантОбновления.Значение.СписокИсправлений.Добавить(СтрокаИсправления.Идентификатор);
				КонецЦикла;
			Иначе
				Для Каждого СтрокаПрименимости Из СтрокаИсправления.Применимость Цикл
					ТекВариантОбновления = ВариантыОбновленийВерсий[
						СтрокаПрименимости.ИмяПрограммы + "|" + СтрокаПрименимости.ВерсияПрограммы];	// см. НовыйВариантОбновления
					Если ТекВариантОбновления <> Неопределено Тогда
						ТекВариантОбновления.ВыбранныеИсправления.Добавить(СтрокаИсправления.Идентификатор);
						ТекВариантОбновления.СписокИсправлений.Добавить(СтрокаИсправления.Идентификатор);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВариантыОбновлений = Новый ФиксированноеСоответствие(СписокВариантовОбновлений);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИнформациюОДоступномОбновлении(Знач ЗаполнитьНачальныеНастройки = Ложь)
	
	Если Не ПустаяСтрока(ИнформацияОДоступномОбновлении.ИмяОшибки) Тогда
		Если ЭтоОшибкаСоединения(ИнформацияОДоступномОбновлении.ИмяОшибки) Тогда
			ОтобразитьОшибкуПодключения(
				ИнформацияОДоступномОбновлении.Сообщение,
				ИнформацияОДоступномОбновлении.ИнформацияОбОшибке);
		Иначе
			ОтобразитьСообщениеСервиса(
				ИнформацияОДоступномОбновлении.Сообщение,
				НСтр("ru = 'Повторить попытку подключения >'"),
				ИнформацияОДоступномОбновлении.ИнформацияОбОшибке);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		
		ВариантОбновления = ИдентификаторАктуальнойВерсии();
		ОтобразитьИнформациюОДоступномОбновленииПлатформы(ЗаполнитьНачальныеНастройки);
		
	ИначеЕсли ДоступноНесколькоВерсийДляОбновлений() Тогда
		ОтобразитьИнформациюОВариантахОбновлений(ЗаполнитьНачальныеНастройки);
		ОбновитьПанельПредупреждений(Истина);
	Иначе
		
		ВариантОбновления = ИдентификаторАктуальнойВерсии();
		ОтобразитьИнформациюОбОбновленииКомпонентов(ЗаполнитьНачальныеНастройки);
		
		// Если доступно обновление конфигурации, тогда отобразить при необходимости предупреждения.
		ВариантОбновленияТекущаяВерсия    = ВариантыОбновлений[ИдентификаторТекущейВерсии()];
		ВариантОбновленияАктуальнаяВерсия = ВариантыОбновлений[ИдентификаторАктуальнойВерсии()];
		ДоступноОбновлениеКонфигурации    =
			ВариантОбновленияАктуальнаяВерсия.Конфигурация <> Неопределено
			И ВариантОбновленияАктуальнаяВерсия.Конфигурация.ДоступноОбновление
			Или ВариантОбновленияТекущаяВерсия <> Неопределено
			И ВариантОбновленияТекущаяВерсия.СписокИсправлений.Количество() > 0
			Или ВариантОбновленияАктуальнаяВерсия <> Неопределено
			И ВариантОбновленияАктуальнаяВерсия.СписокИсправлений.Количество() > 0;
		
		ОбновитьПанельПредупреждений(ДоступноОбновлениеКонфигурации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИнформациюОДоступномОбновленииПлатформы(ЗаполнитьНачальныеНастройки = Ложь)
	
	ЕстьОбновление      = Истина;
	ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
	ОбновлениеПлатформы = ВыбранноеОбновление.Платформа;
	Если Не ОбновлениеПлатформы.ДоступноОбновление Тогда
		
		// Информация о версии платформы отсутствует на Портале 1С:ИТС. В сценарии сообщения о нерекомендуемой версии
		// платформы необходимо очистить кеш минимальной и рекомендованной версии и разрешить работу с программой.
		Если ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
			ПолучениеОбновленийПрограммы.УдалитьИнформациюОВерсияхПлатформы();
		КонецЕсли;
		
		ЕстьОбновление = Ложь;
		ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаНетНовойВерсииПлатформы);
		Элементы.ДекорацияНетВерсии.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Используемая сейчас версия: %1'"),
			ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие());
		
	ИначеЕсли ОбщегоНазначенияКлиентСервер.СравнитьВерсии(
		ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие(),
		ОбновлениеПлатформы.Версия) >= 0 Тогда
		
		ЕстьОбновление = Ложь;
		ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаНетНовойВерсииПлатформы);
		Элементы.ДекорацияНетВерсии.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Рекомендуемая версия платформы 1С:Предприятие: <b>%1</b>
				|Используемая сейчас версия: %2'"),
			ОбновлениеПлатформы.Версия,
			ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие());
		
	КонецЕсли;
	
	Если Не ЕстьОбновление Тогда
		
		Элементы.КнопкаНазад.Доступность  = Ложь;
		Элементы.КнопкаДалее.Доступность  = Ложь;
		Элементы.КнопкаОтмена.Доступность = Истина;
		Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
		Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Далее >'");
		Элементы.КнопкаОтмена.Заголовок   = НСтр("ru = 'Закрыть'");
		Возврат;
		
	Иначе
		
		Установлена = Ложь;
		Если ЭтоФайловаяИБ Тогда
			КаталогУстановкиПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогУстановкиПлатформы1СПредприятие(
				ОбновлениеПлатформы.Версия);
			Установлена = (КаталогУстановкиПлатформы <> "");
		КонецЕсли;
		
		Если Установлена Тогда
			
			// Это файловая ИБ и платформа установлена.
			ОтобразитьСтраницуНоваяВерсияПлатформыУжеУстановленаНаКомпьютере();
			
		Иначе
			
			// Страница информации о доступном обновлении платформы.
			ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаДоступнаяВерсияПлатформы);
			Элементы.НадписьВерсия.Заголовок = НСтр("ru = 'Версия'")
				+ " " + ОбновлениеПлатформы.Версия;
			Элементы.НадписьОсобенностиПерехода.Видимость =
				(Не ПустаяСтрока(ОбновлениеПлатформы.URLОсобенностиПерехода));
			
			Элементы.ДекорацияСообщениеНоваяВерсияПлатформы.Заголовок = ЗаголовокСтраницыНоваяВерсияПлатформы();
			
			// Файловый и клиент-серверный режим.
			Если ЭтоФайловаяИБ Тогда
				
				Элементы.ГруппаРазмер.Видимость                      = Истина;
				Элементы.ЗагрузитьДистрибутивПлатформы2.Видимость    = Ложь;
				Элементы.ГруппаПараметрыУстановкиПлатформы.Видимость = Истина;
				Элементы.НадписьРазмерОбновления.Заголовок =
					ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
						ОбновлениеПлатформы.РазмерОбновления);
				Элементы.КнопкаДалее.Доступность  = Истина;
				Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Далее >'");
				
				// Сохранение дистрибутивов платформы.
				Если ЗаполнитьНачальныеНастройки Тогда
					НастройкиОбновления = ПолучениеОбновленийПрограммы.НастройкиАвтоматическогоОбновления();
					РежимУстановкиПлатформы = НастройкиОбновления.РежимУстановки;
					Если Элементы.ГруппаКаталогСохраненияПлатформы.Видимость Тогда
						Если НастройкиОбновления.КаталогДистрибутивовПлатформы = Неопределено Тогда
							КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
							СохранятьДистрибутивыПлатформыВКаталог = Ложь;
							Элементы.КаталогСохраненияДистрибутиваПлатформы.Доступность = Ложь;
						Иначе
							КаталогСохраненияПлатформы = НастройкиОбновления.КаталогДистрибутивовПлатформы;
							Если ПустаяСтрока(КаталогСохраненияПлатформы) Тогда
								КаталогСохраненияПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы();
							КонецЕсли;
							СохранятьДистрибутивыПлатформыВКаталог = Истина;
							Элементы.КаталогСохраненияДистрибутиваПлатформы.Доступность = Истина;
						КонецЕсли;
					Иначе
						КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				
				// Режим клиент-сервер.
				Элементы.ГруппаРазмер.Видимость                      = Ложь;
				Элементы.ЗагрузитьДистрибутивПлатформы2.Видимость    = Истина;
				Элементы.ГруппаПараметрыУстановкиПлатформы.Видимость = Ложь;
				Элементы.КнопкаДалее.Доступность  = Истина;
				Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Готово'");
				
			КонецЕсли;
			
			Элементы.КнопкаНазад.Доступность  = ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект);
			Элементы.КнопкаОтмена.Доступность = Истина;
			Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
			Элементы.КнопкаОтмена.Заголовок   = НСтр("ru = 'Отмена'");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСтраницуНоваяВерсияПлатформыУжеУстановленаНаКомпьютере()
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаПлатформаУжеУстановлена);
	
	ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
	ОбновлениеПлатформы = ВыбранноеОбновление.Платформа;
	Элементы.НадписьВерсияУстановлено.Заголовок =
		НСтр("ru = 'Версия'") + " " + ОбновлениеПлатформы.Версия;
	
	Если Не ПустаяСтрока(ОбновлениеПлатформы.URLОсобенностиПерехода) Тогда
		
		Элементы.НадписьУстановленоДополнительно.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Версия платформы %1 уже установлена на компьютере.
				|Перед началом работы на новой версии платформы рекомендуется ознакомиться
				|с <a href=""open:V8Update"">особенностями перехода</a> на эту версию платформы.'"),
			ОбновлениеПлатформы.Версия);
		
	Иначе
		
		Элементы.НадписьУстановленоДополнительно.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Версия платформы %1 уже установлена на компьютере.'"),
			ОбновлениеПлатформы.Версия);
		
	КонецЕсли;
	
	Если Элементы.ДекорацияСообщениеПлатформаУжеУстановлена.Видимость Тогда
		Элементы.ДекорацияСообщениеПлатформаУжеУстановлена.Заголовок = ЗаголовокСтраницыНоваяВерсияПлатформы();
	КонецЕсли;
	
	Если ПустаяСтрока(Элементы.ДекорацияСтрУстановленаСоздатьРК.Заголовок) Тогда
		Элементы.ДекорацияСтрУстановленаСоздатьРК.Заголовок =
			ТекстИнструкцииПоПереходуНаНовуюВерсиюПлатформы(Истина);
	КонецЕсли;
	
	Если ПустаяСтрока(Элементы.ДекорацияСтрУстановленаНеСоздаватьРК.Заголовок) Тогда
		Элементы.ДекорацияСтрУстановленаНеСоздаватьРК.Заголовок =
			ТекстИнструкцииПоПереходуНаНовуюВерсиюПлатформы(Ложь);
	КонецЕсли;
	
	Если СоздатьРезервнуюКопию Тогда
		Элементы.СтраницыИнструкцияУстановкаУстановлена.ТекущаяСтраница = Элементы.СтрУстановленаСоздатьРК;
	Иначе
		Элементы.СтраницыИнструкцияУстановкаУстановлена.ТекущаяСтраница = Элементы.СтрУстановленаНеСоздаватьРК;
	КонецЕсли;
	
	Элементы.КнопкаНазад.Доступность  = Не ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект);
	Элементы.КнопкаДалее.Доступность  = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Готово'");
	Элементы.КнопкаОтмена.Заголовок   = НСтр("ru = 'Отмена'");
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИнформациюОбОбновленииКомпонентов(Знач ЗаполнитьНачальныеНастройки)
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаИнформацияОДоступномОбновленииКомпонентов);
	Если ЗаполнитьНачальныеНастройки Тогда
		ЗаполнитьНачальныеНастройкиИОтображениеУстановкиКомпонентов();
	КонецЕсли;
	
	ОтобразитьКомпонентКонфигурации();
	ОтобразитьИсправления();
	ОтобразитьКомпонентПлатформы();
	ОтобразитьКраткуюИнформациюОДоступномОбновлении();
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИнформациюОВариантахОбновлений(ЗаполнитьНачальныеНастройки)
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаВыбораВариантаОбновления);
	Если ЗаполнитьНачальныеНастройки Тогда
		ОтобразитьВариантыОбновлений();
	КонецЕсли;
	
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьВариантыОбновлений()
	
	// Заполнение информации о текущей версии
	ТекущаяВерсия = ИнтернетПоддержкаПользователей.ВерсияКонфигурации();
	
	Элементы.ВариантОбновленияНадпись.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Текущая версия <b>%1</b>, выберите вариант обновления:'"),
		ТекущаяВерсия);
	
	// Удаление ранее созданных элементов
	Для Каждого ТекЭлемент Из Элементы.ГруппаВариантовОбновления.ПодчиненныеЭлементы Цикл
		Элементы.Удалить(ТекЭлемент);
	КонецЦикла;
	
	// Заполнение списка вариантов обновлений
	ДополнительныеВерсии            = ИнформацияОДоступномОбновлении.ДополнительныеВерсии;
	ДанныеОНовойСборкеТекущейВерсии = ВариантыОбновлений.Получить(ИдентификаторНовойСборкиТекущейВерсии());
	НоваяСборкаТекущейВерсии        = ?(ДанныеОНовойСборкеТекущейВерсии = Неопределено,
		"",
		ДанныеОНовойСборкеТекущейВерсии.Конфигурация.Версия);
	
	// Добавление текущей версии в список доступных вариантов обновления
	Если ДанныеОНовойСборкеТекущейВерсии = Неопределено Тогда
		
		ИнформацияОКонфигурации = Новый Структура();
		ИнформацияОКонфигурации.Вставить("ИдентификаторВерсии", ИдентификаторТекущейВерсии());
		ИнформацияОКонфигурации.Вставить("Версия"             , ТекущаяВерсия);
		ИнформацияОКонфигурации.Вставить("URLНовоеВВерсии"    , "");
		
		ДобавитьЭлементыВариантаОбновления(
			ИнформацияОКонфигурации,
			ИдентификаторТекущейВерсии());
		
	Иначе
		
		ДобавитьЭлементыВариантаОбновления(
			ДанныеОНовойСборкеТекущейВерсии.Конфигурация,
			ИдентификаторНовойСборкиТекущейВерсии());
		
	КонецЕсли;
	
	// Добавление дополнительных версий в список доступных вариантов обновления
	ИмяПрограммы = ИнтернетПоддержкаПользователей.ИмяПрограммы();
	Для Каждого ДополнительнаяВерсия Из ДополнительныеВерсии Цикл
		Если НоваяСборкаТекущейВерсии <> ДополнительнаяВерсия.Конфигурация.Версия Тогда
			ИдентификаторВерсии = ИмяПрограммы + "|" + ДополнительнаяВерсия.Конфигурация.Версия;
			ДобавитьЭлементыВариантаОбновления(ДополнительнаяВерсия.Конфигурация, ИдентификаторВерсии);
		КонецЕсли;
	КонецЦикла;
	
	// Добавление актуальной версии в список доступных вариантов обновления
	ДобавитьЭлементыВариантаОбновления(
		ИнформацияОДоступномОбновлении.Конфигурация,
		ИдентификаторАктуальнойВерсии());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПанельПредупреждений(ДоступноОбновление)
	
	// Получение списка исправлений(патчей) для отображения предупреждения на форме.
	УстановитьПривилегированныйРежим(Истина);
	УстановленныеИсправления = ОбновлениеКонфигурации.УстановленныеИсправления();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЕстьНеПримененныеИсправления = Ложь;
	
	Если УстановленныеИсправления <> Неопределено Тогда
		Для Каждого ОписаниеИсправления Из УстановленныеИсправления Цикл
			Если Не ЗначениеЗаполнено(ОписаниеИсправления.Идентификатор) Тогда
				ЕстьНеПримененныеИсправления = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Элементы.НадписьЕстьОшибкиУчета.Видимость = (ДоступноОбновление И ВидимостьПанелиОшибок());
	Элементы.НадписьЕстьРасширения.Видимость = (ЭтоАдминистраторСистемы
		И ДоступноОбновление
		И Не ПолучениеОбновленийПрограммы.ЭтоБазоваяВерсияКонфигурации() // В базовых версиях нет сторонних расширений.
		И ОбновлениеКонфигурации.ПредупреждатьОНаличииРасширений());
	Элементы.НадписьЕстьНеПримененныеИсправления.Видимость = ЕстьНеПримененныеИсправления;
	Элементы.ПанельПредупреждений.Видимость = (Элементы.НадписьЕстьОшибкиУчета.Видимость
		Или Элементы.НадписьЕстьРасширения.Видимость
		Или Элементы.НадписьЕстьНеПримененныеИсправления.Видимость);
	
КонецПроцедуры

// Добавляет новые элементы на форму для выбора варианта обновления.
//
// Параметры:
//  ИнформацияОКонфигурации - Структура:
//    * ИдентификаторВерсии - Строка
//    * Версия - Строка
//    * URLНовоеВВерсии - Строка
//  ИдентификаторВерсии - Строка - идентификатор варианта обновления.
//
&НаСервере
Процедура ДобавитьЭлементыВариантаОбновления(ИнформацияОКонфигурации, ИдентификаторВерсии)
	
	Постфикс          = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	НомерВерсии       = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ИнформацияОКонфигурации.Версия);
	ТекущееОбновление = ВариантыОбновлений[ИдентификаторВерсии];	// см. НовыйВариантОбновления
	
	// Общая группа варианта
	ГруппаВарианта = Элементы.Добавить(
		"ГруппаВариантаОбновления" + Постфикс,
		Тип("ГруппаФормы"),
		Элементы.ГруппаВариантовОбновления);
	ГруппаВарианта.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаВарианта.Отображение         = ОтображениеОбычнойГруппы.Нет;
	ГруппаВарианта.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаВарианта.ОтображатьЗаголовок = Ложь;
	
	// Переключатель варианта
	ЭлементВарианта = Элементы.Добавить(
		"ВариантОбновления" + Постфикс,
		Тип("ПолеФормы"),
		ГруппаВарианта);
	ЭлементВарианта.Вид                  = ВидПоляФормы.ПолеПереключателя;
	ЭлементВарианта.ПутьКДанным          = "ВариантОбновления";
	ЭлементВарианта.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементВарианта.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ЭлементВарианта.Подсказка            = ТекущееОбновление.ПодсказкаОбновления;

	ЭлементВарианта.СписокВыбора.Добавить(
		ИдентификаторВерсии,
		ТекущееОбновление.ЗаголовокОбновления);
	
	// Группа дополнительной информации о варианте
	Если ИдентификаторВерсии = ИдентификаторТекущейВерсии()
		Или ИдентификаторВерсии = ИдентификаторНовойСборкиТекущейВерсии() Тогда
		
		ЕстьИсправления       = (ВариантыОбновлений[ИдентификаторТекущейВерсии()].СписокИсправлений.Количество() > 0);
		ЕстьНоваяКонфигурация = (ТекущееОбновление <> Неопределено
			И ТекущееОбновление.Конфигурация <> Неопределено
			И ТекущееОбновление.Конфигурация.ДоступноОбновление);
		ЕстьНоваяПлатформа    = (ТекущееОбновление <> Неопределено
			И ТекущееОбновление.Платформа <> Неопределено
			И ТекущееОбновление.Платформа.ДоступноОбновление);
		
		Если ЕстьНоваяКонфигурация Тогда
			Если ПустаяСтрока(ИнформацияОКонфигурации.URLНовоеВВерсии) Тогда
				ДополнительнаяИнформацияОВерсии =
					НСтр("ru = 'Доступно обновление'");
			Иначе
				ДополнительнаяИнформацияОВерсии = СтроковыеФункции.ФорматированнаяСтрока(
					"Доступно обновление. <a href=""%1"">Новое в версии</a>",
					ИнформацияОКонфигурации.URLНовоеВВерсии);
			КонецЕсли;
		ИначеЕсли ЕстьИсправления
			И ЕстьНоваяПлатформа Тогда
			
			ДополнительнаяИнформацияОВерсии =
				НСтр("ru = 'Доступна установка исправлений (патчей) и обновление платформы'");
			
		ИначеЕсли ЕстьИсправления
			И Не ЕстьНоваяПлатформа Тогда
			
			ДополнительнаяИнформацияОВерсии = НСтр("ru = 'Доступна установка исправлений (патчей)'");
			
		ИначеЕсли Не ЕстьИсправления
			И ЕстьНоваяПлатформа Тогда
			
			ДополнительнаяИнформацияОВерсии = НСтр("ru = 'Доступно обновление платформы'");
			
		Иначе
			ДополнительнаяИнформацияОВерсии = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нет доступных обновлений для текущей версии %1'"),
				НомерВерсии);
		КонецЕсли;
		
	ИначеЕсли Не ПустаяСтрока(ИнформацияОКонфигурации.URLНовоеВВерсии) Тогда
		ДополнительнаяИнформацияОВерсии = СтроковыеФункции.ФорматированнаяСтрока(
			"<a href=""%1"">Новое в версии</a>",
			ИнформацияОКонфигурации.URLНовоеВВерсии);
	Иначе
		Возврат;
	КонецЕсли;
	
	ГруппаИнформации = Элементы.Добавить(
		"ГруппаДополнительнойИнформацииВариантаОбновления" + Постфикс,
		Тип("ГруппаФормы"),
		ГруппаВарианта);
	ГруппаИнформации.Вид                  = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаИнформации.Отображение          = ОтображениеОбычнойГруппы.Нет;
	ГруппаИнформации.Группировка          = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаИнформации.ОтображатьЗаголовок  = Ложь;
	ГруппаИнформации.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	ДекорацияОтступ = Элементы.Добавить(
		"ДекорацияОтступ" + Постфикс,
		Тип("ДекорацияФормы"),
		ГруппаИнформации);
	ДекорацияОтступ.Вид    = ВидДекорацииФормы.Надпись;
	ДекорацияОтступ.Ширина = 1;
	
	ДекорацияТекст = Элементы.Добавить(
		"ИнформацияОВерсии" + Постфикс,
		Тип("ДекорацияФормы"),
		ГруппаИнформации);
	ДекорацияТекст.Вид                    = ВидДекорацииФормы.Надпись;
	ДекорацияТекст.АвтоМаксимальнаяШирина = Ложь;
	ДекорацияТекст.МаксимальнаяШирина     = 0;
	ДекорацияТекст.Заголовок              = ДополнительнаяИнформацияОВерсии;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачальныеНастройкиИОтображениеУстановкиКомпонентов()
	
	ВыбранноеОбновление       = ВариантыОбновлений[ВариантОбновления];
	ОбновлениеКомКонфигурации = ВыбранноеОбновление.Конфигурация;
	ОбновлениеПлатформы       = ВыбранноеОбновление.Платформа;
	ОбновитьКонфигурацию      = (ОбновлениеКомКонфигурации <> Неопределено
		И ОбновлениеКомКонфигурации.ДоступноОбновление);
	
	УстановитьИсправления = (?(ОбновитьКонфигурацию,
		ВыбранноеОбновление,
		ВариантыОбновлений[ИдентификаторТекущейВерсии()]).СписокИсправлений.Количество() > 0);
	
	Элементы.ГруппаАвтоматическаяУстановка.Видимость = ЭтоАдминистраторСистемы;
	Если Элементы.ГруппаАвтоматическаяУстановка.Видимость Тогда
		ЗагружатьИУстанавливатьИсправленияАвтоматически =
			Константы.ЗагружатьИУстанавливатьИсправленияАвтоматически.Получить();
		Задание = РегламентныеЗаданияСервер.ПолучитьРегламентноеЗадание(
			Метаданные.РегламентныеЗадания.ПолучениеИУстановкаИсправленийКонфигурации);
		Если Задание <> Неопределено Тогда
			Элементы.ДекорацияЗагрузкаИсправленийРасписание.Заголовок =
				ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРасписания(Задание.Расписание);
		КонецЕсли;
		Элементы.ДекорацияЗагрузкаИсправленийРасписание.Доступность =
			ЗагружатьИУстанавливатьИсправленияАвтоматически;
	КонецЕсли;
	
	ОбновитьПлатформу = ОбновлениеПлатформы <> Неопределено
		И ОбновлениеПлатформы.ДоступноОбновление
		И (ОбновитьКонфигурацию
		И ОбновлениеПлатформы.ОбязательностьУстановки < 2
		Или Не ОбновитьКонфигурацию)
		И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(
			ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие(),
			ОбновлениеПлатформы.Версия) < 0;
	
	Если ОбновитьПлатформу
		И ЭтоФайловаяИБ
		И ЭтоАдминистраторСистемы
		И Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость Тогда
		НастройкиОбновления = ПолучениеОбновленийПрограммы.НастройкиАвтоматическогоОбновления();
		РежимУстановкиПлатформы = НастройкиОбновления.РежимУстановки;
		Если Элементы.ГруппаКаталогСохраненияКомпонентаПлатформы.Видимость Тогда
			Если НастройкиОбновления.КаталогДистрибутивовПлатформы = Неопределено Тогда
				КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
				СохранятьДистрибутивыПлатформыВКаталог = Ложь;
				Элементы.КаталогСохраненияДистрибутиваКомпонентаПлатформы.Доступность = Ложь;
			Иначе
				КаталогСохраненияПлатформы = НастройкиОбновления.КаталогДистрибутивовПлатформы;
				Если ПустаяСтрока(КаталогСохраненияПлатформы) Тогда
					КаталогСохраненияПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы();
				КонецЕсли;
				СохранятьДистрибутивыПлатформыВКаталог = Истина;
				Элементы.КаталогСохраненияДистрибутиваКомпонентаПлатформы.Доступность = Истина;
			КонецЕсли;
		Иначе
			КаталогСохраненияПлатформы = КаталогДистрибутиваПлатформыПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьКраткуюИнформациюОДоступномОбновлении()
	
	ВыбранноеОбновление      = ВариантыОбновлений[ВариантОбновления];	// см. НовыйВариантОбновления
	ДополнительноеОбновление = ВариантыОбновлений[ИдентификаторТекущейВерсии()];	// см. НовыйВариантОбновления
	
	ОбновлениеКомКонф = ВыбранноеОбновление.Конфигурация;
	ОбновлениеКомПл   = ВыбранноеОбновление.Платформа;
	
	НетОбновленийВыбраннойВерсии = (ОбновлениеКомКонф = Неопределено
		Или Не ОбновлениеКомКонф.ДоступноОбновление)
		И ВыбранноеОбновление.СписокИсправлений.Количество() = 0
		И (ОбновлениеКомПл = Неопределено
		Или Не ОбновлениеКомПл.ДоступноОбновление);
	НетОбновленийДополнительнойВерсии = (ДополнительноеОбновление = Неопределено
		Или ((ДополнительноеОбновление.Конфигурация = Неопределено
		Или Не ДополнительноеОбновление.Конфигурация.ДоступноОбновление)
		И ДополнительноеОбновление.СписокИсправлений.Количество() = 0
		И (ДополнительноеОбновление.Платформа = Неопределено
		Или Не ДополнительноеОбновление.Платформа.ДоступноОбновление)));
	
	ТекстСообщения = "";
	
	Если ДоступноНесколькоВерсийДляОбновлений() Тогда
		ТекстСообщения = ТекстСообщения
			+ ВыбранноеОбновление.ЗаголовокОбновления
			+ Символы.ПС;
	КонецЕсли;
	
	Если НетОбновленийВыбраннойВерсии
		И НетОбновленийДополнительнойВерсии Тогда
		
		// Нет доступного обновления.
		Если ЭтоСценарийРабочегоОбновления(ЭтотОбъект)
			Или ПустаяСтрока(ЗаголовокНетОбновления) Тогда
			
			ТекстСообщения = ТекстСообщения
				+ НСтр("ru = 'Обновление не требуется. Установлена актуальная версия программы.'");
				
		Иначе
			ТекстСообщения = ТекстСообщения + ЗаголовокНетОбновления;
		КонецЕсли;
		
	Иначе
		
		ПрямоеОбновление = ЭтоСценарийРабочегоОбновления(ЭтотОбъект);
		Если ПрямоеОбновление Тогда
			ТекстСообщения = ТекстСообщения + НСтр("ru = 'Доступно обновление программы.'");
		Иначе
			ТекстСообщения = ТекстСообщения
				+ ?(ПустаяСтрока(ЗаголовокДоступноОбновление), "", ЗаголовокДоступноОбновление);
		КонецЕсли;
		
		// Размер обновления.
		РазмерОбновления = ?(ОбновитьКонфигурацию, ОбновлениеКомКонф.РазмерОбновления, 0)
			+ ?(УстановитьИсправления, РазмерВыбранныхИсправлений, 0)
			+ ?(ОбновитьПлатформу И ЭтоФайловаяИБ, ОбновлениеКомПл.РазмерОбновления, 0);
		Если РазмерОбновления <> 0 Тогда
			ТекстСообщения = ТекстСообщения + ?(ПрямоеОбновление, " ", Символы.ПС)
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Размер дистрибутива: <b>%1</b>'"),
					ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(РазмерОбновления));
		КонецЕсли;
		
		Если ОбновитьКонфигурацию Тогда
			Если ЭтоАдминистраторСистемы И Не ПустаяСтрока(ОбновлениеКомКонф.URLПорядокОбновления) Тогда
				ТекстСообщения = ТекстСообщения
					+ Символы.ПС
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '<b>Внимание!</b> Для продолжения необходимо ознакомиться с информацией о <a href=""%1"">порядке обновления</a>.'"),
						ОбновлениеКомКонф.URLПорядокОбновления);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЭтоАдминистраторСистемы Тогда
			ТекстСообщения = ТекстСообщения
				+ Символы.ПС
				+ НСтр("ru = 'Для обновления программы обратитесь к администратору.'");
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ГруппаОбновлениеПодробноОтступ.Видимость = Элементы.ГруппаОбновлениеКонфигурации.Видимость
		Или Элементы.ГруппаИсправления.Видимость
		Или Элементы.ГруппаОбновлениеПлатформы.Видимость;
	
	Элементы.ДекорацияСообщениеДоступноОбновление.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКнопкиКоманднойПанелиДляКомпонентов()
	
	Элементы.КнопкаНазад.Доступность  = Ложь;
	Элементы.КнопкаОтмена.Видимость   = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораВариантаОбновления Тогда
		
		Элементы.КнопкаДалее.Доступность = Истина;
		Элементы.КнопкаДалее.Видимость   = Истина;
		Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Далее >'");
		Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		
	ИначеЕсли Не ИнформацияОДоступномОбновлении.ДоступноОбновление Или Не ЭтоАдминистраторСистемы Тогда
		
		Элементы.КнопкаНазад.Доступность = ДоступноНесколькоВерсийДляОбновлений();
		Элементы.КнопкаДалее.Видимость   = Ложь;
		Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Закрыть'");
		
	ИначеЕсли Не ОбновитьКонфигурацию И Не УстановитьИсправления И Не ОбновитьПлатформу Тогда
		
		// Не выбрано обновление.
		Элементы.КнопкаНазад.Доступность = ДоступноНесколькоВерсийДляОбновлений();
		Элементы.КнопкаДалее.Доступность = Ложь;
		Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Далее >'");
		Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		
	ИначеЕсли ЭтоФайловаяИБ Тогда
		
		// Выбрано обновление, в файловом варианте нет ограничений на установку обновлений.
		Элементы.КнопкаНазад.Доступность = ДоступноНесколькоВерсийДляОбновлений();
		Элементы.КнопкаДалее.Доступность = Истина;
		Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Далее >'");
		Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		
	ИначеЕсли ОбновитьКонфигурацию Или УстановитьИсправления Тогда
		
		Элементы.КнопкаНазад.Доступность = ДоступноНесколькоВерсийДляОбновлений();
		Если ОбновитьПлатформу Тогда
			// Автоматическое обновление платформы недоступно в клиент-серверном режиме.
			Элементы.КнопкаДалее.Доступность = Ложь;
			Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Далее >'");
			Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		Иначе
			Элементы.КнопкаДалее.Доступность = Истина;
			Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Далее >'");
			Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		КонецЕсли;
		
	Иначе
		
		// Обновить платформу в клиент-серверном режиме работы.
		Элементы.КнопкаНазад.Доступность = ДоступноНесколькоВерсийДляОбновлений();
		Элементы.КнопкаДалее.Доступность = Истина;
		Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Готово'");
		Элементы.КнопкаОтмена.Заголовок  = НСтр("ru = 'Отмена'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьКомпонентКонфигурации()
	
	ВыбранноеОбновление       = ВариантыОбновлений[ВариантОбновления];
	ОбновлениеКомКонфигурации = ВыбранноеОбновление.Конфигурация;
	Если ОбновлениеКомКонфигурации = Неопределено Тогда
		
		Элементы.ГруппаОбновлениеКонфигурации.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаОбновлениеКонфигурации.Видимость = Истина;
		Если Не ОбновлениеКомКонфигурации.ДоступноОбновление Тогда
			
			// Нет обновления.
			Элементы.ОбновитьКонфигурацию.ТолькоПросмотр = Истина;
			Элементы.ДекорацияЗаголовокОбновлениеКонфигурации.Заголовок = НСтр("ru = 'Обновление конфигурации'");
			Элементы.ДекорацияНомерВерсииКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Обновление не требуется. Текущая версия конфигурации: <b>%1</b>
					|%2'"),
				ИнтернетПоддержкаПользователей.ВерсияКонфигурации(),
				ИнтернетПоддержкаПользователей.СинонимКонфигурации());
			
		Иначе
			
			Элементы.ОбновитьКонфигурацию.ТолькоПросмотр = (Не ЭтоАдминистраторСистемы
				Или ПолучениеОбновленияЗавершено(ЭтотОбъект))
				Или ЭтоСценарийПереходаНаДругуюПрограммуИлиРедакцию(ЭтотОбъект)
				Или ЗапрещеноУправлятьПризнакомОбновленияКонфигурации();
			Элементы.ДекорацияЗаголовокОбновлениеКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Обновление конфигурации, <b>%1</b>'"),
				ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
					ОбновлениеКомКонфигурации.РазмерОбновления));
			
			Если Не ПустаяСтрока(ОбновлениеКомКонфигурации.URLНовоеВВерсии) Тогда
				
				Элементы.ДекорацияНомерВерсииКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru = '<b>Версия %1</b>   <a href=""%2"">Новое в версии</a>
						|Текущая версия конфигурации: %3
						|%4'"),
					ОбновлениеКомКонфигурации.Версия,
					ОбновлениеКомКонфигурации.URLНовоеВВерсии,
					ИнтернетПоддержкаПользователей.ВерсияКонфигурации(),
					ИнтернетПоддержкаПользователей.СинонимКонфигурации());
				
			Иначе
				
				Элементы.ДекорацияНомерВерсииКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru = '<b>Версия %1</b>
						|Текущая версия конфигурации: %2
						|%3'"),
					ОбновлениеКомКонфигурации.Версия,
					ИнтернетПоддержкаПользователей.ВерсияКонфигурации(),
					ИнтернетПоддержкаПользователей.СинонимКонфигурации());
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИсправления()
	
	Исправления                    = Неопределено;
	КоличествоИсправленийДляВерсии = 0;
	ВыбранноеОбновление            = ?(ОбновитьКонфигурацию,
		ВариантыОбновлений[ВариантОбновления],
		ВариантыОбновлений[ИдентификаторТекущейВерсии()]);
	
	Если ПустаяСтрока(АдресИсправления) Тогда
		Элементы.ГруппаИсправления.Видимость = Ложь;
		Возврат;
	Иначе
		Исправления                    = ПолучитьИзВременногоХранилища(АдресИсправления);
		КоличествоИсправленийДляВерсии = ВыбранноеОбновление.СписокИсправлений.Количество();
	КонецЕсли;
	
	Элементы.ГруппаИсправления.Видимость = Истина;
	
	РазмерВыбранныхИсправлений = 0;
	Если КоличествоИсправленийДляВерсии > 0 Тогда
		
		Элементы.УстановитьИсправления.ТолькоПросмотр = Ложь;
		Если ОбновитьКонфигурацию Тогда
			
			Элементы.ДекорацияПатчиВерсияКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Для новой версии конфигурации %1</b>'"),
				ВыбранноеОбновление.Конфигурация.Версия);
			
		Иначе
			
			Элементы.ДекорацияПатчиВерсияКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Для текущей версии конфигурации %1</b>'"),
				ИнтернетПоддержкаПользователей.ВерсияКонфигурации());
			
		КонецЕсли;
		
		СписокВыбранныхИсправлений = ВыбранноеОбновление.ВыбранныеИсправления;
		Для Каждого ЭлементСписка Из СписокВыбранныхИсправлений Цикл
			СтрокаИсправление          = Исправления.Найти(ЭлементСписка.Значение, "Идентификатор");
			РазмерВыбранныхИсправлений = РазмерВыбранныхИсправлений
				+ ?(СтрокаИсправление.Отозвано, 0, СтрокаИсправление.Размер);
		КонецЦикла;
		
		Элементы.ДекорацияПатчиИсправляемыеОшибки.Видимость = Истина;
		Если СписокВыбранныхИсправлений.Количество() = 0 Тогда
			УстановитьИсправления = Ложь;
			Элементы.ДекорацияПатчиИсправляемыеОшибки.Заголовок = НСтр("ru = 'Исправляемые ошибки (не выбраны)'");
		Иначе
			УстановитьИсправления = (ОбновитьКонфигурацию Или УстановитьИсправления);
			Элементы.ДекорацияПатчиИсправляемыеОшибки.Заголовок =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исправляемые ошибки (%1 из %2)'"),
					СписокВыбранныхИсправлений.Количество(),
					КоличествоИсправленийДляВерсии);
		КонецЕсли;
		
		Если РазмерВыбранныхИсправлений = 0 Тогда
			Элементы.ДекорацияЗаголовокПатчи.Заголовок = НСтр("ru = 'Исправления (патчи)'");
		Иначе
			Элементы.ДекорацияЗаголовокПатчи.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Исправления (патчи), <b>%1</b>'"),
				ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
					РазмерВыбранныхИсправлений));
		КонецЕсли;
		
		Элементы.УстановитьИсправления.ТолькоПросмотр = (Не ЭтоАдминистраторСистемы
			Или ПолучениеОбновленияЗавершено(ЭтотОбъект));
		
	Иначе
		
		УстановитьИсправления = Ложь;
		Элементы.УстановитьИсправления.ТолькоПросмотр = Истина;
		Элементы.ДекорацияЗаголовокПатчи.Заголовок = НСтр("ru = 'Исправления (патчи)'");
		Элементы.ДекорацияПатчиВерсияКонфигурации.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Исправление ошибок для версии <b>%1</b> не требуется.'"),
			?(ОбновитьКонфигурацию,
				ВыбранноеОбновление.Конфигурация.Версия,
				ИнтернетПоддержкаПользователей.ВерсияКонфигурации()));
		Элементы.ДекорацияПатчиИсправляемыеОшибки.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьКомпонентПлатформы()
	
	ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
	ОбновлениеКомПл     = ВыбранноеОбновление.Платформа;
	
	Элементы.ДекорацияПояснениеПлатформаКлиентСервер.Видимость = Ложь;
	
	Если ОбновлениеКомПл = Неопределено Тогда
		
		Элементы.ГруппаОбновлениеПлатформы.Видимость = Ложь;
		
	ИначеЕсли Не ОбновлениеКомПл.ДоступноОбновление Тогда
		
		Элементы.ГруппаОбновлениеПлатформы.Видимость = Истина;
		
		// Нет обновления.
		Элементы.ОбновитьПлатформу.ТолькоПросмотр = Истина;
		Элементы.ДекорацияЗаголовокОбновлениеПлатформы.Заголовок =
			НСтр("ru = 'Обновление платформы 1С:Предприятие'");
		Элементы.ДекорацияНомерВерсииПлатформы.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Обновление не требуется. Текущая версия платформы 1С:Предприятие: <b>%1</b>'"),
			ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие());
		
		Элементы.ДекорацияПояснениеПлатформа.Видимость                 = Ложь;
		Элементы.ЗагрузитьДистрибутивПлатформы1.Видимость              = Ложь;
		Элементы.ГруппаПараметрыУстановкиКомпонентаПлатформа.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаОбновлениеПлатформы.Видимость = Истина;
		
		Элементы.ОбновитьПлатформу.ТолькоПросмотр = (Не ЭтоАдминистраторСистемы
			Или ОбновитьКонфигурацию И ОбновлениеКомПл.ОбязательностьУстановки = 0
			Или ПолучениеОбновленияЗавершено(ЭтотОбъект));
		
		Если ОбновитьКонфигурацию И ОбновлениеКомПл.ОбязательностьУстановки = 0 Тогда
			ОбновитьПлатформу = Истина;
		КонецЕсли;
		
		Если ЭтоФайловаяИБ Тогда
			Элементы.ДекорацияЗаголовокОбновлениеПлатформы.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Обновление платформы 1С:Предприятие, <b>%1</b>'"),
				ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
					ОбновлениеКомПл.РазмерОбновления));
		Иначе
			Элементы.ДекорацияЗаголовокОбновлениеПлатформы.Заголовок =
				НСтр("ru = 'Обновление платформы 1С:Предприятие'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ОбновлениеКомПл.URLОсобенностиПерехода) Тогда
			
			Элементы.ДекорацияНомерВерсииПлатформы.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Версия %1</b>   <a href=""%2"">Особенности перехода на новую версию платформы</a>
					|Текущая версия платформы 1С:Предприятие: %3'"),
				ОбновлениеКомПл.Версия,
				ОбновлениеКомПл.URLОсобенностиПерехода,
				ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие());
			
		Иначе
			
			Элементы.ДекорацияНомерВерсииПлатформы.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Версия %1</b>
					|Текущая версия платформы 1С:Предприятие: %2'"),
				ОбновлениеКомПл.Версия,
				ИнтернетПоддержкаПользователей.ТекущаяВерсияПлатформы1СПредприятие());
			
		КонецЕсли;
		
		Если ЭтоФайловаяИБ Тогда
			
			Элементы.ЗагрузитьДистрибутивПлатформы1.Видимость = Ложь;
			Если ОбновитьКонфигурацию Тогда
				
				Элементы.ДекорацияПояснениеПлатформа.Видимость = Истина;
				Если ОбновлениеКомПл.ОбязательностьУстановки = 0 Тогда
					Элементы.ДекорацияПояснениеПлатформа.Заголовок =
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обновление платформы является обязательным для работы с новой версией конфигурации %1.'"),
							ВыбранноеОбновление.Конфигурация.Версия);
				ИначеЕсли ОбновлениеКомПл.ОбязательностьУстановки = 1 Тогда
					Элементы.ДекорацияПояснениеПлатформа.Заголовок =
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Новая версия %1 платформы 1С:Предприятие рекомендуется для работы с новой версией конфигурации %2.'"),
							ОбновлениеКомПл.Версия,
							ВыбранноеОбновление.Конфигурация.Версия);
				Иначе
					Элементы.ДекорацияПояснениеПлатформа.Заголовок =
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обновление платформы не является обязательным для работы с новой версией конфигурации %1.'"),
							ВыбранноеОбновление.Конфигурация.Версия);
				КонецЕсли;
				
			ИначеЕсли ОбновлениеКомПл.ОбязательностьУстановки < 2 Тогда
				
				Элементы.ДекорацияПояснениеПлатформа.Видимость = Истина;
				Элементы.ДекорацияПояснениеПлатформа.Заголовок =
					НСтр("ru = 'Рекомендуется установить новую версию платформы 1С:Предприятие.'");
				
			Иначе
				
				Элементы.ДекорацияПояснениеПлатформа.Видимость = Ложь;
				
			КонецЕсли;
			
			Элементы.ГруппаПараметрыУстановкиКомпонентаПлатформа.Видимость = ЭтоАдминистраторСистемы;
			Элементы.ДекорацияПояснениеПлатформаКлиентСервер.Видимость     = Ложь;
			
		Иначе
			
			// Отображение для клиент-серверной информационной базы.
			Элементы.ДекорацияПояснениеПлатформа.Видимость = Истина;
			
			Если ОбновитьКонфигурацию Тогда
				
				Если ОбновлениеКомПл.ОбязательностьУстановки = 0 Тогда
					Элементы.ДекорацияПояснениеПлатформа.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
						НСтр("ru = 'Для работы новой версии конфигурации требуется платформа 1С:Предприятие не ниже версии <b>%1</b>.'"),
						ВыбранноеОбновление.Конфигурация.МинимальнаяВерсияПлатформы);
				ИначеЕсли ОбновлениеКомПл.ОбязательностьУстановки = 1 Тогда
					Элементы.ДекорацияПояснениеПлатформа.Заголовок =
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Новая версия %1 платформы 1С:Предприятие рекомендуется для работы с новой версией конфигурации %2.'"),
							ОбновлениеКомПл.Версия,
							ВыбранноеОбновление.Конфигурация.Версия);
				Иначе
					Элементы.ДекорацияПояснениеПлатформа.Заголовок =
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обновление платформы не является обязательным для работы с новой версией конфигурации %1.'"),
							ВыбранноеОбновление.Конфигурация.Версия);
				КонецЕсли;
				
			ИначеЕсли ОбновлениеКомПл.ОбязательностьУстановки < 2 Тогда
				
				Элементы.ДекорацияПояснениеПлатформа.Видимость = Истина;
				Элементы.ДекорацияПояснениеПлатформа.Заголовок =
					НСтр("ru = 'Рекомендуется установить новую версию платформы 1С:Предприятие.'");
				
			Иначе
				
				Элементы.ДекорацияПояснениеПлатформа.Видимость = Ложь;
				
			КонецЕсли;
			
			Элементы.ЗагрузитьДистрибутивПлатформы1.Видимость              = ЭтоАдминистраторСистемы;
			Элементы.ДекорацияПояснениеПлатформаКлиентСервер.Видимость     = ЭтоАдминистраторСистемы;
			Элементы.ГруппаПараметрыУстановкиКомпонентаПлатформа.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииФлагаКомпонентаКонфигурацияНаСервере()
	
	ОтобразитьИсправления();
	ОтобразитьКомпонентПлатформы();
	ОтобразитьКраткуюИнформациюОДоступномОбновлении();
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииИсправленияНаСервере()
	
	ОтобразитьКраткуюИнформациюОДоступномОбновлении();
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ПриВыбореИсправленийНаСервере()
	
	Если Элементы.УстановитьИсправления.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьИсправления();
	ОтобразитьКраткуюИнформациюОДоступномОбновлении();
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииФлагаПлатформыНаСервере()
	
	ОтобразитьКраткуюИнформациюОДоступномОбновлении();
	НастроитьКнопкиКоманднойПанелиДляКомпонентов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореИсправлений(Результат, ИдентификаторВыбранногоОбновления) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("СписокЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	СписокВариантовОбновлений = Новый Соответствие(ВариантыОбновлений);
	
	ВыбранноеОбновление = СписокВариантовОбновлений[ИдентификаторВыбранногоОбновления];
	ВыбранноеОбновление.ВыбранныеИсправления = Результат;
	
	ВариантыОбновлений = Новый ФиксированноеСоответствие(СписокВариантовОбновлений);
	
	ПриВыбореИсправленийНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросИсправленияПодключитьИнтернетПоддержкуПриВключенииУстановкиИсправлений(
	КодВозврата,
	ДополнительныеПараметры) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			Новый ОписаниеОповещения("ПодключениеИнтернетПоддержкиЗавершениеАвтоматическаяУстановкаИсправлений", ЭтотОбъект),
			ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеИнтернетПоддержкиЗавершениеАвтоматическаяУстановкаИсправлений(
	Результат,
	ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗагружатьИУстанавливатьИсправленияАвтоматически = Истина;
		ПолучениеОбновленийПрограммыВызовСервера.ВключитьОтключитьАвтоматическуюУстановкуИсправлений(
			ЗагружатьИУстанавливатьИсправленияАвтоматически);
		Элементы.ДекорацияЗагрузкаИсправленийРасписание.Доступность =
			ЗагружатьИУстанавливатьИсправленияАвтоматически;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРасписанияУстановкиИсправлений(Расписание, Форма) Экспорт
	
	Если Расписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПериодПовтораВТечениеДня = Расписание.ПериодПовтораВТечениеДня;
	Если ПериодПовтораВТечениеДня > 0
		И ПериодПовтораВТечениеДня < 3600 Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Интервал автоматической установки не может быть чаще, чем один раз в час.'"));
		Возврат;
	КонецЕсли;
	
	ПриИзмененииРасписанияУстановкиИсправленийНаСервере(Расписание);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Шаг получения и установки файлов обновлений.

&НаКлиенте
Процедура НачатьПолучениеОбновлений()
	
	// В процессе подготовки может быть обращение к сервису для получения информации о загружаемых файлах.
	Состояние(, , НСтр("ru = 'Подготовка к получению и установки обновлений'"));
	
	ПлатформаУстановлена = Ложь;
	Если Не (ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект))
		И Не ОбновитьКонфигурацию
		И Не УстановитьИсправления Тогда
		// Обработка случая: устанавливается только платформа и платформа уже установлена.
		ВыбранноеОбновление       = ВариантыОбновлений[ВариантОбновления];
		КаталогУстановкиПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогУстановкиПлатформы1СПредприятие(
			ВыбранноеОбновление.Платформа.Версия);
		ПлатформаУстановлена = (КаталогУстановкиПлатформы <> "");
	КонецЕсли;
	
	Если ПлатформаУстановлена Тогда
		ОтобразитьСтраницуНоваяВерсияПлатформыУжеУстановленаНаКомпьютере();
		Возврат;
		
	ИначеЕсли ПолучениеОбновленияЗавершено(ЭтотОбъект) Тогда
		
		// Если по нажатию кнопки "Назад" был выполнен переход к шагу информации
		// о доступном обновлении, тогда возврат к шагу "Установка завершена".
		ОтобразитьПолучениеОбновленийЗавершено();
		Возврат;
		
	КонецЕсли;
	
	РезультатИнициализации = ИнициализацияПолученияОбновленийНаСервере();
	
	Состояние();
	
	Если РезультатИнициализации.НачатьПолучениеИУстановкуОбновлений Тогда
		НачатьПолучениеИУстановкуОбновлений();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки()
	
	Возврат ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеФайловОбновленийНаСервере(
	Знач ОбновлениеКомКонфигурации,
	Знач ПараметрыИсправления,
	Знач ОбновлениеПлатформы,
	Знач Логин,
	Знач Пароль,
	Знач СохранитьДанныеАутентификации)
	
	Если Не ПолучениеОбновленийПрограммы.ЭтоАдминистраторСистемы() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав.'");
	КонецЕсли;
	
	Если ПараметрыИсправления = Неопределено Тогда
		ИдентификаторыПолучаемыхИсправлений = Неопределено;
		ИдентификаторыОтозванныхИсправлений = Неопределено;
	Иначе
		ИдентификаторыПолучаемыхИсправлений = ПараметрыИсправления.ИдентификаторыПолучаемыхИсправлений;
		ИдентификаторыОтозванныхИсправлений = Неопределено;
		Если Не ПустаяСтрока(ПараметрыИсправления.АдресИсправления) Тогда
			ИдентификаторыОтозванныхИсправлений = Новый Массив;
			ТаблицаИсправления = ПолучитьИзВременногоХранилища(ПараметрыИсправления.АдресИсправления);
			Строки = ТаблицаИсправления.НайтиСтроки(
				Новый Структура(?(ОбновлениеКомКонфигурации <> Неопределено, "ДляНовойВерсии", "ДляТекущейВерсии"), Истина));
			Для Каждого ТекущаяСтрока Из Строки Цикл
				Если ТекущаяСтрока.Отозвано Тогда
					ИдентификаторыОтозванныхИсправлений.Добавить(Строка(ТекущаяСтрока.Идентификатор));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если СохранитьДанныеАутентификации Тогда
		
		Результат = ПолучениеОбновленийПрограммы.ОписаниеФайловОбновлений(
			ОбновлениеКомКонфигурации,
			ИдентификаторыПолучаемыхИсправлений,
			ОбновлениеПлатформы,
			Логин,
			Пароль);
		
		Если ПустаяСтрока(Результат.ИмяОшибки) Тогда
			СохранитьДанныеАутентификации(Новый Структура("Логин, Пароль", Логин, Пароль));
		КонецЕсли;
		
	Иначе
		
		УстановитьПривилегированныйРежим(Истина);
		СохраненныеДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если СохраненныеДанныеАутентификации = Неопределено Тогда
			Результат = Новый Структура;
			Результат.Вставить("ИмяОшибки", "НеЗаполненыДанныеАутентификации");
			Возврат Результат;
		КонецЕсли;
		
		Результат = ПолучениеОбновленийПрограммы.ОписаниеФайловОбновлений(
			ОбновлениеКомКонфигурации,
			ИдентификаторыПолучаемыхИсправлений,
			ОбновлениеПлатформы,
			СохраненныеДанныеАутентификации.Логин,
			СохраненныеДанныеАутентификации.Пароль);
		
	КонецЕсли;
	
	Результат.Вставить("ОтозванныеИсправления", ИдентификаторыОтозванныхИсправлений);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьДанныеАутентификации(Знач ДанныеАутентификации)
	
	// Проверка права записи данных.
	Если Не ИнтернетПоддержкаПользователей.ПравоЗаписиПараметровИПП() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для записи данных аутентификации Интернет-поддержки.'");
	КонецЕсли;
	
	// Запись данных.
	УстановитьПривилегированныйРежим(Истина);
	ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(ДанныеАутентификации);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеИУстановкуОбновлений()
	
	Если ЭтоФайловаяИБ Тогда
		
		// Старт фонового задания был выполнен при инициализации получения обновлений.
		
		Если КонтекстОбновления <> Неопределено Тогда
			
			// При возобновлении отобразить текущее состояние задания.
			ОтобразитьСостояниеКонтекстаОбновления();
			
			// Очистить результат получения и установки обновлений.
			// Будет вновь получен из фонового задания.
			КонтекстОбновления = Неопределено;
			
		КонецЕсли;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания           = Ложь;
		ПараметрыОжидания.Интервал                       = 1;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения     = Истина;
		ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения(
			"ПриОбновленииПрогрессаПолученияИУстановкиОбновлений",
			ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения(
				"ПриЗавершенииПолученияИУстановкиОбновлений",
				ЭтотОбъект),
			ПараметрыОжидания);
		
	Иначе
		
		// В клиент-серверном режиме выполняется только получение файлов
		// обновления конфигурации и исправлений (без платформы).
		// Получение файлов выполняется синхронно.
		Если КонтекстОбновления = Неопределено Тогда
			
			ПараметрыСозданияКонтекста = Новый Структура;
			ПараметрыСозданияКонтекста.Вставить("ОписаниеФайловОбновлений", ОписаниеФайловОбновлений);
			ПараметрыСозданияКонтекста.Вставить("КонтекстОбновления"      , КонтекстОбновления);
			ПараметрыСозданияКонтекста.Вставить("ОбновитьКонфигурацию"    , ОбновитьКонфигурацию);
			ПараметрыСозданияКонтекста.Вставить("УстановитьИсправления"   , УстановитьИсправления);
			ПараметрыСозданияКонтекста.Вставить("ОбновитьПлатформу"       , Ложь);
			ПараметрыСозданияКонтекста.Вставить("ЭтоWindowsКлиент"        , ОбщегоНазначенияКлиент.ЭтоWindowsКлиент());
			КонтекстОбновления = ПолучениеОбновленийПрограммыКлиентСервер.НовыйКонтекстПолученияИУстановкиОбновлений(
				ПараметрыСозданияКонтекста);
			
		Иначе
			
			// Сброс состояния ошибки.
			КонтекстОбновления.ИмяОшибки          = "";
			КонтекстОбновления.Сообщение          = "";
			КонтекстОбновления.ИнформацияОбОшибке = "";
			
		КонецЕсли;
		
		// Инициализация страницы получения обновлений был выполнен при инициализации получения обновлений.
		
		ОтобразитьСостояниеКонтекстаОбновления();
		
		// Выполняется в обработчике ожидания с минимальным интервалом,
		// чтобы мог быть обновлен интерфейс помощника и отобразилась
		// страница шага получения обновления.
		ПодключитьОбработчикОжидания("ИтерацияПолученияФайловОбновления", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИтерацияПолученияФайловОбновления()
	
	ИндексТекущегоОбновления  = КонтекстОбновления.ИндексТекущегоОбновления;
	ИндексТекущегоИсправления = КонтекстОбновления.ИндексТекущегоИсправления;
	ОбъемПолученныхФайлов = КонтекстОбновления.ОбъемПолученныхФайлов;
	
	Для Итератор = ИндексТекущегоОбновления По КонтекстОбновления.ОбновленияКонфигурации.ВГраница() Цикл
		
		ТекОбновление = КонтекстОбновления.ОбновленияКонфигурации[Итератор];
		
		ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Получение обновления конфигурации.
					|URL: %1;
					|Размер: %2;
					|Формат файла обновления: %3;
					|Контрольная сумма файла обновления: %4;
					|Каталог дистрибутива: %5;
					|Выполнить обработчики обновления: %6;
					|Имя файла обновления (cfu): %7.'"),
				ТекОбновление.URLФайлаОбновления,
				ТекОбновление.РазмерФайла,
				ТекОбновление.ФорматФайлаОбновления,
				ТекОбновление.КонтрольнаяСумма,
				ТекОбновление.КаталогДистрибутива,
				ТекОбновление.ПрименитьОбработчикиОбновления,
				ТекОбновление.ОтносительныйПутьCFUФайла));
		
		Если Не ПолучениеОбновленийПрограммыКлиентСервер.ОбновлениеКонфигурацииПолучено(ТекОбновление, КонтекстОбновления) Тогда
			
			КонтекстОбновления.ТекущееДействие =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Получение файла %1 из %2'"),
					Строка(Итератор + 1),
					КонтекстОбновления.КоличествоФайлов);
			КонтекстОбновления.Прогресс = 100 * (ОбъемПолученныхФайлов / КонтекстОбновления.ОбъемФайлов);
			ОтобразитьСостояниеКонтекстаОбновления();
			
			КонтекстОбновления.ИндексТекущегоОбновления = Итератор;
			КонтекстОбновления.ОбъемПолученныхФайлов    = ОбъемПолученныхФайлов;
			
			ПодключитьОбработчикОжидания("ЗагрузитьОбновлениеКонфигурации", 0.1, Истина);
			Возврат;
			
		Иначе
			
			ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Обновление конфигурации уже было получено ранее.'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Если не произошло возврата, все файлы обновления конфигурации уже загружены,
	// далее загружаются файлы исправлений.
	Для Итератор = ИндексТекущегоИсправления По КонтекстОбновления.Исправления.ВГраница() Цикл
		
		КонтекстОбновления.ТекущееДействие =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Получение файла %1 из %2'"),
				Строка(КонтекстОбновления.КоличествоОбновленийКонфигурации + Итератор + 1),
				КонтекстОбновления.КоличествоФайлов);
		КонтекстОбновления.Прогресс = 100 * (ОбъемПолученныхФайлов / КонтекстОбновления.ОбъемФайлов);
		ОтобразитьСостояниеКонтекстаОбновления();
		
		КонтекстОбновления.ИндексТекущегоИсправления = Итератор;
		КонтекстОбновления.ОбъемПолученныхФайлов     = ОбъемПолученныхФайлов;
		
		ПодключитьОбработчикОжидания("ЗагрузитьИсправление", 0.1, Истина);
		Возврат;
		
	КонецЦикла;
	
	// Если не произошел переход на новую итерацию загрузки файлов,
	// тогда все файлы успешно получены.
	Если ОбновитьКонфигурацию Тогда
		
		ОтобразитьПолучениеОбновленийЗавершено(Истина);
		
	Иначе
		
		// Если устанавливаются только исправления,
		// тогда установить исправления.
		КонтекстОбновления.ТекущееДействие = НСтр("ru = 'Установка исправлений'");
		КонтекстОбновления.Прогресс = 100;
		ОтобразитьСостояниеКонтекстаОбновления();
		
		ПодключитьОбработчикОжидания("УстановитьУдалитьИсправления", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьУдалитьИсправления()
	
	ИсправленияДляУстановки = Новый Массив;
	Для Каждого ТекИсправление Из КонтекстОбновления.Исправления Цикл
		ОписаниеИсправления = Новый Структура;
		ОписаниеИсправления.Вставить("Имя",           ТекИсправление.Имя);
		ОписаниеИсправления.Вставить("АдресФайла",    ТекИсправление.АдресФайла);
		ОписаниеИсправления.Вставить("Идентификатор", ТекИсправление.Идентификатор);
		ИсправленияДляУстановки.Добавить(ОписаниеИсправления);
	КонецЦикла;
	
	РезультатУстановки = УстановитьУдалитьИсправленияНаСервере(
		ИсправленияДляУстановки,
		КонтекстОбновления.ОтозванныеИсправления);
	
	Вложения = Новый Массив();
	
	Если Не РезультатУстановки.Ошибка Тогда
		ОтобразитьПолучениеОбновленийЗавершено(Истина);
	Иначе
		
		ЗаполнитьЗначенияСвойств(КонтекстОбновления, РезультатУстановки);
		КонтекстОбновления.ИмяОшибки = "ОшибкаУстановкиИсправлений";
		
		Если Не ПустаяСтрока(РезультатУстановки.СообщениеТехподдержке) Тогда
			Вложения.Добавить(
				Новый Структура(
					"Представление, ВидДанных, Данные",
					НСтр("ru = 'Информация об ошибках.txt'"),
					"Текст",
					РезультатУстановки.СообщениеТехподдержке));
		КонецЕсли;
		
		ОтобразитьВнутреннююОшибку(
			КонтекстОбновления.Сообщение,
			Неопределено,
			КонтекстОбновления.ИнформацияОбОшибке);
			
	КонецЕсли;
	
	ВложенияТехподдержке = Новый ФиксированныйМассив(Вложения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьУдалитьИсправленияНаСервере(Знач ИсправленияДляУстановки, Знач ОтозванныеИсправления)
	
	Если Не ПолучениеОбновленийПрограммы.ЭтоАдминистраторСистемы() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для установки исправлений (патчей).'");
	КонецЕсли;
	
	Возврат ПолучениеОбновленийПрограммы.УстановкаИУдалениеИсправлений(
		ИсправленияДляУстановки,
		ОтозванныеИсправления);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьОбновлениеКонфигурации()
	
	Обновление = КонтекстОбновления.ОбновленияКонфигурации[КонтекстОбновления.ИндексТекущегоОбновления];
	ПолучениеОбновленийПрограммыКлиентСервер.СоздатьКаталогиДляПолученияОбновления(Обновление, КонтекстОбновления);
	Если Не ПустаяСтрока(КонтекстОбновления.ИмяОшибки) Тогда
		ОтобразитьВнутреннююОшибку(
			КонтекстОбновления.Сообщение,
			Неопределено,
			КонтекстОбновления.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка файла обновления конфигурации %1 (выполняется на сервере информационной базы).'"),
			Обновление.ИмяПолученногоФайла));
	
	РезультатПолучения = ПолучитьФайлОбновленияНаСервере(Обновление.URLФайлаОбновления);
	
	Если ПустаяСтрока(РезультатПолучения.КодОшибки) Тогда
		ПолучаемыеФайлы = Новый Массив;
		ПолучаемыеФайлы.Добавить(
			Новый ОписаниеПередаваемогоФайла(Обновление.ИмяПолученногоФайла, РезультатПолучения.АдресФайла));
		НачатьПолучениеФайловССервера(
			Новый ОписаниеОповещения("ПриПолученииФайлаОбновленияКонфигурации",
				ЭтотОбъект,
				РезультатПолучения),
			ПолучаемыеФайлы);
	Иначе
		ПриПолученииФайлаОбновленияКонфигурации(Неопределено, РезультатПолучения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИсправление()
	
	Исправление = КонтекстОбновления.Исправления[КонтекстОбновления.ИндексТекущегоИсправления];
	ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Получение файла исправления (патча).
				|URL: %1;
				|Идентификатор: %2;
				|Размер: %3;
				|Локальный файл: %4.'"),
			Исправление.URLФайла,
			Исправление.Идентификатор,
			Исправление.Размер,
			Исправление.ИмяПолученногоФайла));
	
	Если Исправление.Получено Тогда

		// Загрузить файл исправления во временное хранилище для последующей установки.
		ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Исправление (патч) уже было получено ранее.'"));
		ОбработчикПриПомещении = Новый ОписаниеОповещения("ПриПомещенииФайлаИсправления", ЭтотОбъект, Исправление);
		
		НачатьПомещениеФайлаНаСервер(
			ОбработчикПриПомещении,
			,
			,
			,
			Исправление.ИмяПолученногоФайла,
			?(ОбновитьКонфигурацию,
				Новый УникальныйИдентификатор,
				УникальныйИдентификатор));
		
		Возврат;
		
	КонецЕсли;
	
	// Если устанавливаются только исправления, тогда файл должен
	// остаться во временном хранилище формы, поэтому передается
	// идентификатор формы.
	// При обновлении конфигурации файл должен быть доступен во временном хранилище
	// и после закрытия формы, т.к. файлы будут переданы в программный
	// интерфейс установщика обновлений, поэтому используется Новый УникальныйИдентификатор.
	РезультатПолучения = ПолучитьФайлИсправленияНаСервере(
		Исправление.URLФайла,
		Исправление.Идентификатор,
		?(ОбновитьКонфигурацию,
			Новый УникальныйИдентификатор,
			УникальныйИдентификатор));
	
	Если ПустаяСтрока(РезультатПолучения.КодОшибки) Тогда
		
		ПолучаемыеФайлы = Новый Массив;
		ПолучаемыеФайлы.Добавить(
			Новый ОписаниеПередаваемогоФайла(Исправление.ИмяПолученногоФайла, РезультатПолучения.АдресФайла));
		НачатьПолучениеФайловССервера(
			Новый ОписаниеОповещения("ПриПолученииФайлаИсправления",
				ЭтотОбъект,
				РезультатПолучения),
			ПолучаемыеФайлы);
		
	Иначе
		
		ПриПолученииФайлаИсправления(Неопределено, РезультатПолучения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПомещенииФайлаИсправления(ОписаниеФайла, ДополнительныеПараметры) Экспорт
	
	Исправление = КонтекстОбновления.Исправления[КонтекстОбновления.ИндексТекущегоИсправления];
	Исправление.АдресФайла = ОписаниеФайла.Адрес;
	
	// Увеличить счетчик файлов.
	КонтекстОбновления.ИндексТекущегоИсправления = КонтекстОбновления.ИндексТекущегоИсправления + 1;
	КонтекстОбновления.ОбъемПолученныхФайлов     = КонтекстОбновления.ОбъемПолученныхФайлов + Исправление.Размер;
	
	// Перейти на следующую итерацию получения обновления.
	ПодключитьОбработчикОжидания("ИтерацияПолученияФайловОбновления", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПолученииФайлаОбновленияКонфигурации(ПолученныеФайлы, РезультатПолучения) Экспорт
	
	Обновление = КонтекстОбновления.ОбновленияКонфигурации[КонтекстОбновления.ИндексТекущегоОбновления];
	Если ПустаяСтрока(РезультатПолучения.КодОшибки) Тогда
		
		ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Файл обновления успешно загружен.'"));
		
		ПолучениеОбновленийПрограммыКлиентСервер.ЗавершитьПолучениеОбновления(Обновление, КонтекстОбновления);
		Если Не ПустаяСтрока(КонтекстОбновления.ИмяОшибки) Тогда
			ОтобразитьВнутреннююОшибку(
				КонтекстОбновления.Сообщение,
				Неопределено,
				КонтекстОбновления.ИнформацияОбОшибке);
			Возврат;
		КонецЕсли;
		
		// Увеличить счетчик файлов.
		КонтекстОбновления.ИндексТекущегоОбновления = КонтекстОбновления.ИндексТекущегоОбновления + 1;
		КонтекстОбновления.ОбъемПолученныхФайлов    = КонтекстОбновления.ОбъемПолученныхФайлов + Обновление.РазмерФайла;
		
		// Перейти на следующую итерацию получения обновления.
		ПодключитьОбработчикОжидания("ИтерацияПолученияФайловОбновления", 0.1, Истина);
		Возврат;
		
	КонецЕсли;
	
	// При ошибке получения файла.
	Если РезультатПолучения.КодОшибки = "НеЗаполненыДанныеАутентификации" Тогда
		ОтобразитьПодключениеКПорталу();
		Возврат;
	КонецЕсли;
	
	// При ошибке получения файла.
	
	СообщениеЖурнала =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при получении файла дистрибутива конфигурации (%1). %2'"),
			Обновление.URLФайлаОбновления,
			РезультатПолучения.ИнформацияОбОшибке);
	ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
	
	КонтекстОбновления.ИмяОшибки = РезультатПолучения.КодОшибки;
	КонтекстОбновления.ИнформацияОбОшибке = СообщениеЖурнала;
	КонтекстОбновления.Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка при получении файла дистрибутива конфигурации. %1'"),
		РезультатПолучения.СообщениеОбОшибке);
	
	Если ЭтоОшибкаСоединения(КонтекстОбновления.ИмяОшибки) Тогда
		ОтобразитьОшибкуПодключения(
			КонтекстОбновления.Сообщение,
			СообщениеЖурнала);
	Иначе
		ОтобразитьВнутреннююОшибку(
			КонтекстОбновления.Сообщение,
			Неопределено,
			СообщениеЖурнала);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПолученииФайлаИсправления(ПолученныеФайлы, РезультатПолучения) Экспорт
	
	Исправление = КонтекстОбновления.Исправления[КонтекстОбновления.ИндексТекущегоИсправления];
	Если ПустаяСтрока(РезультатПолучения.КодОшибки) Тогда
		
		ПолучениеОбновленийПрограммыКлиент.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Файл исправления (патча) успешно загружен.'"));
		
		Исправление.АдресФайла = РезультатПолучения.АдресФайла;
		
		// Увеличить счетчик файлов.
		КонтекстОбновления.ИндексТекущегоИсправления = КонтекстОбновления.ИндексТекущегоИсправления + 1;
		КонтекстОбновления.ОбъемПолученныхФайлов     = КонтекстОбновления.ОбъемПолученныхФайлов + Исправление.Размер;
		
		// Перейти на следующую итерацию получения обновления.
		ПодключитьОбработчикОжидания("ИтерацияПолученияФайловОбновления", 0.1, Истина);
		Возврат;
		
	КонецЕсли;
	
	// При ошибке получения файла.
	Если РезультатПолучения.КодОшибки = "НеЗаполненыДанныеАутентификации" Тогда
		ОтобразитьПодключениеКПорталу();
		Возврат;
	КонецЕсли;
	
	// При ошибке получения файла.
	СообщениеЖурнала =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при получении файла исправления (патча) (%1). %2'"),
			Исправление.URLФайла,
			РезультатПолучения.ПодробноеОписаниеОшибки);
	ПолучениеОбновленийПрограммыВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
	
	КонтекстОбновления.ИмяОшибки = РезультатПолучения.КодОшибки;
	КонтекстОбновления.ИнформацияОбОшибке = СообщениеЖурнала;
	КонтекстОбновления.Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка при получении файла исправления (патча). %1'"),
		РезультатПолучения.КраткоеОписаниеОшибки);
	
	Если ЭтоОшибкаСоединения(КонтекстОбновления.ИмяОшибки) Тогда
		ОтобразитьОшибкуПодключения(
			КонтекстОбновления.Сообщение,
			СообщениеЖурнала);
	Иначе
		ОтобразитьВнутреннююОшибку(
			КонтекстОбновления.Сообщение,
			Неопределено,
			СообщениеЖурнала);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьФайлОбновленияНаСервере(Знач URLФайла)
	
	ИнтернетПоддержкаПользователей.ПроверитьURL(URLФайла);
	
	Если Не ПолучениеОбновленийПрограммы.ЭтоАдминистраторСистемы() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав.'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАутентификации = Неопределено Тогда
		РезультатПолучения = Новый Структура;
		РезультатПолучения.Вставить("КодОшибки", "НеЗаполненыДанныеАутентификации");
		Возврат РезультатПолучения;
	КонецЕсли;
	
	ДопПараметры = Новый Структура("ФорматОтвета, Таймаут", 2, 43200);
	РезультатПолучения = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLФайла,
		ДанныеАутентификации.Логин,
		ДанныеАутентификации.Пароль,
		ДопПараметры);
	
	Если ПустаяСтрока(РезультатПолучения.КодОшибки) Тогда
		АдресФайла = ПоместитьВоВременноеХранилище(РезультатПолучения.Содержимое);
		РезультатПолучения.Вставить("АдресФайла", АдресФайла);
		РезультатПолучения.Удалить("Содержимое");
	КонецЕсли;
	
	Возврат РезультатПолучения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьФайлИсправленияНаСервере(Знач URLФайла, Знач ИдентификаторИсправления, Знач ИдентификаторФормы)
	
	ИнтернетПоддержкаПользователей.ПроверитьURL(URLФайла);
	
	Если Не ПолучениеОбновленийПрограммы.ЭтоАдминистраторСистемы() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав.'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАутентификации = Неопределено Тогда
		РезультатПолучения = Новый Структура;
		РезультатПолучения.Вставить("КодОшибки", "НеЗаполненыДанныеАутентификации");
		Возврат РезультатПолучения;
	КонецЕсли;
	
	РезультатПолучения = ПолучениеОбновленийПрограммы.ЗагрузитьФайлИсправления(
		URLФайла,
		ИдентификаторИсправления,
		ДанныеАутентификации);
	
	Если РезультатПолучения.Ошибка Тогда
		РезультатПолучения.Вставить("КодОшибки", "PatchFileDownloadError");
	Иначе
		РезультатПолучения.Вставить("КодОшибки", "");
		АдресФайла = ПоместитьВоВременноеХранилище(РезультатПолучения.Содержимое, ИдентификаторФормы);
		РезультатПолучения.Вставить("АдресФайла", АдресФайла);
		РезультатПолучения.Удалить("Содержимое");
	КонецЕсли;
	
	Возврат РезультатПолучения;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьСтраницуПолучениеОбновления()
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		// Сценарий перехода на новую версию платформы.
		Элементы.ДекорацияПолучениеДистрибутива.Заголовок =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выполняется получение файла дистрибутива платформы 1С:Предприятие (%1).'") + " "
					+ НСтр("ru = 'Получение файла может занять от нескольких минут до нескольких часов в зависимости от размера обновления,'")
					+ " "
					+ НСтр("ru = 'скорости подключения к Интернету и производительности компьютера.'"),
				РазмерОбновленияСтр(ЭтотОбъект));
	Иначе
		// Другие сценарии.
		Элементы.ДекорацияПолучениеДистрибутива.Заголовок =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выполняется получение файлов обновления (%1). Получение файлов может занять от нескольких минут до'")
					+ " " + НСтр("ru = 'нескольких часов в зависимости от размера обновления,'")
					+ " " + НСтр("ru = 'скорости подключения к Интернету и производительности компьютера.'"),
				РазмерОбновленияСтр(ЭтотОбъект));
	КонецЕсли;
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаПолучениеИУстановка);
	Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаПолучение;
	
	Элементы.КнопкаНазад.Доступность  = Ложь;
	Элементы.КнопкаДалее.Доступность  = Ложь;
	Элементы.КнопкаОтмена.Видимость   = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	Элементы.КнопкаДалее.Заголовок  = НСтр("ru = 'Далее >'");
	Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена'");
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеОбновленийВФоновомРежиме()
	
	// Эта ветвь сценария может отрабатывать только в файловом режиме работы.
	Если Не ЭтоФайловаяИБ Или Не ЭтоАдминистраторСистемы Тогда
		ВызватьИсключение
			НСтр("ru = 'Использование обновления платформы 1С:Предприятие недоступно в текущем режиме работы.'");
	КонецЕсли;
	
	// Сохранение настроек установки платформы.
	НастройкиОбновления = ПолучениеОбновленийПрограммы.НастройкиАвтоматическогоОбновления();
	НастройкиОбновления.РежимУстановки = РежимУстановкиПлатформы;
	Если Не СохранятьДистрибутивыПлатформыВКаталог Тогда
		НастройкиОбновления.КаталогДистрибутивовПлатформы = Неопределено;
	ИначеЕсли КаталогСохраненияПлатформы = ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы() Тогда
		НастройкиОбновления.КаталогДистрибутивовПлатформы = "";
	Иначе
		НастройкиОбновления.КаталогДистрибутивовПлатформы = КаталогСохраненияПлатформы;
	КонецЕсли;
	ПолучениеОбновленийПрограммы.ЗаписатьНастройкиАвтоматическогоОбновления(
		НастройкиОбновления);
	
	// Запуск фонового задания для получения и установки обновлений.
	ПараметрыПроцедуры = Новый Структура;
	Если Не СохранятьДистрибутивыПлатформыВКаталог Тогда
		ПараметрыПроцедуры.Вставить("КаталогХраненияДистрибутивовПлатформы", Неопределено);
	Иначе
		ПараметрыПроцедуры.Вставить("КаталогХраненияДистрибутивовПлатформы", КаталогСохраненияПлатформы);
	КонецЕсли;
	ПараметрыПроцедуры.Вставить("РежимУстановкиПлатформы", РежимУстановкиПлатформы);
	
	ПараметрыПроцедуры.Вставить("ОписаниеФайловОбновлений", ОписаниеФайловОбновлений);
	ПараметрыПроцедуры.Вставить("КонтекстОбновления"      , КонтекстОбновления);
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
		ПараметрыПроцедуры.Вставить("ОбновитьКонфигурацию" , Ложь);
		ПараметрыПроцедуры.Вставить("УстановитьИсправления", Ложь);
		ПараметрыПроцедуры.Вставить("ОбновитьПлатформу"    , Истина);
	Иначе
		ПараметрыПроцедуры.Вставить("ОбновитьКонфигурацию" , ОбновитьКонфигурацию);
		ПараметрыПроцедуры.Вставить("УстановитьИсправления", УстановитьИсправления);
		ПараметрыПроцедуры.Вставить("ОбновитьПлатформу"    , ОбновитьПлатформу);
	КонецЕсли;
	ПараметрыПроцедуры.Вставить("ЭтоWindowsКлиент", ОбщегоНазначения.ЭтоWindowsКлиент());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне      = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение   = 0;
	ПараметрыВыполнения.КлючФоновогоЗадания = "ПолучениеИУстановкаОбновленийПрограммы_"
		+ Строка(Новый УникальныйИдентификатор);
	
	Попытка
		
		РезультатНачалаЗадания = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			"ПолучениеОбновленийПрограммы.ЗагрузитьИУстановитьОбновленияВФоновомРежиме",
			ПараметрыПроцедуры);
		Если РезультатНачалаЗадания.Статус = "Отменено" Тогда
			ВызватьИсключение НСтр("ru = 'Задание отменено администратором.'");
		ИначеЕсли РезультатНачалаЗадания.Статус = "Ошибка" Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить фоновое задание. %1'"),
				РезультатНачалаЗадания.ПодробноеПредставлениеОшибки);
		КонецЕсли;
		
	Исключение
		
		ИнформацияОбОшибке          = ИнформацияОбОшибке();
		СообщениеЖурналаРегистрации =
			НСтр("ru = 'Ошибка запуска задания.'") + Символы.ПС
				+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ПолучениеОбновленийПрограммы.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурналаРегистрации);
		
		ОтобразитьВнутреннююОшибку(
			НСтр("ru = 'Ошибка начала задания.'")
				+ Символы.ПС
				+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке),
			Неопределено,
			СообщениеЖурналаРегистрации);
		Возврат;
		
	КонецПопытки;
	
	ДлительнаяОперация = РезультатНачалаЗадания;
	
	Если КонтекстОбновления = Неопределено Тогда
		Прогресс = 0;
		ТекущееДействие = НСтр("ru = 'Подготовка к получению обновления...'");
	КонецЕсли;
	
	// Отобразить страницу Получение и установка.
	ИнициализироватьСтраницуПолучениеОбновления();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСостояниеКонтекстаОбновления()
	
	Если КонтекстОбновления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееДействие = КонтекстОбновления.ТекущееДействие;
	Прогресс        = КонтекстОбновления.Прогресс;
	
	Если Не КонтекстОбновления.ФайлыОбновленияПолучены Тогда
		
		Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаПолучение;
		
	ИначеЕсли КонтекстОбновления.ОбновитьПлатформу Тогда
		// Если файлы обновления получены и необходимо установить платформу,
		// тогда отображается состояние установки платформы 1С:Предприятие.
		
		Если РежимУстановкиПлатформы = 0 Тогда
			Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаУстановкаТихий;
			Элементы.КнопкаОтмена.Доступность                   = Ложь;
		Иначе
			Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаУстановкаИнтерактивный;
			Элементы.КнопкаОтмена.Доступность                   = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОбновленииПрогрессаПолученияИУстановкиОбновлений(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс = Неопределено
		Или Прогресс.Прогресс = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПрогресса = Прогресс.Прогресс;
	
	ОписательСостояния = СтруктураПрогресса.ДополнительныеПараметры;
	Если ОписательСостояния = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстОбновления = ОписательСостояния.ДопПараметры;
	Если КонтекстОбновления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьСостояниеКонтекстаОбновления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПолученияИУстановкиОбновлений(Результат, ДополнительныеПараметры) Экспорт
	
	// Длительная операция была отменена
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписательСостояния = СостояниеЗадания(Результат);
	Если ОписательСостояния = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстОбновления = ОписательСостояния.ДопПараметры;
	Если КонтекстОбновления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Ошибка, Установка, УстановкаОтменена, Завершено.
	Если ОписательСостояния.КодСостояния = "Ошибка" Тогда
		
		Вложения                    = Новый Массив();
		СообщениеЖурналаРегистрации = КонтекстОбновления.ИнформацияОбОшибке;
		
		Если ЭтоОшибкаСоединения(КонтекстОбновления.ИмяОшибки) Тогда
			ОтобразитьОшибкуПодключения(
				КонтекстОбновления.Сообщение,
				СообщениеЖурналаРегистрации);
		ИначеЕсли КонтекстОбновления.ИмяОшибки = "НеЗаполненыДанныеАутентификации" Тогда
			ОтобразитьПодключениеКПорталу();
		ИначеЕсли РежимУстановкиПлатформы = 0
			И ПолучениеОбновленийПрограммыКлиентСервер.ЭтоКодВозвратаОграниченияСистемныхПолитик(
				КонтекстОбновления.КодВозвратаПрограммыУстановки) Тогда
			ОтобразитьВнутреннююОшибку(
				КонтекстОбновления.Сообщение,
				НСтр("ru = 'Установить с ручными настройками >'"),
				СообщениеЖурналаРегистрации);
		Иначе
			ОтобразитьВнутреннююОшибку(
				КонтекстОбновления.Сообщение,
				Неопределено,
				СообщениеЖурналаРегистрации);
		КонецЕсли;
		
		Если Не ПустаяСтрока(КонтекстОбновления.СообщениеТехподдержке) Тогда
			Вложения.Добавить(
				Новый Структура(
					"Представление, ВидДанных, Данные",
					НСтр("ru = 'Информация об ошибках.txt'"),
					"Текст",
					КонтекстОбновления.СообщениеТехподдержке));
		КонецЕсли;
		
		ВложенияТехподдержке = Новый ФиксированныйМассив(Вложения);
		
	ИначеЕсли ОписательСостояния.КодСостояния = "УстановкаПлатформыОтменена" Тогда
		ОтобразитьИнформациюОДоступномОбновлении();
	ИначеЕсли ОписательСостояния.КодСостояния = "Завершено" Тогда
		ОтобразитьПолучениеОбновленийЗавершено(Истина);
	КонецЕсли;
	
	ДлительнаяОперация = Неопределено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СостояниеЗадания(Знач РезультатДлительнойОперации)
	
	Результат = Новый Структура("КодСостояния, ДопПараметры", "", Неопределено);
	
	Если РезультатДлительнойОперации.Статус = "Выполнено" Тогда
		
		Возврат ПолучитьИзВременногоХранилища(РезультатДлительнойОперации.АдресРезультата);
		
	ИначеЕсли РезультатДлительнойОперации.Статус = "Ошибка" Тогда
		
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при проверке состояния задания. %1'"),
			РезультатДлительнойОперации.ПодробноеПредставлениеОшибки);
		ПолучениеОбновленийПрограммы.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		
		Результат.КодСостояния = "Ошибка";
		ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при проверке состояния задания. %1'"),
			РезультатДлительнойОперации.КраткоеПредставлениеОшибки);
		
		Результат.ДопПараметры = Новый Структура(
			"Сообщение, ИнформацияОбОшибке, ИмяОшибки",
			ТекстСообщенияПользователю,
			СообщениеЖурнала,
			"НеУдалосьПроверитьСостояниеЗадания");
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(Знач ИДЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИДЗадания);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерОбновленияСтр(ФормаПомощника)
	
	ВыбранноеОбновление = ФормаПомощника.ВариантыОбновлений[ФормаПомощника.ВариантОбновления];
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ФормаПомощника)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ФормаПомощника) Тогда
		
		Возврат ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
			ВыбранноеОбновление.Платформа.РазмерОбновления);
		
	Иначе
		
		Возврат ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРазмераФайла(
			?(ФормаПомощника.ОбновитьКонфигурацию,
				ВыбранноеОбновление.Конфигурация.РазмерОбновления,
				0)
			+ ?(ФормаПомощника.УстановитьИсправления,
				ФормаПомощника.РазмерВыбранныхИсправлений,
				0)
			+ ?(ФормаПомощника.ОбновитьПлатформу,
				ВыбранноеОбновление.Платформа.РазмерОбновления,
				0));
		
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Остальные шаги помощника.

&НаСервере
Процедура ОтобразитьВнутреннююОшибку(
	Знач СообщениеОбОшибке,
	Знач ЗаголовокКнопкиДалее = Неопределено,
	Знач ПодробноеОписаниеВнутрОшибки = "")
	
	ОтобразитьСообщениеСервиса(СообщениеОбОшибке, ЗаголовокКнопкиДалее, ПодробноеОписаниеВнутрОшибки);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьПодключениеКПорталу(Знач Сообщение = "")
	
	Если Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПодключениеКПорталу Тогда
		ЗаполнитьДанныеДляПодключения();
	КонецЕсли;
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаПодключениеКПорталу);
	Если ПустаяСтрока(Сообщение) Тогда
		Элементы.ПанельОшибкаПодключения.Видимость = Ложь;
	Иначе
		Элементы.ПанельОшибкаПодключения.Видимость                       = Истина;
		Элементы.ДекорацияСообщениеОбОшибкеПолученияОбновления.Заголовок = Сообщение;
	КонецЕсли;
	
	Элементы.КнопкаНазад.Доступность  = Истина;
	Элементы.КнопкаДалее.Доступность  = Истина;
	Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Далее >'");
	Элементы.КнопкаОтмена.Видимость   = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДляПодключения()
	
	Если Не ЭтоАдминистраторСистемы Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	Если ДанныеАутентификации = Неопределено Тогда
		Логин  = "";
		Пароль = "";
	Иначе
		Логин  = ДанныеАутентификации.Логин;
		Пароль = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСообщениеСервиса(Сообщение, ЗаголовокКнопкиДалее, ПодробноеОписаниеВнутрОшибки = "")
	
	ПодробноеОписаниеОшибки = ПодробноеОписаниеВнутрОшибки;
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаСообщениеСервиса);
	
	// Сервис может вернуть сообщение в формате HTML
	Если ВРег(Лев(Сообщение, 6)) <> "<BODY>" Тогда
		// Отобразить ссылку для перехода в журнал регистрации.
		ПредставлениеHTML =
			ИнтернетПоддержкаПользователей.ПодставитьДомен(
				Сообщение
				+ Символы.ПС
				+ Символы.ПС
				+ НСтр("ru = 'Технические подробности см. в <a href=""open:log"" >журнале регистрации</a>.'")
				+ ?(ДоступнаОтправкаСообщенийТехПоддержке,
					Символы.ПС
						+ Символы.ПС
						+ НСтр("ru = 'При возникновении проблем напишите в <a href=""mailto:webits-info@1c.ru"">техподдержку</a>.'"),
					""),
				ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами().ДоменРасположенияСерверовИПП);
		Элементы.ДекорацияСообщениеСервиса.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ПредставлениеHTML);
	Иначе
		Элементы.ДекорацияСообщениеСервиса.Заголовок = ФорматированнаяСтрокаИзHTML(Сообщение);
	КонецЕсли;
	
	Элементы.КнопкаНазад.Доступность =
		(ИнформацияОДоступномОбновлении <> Неопределено И ПустаяСтрока(ИнформацияОДоступномОбновлении.ИмяОшибки));
	Элементы.КнопкаОтмена.Видимость   = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена'");
	
	Если ЗаголовокКнопкиДалее = Неопределено Тогда
		Элементы.КнопкаДалее.Доступность  = Ложь;
		Элементы.КнопкаДалее.Заголовок  = НСтр("ru = 'Далее >'");
	Иначе
		Элементы.КнопкаДалее.Доступность  = Истина;
		Элементы.КнопкаДалее.Заголовок  = ЗаголовокКнопкиДалее;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкуПодключения(Знач Сообщение, Знач ОписаниеОшибкиПодключения)
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаНеУдалосьПодключитьсяКСервису);
	
	ПодробноеОписаниеОшибки = ОписаниеОшибкиПодключения;
	Если ПустаяСтрока(Сообщение) Тогда
		Элементы.ДекорацияОшибкаПодключения.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияОшибкаПодключения.Видимость = Истина;
		Элементы.ДекорацияОшибкаПодключения.Заголовок = Сообщение;
	КонецЕсли;
	
	Элементы.КнопкаНазад.Доступность =
		(ПустаяСтрока(ИнформацияОДоступномОбновлении.ИмяОшибки)
		Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект));
	Элементы.КнопкаДалее.Заголовок    = НСтр("ru = 'Повторить попытку подключения >'");
	Элементы.КнопкаДалее.Доступность  = Истина;
	Элементы.КнопкаОтмена.Видимость   = Истина;
	Элементы.КнопкаОтмена.Доступность = Истина;
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПолучениеОбновленийЗавершено(Инициализировать = Ложь)
	
	Если Не ОбновитьКонфигурацию Тогда
		
		// Отобразить завершение установки исправлений и/или платформы.
		ОтобразитьПолучениеОбновленийЗавершеноНаСервере(Инициализировать);
		
	Иначе
		
		Элементы.СтраницыПолучениеУстановка.ТекущаяСтраница = Элементы.СтраницаПолучениеЗавершено;
		Прогресс        = 100;
		ТекущееДействие = "";
		
		Если ПараметрыАдминистрирования = Неопределено Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПослеПолученияПараметровАдминистрирования",
				ЭтотОбъект,
				Инициализировать);
			ЗаголовокФормы = НСтр("ru = 'Установка обновления'");
			Если ЭтоФайловаяИБ Тогда
				ПоясняющаяНадпись = НСтр("ru = 'Для установки обновления необходимо ввести
					|параметры администрирования информационной базы'");
				ЗапрашиватьПараметрыАдминистрированияКластера = Ложь;
			Иначе
				ПоясняющаяНадпись = НСтр("ru = 'Для установки обновления необходимо ввести параметры
					|администрирования кластера серверов и информационной базы'");
				ЗапрашиватьПараметрыАдминистрированияКластера = Истина;
			КонецЕсли;
			
			СоединенияИБКлиент.ПоказатьПараметрыАдминистрирования(
				ОписаниеОповещения,
				Истина,
				ЗапрашиватьПараметрыАдминистрированияКластера,
				ПараметрыАдминистрирования,
				ЗаголовокФормы,
				ПоясняющаяНадпись);
			
		Иначе
			
			ПослеПолученияПараметровАдминистрирования(ПараметрыАдминистрирования, Инициализировать);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияПараметровАдминистрирования(Результат, Инициализировать) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ПараметрыАдминистрирования = Результат;
		ПослеПолученияПараметровАдминистрированияНаСервере(
			ПараметрыАдминистрирования,
			Инициализировать);
		
		Если Инициализировать
			И Элементы.Страницы.ТекущаяСтраница = Элементы.ВыборРежимаОбновленияФайловыйРежим Тогда
			
			Элементы.НадписьРезервнаяКопияФайл.Заголовок =
				ОбновлениеКонфигурацииКлиент.ЗаголовокСозданияРезервнойКопии(ЭтотОбъект);
			
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Для установки обновления необходимо ввести параметры администрирования.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
		ТекстСообщения = НСтр("ru = 'Не удалось установить обновление программы, т.к. не были введены
			|корректные параметры администрирования информационной базы.'");
		ПолучениеОбновленийПрограммыКлиент.ЗаписатьОшибкуВЖурналРегистрации(ТекстСообщения);
		
		ОтобразитьВнутреннююОшибку(
			НСтр("ru = 'Не удалось установить обновление программы, т.к. не были введены
				|корректные параметры администрирования информационной базы.'"),
				,
				ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьПолучениеОбновленийЗавершеноНаСервере(Знач Инициализировать)
	
	ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
	
	Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
			Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект)
			Или (Не ОбновитьКонфигурацию И ОбновитьПлатформу) Тогда
		
		// Завершено обновление платформы, "Не ОбновитьКонфигурацию" означает,
		// что обновлена только платформа и исправления.
		
		ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаУстановкаПлатформыЗавершена);
		
		ШаблонЗаголовка = НСтр("ru = 'Установлена версия: <b>%1</b>
			|Дистрибутив платформы скопирован в каталог <a href=""open:DistribFolder"">%2</a>'");
		
		УстановленнаяВерсияПлатформы = ВыбранноеОбновление.Платформа.Версия;
		КаталогУстановкиПлатформы    =
			ПолучениеОбновленийПрограммыКлиентСервер.КаталогУстановкиПлатформы1СПредприятие(
				УстановленнаяВерсияПлатформы);
		
		Элементы.ДекорацияИнформацияУстановкаЗавершена.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			ШаблонЗаголовка,
			УстановленнаяВерсияПлатформы,
			КонтекстОбновления.КаталогДистрибутиваПлатформы);
		
		Если ПустаяСтрока(Элементы.ДекорацияСтрЗавершеноСоздатьРК.Заголовок) Тогда
			Элементы.ДекорацияСтрЗавершеноСоздатьРК.Заголовок =
				ТекстИнструкцииПоПереходуНаНовуюВерсиюПлатформы(Истина);
		КонецЕсли;
		
		Если ПустаяСтрока(Элементы.ДекорацияСтрЗавершеноНеСоздаватьРК.Заголовок) Тогда
			Элементы.ДекорацияСтрЗавершеноНеСоздаватьРК.Заголовок =
				ТекстИнструкцииПоПереходуНаНовуюВерсиюПлатформы(Ложь);
		КонецЕсли;
		
		Если СоздатьРезервнуюКопию Тогда
			Элементы.СтраницыИнструкцияУстановкаЗавершена.ТекущаяСтраница = Элементы.СтрЗавершеноСоздатьРК;
		Иначе
			Элементы.СтраницыИнструкцияУстановкаЗавершена.ТекущаяСтраница = Элементы.СтрЗавершеноНеСоздаватьРК;
		КонецЕсли;
		
		Элементы.КнопкаНазад.Доступность  = Истина;
		Элементы.КнопкаДалее.Доступность  = Истина;
		Элементы.КнопкаОтмена.Видимость   = Истина;
		Элементы.КнопкаОтмена.Доступность = Истина;
		Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
		Элементы.КнопкаДалее.Заголовок  = НСтр("ru = 'Готово'");
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена'");
		
	ИначеЕсли Не ОбновитьКонфигурацию Тогда
		
		// Установлены только исправления.
		ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаУстановленыИсправления);
		
		// Соединения.ЗавершениеРаботыПользователей
		СоединенияИнфо = СоединенияИБ.ИнформацияОСоединениях(Ложь);
		// Конец Соединения.ЗавершениеРаботыПользователей
		
		Элементы.ДекорацияАктивныеПользователи.Видимость = СоединенияИнфо.НаличиеАктивныхСоединений;
		
		Элементы.КнопкаНазад.Доступность  = Истина;
		Элементы.КнопкаДалее.Доступность  = Истина;
		Элементы.КнопкаОтмена.Видимость   = Ложь;
		Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
		Элементы.КнопкаДалее.Заголовок  = НСтр("ru = 'Готово'");
		
	Иначе
		
		Элементы.КнопкаОтмена.Видимость = Истина;
		
		Если ЭтоФайловаяИБ Тогда
			
			ОтобразитьСтраницу(Элементы.Страницы, Элементы.ВыборРежимаОбновленияФайловыйРежим);
			
			Если Инициализировать Тогда
				
				Элементы.ПорядокОбновленияФайл.Видимость = Не ПустаяСтрока(
					ВыбранноеОбновление.Конфигурация.URLПорядокОбновления);
				
				Если Элементы.ПорядокОбновленияФайл.Видимость Тогда
					Элементы.НадписьПорядокОбновленияФайл.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
						НСтр("ru = 'Для продолжения необходимо ознакомиться с <a href=""%1"">порядком обновления</a>.'"),
						ВыбранноеОбновление.Конфигурация.URLПорядокОбновления);
				КонецЕсли;
				
				ЗаполнитьНастройкиУстановкиОбновленийФайловыйРежим();
				
			КонецЕсли;
			
			// Соединения.ЗавершениеРаботыПользователей
			СоединенияИнфо = СоединенияИБ.ИнформацияОСоединениях(Ложь);
			// Конец Соединения.ЗавершениеРаботыПользователей
			
			Элементы.ГруппаСоединений.Видимость = СоединенияИнфо.НаличиеАктивныхСоединений;
			
			Если СоединенияИнфо.НаличиеАктивныхСоединений Тогда
				ВсеСтраницы = Элементы.ПанельАктивныеПользователи.ПодчиненныеЭлементы;
				
				Если СоединенияИнфо.НаличиеCOMСоединений Тогда
					Элементы.ПанельАктивныеПользователи.ТекущаяСтраница = ВсеСтраницы.АктивныеСоединения;
				ИначеЕсли СоединенияИнфо.НаличиеСоединенияКонфигуратором Тогда
					Элементы.ПанельАктивныеПользователи.ТекущаяСтраница = ВсеСтраницы.СоединениеКонфигуратора;
				Иначе
					Элементы.ПанельАктивныеПользователи.ТекущаяСтраница = ВсеСтраницы.АктивныеПользователи;
				КонецЕсли;
				
			КонецЕсли;
			
			Элементы.КнопкаНазад.Доступность = Истина;
			Элементы.КнопкаДалее.Доступность = (Не СоединенияИнфо.НаличиеАктивныхСоединений
				Или РежимОбновления = 1);
			Элементы.КнопкаОтмена.Доступность = Истина;
			Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
			Элементы.КнопкаДалее.Заголовок = ?(
				РежимОбновления = 0,
				НСтр("ru = 'Далее >'"),
				НСтр("ru = 'Готово'"));
			
			Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена'");
			
		Иначе
			
			ОтобразитьСтраницу(Элементы.Страницы, Элементы.ВыборРежимаОбновленияСерверныйРежим);
			Если Инициализировать Тогда
				
				Элементы.ПорядокОбновленияСервер.Видимость = Не ПустаяСтрока(
					ВыбранноеОбновление.Конфигурация.URLПорядокОбновления);
				
				Если Элементы.ПорядокОбновленияСервер.Видимость Тогда
					Элементы.НадписьПорядокОбновленияСервер.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
						НСтр("ru = 'Для продолжения необходимо ознакомиться с <a href=""%1"">порядком обновления</a>.'"),
						ВыбранноеОбновление.Конфигурация.URLПорядокОбновления);
				КонецЕсли;
				
				ЗаполнитьНастройкиУстановкиОбновленийКлиентСерверныйРежим();
				
			КонецЕсли;
			
			Если КодЗадачиПланировщика = 0 И Не ДатаВремяОбновленияУстановлена Тогда
				ДатаВремяОбновления = ВернутьДату(
					ДобавитьДни(НачалоДня(ТекущаяДатаСеанса()), 1), ДатаВремяОбновления);
				ДатаВремяОбновленияУстановлена = Истина;
			КонецЕсли;
			
			ЕстьОтложенныеОбработчики = (ОбновлениеИнформационнойБазы.СтатусОтложенногоОбновления() = "СтатусНеВыполнено");
			Элементы.НадписьОтложенныеОбработчики.Видимость = ЕстьОтложенныеОбработчики;
			
			СтраницыПанелиИнформацииПерезагрузки1 = Элементы.СтраницыИнформацииПерезагрузки1.ПодчиненныеЭлементы;
			Элементы.СтраницыИнформацииПерезагрузки1.ТекущаяСтраница = ?(
				РежимОбновления = 0,
				СтраницыПанелиИнформацииПерезагрузки1.СтраницаПерезагрузкиСейчас1,
				СтраницыПанелиИнформацииПерезагрузки1.СтраницаЗапланированнойПерезагрузки);
			
			Если РежимОбновления <> 0 Тогда
				
				// Соединения.ЗавершениеРаботыПользователей
				СоединенияИнфо = СоединенияИБ.ИнформацияОСоединениях(Ложь);
				// Конец Соединения.ЗавершениеРаботыПользователей
				
				НаличиеСоединений = (СоединенияИнфо.НаличиеАктивныхСоединений И РежимОбновления = 0);
				Элементы.ГруппаСоединений1.Видимость = НаличиеСоединений;
				
				Если НаличиеСоединений Тогда
					ВсеСтраницы = Элементы.ПанельАктивныеПользователи1.ПодчиненныеЭлементы;
					Элементы.ПанельАктивныеПользователи1.ТекущаяСтраница = ? (СоединенияИнфо.НаличиеCOMСоединений, 
						ВсеСтраницы.АктивныеСоединения1, ВсеСтраницы.АктивныеПользователи1);
				КонецЕсли;
				
			КонецЕсли;
			
			Элементы.ПолеДатаВремяОбновления.Доступность = (РежимОбновления = 2);
			Элементы.АдресЭлектроннойПочты.Доступность   = ВыслатьОтчетНаПочту;
			
			Элементы.КнопкаНазад.Доступность  = Истина;
			Элементы.КнопкаДалее.Доступность  = Истина;
			Элементы.КнопкаОтмена.Доступность = Истина;
			Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Ложь;
			Элементы.КнопкаДалее.Заголовок = ?(
				РежимОбновления = 0,
				НСтр("ru = 'Далее >'"),
				НСтр("ru = 'Готово'"));
			Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена'");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиУстановкиОбновленийФайловыйРежим()
	
	// СтандартныеПодсистемы.ОбновлениеКонфигурации
	НастройкиОбновления = ОбновлениеКонфигурации.НастройкиОбновленияКонфигурации();
	РежимОбновления                   = НастройкиОбновления.РежимОбновления;
	СоздаватьРезервнуюКопию           = НастройкиОбновления.СоздаватьРезервнуюКопию;
	ИмяКаталогаРезервнойКопииИБ       = НастройкиОбновления.ИмяКаталогаРезервнойКопииИБ;
	ВосстанавливатьИнформационнуюБазу = НастройкиОбновления.ВосстанавливатьИнформационнуюБазу;
	// Конец СтандартныеПодсистемы.ОбновлениеКонфигурации
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиУстановкиОбновленийКлиентСерверныйРежим()
	
	// СтандартныеПодсистемы.ОбновлениеКонфигурации
	НастройкиОбновления = ОбновлениеКонфигурации.НастройкиОбновленияКонфигурации();
	РежимОбновления       = НастройкиОбновления.РежимОбновления;
	ДатаВремяОбновления   = НастройкиОбновления.ДатаВремяОбновления;
	ВыслатьОтчетНаПочту   = НастройкиОбновления.ВыслатьОтчетНаПочту;
	АдресЭлектроннойПочты = НастройкиОбновления.АдресЭлектроннойПочты;
	КодЗадачиПланировщика = НастройкиОбновления.КодЗадачиПланировщика;
	// Конец СтандартныеПодсистемы.ОбновлениеКонфигурации
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВернутьДату(Дата, Время)
	Возврат Дата(Год(Дата), Месяц(Дата), День(Дата), Час(Время), Минута(Время), Секунда(Время));
КонецФункции

// Добавляет заданное количество дней к дате.
//
// Параметры:
//  Дата		- Дата	- Исходная дата.
//  ЧислоДней	- Число	- Количество дней, добавляемых к исходной дате.
//
&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьДни(Знач Дата, Знач ЧислоДней)
	
	Если ЧислоДней > 0 Тогда
		Разница = День(Дата) + ЧислоДней - День(КонецМесяца(Дата));
		Если Разница > 0 Тогда
			НоваяДата = ДобавитьМесяц(Дата, 1);	
			Возврат Дата(Год(НоваяДата), Месяц(НоваяДата), Разница, 
				Час(НоваяДата), Минута(НоваяДата), Секунда(НоваяДата));
		КонецЕсли;
	ИначеЕсли ЧислоДней < 0 Тогда
		Разница = День(Дата) + ЧислоДней - День(НачалоМесяца(Дата));
		Если Разница < 1 Тогда
			НоваяДата = ДобавитьМесяц(Дата, -1);	
			Возврат Дата(Год(НоваяДата), Месяц(НоваяДата), День(КонецМесяца(НоваяДата)) - Разница, 
				Час(НоваяДата), Минута(НоваяДата), Секунда(НоваяДата));
		КонецЕсли;
	КонецЕсли; 
	Возврат Дата(Год(Дата), Месяц(Дата), День(Дата) + ЧислоДней, Час(Дата), Минута(Дата), Секунда(Дата));
	
КонецФункции

&НаСервере
Процедура ОтобразитьНерекомендуемаяВерсияПлатформы()
	
	ОтобразитьСтраницу(Элементы.Страницы, Элементы.СтраницаНерекомендуемаяВерсияПлатформы);

	Если ПолучениеОбновленийПрограммы.СлужебнаяДоступноИспользованиеПолученияОбновленийПлатформы(Ложь) Тогда
		Элементы.КнопкаНазад.Доступность = Ложь;
		Элементы.КнопкаДалее.Заголовок   = НСтр("ru = 'Обновить платформу >'");
		Элементы.КнопкаДалее.Видимость   = Истина;
		Элементы.КнопкаДалее.Доступность = Истина;
	Иначе
		// Если обновление платформы недоступно, отобразить только сообщение пользователю.
		Элементы.КнопкаНазад.Видимость          = Ложь;
		Элементы.КнопкаДалее.Видимость          = Ложь;
		Элементы.КнопкаОтмена.КнопкаПоУмолчанию = Истина;
		Элементы.Справка.Видимость              = Ложь;
	КонецЕсли;
	
	Элементы.КнопкаОтмена.Доступность                  = Истина;
	Элементы.КнопкаОтмена.Заголовок                    = НСтр("ru = 'Завершить работу'");
	Элементы.ПродолжитьРаботуНаТекущейВерсии.Видимость = Не РаботаВПрограммеЗапрещена;
	
КонецПроцедуры

&НаСервере
Функция ЗаголовокСтраницыНоваяВерсияПлатформы()
	
	ВыбранноеОбновление = ВариантыОбновлений[ВариантОбновления];
	
	Возврат ?(ВыбранноеОбновление.Платформа.РекомендуетсяПереход,
		НСтр("ru = 'Рекомендуется перейти на новую версию платформы'"),
		НСтр("ru = 'Доступна новая версия платформы'"));
	
КонецФункции

&НаСервере
Функция ТекстИнструкцииПоПереходуНаНовуюВерсиюПлатформы(ПараметрСоздатьРезервнуюКопию)
	
	Строки = Новый Массив;
	
	ЭтоБазоваяВерсияКонфигурации = ПолучениеОбновленийПрограммы.ЭтоБазоваяВерсияКонфигурации();
	ВнедренаПодсистемаРезервногоКопирования =
		ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РезервноеКопированиеИБ");
	
	Если ВнедренаПодсистемаРезервногоКопирования
		И ПараметрСоздатьРезервнуюКопию Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЗавершениеРаботыПользователей") Тогда
			Строки.Добавить(
				НСтр("ru = 'Будет автоматически завершена работа <a href=""open:ActiveUsers"">активных пользователей</a> программы, создана резервная копия'"));
		Иначе
			Строки.Добавить(
				НСтр("ru = 'Будет автоматически завершена работа активных пользователей программы, создана резервная копия'"));
		КонецЕсли;
		
		Если ЭтоБазоваяВерсияКонфигурации Тогда
			Строки.Добавить(
				НСтр("ru = 'информационной базы и запущен сеанс работы с программой на новой версии платформы.'"));
		Иначе
			Строки.Добавить(
				НСтр("ru = 'информационной базы и запущен сеанс работы с программой на новой версии платформы.
					|На компьютерах других пользователей программы необходимо обновить платформу 1С:Предприятие вручную.'"));
		КонецЕсли;
		
	Иначе
		
		Строки.Добавить(НСтр("ru = 'Чтобы начать работать на новой версии платформы:'"));
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЗавершениеРаботыПользователей") Тогда
			Строки.Добавить(
				НСтр("ru = '- завершите работу всех <a href=""open:ActiveUsers"">активных пользователей</a> программы;'"));
		Иначе
			Строки.Добавить(НСтр("ru = '- завершите работу всех активных пользователей программы;'"));
		КонецЕсли;
		
		Строки.Добавить(НСтр("ru = '- завершите текущий сеанс работы с программой;'"));
		
		Если Не ЭтоБазоваяВерсияКонфигурации Тогда
			Строки.Добавить(НСтр("ru = '- установите новую версию платформы на компьютеры других пользователей программы;'"));
		КонецЕсли;
		
		Строки.Добавить(НСтр("ru = '- откройте программу на новой версии платформы;'"));
		
		Строки.Добавить();
		Если ВнедренаПодсистемаРезервногоКопирования Тогда
			Строки.Добавить(
				НСтр("ru = 'Перед началом работы на новой версии платформы рекомендуется создать резервную копию.'"));
		Иначе
			Строки.Добавить(
				НСтр("ru = 'Перед началом работы на новой версии платформы рекомендуется <a href=""open:RCInstruction"">создать резервную копию</a>.'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(
		СтрСоединить(Строки, Символы.ПС));
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииПараметровРезервногоКопирования(ПараметрыРезервногоКопирования, ДопПараметры) Экспорт
	
	Если ТипЗнч(ПараметрыРезервногоКопирования) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРезервногоКопирования);
		ОтобразитьПолучениеОбновленийЗавершено();
		Элементы.НадписьРезервнаяКопияФайл.Заголовок =
			ОбновлениеКонфигурацииКлиент.ЗаголовокСозданияРезервнойКопии(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПарольАдминистратора(Знач ПараметрыАдминистрирования)
	
	АдминистраторИБ = ПользователиИнформационнойБазы.НайтиПоИмени(
		ПараметрыАдминистрирования.ИмяАдминистратораИнформационнойБазы);
	
	Если Не АдминистраторИБ.АутентификацияСтандартная Тогда
		
		АдминистраторИБ.АутентификацияСтандартная = Истина;
		АдминистраторИБ.Пароль = ПараметрыАдминистрирования.ПарольАдминистратораИнформационнойБазы;
		АдминистраторИБ.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьПанелиОшибок()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтрольВеденияУчета") Тогда
		МодульКонтрольВеденияУчета       = ОбщегоНазначения.ОбщийМодуль("КонтрольВеденияУчета");
		СводнаяИнформацияПоВидамПроверок = МодульКонтрольВеденияУчета.СводнаяИнформацияПоВидамПроверок("СистемныеПроверки");
		Возврат СводнаяИнформацияПоВидамПроверок.Количество > 0;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохранитьПризнакАвтоматическойЗагрузкиИУстановкиИсправленийНаСервере(
	Знач ЗагружатьИУстанавливатьИсправленияАвтоматически)
	
	Если ЗагружатьИУстанавливатьИсправленияАвтоматически
		И Не ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		Возврат Ложь;
	Иначе
		ПолучениеОбновленийПрограммыВызовСервера.ВключитьОтключитьАвтоматическуюУстановкуИсправлений(
			ЗагружатьИУстанавливатьИсправленияАвтоматически);
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьВариантИнформированияОВыходеОбновленийНаСервере(Знач НовоеЗначение)
	
	НастройкиОбновления = ПолучениеОбновленийПрограммы.НастройкиАвтоматическогоОбновления();
	НастройкиОбновления.ВариантИнформированияОВыходеОбновлений = НовоеЗначение;
	
	ПолучениеОбновленийПрограммы.ЗаписатьНастройкиАвтоматическогоОбновления(НастройкиОбновления);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРасписанияУстановкиИсправленийНаСервере(Знач Расписание)
	
	ПолучениеОбновленийПрограммыВызовСервера.УстановитьРасписаниеЗаданияУстановкиИсправлений(Расписание);
	
	Элементы.ДекорацияЗагрузкаИсправленийРасписание.Заголовок =
		ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРасписания(Расписание);
	
КонецПроцедуры

&НаСервере
Функция ИнициализацияПолученияОбновленийНаСервере()
	
	Результат = Новый Структура;
	Результат.Вставить("НачатьПолучениеИУстановкуОбновлений", Ложь);
	
	Если Не ИзмененЛогинПароль
		И Не ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		
		ОтобразитьПодключениеКПорталу();
		Возврат Результат;
		
	ИначеЕсли ОписаниеФайловОбновлений <> Неопределено
		И ПустаяСтрока(ОписаниеФайловОбновлений.ИмяОшибки) Тогда
		
		// Описание загружаемых файлов уже успешно получено,
		// продолжить получение и установку обновлений.
		// Эта ветвь работает только при возникновении ошибки получения файлов
		// или если пользователь отменил установку платформы в интерактивном режиме.
		
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСообщениеСервиса
			И КонтекстОбновления <> Неопределено
			И ПолучениеОбновленийПрограммыКлиентСервер.ЭтоКодВозвратаОграниченияСистемныхПолитик(
				КонтекстОбновления.КодВозвратаПрограммыУстановки)
			И РежимУстановкиПлатформы = 0 Тогда
			
			// Обход ошибки политик безопасности при установке с настройками по умолчанию (в "тихом" режиме).
			// Если возникла ошибка ограничения политик безопасности при установке в тихом режиме,
			// тогда выполнить установку в полном интерактивном режиме.
			РежимУстановкиПлатформы = 1;
			
		КонецЕсли;
		
		Результат.НачатьПолучениеИУстановкуОбновлений = Истина;
		
	Иначе
		
		// Если описание файлов обновлений еще не получено или
		// произошла ошибка при получении описания файлов, тогда
		// выполнить получение информации о файлах.
		
		ВыбранноеОбновление            = ВариантыОбновлений[ВариантОбновления];
		ВыбранноеОбновлениеИсправлений = ?(ОбновитьКонфигурацию,
			ВариантыОбновлений[ВариантОбновления],
			ВариантыОбновлений[ИдентификаторТекущейВерсии()]);
		
		Если ЭтоСценарийПереходаНаНовуюВерсиюПлатформы(ЭтотОбъект)
			Или ЭтоСценарийСообщениеОНерекомендуемойВерсииПлатформы(ЭтотОбъект) Тогда
			
			ИнформацияОбновлениеКонфигурации = Неопределено;
			ИнформацияОбновлениеПлатформы    = ВыбранноеОбновление.Платформа;
			
		Иначе
			
			ИнформацияОбновлениеКонфигурации = ?(
				ОбновитьКонфигурацию,
				ВыбранноеОбновление.Конфигурация,
				Неопределено);
			
			ИнформацияОбновлениеПлатформы = ?(
				ОбновитьПлатформу,
				ВыбранноеОбновление.Платформа,
				Неопределено);
			
		КонецЕсли;
		
		Если УстановитьИсправления Тогда
			ПараметрыИсправлений = Новый Структура;
			ПараметрыИсправлений.Вставить("АдресИсправления"                   , АдресИсправления);
			ПараметрыИсправлений.Вставить("ИдентификаторыПолучаемыхИсправлений",
				ВыбранноеОбновлениеИсправлений.ВыбранныеИсправления.ВыгрузитьЗначения());
		Иначе
			ПараметрыИсправлений = Неопределено;
		КонецЕсли;
		
		ОписаниеФайловОбновлений = ОписаниеФайловОбновленийНаСервере(
			ИнформацияОбновлениеКонфигурации,
			ПараметрыИсправлений,
			ИнформацияОбновлениеПлатформы,
			Логин,
			Пароль,
			ИзмененЛогинПароль);
		
		Если ПустаяСтрока(ОписаниеФайловОбновлений.ИмяОшибки) Тогда
			
			// Произошла успешная авторизация.
			ИзмененЛогинПароль = Ложь;
			Логин  = "";
			Пароль = "";
			
			Результат.НачатьПолучениеИУстановкуОбновлений = Истина;
			
		Иначе
			
			// Обработка ошибки получения информации о доступных обновлениях.
			Если ЭтоОшибкаСоединения(ОписаниеФайловОбновлений.ИмяОшибки) Тогда
				
				ОтобразитьОшибкуПодключения(
					ОписаниеФайловОбновлений.Сообщение,
					ОписаниеФайловОбновлений.ИнформацияОбОшибке);
				
			ИначеЕсли ОписаниеФайловОбновлений.ИмяОшибки = "НеЗаполненыДанныеАутентификации" Тогда
				
				ОтобразитьПодключениеКПорталу();
				
			ИначеЕсли ОписаниеФайловОбновлений.ИмяОшибки = "LoginError" Тогда
				
				ОтобразитьПодключениеКПорталу(
					ФорматированнаяСтрокаИзHTML(ОписаниеФайловОбновлений.Сообщение));
				
			Иначе
				
				ОтобразитьСообщениеСервиса(
					ОписаниеФайловОбновлений.Сообщение,
					НСтр("ru = 'Повторить попытку >'"),
					ОписаниеФайловОбновлений.ИнформацияОбОшибке);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат.НачатьПолучениеИУстановкуОбновлений Тогда
		Если ЭтоФайловаяИБ Тогда
			НачатьПолучениеОбновленийВФоновомРежиме();
		Иначе
			ИнициализироватьСтраницуПолучениеОбновления();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПослеПолученияПараметровАдминистрированияНаСервере(
	Знач ПараметрыАдминистрирования,
	Знач Инициализировать)
	
	УстановитьПарольАдминистратора(ПараметрыАдминистрирования);
	ОтобразитьПолучениеОбновленийЗавершеноНаСервере(Инициализировать);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКаталогСохраненияПлатформы(Элемент)
	
	#Если Не ВебКлиент Тогда
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Заголовок = НСтр("ru = 'Каталог хранения дистрибутивов платформы 1С:Предприятие'");
	ДиалогВыбора.Каталог   = Элемент.ТекстРедактирования;
	Если НЕ ДиалогВыбора.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	КаталогСохраненияПлатформы = ДиалогВыбора.Каталог;
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КаталогДистрибутиваПлатформыПоУмолчанию()
	
	Возврат ПолучениеОбновленийПрограммыКлиентСервер.КаталогДляРаботыСОбновлениямиПлатформы()
		+ ?(ПолучениеОбновленийПрограммыКлиентСервер.Это64РазрядноеПриложение(), "setup_64\", "setup\");
	
КонецФункции

// Преобразует переданную строку:
// в форматированную строку, если строка начинается с "<body>" и заканчивается "</body>";
// В противном случае строка остается без изменений.
//
// Параметры:
//  ТекстСообщения - Строка - исходная строка.
//
// Возвращаемое значение:
//  Строка - результат преобразования.
//
&НаСервереБезКонтекста
Функция ФорматированнаяСтрокаИзHTML(ТекстСообщения)
	
	Документ = Новый ФорматированныйДокумент();
	Документ.УстановитьHTML("<html>" + ТекстСообщения + "</html>", Новый Структура());
	Возврат Документ.ПолучитьФорматированнуюСтроку();
	
КонецФункции
#КонецОбласти