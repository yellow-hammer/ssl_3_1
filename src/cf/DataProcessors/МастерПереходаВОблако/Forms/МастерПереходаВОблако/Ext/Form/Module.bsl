
#Область ОписаниеПеременных

&НаКлиенте
Перем ЗаписьДанных;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяКонфигурации = ВидыПриложенийСервер.ИмяТекущегоВидаПриложения();
	СинонимКонфигурации = ВидыПриложенийСервер.ПредставлениеТекущегоВидаПриложения();
	ВерсияКонфигурации = Метаданные.Версия;
	ЧасовойПояс = ЧасовойПоясСеанса();
	НаименованиеПриложения = СинонимКонфигурации;
	РазделениеВключено = РаботаВМоделиСервиса.РазделениеВключено();
	
	Элементы.КартинкаКоробкаОблако.Видимость = Не РазделениеВключено;
	Элементы.КартинкаКоробкаФайл.Видимость = Не РазделениеВключено;
	Элементы.КартинкаОблакоОблако.Видимость = РазделениеВключено;
	Элементы.КартинкаОблакоФайл.Видимость = РазделениеВключено;
	
	Элементы.ПолучитьФайлВыгрузкиВМоделиСервиса.Видимость = РазделениеВключено;
	Элементы.ПолучитьФайлВыгрузки.Видимость = Не РазделениеВключено;
	Элементы.ПримечаниеТребуетсяОбновлениеВМоделиСервиса.Видимость = РазделениеВключено;
	Элементы.ПримечаниеТребуетсяОбновление.Видимость = Не РазделениеВключено;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РазмерПриложений") Тогда
		МодульРазмерПриложений = ОбщегоНазначения.ОбщийМодуль("РазмерПриложений");
		ПоддерживаетсяРасчетРазмера = МодульРазмерПриложений.ПоддерживаетсяРасчетРазмераПриложений();
	Иначе
		ПоддерживаетсяРасчетРазмера = Ложь;
	КонецЕсли;
	Элементы.ГруппаРазмерПриложения.Видимость = ПоддерживаетсяРасчетРазмера;
	
	//@skip-warning
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		Элементы.ПримечаниеТребуетсяОбновление.Заголовок = Строка(Элементы.ПримечаниеТребуетсяОбновление.Заголовок);
	КонецЕсли;	
	
	Если РазделениеВключено Тогда
		Элементы.ПараметрыСравненияШапка.Заголовок = СтрШаблон(НСтр("ru = 'Выберите вариант%1переноса:'"), Символы.ПС);
		Элементы.Параметр1.Заголовок = НСтр("ru = 'Можно продолжать работу в текущем приложении в процессе переноса'");
		Элементы.Параметр2.Заголовок = НСтр("ru = 'Данные автоматически обновятся до нужной версии в процессе переноса'");
		Элементы.СпособПереходаФайл.СписокВыбора[0].Представление =	НСтр("ru = 'Получите файл выгрузки для ручного переноса.'"); 
		Элементы.СпособПереходаОблако.СписокВыбора[0].Представление = СтрШаблон(
			НСтр("ru = 'Введите адрес сервиса%1для автоматического переноса:'"), Символы.ПС);
		
		ТекстРекомендации = НСтр("ru = 'Расширенная информация для службы поддержки записана в журнал регистрации.
			|Если устранить ошибку не удается, рекомендуется обратиться в службу технической поддержки.'");
	Иначе
		ТекстРекомендации = НСтр("ru = 'Расширенная информация для службы поддержки записана в журнал регистрации.
			|Если устранить ошибку не удается, рекомендуется обратиться в службу технической поддержки, предоставив для расследования информационную базу и выгрузку журнала регистрации.'");
	КонецЕсли; 
	
	Элементы.ОшибкаРекомендация.Заголовок = ТекстРекомендации;
	
	ЗаголовокПриветствия = ?(РазделениеВключено, 
		НСтр("ru = 'Перенос данных приложения'"),
		НСтр("ru = 'Переход в облачный сервис'"));
		
	Заголовок = ЗаголовокПриветствия;
	
	Элементы.Назад.Видимость = Ложь;
	Элементы.ПарольОткрытый.Видимость = Ложь;
	Элементы.ПарольОткрытый.КартинкаКнопкиВыбора = Элементы.КартинкаОткрыто.Картинка;
	Элементы.ПарольЗакрытый.КартинкаКнопкиВыбора = Элементы.КартинкаЗакрыто.Картинка;
	
	ВариантРегистрации = ВариантАвтоматическоеНазначениеОО();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ИдентификаторТекущегоПользователя = ТекущийПользователь.УникальныйИдентификатор();
	КонецЕсли; 
	
	Элементы.ПодобратьООАвтоматически.СписокВыбора.Добавить(
		ВариантАвтоматическоеНазначениеОО(), НСтр("ru = 'Подобрать обслуживающую организацию автоматически'"));
	Элементы.ВыбратьОО.СписокВыбора.Добавить(
		ВариантВыборОО(), НСтр("ru = 'Выбрать организацию'"));
	Элементы.ВвестиКодАктивации.СписокВыбора.Добавить(
		ВариантВводКодаАктивации(), НСтр("ru = 'Ввести код активации, полученный ранее'"));
	
	ЗаполнитьПользователейИБ();
			
	ЗаполнитьРасширенияИБ();
	
	ПараметрыПараллельнойЗагрузки = ВыгрузкаЗагрузкаДанныхСлужебный.ПараметрыПараллельнойВыгрузкиЗагрузкиДанных();
	
	Если ПараметрыПараллельнойЗагрузки.ДостпноИспользование Тогда
		КоличествоЗаданийВыгрузкиДанных = ПараметрыПараллельнойЗагрузки.КоличествоПотоков;
	Иначе
		КоличествоЗаданийВыгрузкиДанных = 1;
		Элементы.ГруппаПараллельнаяВыгрузка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПриветствие Тогда
		Если Не СпособПерехода = СпособФайл() И Не ЗначениеЗаполнено(АдресСервиса) Тогда
			Если РазделениеВключено Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Не выполнена проверка доступности переноса в сервис. Укажите корректный адрес сервиса.'"),,
					"АдресСервиса",, Отказ);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Не выполнена проверка доступности перехода в сервис. Укажите корректный адрес сервиса.'"),,
					"АдресСервиса",, Отказ);
			КонецЕсли; 
		КонецЕсли; 
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВход Тогда
		ПроверяемыеРеквизиты.Добавить("Логин");
		ПроверяемыеРеквизиты.Добавить("Пароль");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводСведений Тогда
		Если ВариантРегистрации = ВариантВводКодаАктивации() Тогда
			ПроверяемыеРеквизиты.Добавить("КодАктивации");
		КонецЕсли;
		ПроверяемыеРеквизиты.Добавить("РегистрацияИмя");
		Если ВариантРегистрации <> ВариантВводКодаАктивации() Тогда 
			ПроверяемыеРеквизиты.Добавить("РегистрацияПочта");
		КонецЕсли; 
		ПроверяемыеРеквизиты.Добавить("РегистрацияПароль");
		ПроверяемыеРеквизиты.Добавить("РегистрацияПодтверждениеПароля");
		
		Если РегистрацияПароль <> РегистрацияПодтверждениеПароля Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Пароль и подтверждение пароля не совпадают.'"),, 
				"РегистрацияПодтверждениеПароля",, Отказ); 
			Возврат;	
		КонецЕсли; 
		Если Не Подтверждение Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Требуется подтверждение'"),, 
				"Подтверждение",, Отказ); 
			Возврат;
		КонецЕсли; 	
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводКодаАктивации Тогда
		ПроверяемыеРеквизиты.Добавить("КодАктивации");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборВариантаРегистрации Тогда
		Если ВариантРегистрации = ВариантВыборОО() Тогда
			ПроверяемыеРеквизиты.Добавить("ООКод");
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборСпособаПерехода Тогда
		ПроверяемыеРеквизиты.Добавить("АбонентКод");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСопоставлениеПользователей Тогда
		НомерСтроки = -1;
		Для Каждого Строка Из ПраваПользователей Цикл
		    НомерСтроки = НомерСтроки + 1;
			Если ЗначениеЗаполнено(Строка.Право) И Не ЗначениеЗаполнено(Строка.Идентификатор) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Для пользователя сервиса ''%1'' с правом ''%2'' не указан пользователь из базы.'"), 
						Строка.ПолноеИмя, ПредставлениеПраваПользователя(Строка.Право)),, 
					СтрШаблон("ПраваПользователей[%1].ПолноеИмяПользователяИБ", Формат(НомерСтроки,"ЧГ=0")),, Отказ); 
			КонецЕсли; 
			Если ЗначениеЗаполнено(Строка.Идентификатор) И ЗначениеЗаполнено(Строка.Логин) И Не ЗначениеЗаполнено(Строка.Право) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Для пользователя сервиса ''%1'' не указано право пользователя из базы.'"), Строка.ПолноеИмя),, 
					СтрШаблон("ПраваПользователей[%1].Право", Формат(НомерСтроки,"ЧГ=0")),, Отказ); 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Форма = ПараметрыПриложения.Получить("ТехнологияСервиса.МиграцияПриложений.ФормаПереходВСервис");
	Если Форма <> Неопределено Тогда
		Закрыть();
		Если Не Форма.Открыта() Тогда
			Форма.Открыть();
		Иначе
			Форма.Активизировать();
		КонецЕсли;
	Иначе
		ПроверитьАдресСервиса();
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьЗаданиеПодготовки(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Или ЗакрытьБезусловно = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемОповещение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Закрыть помощник?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособПереходаОблакоПриИзменении(Элемент)
	
	ПриИзмененииСпособаПереходаНаГлавной();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПереходаФайлПриИзменении(Элемент)
	
	ПриИзмененииСпособаПереходаНаГлавной();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервисаПриИзменении(Элемент)
	
	ПроверитьАдресСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановлениеРегистрацияОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаСтроки = СсылкаРегистрация() Тогда
		СтандартнаяОбработка = Ложь;
		Если ДоступноПриглашениеДляРегистрации Тогда
			УстановитьСтраницуВыборВариантаРегистрации(Элементы.Страницы.ТекущаяСтраница);
		Иначе
			ВариантРегистрации = ВариантЗапросНаРегистрацию();
			УстановитьСтраницуВводСведений(Элементы.Страницы.ТекущаяСтраница);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбслуживающаяОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресОблачногоСервиса", АдресСервиса);
	Оповещение = Новый ОписаниеОповещения("ВыборОрганизацииЗавершение", ЭтотОбъект);
	ВариантРегистрации = ВариантВыборОО();
	ИмяФормыВыбора = "Обработка.МастерПереходаВОблако.Форма.ВыборОбслуживающейОрганизации";
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбслуживающаяОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольЗакрытыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.ПарольЗакрытый.Видимость = Ложь;
	Элементы.ПарольОткрытый.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольОткрытыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.ПарольЗакрытый.Видимость = Истина;
	Элементы.ПарольОткрытый.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьПартнераПриИзменении(Элемент)
	
	ПриИзмененииВариантаРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКодАктивацииПриИзменении(Элемент)
	
	ПриИзмененииВариантаРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура КодАктивацииИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.КодАктивации.ЦветТекста = WebЦвета.Черный;
	
КонецПроцедуры

&НаКлиенте
Процедура КодАктивацииПриИзменении(Элемент)
	
	ПроверитьКодАктивации();
	
КонецПроцедуры

&НаКлиенте
Процедура КодАктивацииЗапросНаРегистрациюПриИзменении(Элемент)
	
	НачатьАктивациюКодаРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияИмяПриИзменении(Элемент)
	
	ПоказатьПризнакЗаполнения(РегистрацияИмя, Элементы.ПроверкаИмени.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияПочтаПриИзменении(Элемент)
	
	ПроверитьАдресПочты();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияТелефонПриИзменении(Элемент)
	
	ПоказатьПризнакЗаполнения(РегистрацияТелефон, Элементы.ПроверкаТелефона.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияПарольПриИзменении(Элемент)
	
	ПроверитьВводПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияПодтверждениеПароляПриИзменении(Элемент)
	
	ПроверитьВводПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПереходаВыгрузкаПриИзменении(Элемент)
	
	ПриИзмененииСпособаПерехода();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПереходаМиграцияПриИзменении(Элемент)
	
	ПриИзмененииСпособаПерехода();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаШапкаНажатие(Элемент)
	
	СпособПерехода = СпособВыгрузка();
	ПриИзмененииСпособаПерехода();
	
КонецПроцедуры

&НаКлиенте
Процедура МиграцияШапкаНажатие(Элемент)
	
	СпособПерехода = СпособМиграция();
	ПриИзмененииСпособаПерехода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьФайлВыгрузкиОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УстановитьСтраницуВыгрузкиДанных(Элементы.Страницы.ТекущаяСтраница, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПримечаниеТребуетсяОбновлениеОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗакрытьБезусловно = Истина;
	Закрыть();
	ПерейтиПоНавигационнойСсылке("e1cib/app/Обработка.ОбновлениеПрограммы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИнформацияОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПерейтиПоНавигационнойСсылке(СтрШаблон("%1?N=%2&P=%3&OIDA-", АдресЛичногоКабинета, Логин, Пароль));
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияПочтаРасширеннаяПодсказкаОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
		УстановитьСтраницуВводКодаАктивации(Элементы.страницы.ТекущаяСтраница);	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеТекстОбработкаСсылки(Элемент, НавигационнаяСсылкаСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаСтроки = СсылкаПовторитьПопыткуСозданияПриложения() Тогда
		СоздатьПриложениеИзФайла();
		
	ИначеЕсли НавигационнаяСсылкаСтроки = СсылкаПовторитьПопыткуПередачиДанных() Тогда
		НачатьПередачуДанных(Элементы.Страницы.ТекущаяСтраница);	
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСредняяОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьСправку("ОбщаяФорма.ЗагрузкаДанныхИзСервиса");
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасширенияДляВосстановления

&НаКлиенте
Процедура РасширенияДляВосстановленияИспользоватьРасширениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресХраненияРасширений", АдресХраненияРасширений);
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборРасширения", ЭтотОбъект);
	ОткрытьФорму("Обработка.МастерПереходаВОблако.Форма.ВыборРасширений", 
		ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура РасширенияДляВосстановленияИспользоватьРасширениеОчистка(Элемент, СтандартнаяОбработка)
	Элементы.РасширенияДляВосстановления.ТекущиеДанные.Версия = "";
КонецПроцедуры

&НаКлиенте
Процедура РасширенияДляВосстановленияИспользоватьРасширениеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПраваПользователей

&НаКлиенте
Процедура ПраваПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПраваПользователейГиперссылка" Тогда
		СтандартнаяОбработка = Ложь;
		СоздатьПользователяСервиса();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваПользователейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СоздатьПользователяСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваПользователейПользовательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокПользователей = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из ПользователиИБ Цикл
		ЭлементСписка = СписокПользователей.Добавить(СтрокаТаблицы, СтрокаТаблицы.ПолноеИмя);
		Если СтрокаТаблицы.Идентификатор = Элементы.ПраваПользователей.ТекущиеДанные.Идентификатор Тогда
			НачальноеЗначение = ЭлементСписка;
		КонецЕсли;
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ВыборПользователяИБ", ЭтотОбъект);
	ПоказатьВыборИзСписка(Оповещение, СписокПользователей, Элемент, НачальноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваПользователейПолноеИмяПользователяИБОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ПраваПользователей.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Идентификатор) Тогда
		Поиск = ПользователиИБ.НайтиСтроки(Новый Структура("Идентификатор", ТекущиеДанные.Идентификатор));
		Если Поиск.Количество() > 0 Тогда
			Поиск[0].ЛогинПользователяСервиса = "";
		КонецЕсли;
		ТекущиеДанные.Идентификатор = Неопределено;
		ТекущиеДанные.Пользователь = Неопределено;
		ТекущиеДанные.Право = Неопределено;
		ТекущиеДанные.ПолноеИмяПользователяИБ = Неопределено;
	КонецЕсли; 
	
	ДополнитьСписокНеСопоставленнымиПользователями();
	ОбновитьСтатусСопоставленияПользователей();
	
КонецПроцедуры
 
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ПереходДалее(Элементы.Страницы.ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ПереходНазад(Элементы.Страницы.ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытьПароль(Команда)
	
	Элементы.РегистрацияПароль.РежимПароля = Не Элементы.РегистрацияПароль.РежимПароля;
	Элементы.РегистрацияПодтверждениеПароля.РежимПароля = Не Элементы.РегистрацияПодтверждениеПароля.РежимПароля;
	Если Элементы.РегистрацияПароль.РежимПароля Тогда
		Элементы.ПоказатьСкрытьПароль.Картинка = Элементы.КартинкаЗакрыто.Картинка;
	Иначе
		Элементы.ПоказатьСкрытьПароль.Картинка = Элементы.КартинкаОткрыто.Картинка;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция МенеджерСервисаПоддерживаетМиграциюСРасширениями()
	Возврат ВерсияПрограммногоИнтерфейса >= 24;
КонецФункции

&НаКлиенте
Функция ПроверитьВозможностьАвтоматическогоПереходаСРасширениями()	
	
	Если СпособПерехода = СпособМиграция() И МенеджерСервисаПоддерживаетМиграциюСРасширениями() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		// Поддержка обратной совместимости.
		ТекстПредупреждения = Вычислить(
			"ОбщегоНазначенияВызовСервераБТС.ТекстПредупрежденияОбАктивныхРасширенияхИзменяющихСтруктуруДанных()");
	Исключение
		ТекстПредупреждения = "";
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтрокиВопроса = Новый Массив;
	СтрокиВопроса.Добавить(НСтр("ru = 'Автоматический переход в сервис с расширениями, изменяющими структуру данных, в данный момент не поддерживается.'"));
	СтрокиВопроса.Добавить(Символы.ПС);
	СтрокиВопроса.Добавить(НСтр("ru = 'Для продолжения необходимо удалить все расширения, изменяющие структуру данных, или использовать вариант ручного перехода.'"));
	СтрокиВопроса.Добавить(НСтр("ru = 'Рекомендуем предварительно создать резервную копию.'"));
	СтрокиВопроса.Добавить(Символы.ПС);
	СтрокиВопроса.Добавить(ТекстПредупреждения);
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыВопроса.Картинка = БиблиотекаКартинок.Предупреждение32;
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Активны расширения конфигурации, изменяющие структуру данных'");

	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
		Неопределено,
		СтрСоединить(СтрокиВопроса, Символы.ПС),
		РежимДиалогаВопрос.ОК,
		ПараметрыВопроса);
		
	Возврат Ложь;	
		
КонецФункции

#Область ВыгрузкаДанных

&НаКлиенте
Процедура НачатьВыгрузкуДанных()
	
	Если РежимВыгрузкиДляТехническойПоддержки Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыгрузкаДанныхДляТехническойПоддержкиПроверкаЗавершение", ЭтотОбъект);
		
		ЧастиСтрокиВопроса = Новый Массив;
		ЧастиСтрокиВопроса.Добавить(
			НСтр("ru = 'В режиме выгрузки для технической поддержки не будут выгружаться присоединенные файлы, версии объектов и др.'"));
		ЧастиСтрокиВопроса.Добавить(Символы.Пс);
		ЧастиСтрокиВопроса.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Полученную выгрузку следует использовать только в целях расследования проблем и тестирования.'"), 
			Новый Шрифт(,, Истина), WebЦвета.Красный));
		ЧастиСтрокиВопроса.Добавить(Символы.Пс);
		ЧастиСтрокиВопроса.Добавить(НСтр("ru = 'Продолжить?'"));
		ПоказатьВопрос(ОписаниеОповещения, Новый ФорматированнаяСтрока(ЧастиСтрокиВопроса), 
			РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.Отмена);
		
	Иначе
		ПослеПроверкиРежимаВыгрузкиДляТехническойПоддержки();	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте 
Процедура ПослеПроверкиРежимаВыгрузкиДляТехническойПоддержки()
	
	Попытка
		// Поддержка обратной совместимости.		
		ТекстПредупреждения = Вычислить(
			"ОбщегоНазначенияВызовСервераБТС.ТекстПредупрежденияОбАктивныхРасширенияхИзменяющихСтруктуруДанных()");
	Исключение
		ТекстПредупреждения = "";
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ЗапуститьПроверкуВыгружаемыхДанных();
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияВерхняя.Заголовок = ТекстПредупреждения;

	УстановитьСтраницуПредупреждениеРасширения(Элементы.Страницы.ТекущаяСтраница);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаДанныхДляТехническойПоддержкиПроверкаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ПослеПроверкиРежимаВыгрузкиДляТехническойПоддержки();	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте 
Процедура ЗапуститьВыгрузкуДанных()
	
	Оповещение = Новый ОписаниеОповещения("ЗапуститьВыгрузкуДанныхПослеУстановкиРасширения", ЭтотОбъект);
	
	Если ВыполняетсяСохранениеФайла И ЭтоВебКлиент() Тогда
		ТекстПредложения = НСтр("ru = 'Файл выгрузки может оказаться большим. В этом случае потребуется расширение для работы с 1С:Предприятием.
                                 |С этим расширением работа в веб-клиенте станет удобней не только при работе с большими файлами.'");
		ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботы1СПредприятия(Оповещение, ТекстПредложения);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Асинх Процедура ЗапуститьВыгрузкуДанныхПослеУстановкиРасширения(Подключено, ДополнительныеПараметры) Экспорт
	
	РасширениеРаботыСФайламиПодключено = Подключено;
	
	Если ВыполняетсяСохранениеФайла И Не РасширениеРаботыСФайламиПодключено И РазмерПриложенияБольшеДопустимого() Тогда
		УстановитьСтраницуРасширениеРаботыСФайламиНеПодключено(Элементы.Страницы.ТекущаяСтраница);
		Возврат;
	КонецЕсли;
	
	Если ВыполняетсяСохранениеФайла И РасширениеРаботыСФайламиПодключено Тогда
		
		ИмяФайлаНаКлиенте = ПолучитьИмяФайлаВыгрузкиДанных(РежимВыгрузкиДляТехническойПоддержки);
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Диалог.Заголовок = НСтр("ru = 'Получение файла выгрузки'");
		Диалог.ПолноеИмяФайла = ИмяФайлаНаКлиенте;
		Диалог.Фильтр = СтрШаблон(НСтр("ru = 'Архивы %1'"), "(*.zip)|*.zip");
		Результат = Ждать Диалог.ВыбратьАсинх();
		Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
			Закрыть();
			Возврат;
		КонецЕсли;
		ИмяФайлаНаКлиенте = Результат[0]; 
		ПотокЗаписи = Ждать ФайловыеПотоки.ОткрытьАсинх(ИмяФайлаНаКлиенте, РежимОткрытияФайла.Создать, ДоступКФайлу.Запись);
		ЗаписьДанных = Новый ЗаписьДанных(ПотокЗаписи);
			
	КонецЕсли;
	
	ЗапуститьВыгрузкуДанныхПослеДиалога();

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыгрузкуДанныхПослеДиалога()
	
	ЗапуститьВыгрузкуДанныхНаСервере();
	
	Заголовок = ЗаголовокВыгрузкаДанных();
	УстановитьКонтролыПоУмолчанию();
	Элементы.Назад.Доступность = Ложь;
	ПоказатьСостояниеОжидание("СостояниеКартинка");
	
	Элементы.СостояниеТекст.Видимость = Ложь;
	Элементы.ПредставлениеСостояния.Видимость = Истина;

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
	Элементы.Далее.Видимость = Ложь;
	Элементы.ПредставлениеСостояния.Видимость = Истина; 

	Если ВыполняетсяСохранениеФайла Тогда
		Элементы.СтраницаОжиданиеЗаголовокОписания.Видимость = Ложь;
	КонецЕсли; 
	
	ПодключитьОбработчикОжидания("ПроверитьГотовностьВыгрузки", 5, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьВыгрузкуДанныхНаСервере()
	
	Попытка
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ИмяМетодаВыгрузкиДанных = "ВыгрузкаЗагрузкаОбластейДанных.ВыгрузитьТекущуюОбластьВАрхив";
		
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(АдресХранилища);
		ПараметрыЗадания.Добавить(РежимВыгрузкиДляТехническойПоддержки);

		УстановитьПривилегированныйРежим(Истина);
		ДанныеСхемыКонфигурации = СхемаКонфигурации.ДвоичныеДанныеСхемы(Ложь, Ложь);
		ПараметрыЗадания.Добавить(ДанныеСхемыКонфигурации);
		
		Если ВыполняетсяСохранениеФайла И РасширениеРаботыСФайламиПодключено Тогда
			ПараметрыЗадания.Добавить(Неопределено);
		Иначе
			ИмяФайлаВременногоХранилища = ФайлыБТС.НовыйФайлВременногоХранилища("data2xml", "zip", 120);
			ФайлыБТС.ЗаблокироватьФайлВременногоХранилища(ИмяФайлаВременногоХранилища, УникальныйИдентификатор);
			ИмяФайлаВыгрузки = ФайлыБТС.ПолноеИмяФайлаВременногоХранилища(ИмяФайлаВременногоХранилища);

			ПараметрыЗадания.Добавить(ИмяФайлаВыгрузки);
		КонецЕсли;

		ВыгружатьДанныеРасширений = Не РаботаВМоделиСервиса.РазделениеВключено();
		ПараметрыЗадания.Добавить(ВыгружатьДанныеРасширений);
		
		ПараметрыВыгрузки = Новый Структура;
		ПараметрыВыгрузки.Вставить(
			"ВыгружатьЗарегистрированныеИзмененияДляУзловПланаОбмена",
			ВыгружатьЗарегистрированныеИзмененияДляУзловПланаОбмена);
		ПараметрыВыгрузки.Вставить(
			"КоличествоПотоков",
			КоличествоЗаданийВыгрузкиДанных);
		
		Если ПоддерживаетсяИнтерактивныйВызовПроверкиВыгружаемыхДанных() Тогда
			ПараметрыВыгрузки.Вставить(
				"ПропуститьПроверкуВыгружаемыхДанных",
				Истина);
		КонецЕсли;
				
		ИдентификаторСостояния = Новый УникальныйИдентификатор();
		ПараметрыВыгрузки.Вставить("ИдентификаторСостояния", ИдентификаторСостояния);
			 
		ПредставлениеСостояния =  ВыгрузкаЗагрузкаДанныхКлиентСервер.ПредставлениеСостоянияПодготовкиВыгрузкиЗагрузкиОбластиДанных(Ложь) 
			+ Символы.ПС 
			+ ВыгрузкаЗагрузкаДанныхКлиентСервер.ПодсказкаДлительнойОперации();
			
		Если ВыполняетсяСохранениеФайла И РасширениеРаботыСФайламиПодключено Тогда
			АдресЧастиВыгрузки = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
			ПараметрыВыгрузки.Вставить("ВыгрузитьНаКлиента", Истина);
		КонецЕсли;
					
		ПараметрыЗадания.Добавить(ПараметрыВыгрузки);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Задание = ФоновыеЗадания.Выполнить(ИмяМетодаВыгрузкиДанных,	ПараметрыЗадания,,	
			НСтр("ru = 'Подготовка выгрузки области данных'"));
			
		ИдентификаторЗадания = Задание.УникальныйИдентификатор;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Выгрузка данных.Интерактивный запуск'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Информация);
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ОбработатьОшибку(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ВызватьИсключение СтрШаблон(
			ШаблонОписанияОшибкиВыгрузкиДанных(), 
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));

	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьГотовностьВыгрузки() 
	
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Попытка
		СостояниеВыгрузки = СостояниеВыгрузки(
			ИдентификаторЗадания,
			ИдентификаторСостояния,
			АдресХранилища);
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ОбработатьОшибку(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ПоказатьОшибкуВыгрузкиДанных(ИнформацияОбОшибке);
		
		Возврат;
		
	КонецПопытки;
			
	Если СостояниеВыгрузки.ПредставлениеСостояния <> Неопределено Тогда	
		ПредставлениеСостояния  = СостояниеВыгрузки.ПредставлениеСостояния
			+ Символы.ПС 
			+ ВыгрузкаЗагрузкаДанныхКлиентСервер.ПодсказкаДлительнойОперации();
	КонецЕсли;
						
	ПроцентЗавершения = СостояниеВыгрузки.ПроцентЗавершения;
	Элементы.ГруппаПроцентЗавершения.Видимость = СостояниеВыгрузки.ПроцентЗавершения <> Неопределено; 
	
	Если ВыполняетсяСохранениеФайла И РасширениеРаботыСФайламиПодключено Тогда
		Для НомерЧасти = 1 По СостояниеВыгрузки.КоличествоЧастей Цикл
			ДвоичныеДанные = ПолучитьЧасть(ИдентификаторСостояния);
			Ждать ЗаписьДанных.ЗаписатьАсинх(ДвоичныеДанные);
		КонецЦикла;
	КонецЕсли;
						
	Если Не СостояниеВыгрузки.Завершена Тогда
		Прошло = ТекущаяУниверсальнаяДатаВМиллисекундах() - Начало;
	   	ПодключитьОбработчикОжидания("ПроверитьГотовностьВыгрузки", Макс(5 - Прошло / 1000, 0.1), Истина);
		Возврат;
	КонецЕсли;
		
	ОбработатьРезультатВыгрузкиНаСервере();

	Если ЗначениеЗаполнено(ТекстПредупрежденийВыгрузки) Тогда

		Если ВыполняетсяСохранениеФайла Тогда
			ШаблонВопроса = НСтр("ru = 'Часть данных не выгружена. Сохранить выгрузку? 
				|
				|%1'");
		Иначе
			ШаблонВопроса = НСтр("ru = 'Часть данных не выгружена. Продолжить выгрузку? 
				|
				|%1'");
		КонецЕсли;
		
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения(
			"ПодверждениеСохраненияФайлаВыгрузкиЗавершение",
			ЭтотОбъект);

		ТекстВопроса = СтрШаблон(ШаблонВопроса, ТекстПредупрежденийВыгрузки);
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.Предупреждение32;
		ПараметрыВопроса.Заголовок = НСтр("ru = 'Часть данных не выгружена'");

		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
				ОписаниеОповещенияОЗавершении,
				ТекстВопроса,
				РежимДиалогаВопрос.ОКОтмена,
				ПараметрыВопроса);
				
	Иначе
		
		ЗавершитьВыгрузку();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПодверждениеСохраненияФайлаВыгрузкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> Неопределено И РезультатВопроса.Значение = КодВозвратаДиалога.ОК Тогда
		ЗавершитьВыгрузку();
	Иначе
		Если ВыполняетсяСохранениеФайла Тогда
			Если РасширениеРаботыСФайламиПодключено Тогда
				Ждать ЗаписьДанных.ЗакрытьАсинх();
				Ждать ЗаписьДанных.ЦелевойПоток().ЗакрытьАсинх();
				Ждать УдалитьФайлыАсинх(ИмяФайлаНаКлиенте);
			КонецЕсли;
		КонецЕсли;
		УдалитьВременныеДанныеПослеСохранения();
		ЗакрытьБезусловно = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗавершитьВыгрузку()
	
	Если ВыполняетсяСохранениеФайла Тогда
		Если РасширениеРаботыСФайламиПодключено Тогда
			Ждать ЗаписьДанных.ЗакрытьАсинх();
			Ждать ЗаписьДанных.ЦелевойПоток().ЗакрытьАсинх();
			УстановитьСтраницуФайлПолучен(Элементы.Страницы.ТекущаяСтраница);
		Иначе
			СохранитьФайлВыгрузки();
		КонецЕсли;
	Иначе
		НачатьПередачуДанных(Элементы.Страницы.ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура СохранитьФайлВыгрузки()
	
	ИмяФайлаНаКлиенте = ПолучитьИмяФайлаВыгрузкиДанных(РежимВыгрузкиДляТехническойПоддержки);
	
	Если Не ЗначениеЗаполнено(АдресДанныхВыгрузки) Тогда
		Если РазмерФайлаВыгрузки <= ФайлыБТСКлиентСервер.ПриемлемыйРазмерВременногоХранилища() Тогда
			АдресДанныхВыгрузки = ПереместитьДанныеВыгрузкиИзФайлаВХранилище(ИмяФайлаВременногоХранилища,
				УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПередачи = ФайлыБТСКлиент.ПараметрыПолученияФайла();
	Если ЗначениеЗаполнено(АдресДанныхВыгрузки) Тогда
		ПараметрыПередачи.ИмяФайлаИлиАдрес = АдресДанныхВыгрузки;
	Иначе
		ПараметрыПередачи.ИмяФайлаИлиАдрес = ИмяФайлаВременногоХранилища;
	КонецЕсли;
	ПараметрыПередачи.ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеСохраненияФайлаВыгрузки", ЭтотОбъект);
	ПараметрыПередачи.БлокируемаяФорма = ЭтотОбъект;
	ПараметрыПередачи.ЗаголовокДиалогаСохранения = НСтр("ru = 'Получение файла выгрузки'");
	ПараметрыПередачи.ФильтрДиалогаСохранения = СтрШаблон(НСтр("ru = 'Архивы %1'"), "(*.zip)|*.zip");
	ПараметрыПередачи.ИмяФайлаДиалогаСохранения = ИмяФайлаНаКлиенте;
	
	ФайлыБТСКлиент.ПолучитьФайлИнтерактивно(ПараметрыПередачи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФайлаВыгрузки(ОписаниеФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеФайла = Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаВопросаОбОшибкеПолученияФайла", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'Файл выгрузки подготовлен, но не получен клиентом.%1Повторить попытку сохранения?'"),
			Символы.ПС);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	УдалитьВременныеДанныеПослеСохранения();
	
	ЗакрытьБезусловно = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаОбОшибкеПолученияФайла(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		УдалитьВременныеДанныеПослеСохранения();
		ЗакрытьБезусловно = Истина;
		Закрыть();
		Возврат;
		
	КонецЕсли; 
	
	СохранитьФайлВыгрузки();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВременныеДанныеПослеСохранения()
	
	Если Не ЗначениеЗаполнено(ИмяФайлаВременногоХранилища) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлыБТС.УдалитьФайлВременногоХранилища(ИмяФайлаВременногоХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуВыгрузкиДанных(Знач ИнформацияОбОшибке)
	
	ТекстОшибки = СтрШаблон(
		НСтр("ru = 'При выгрузке данных произошла ошибка.
		|%1'"),
		ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	Элементы.СтраницыОжидания.ТекущаяСтраница = Элементы.СтраницаОшибкаОжидания;
	Элементы.Назад.Доступность = Истина;

КонецПроцедуры

&НаСервереБезКонтекста																	
Функция СостояниеВыгрузки(ИдентификаторЗадания, ИдентификаторСостояния, АдресХранилища)
	
	СостояниеВыгрузки = Новый Структура();
	СостояниеВыгрузки.Вставить("Завершена", Ложь);
	СостояниеВыгрузки.Вставить("ПредставлениеСостояния", Неопределено);
	СостояниеВыгрузки.Вставить("ПроцентЗавершения", Неопределено);
			
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	ЗаданиеАктивно = Ложь;
			
	Если Задание = Неопределено Тогда
		РезультатВыгрузки = ПолучитьИзВременногоХранилища(АдресХранилища);
		Если РезультатВыгрузки = Неопределено Тогда
			ВызватьИсключение(НСтр("ru = 'При подготовке выгрузки произошла ошибка - не найдено задание подготавливающее выгрузку.'"));
		КонецЕсли;
	Иначе
		
		Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			
			ЗаданиеАктивно = Истина;
					
		ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ОшибкаЗадания = Задание.ИнформацияОбОшибке;
			Если ОшибкаЗадания <> Неопределено Тогда
				ВызватьИсключение(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ОшибкаЗадания));
			Иначе
				ВызватьИсключение(НСтр("ru = 'При подготовке выгрузки произошла ошибка - задание подготавливающее выгрузку завершилось с неизвестной ошибкой.'"));
			КонецЕсли;
		ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
			ВызватьИсключение(НСтр("ru = 'При подготовке выгрузки произошла ошибка - задание подготавливающее выгрузку было отменено администратором.'"));
		КонецЕсли;
				
	КонецЕсли;
	
 	СостояниеВыгрузки.Завершена = Не ЗаданиеАктивно;
 					
	СостояниеВыгрузкиЗагрузкиОбластиДанных = ВыгрузкаЗагрузкаДанных.СостояниеВыгрузкиЗагрузкиОбластиДанных(
		ИдентификаторСостояния);	
				
	Если ЗначениеЗаполнено(СостояниеВыгрузкиЗагрузкиОбластиДанных) Тогда
		СостояниеВыгрузки.ПредставлениеСостояния = ВыгрузкаЗагрузкаДанных.ПредставлениеСостоянияВыгрузкиЗагрузкиОбластиДанных(
			СостояниеВыгрузкиЗагрузкиОбластиДанных);
		СостояниеВыгрузки.ПроцентЗавершения = ВыгрузкаЗагрузкаДанных.ПроцентЗавершенияВыгрузкиЗагрузкиОбластиДанных(
			СостояниеВыгрузкиЗагрузкиОбластиДанных);
	КонецЕсли;
		
	Если Истина Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Идентификатор", ИдентификаторСостояния);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	РегистрСведений.ЧастиВыгрузкиЗагрузкиОбластейДанных КАК ЧастиВыгрузкиЗагрузкиОбластейДанных
		|ГДЕ
		|	ЧастиВыгрузкиЗагрузкиОбластейДанных.Идентификатор = &Идентификатор";
		УстановитьПривилегированныйРежим(Истина);
		СостояниеВыгрузки.Вставить("КоличествоЧастей", Запрос.Выполнить().Выгрузить()[0].Количество);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
		
	Возврат СостояниеВыгрузки;

КонецФункции

&НаСервере
Процедура ОбработатьРезультатВыгрузкиНаСервере()
		
	РезультатВыгрузки = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если РезультатВыгрузки = Неопределено Тогда
		ВызватьИсключение(НСтр("ru = 'При выгрузке данных произошла ошибка - не найден результат выгрузки'"));
	КонецЕсли;
			
	ТекстПредупрежденийВыгрузки = "";	
		
	Если ТипЗнч(РезультатВыгрузки) = Тип("Строка") Тогда
		ИмяФайлаВыгрузки = РезультатВыгрузки;
	Иначе
		ИмяФайлаВыгрузки = РезультатВыгрузки.ИмяФайла;
		Если ЗначениеЗаполнено(РезультатВыгрузки.Предупреждения) Тогда
			Разделитель = "
			|-----------------------------------------------
			|";
			
			ТекстПредупрежденийВыгрузки = СтрСоединить(РезультатВыгрузки.Предупреждения, Разделитель);
		КонецЕсли;
	КонецЕсли; 
	
	Если Не ВыполняетсяСохранениеФайла Тогда
		ОбъектФС = Новый Файл(ИмяФайлаВыгрузки);
		Если Не ОбъектФС.Существует() Или Не ОбъектФС.ЭтоФайл() Тогда
			ВызватьИсключение(НСтр("ru = 'При подготовке выгрузки произошла ошибка - не найден файл результата'"));
		КонецЕсли;
		РазмерФайлаВыгрузки = ОбъектФС.Размер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьЗаданиеПодготовки(Знач ИдентификаторЗадания)
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если Задание = Неопределено Или Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Задание.Отменить();
	Исключение
		// Возможно задание как раз в этот момент закончилось и ошибки нет
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Отмена выполнения задания подготовки выгрузки области данных'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьОшибку(Знач ПодробноеПредставление)
	
	ТекстЗаписиЖР = СтрШаблон(
		НСтр("ru = 'При выгрузке данных произошла ошибка:
			 |
			 |-----------------------------------------
			 |%1
			 |-----------------------------------------'"), ПодробноеПредставление);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Выгрузка данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка, , , ТекстЗаписиЖР);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПереместитьДанныеВыгрузкиИзФайлаВХранилище(ИмяФайлаВременногоХранилища, ИдентификаторФормы)

	ПолноеИмяФайла = ФайлыБТС.ПолноеИмяФайлаВременногоХранилища(ИмяФайлаВременногоХранилища);
	Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПолноеИмяФайла), ИдентификаторФормы);
	ФайлыБТС.УдалитьФайлВременногоХранилища(ИмяФайлаВременногоХранилища);
	ИмяФайлаВременногоХранилища = Неопределено;
	
	Возврат Адрес;

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяФайлаВыгрузкиДанных(Знач ЭтоРежимВыгрузкиДляТехническойПоддержки)
	
 	ЧастиИмениФайла = Новый Массив();
 	ЧастиИмениФайла.Добавить("data_dump");
 	
 	Если ЭтоРежимВыгрузкиДляТехническойПоддержки Тогда
 		ЧастиИмениФайла.Добавить("technical_support");
 	КонецЕсли;
 	
 	НомерОбласти = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
 	
 	Если ЗначениеЗаполнено(НомерОбласти) Тогда
 		ЧастиИмениФайла.Добавить(Формат(НомерОбласти, "ЧГ=0;"));
 	КонецЕсли;
 	
 	ЧастиИмениФайла.Добавить(СокрЛП(ВидыПриложенийСервер.ИмяТекущегоВидаПриложения()));
 	ЧастиИмениФайла.Добавить(СокрЛП(Метаданные.Версия));
 	
 	Возврат СтрСоединить(ЧастиИмениФайла, "_") + ".zip";
 	
КонецФункции 

#КонецОбласти 

#Область ПроверкаДанных

&НаКлиенте 
Процедура ЗапуститьПроверкуВыгружаемыхДанных() Экспорт
	
	Если Не ПоддерживаетсяИнтерактивныйВызовПроверкиВыгружаемыхДанных() Тогда
		ТекстОшибокВыгружаемыхДанных = Неопределено;
		ЗавершитьПроверкуВыгружаемыхДанных();
		Возврат;
	КонецЕсли;
	
	ЗапуститьПроверкуВыгружаемыхДанныхНаСервере();
	
	Заголовок = НСтр("ru = 'Проверка выгружаемых данных'");
	УстановитьКонтролыПоУмолчанию();
	Элементы.Назад.Доступность = Ложь;
	Элементы.Далее.Видимость = Ложь;
	
	ПоказатьСостояниеОжидание("СостояниеКартинка");
	Элементы.СостояниеТекст.Заголовок = 
		НСтр("ru = 'Выполняется проверка выгружаемых данных. Операция может занять длительное время.
				 |Пожалуйста, подождите...'"); 
	Элементы.СтраницаОжиданиеЗаголовокОписания.Видимость = Ложь;	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
	
	ИтерацияПроверки = 1;
	ПодключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных", 5);
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьПроверкуВыгружаемыхДанныхНаСервере()
	
	Попытка
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ИмяМетодаПроверкиДанных = "ВыгрузкаЗагрузкаДанных.ПоместитьОшибкиВыгружаемыхДанныхВоВременноеХранилище";
		
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(АдресХранилища);
			
		Задание = ФоновыеЗадания.Выполнить(ИмяМетодаПроверкиДанных,	ПараметрыЗадания,,	
			НСтр("ru = 'Проверка выгружаемых данных'"));
			
		ИдентификаторЗадания = Задание.УникальныйИдентификатор;
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ОбработатьОшибкуПроверкиВыгружаемыхДанных(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		ВызватьИсключение СтрШаблон(
				НСтр("ru = 'При проверке выгружаемых данных произошла ошибка: %1.'") + Символы.ПС + ПодсказкаПриОшибке(),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоддерживаетсяИнтерактивныйВызовПроверкиВыгружаемыхДанных()
	
	МинимальнаяПоддерживающаяВерсия = "2.0.11.5";
	Возврат ОбщегоНазначенияКлиентСервер.СравнитьВерсии(
		ТехнологияСервиса.ВерсияБиблиотеки(),
		МинимальнаяПоддерживающаяВерсия) >= 0;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗавершениеПроверкиВыгружаемыхДанных()
	
	Попытка
		ПроверкаЗавершена = ПроверкаВыгружаемыхДанныхЗавершена();
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ОтключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных");
		
		ОбработатьОшибкуПроверкиВыгружаемыхДанных(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		ПоказатьОшибкуПроверкиВыгружаемыхДанных(ИнформацияОбОшибке);
		
	КонецПопытки;
	
	Если ПроверкаЗавершена = Истина Тогда
		
		ОтключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных");
	
		ЗавершитьПроверкуВыгружаемыхДанных();
		
	Иначе
		
		ИтерацияПроверки = ИтерацияПроверки + 1;
		
		Если ИтерацияПроверки = 3 Тогда
			ОтключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных");
			ПодключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных", 15);
		ИначеЕсли ИтерацияПроверки = 4 Тогда
			ОтключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных");
			ПодключитьОбработчикОжидания("ПроверитьЗавершениеПроверкиВыгружаемыхДанных", 30);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПроверкуВыгружаемыхДанных()
	Если ЗначениеЗаполнено(ТекстОшибокВыгружаемыхДанных) Тогда
		Заголовок = НСтр("ru = 'Обнаружены ошибки в выгружаемых данных'");
		УстановитьКонтролыПоУмолчанию();
		Элементы.Назад.Доступность = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибкиВыгружаемыхДанных;
		Элементы.Далее.Заголовок = НСтр("ru = 'Повторить проверку'");
	Иначе
		Элементы.Далее.Заголовок = НСтр("ru = 'Далее'");
		Если СпособПерехода = СпособМиграция() Тогда
			ПерейтиВФормуМиграцииПриложения();
		Иначе
			ЗапуститьВыгрузкуДанных();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьОшибкуПроверкиВыгружаемыхДанных(Знач ПодробноеПредставление)
	
	ШаблонЗаписиЖР = НСтр("ru = 'При проверке выгружаемых данных произошла ошибка:
                           |
                           |-----------------------------------------
                           |%1
                           |-----------------------------------------'");
	ТекстЗаписиЖР = СтрШаблон(ШаблонЗаписиЖР, ПодробноеПредставление);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Проверка выгружаемых данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ТекстЗаписиЖР);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуПроверкиВыгружаемыхДанных(Знач ИнформацияОбОшибке)
	
	ТекстОшибки = СтрШаблон(
		НСтр("ru = 'При проверке выгружаемых данных произошла ошибка.
		|%1'"),
		ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	Элементы.СтраницыОжидания.ТекущаяСтраница = Элементы.СтраницаОшибкаОжидания;
	Элементы.Назад.Доступность = Истина;

КонецПроцедуры

&НаСервере
Функция ПроверкаВыгружаемыхДанныхЗавершена()
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
			
	Если Задание = Неопределено Тогда
		ОшибкиВыгружаемыхДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
		Если ОшибкиВыгружаемыхДанных = Неопределено Тогда
			ВызватьИсключение(НСтр("ru = 'При выполнении проверки выгружаемых данных произошла ошибка - не найдено задание выполняющее проверку.'"));
		КонецЕсли;
	Иначе
		
		Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Возврат Ложь;
		ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ОшибкаЗадания = Задание.ИнформацияОбОшибке;
			Если ОшибкаЗадания <> Неопределено Тогда
				ВызватьИсключение(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ОшибкаЗадания));
			Иначе
				ВызватьИсключение(НСтр("ru = 'При проверке выгружаемых данных произошла ошибка - задание выполняющие проверку завершилось с неизвестной ошибкой.'"));
			КонецЕсли;
		ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
			ВызватьИсключение(НСтр("ru = 'При проверке выгружаемых данных произошла ошибка - задание выполняющие проверку было отменено администратором.'"));
		КонецЕсли;
		
		ОшибкиВыгружаемыхДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
		
	КонецЕсли;
	
	ЧастиТекстаОшибки = Новый Массив;
	Для Каждого ОшибкаВыгружаемыхДанных Из ОшибкиВыгружаемыхДанных Цикл
		ЧастиТекстаОшибки.Добавить("● ");
		ЧастиТекстаОшибки.Добавить(ОшибкаВыгружаемыхДанных);
		ЧастиТекстаОшибки.Добавить(Символы.ПС);
	КонецЦикла;
	ТекстОшибокВыгружаемыхДанных = СтрСоединить(ЧастиТекстаОшибки);
		
	ИдентификаторЗадания = Неопределено;
	Возврат Истина;

КонецФункции

#КонецОбласти

#Область ОбработкаПереходов

&НаКлиенте
Процедура ПереходДалее(ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаПриветствие И ПроверитьЗаполнение() Тогда
		Если СпособПерехода = СпособФайл() Тогда
			УстановитьСтраницуВыгрузкиДанных(Элементы.Страницы.ТекущаяСтраница, Истина);
		Иначе
			УстановитьСтраницуВход(ТекущаяСтраница);
		КонецЕсли; 
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВход И ПроверитьЗаполнение() Тогда
		НачатьВходВСервис(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВыборВариантаРегистрации И ПроверитьЗаполнение() Тогда
		УстановитьСтраницуВводСведений(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВводСведений И ПроверитьЗаполнение() Тогда
		НачатьРегистрациюВСервисе(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВводКодаАктивации Тогда
		УстановитьСтраницуВход(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВыборСпособаПерехода И ПроверитьЗаполнение() Тогда
		Если Не ПроверитьВозможностьАвтоматическогоПереходаСРасширениями() Тогда
			УстановитьСтраницуСпособПерехода(ТекущаяСтраница);
		ИначеЕсли ПользователиИБ.Количество() > 0 И РасширенияДляВосстановления.Количество() = 0 Тогда
			НачатьСопоставлениеПользователей(ТекущаяСтраница);
		ИначеЕсли СпособПерехода = СпособМиграция() И РасширенияДляВосстановления.Количество() = 0 Тогда
			ЗапуститьПроверкуВыгружаемыхДанных();
		ИначеЕсли РасширенияДляВосстановления.Количество() > 0 Тогда 
			НачатьСопоставлениеРасширений(ТекущаяСтраница);
		Иначе			
			УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Ложь);
		КонецЕсли;
	
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаРасширения И ПроверитьЗаполнение() 
		И ПроверитьЗаполнениеРасширенийДляВосстановления() Тогда
		Если ПользователиИБ.Количество() > 0 Тогда
			НачатьСопоставлениеПользователей(ТекущаяСтраница);
		ИначеЕсли СпособПерехода = СпособМиграция() Тогда
			ЗапуститьПроверкуВыгружаемыхДанных()
		Иначе			
			УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Ложь);
		КонецЕсли;
			
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаСопоставлениеПользователей И ПроверитьЗаполнение() Тогда
		Если СпособПерехода = СпособМиграция() Тогда
			ЗапуститьПроверкуВыгружаемыхДанных();
		Иначе			
			УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Ложь);
		КонецЕсли;

	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанных Тогда
		НачатьВыгрузкуДанных();
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаЗавершение Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаФайлПолучен Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаРасширениеРаботыСФайламиНеПодключено Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаПредупреждениеРасширения
		Или ТекущаяСтраница = Элементы.СтраницаОшибкиВыгружаемыхДанных Тогда
		ЗапуститьПроверкуВыгружаемыхДанных();			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходНазад(ТекущаяСтраница)
	
	Элементы.Назад.Видимость = Истина;
	Если ТекущаяСтраница = Элементы.СтраницаВыборВариантаРегистрации Тогда
		УстановитьСтраницуВход(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВводСведений Тогда
		Если ВариантРегистрации = ВариантЗапросНаРегистрацию()  Тогда
			УстановитьСтраницуВход(ТекущаяСтраница);
		Иначе
			УстановитьСтраницуВыборВариантаРегистрации(ТекущаяСтраница);
		КонецЕсли; 
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВход Тогда
		УстановитьСтраницуПриветствие(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаОжидание Тогда
		Если СпособПерехода = СпособФайл() Тогда
			ТекущаяСтраница = Элементы.СтраницаПриветствие;
			УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Истина);
		ИначеЕсли ИмяСтраницыПереходаКВыгрузке = Элементы.СтраницаВыборСпособаПерехода.Имя Тогда
			ТекущаяСтраница = Элементы.СтраницаВыборСпособаПерехода;
			УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Истина);
		ИначеЕсли Не ВыполняласьРегистрация Тогда
			УстановитьСтраницуВход(ТекущаяСтраница);
		Иначе
			УстановитьСтраницуВводСведений(ТекущаяСтраница);
		КонецЕсли;
		Элементы.СтраницыОжидания.ТекущаяСтраница = Элементы.СтраницаОписаниеОжидания;
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВыборСпособаПерехода Тогда
		УстановитьСтраницуВход(ТекущаяСтраница);
	
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаРасширения Тогда
		УстановитьСтраницуСпособПерехода(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаСопоставлениеПользователей Тогда
		Если СпособПерехода = СпособМиграция() И РасширенияДляВосстановления.Количество() > 0 Тогда
			УстановитьСтраницуСопоставлениеРасширений(ТекущаяСтраница);
		Иначе
			УстановитьСтраницуСпособПерехода(ТекущаяСтраница);
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанных Тогда
		Если ВыполняетсяСохранениеФайла Тогда
			Если ИмяСтраницыПереходаКВыгрузке = Элементы.СтраницаПриветствие.Имя Тогда
				УстановитьСтраницуПриветствие(ТекущаяСтраница);
			Иначе
				УстановитьСтраницуСпособПерехода(ТекущаяСтраница);
			КонецЕсли;
		ИначеЕсли ПользователиИБ.Количество() > 0 Тогда
			УстановитьСтраницуСопоставлениеПользователей(ТекущаяСтраница);
		Иначе
			УстановитьСтраницуСпособПерехода(ТекущаяСтраница);
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВводКодаАктивации Тогда
		УстановитьСтраницуВход(ТекущаяСтраница);
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаПредупреждениеРасширения Тогда
		УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКонтролыПоУмолчанию()
	
	Элементы.Далее.Заголовок = НСтр("ru = 'Далее'");
	Элементы.Далее.Видимость = Истина;
	Элементы.Далее.Доступность = Истина;
	
	Элементы.Назад.Заголовок = НСтр("ru = '< Назад'");
	Элементы.Назад.Видимость = Истина;
	Элементы.Назад.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуПриветствие(ТекущаяСтраница)
	
	Заголовок = ЗаголовокПриветствия;
	УстановитьКонтролыПоУмолчанию();
	
	Элементы.Назад.Видимость = Ложь;
	Если СпособПерехода = СпособМиграция() Тогда
		СпособПерехода = ?(ЗначениеЗаполнено(АдресСервиса), СпособВыгрузка(), СпособФайл());
		ПриИзмененииСпособаПерехода();
	КонецЕсли; 
	
	ТекущаяСтраница = Элементы.СтраницаПриветствие;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуВход(ТекущаяСтраница)
	
	Заголовок = ЗаголовокВход();
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Заголовок = НСтр("ru = 'Войти'");
	
	ТекущаяСтраница = Элементы.СтраницаВход;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуВыборВариантаРегистрации(ТекущаяСтраница)
	
	Если КоличествоОбслуживающихОрганизаций = 0 Тогда
		Заголовок = НСтр("ru = 'Шаг регистрации 1 из 2: Выбор варианта'");
		Элементы.ВыбратьОрганизациюЗаголовок.Видимость = Ложь;
		Элементы.ЗаголовокВвестиКодАктивации.Видимость = Ложь;
		Элементы.ПодобратьООАвтоматически.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.ПодобратьООАвтоматически.СписокВыбора[0].Представление = НСтр("ru = 'Начать новую регистрацию'"); 
	Иначе
		Заголовок = НСтр("ru = 'Шаг регистрации 1 из 2: Выбор организации'");
		Элементы.ВыбратьОрганизациюЗаголовок.Видимость = Истина;
		Элементы.ЗаголовокВвестиКодАктивации.Видимость = Истина;
		Элементы.ПодобратьООАвтоматически.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ПодобратьООАвтоматически.СписокВыбора[0].Представление = 
			НСтр("ru = 'Подобрать обслуживающую организацию автоматически'"); 
	КонецЕсли;
	КодАктивации = Неопределено;
	УстановитьКонтролыПоУмолчанию();
	Элементы.ГруппаВыбратьОО.Видимость = Не (КоличествоОбслуживающихОрганизаций = 0);
	ТекущаяСтраница = Элементы.СтраницаВыборВариантаРегистрации;

КонецПроцедуры
 
&НаКлиенте
Процедура УстановитьСтраницуВводСведений(ТекущаяСтраница)
	
	Если ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
		Заголовок = НСтр("ru = 'Шаг регистрации 1 из 2: Ввод сведений'") 
	Иначе
		Заголовок = НСтр("ru = 'Шаг регистрации 2 из 2: Ввод сведений'");
	КонецЕсли;
	
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Заголовок = НСтр("ru = 'Зарегистрироваться'");
	
	Если ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
		Элементы.ИнформацияВариантаРегистрации.ТекущаяСтраница = Элементы.СтраницаИнформацияПоОрганизации;
		Элементы.ДанныеВыбраннойОрганизации.Заголовок = СтрокаКонтактовСлужбыПоддержки();
	ИначеЕсли ВариантРегистрации = ВариантВводКодаАктивации() Тогда
		Элементы.ИнформацияВариантаРегистрации.ТекущаяСтраница = Элементы.СтраницаКодАктивации;
		ПроверитьКодАктивации();
	Иначе
		Элементы.ИнформацияВариантаРегистрации.ТекущаяСтраница = Элементы.СтраницаИнформацияПоОрганизации;
		Если ВариантРегистрации = ВариантВыборОО() Тогда
			ЗаголовокДанныхВыбора = СтрокаКонтактовОбслуживающейОрганизации();
		ИначеЕсли ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
			ЗаголовокДанныхВыбора = СтрокаКонтактовСлужбыПоддержки();
		Иначе
			ЗаголовокДанныхВыбора = ?(КоличествоОбслуживающихОрганизаций = 0, 
				СтрокаКонтактовСлужбыПоддержки(), СтрокаАвтоподбораОрганизации());
		КонецЕсли; 
		Элементы.ДанныеВыбраннойОрганизации.Заголовок = ЗаголовокДанныхВыбора;
	КонецЕсли;
	
	Элементы.КодАктивации.Видимость = (ВариантРегистрации = ВариантВводКодаАктивации());
	Элементы.РегистрацияПочта.ТолькоПросмотр = (ВариантРегистрации = ВариантВводКодаАктивации());
	ТекущаяСтраница = Элементы.СтраницаВводСведений;
	
	Если ВариантРегистрации = ВариантВводКодаАктивации() Тогда
		ТекущийЭлемент = Элементы.КодАктивации;
	КонецЕсли; 
	
	ПоказатьПризнакЗаполнения(РегистрацияИмя, Элементы.ПроверкаИмени.Имя);
	ПроверитьАдресПочты();
	ПоказатьПризнакЗаполнения(РегистрацияТелефон, Элементы.ПроверкаТелефона.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуВводКодаАктивации(ТекущаяСтраница)
	
	Заголовок = НСтр("ru = 'Шаг регистрации 2 из 2: Ввод кода активации'");
	УстановитьКонтролыПоУмолчанию();

	ТекущаяСтраница = Элементы.СтраницаВводКодаАктивации;
	
КонецПроцедуры
 
&НаКлиенте
Процедура УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания)
	
	Заголовок = ПараметрыОжидания.ЗаголовокСтраницы;
	Если ВыполняетсяСохранениеФайла Тогда
		Элементы.СтраницаОжиданиеЗаголовокОписания.Видимость = Ложь;
	Иначе
		Элементы.СтраницаОжиданиеЗаголовокОписания.Видимость = ПараметрыОжидания.ОтображатьЗаголовокОписания;
	КонецЕсли;
	
	Элементы.СтраницаОжиданияОписание.Заголовок = ПараметрыОжидания.ОписаниеОжидания;
	
	Элементы.ПредставлениеСостояния.Видимость = Ложь;
	Элементы.СостояниеТекст.Видимость = Истина;
	Элементы.СостояниеТекст.Заголовок = Символы.ПС + ПараметрыОжидания.ОписаниеСостояния;
	
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Видимость = Ложь;
	Элементы.Назад.Доступность = Ложь;
	
	ПоказатьСостояниеОжидание("СостояниеКартинка");
	
	ТекущаяСтраница = Элементы.СтраницаОжидание;
	СчетчикОжидания = 0;
	ДопустимоеОжидание = ПараметрыОжидания.ДопустимоеОжидание;
	ПроверитьРезультатЗапроса();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуСпособПерехода(ТекущаяСтраница)
	
	Если РазделениеВключено Тогда
		ШаблонЗаголовка = НСтр("ru = 'Шаг 2 из %1: Выбор способа переноса'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Шаг 2 из %1: Выбор способа перехода'");
	КонецЕсли;
	
	Заголовок = СтрШаблон(ШаблонЗаголовка, ?(ПользователиИБ.Количество() = 0, 3, 4));
	УстановитьКонтролыПоУмолчанию();
	Если МиграцияДоступна Или ВыгрузкаДоступна Тогда
		Элементы.Далее.Доступность = Истина;
		СпособПерехода = СпособВыгрузка();
	Иначе 
		Элементы.Далее.Доступность = Ложь;
		СпособПерехода = -1;
	КонецЕсли;
	
	ТекущаяСтраница = Элементы.СтраницаВыборСпособаПерехода;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуСопоставлениеПользователей(ТекущаяСтраница)
	
	Заголовок = ЗаголовокСопоставлениеПользователей();
	ТекущаяСтраница = Элементы.СтраницаСопоставлениеПользователей;
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Заголовок = НСтр("ru = 'Продолжить'");
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьСтраницуСопоставлениеРасширений(ТекущаяСтраница)
	
	Заголовок = ЗаголовокСопоставлениеРасширений();
	ТекущаяСтраница = Элементы.СтраницаРасширения;
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Заголовок = НСтр("ru = 'Продолжить'");
	
КонецПроцедуры

&НаКлиенте 
Процедура УстановитьСтраницуВыгрузкиДанных(ТекущаяСтраница, ЭтоСохранениеФайла)
	
	ВыполняетсяСохранениеФайла = ЭтоСохранениеФайла;
	ИмяСтраницыПереходаКВыгрузке = ТекущаяСтраница.Имя;
	
	Заголовок = ЗаголовокВыгрузкаДанных();
	Элементы.ГруппаНаименованиеПриложения.Видимость = Не ВыполняетсяСохранениеФайла;
	
	ТекущаяСтраница = Элементы.СтраницаВыгрузкаДанных;
	УстановитьКонтролыПоУмолчанию();
	Элементы.Далее.Заголовок = ?(ВыполняетсяСохранениеФайла, НСтр("ru = 'Продолжить'"), НСтр("ru = 'Выгрузить данные'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуЗавершение(ТекущаяСтраница)
	
	Заголовок = НСтр("ru = 'Выгрузка завершена'");
	ТекущаяСтраница = Элементы.СтраницаЗавершение;
	УстановитьКонтролыПоУмолчанию();
	
	Элементы.Далее.Заголовок = НСтр("ru = 'Завершить'");
    Элементы.Назад.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуПредупреждениеРасширения(ТекущаяСтраница)
	
	Заголовок = НСтр("ru = 'Активны расширения конфигурации, изменяющие структуру данных'");
	ТекущаяСтраница = Элементы.СтраницаПредупреждениеРасширения;
	УстановитьКонтролыПоУмолчанию();
	
	Элементы.ДекорацияСредняя.Видимость = РазделениеВключено;
	Элементы.Далее.Заголовок = НСтр("ru = 'ОК'");	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуФайлПолучен(ТекущаяСтраница)
	
	Заголовок = НСтр("ru = 'Выгрузка завершена'");
	ТекущаяСтраница = Элементы.СтраницаФайлПолучен;
	УстановитьКонтролыПоУмолчанию();
	
	Элементы.Далее.Заголовок = НСтр("ru = 'Завершить'");
    Элементы.Назад.Видимость = Ложь;
	Элементы.Отмена.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуРасширениеРаботыСФайламиНеПодключено(ТекущаяСтраница)
	
	Заголовок = НСтр("ru = 'Выгрузка отменена'");
	ТекущаяСтраница = Элементы.СтраницаРасширениеРаботыСФайламиНеПодключено;
	УстановитьКонтролыПоУмолчанию();
	
	Элементы.Далее.Заголовок = НСтр("ru = 'Завершить'");
    Элементы.Назад.Видимость = Ложь;
	Элементы.Отмена.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВФормуМиграцииПриложения()
	
	ФормаМиграции = ПолучитьФорму("Обработка.МастерПереходаВОблако.Форма.МиграцияПриложения");
	ФормаМиграции.ВерсияПрограммногоИнтерфейса = ВерсияПрограммногоИнтерфейса;
	
	ЧастиАдреса = СтрРазделить(НРег(СокрЛП(АдресСервиса)), "/", Ложь);
	Протокол = ЧастиАдреса[0];
	Если СтрЗаканчиваетсяНа(Протокол, ":") Тогда
		ЧастиАдреса.Удалить(0);
	КонецЕсли;
	ИмяСервера = ЧастиАдреса[0];
	
	ФормаМиграции.АдресСервиса = "https://" + ИмяСервера;
	ФормаМиграции.Логин = Логин;
	ФормаМиграции.Пароль = Пароль;
	ФормаМиграции.КодАбонента = КодАбонента;
	ФормаМиграции.АдресПрограммногоИнтерфейса = АдресПрограммногоИнтерфейса;
	ФормаМиграции.АдресРегистрации = АдресРегистрации;
	ФормаМиграции.АдресВосстановления = АдресВосстановления;
	ФормаМиграции.РегистрацияРазрешена = ДоступенЗапросНаРегистрацию;
	ФормаМиграции.СостояниеРегистрацииВСервисе = "Зарегистрирован";
	ФормаМиграции.Элементы.Назад.Видимость = Ложь;
	
	РольВладелец = "Владелец";
	РольПользователь = "Пользователь";
	ПраваПользователейМиграции = ФормаМиграции.ПраваПользователей; // ТаблицаЗначений
	
	Если ПраваПользователей.Количество() > 0 Тогда
		Для Каждого Строка Из ПраваПользователей Цикл
			Если ЗначениеЗаполнено(Строка.Логин) Тогда
				НоваяСтрока = ПраваПользователейМиграции.Добавить();
				НоваяСтрока.Логин = Строка.Логин;
				НоваяСтрока.Наименование = Строка.ПолноеИмя;
				НоваяСтрока.Пользователь = Строка.Пользователь;
				НоваяСтрока.Право = ПредставлениеПраваПользователя(Строка.Право);
				НоваяСтрока.ЭлектроннаяПочта = Строка.ЭлектроннаяПочта;
				НоваяСтрока.Роль = ?(Строка.ЭтоВладелецАбонента, РольВладелец, РольПользователь);
			КонецЕсли;
		КонецЦикла;
	Иначе
		НоваяСтрока = ПраваПользователейМиграции.Добавить();
		НоваяСтрока.Логин = Логин;
		НоваяСтрока.Наименование = АбонентНаименование;
		НоваяСтрока.Право = ПредставлениеПраваПользователя(ПравоЗапускИАдминистрирование());
		НоваяСтрока.Роль = РольВладелец; 
	КонецЕсли;
		
	Для Каждого СтрокаРасширение Из РасширенияДляВосстановления Цикл
		Если Не СтрокаРасширение.Восстановить Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ФормаМиграции.РасширенияДляВосстановления.Добавить();
		НоваяСтрока.Имя = СтрокаРасширение.ИспользоватьРасширениеИмя;
		НоваяСтрока.Версия = СтрокаРасширение.Версия;
	КонецЦикла;
	
	ЗакрытьБезусловно = Истина;
	Закрыть();
	ФормаМиграции.Открыть();
	ФормаМиграции.Элементы.Страницы.ТекущаяСтраница = ФормаМиграции.Элементы.СтраницаСозданиеПриложение;

КонецПроцедуры

&НаКлиенте
Функция ЗаголовокВход()
	
	Возврат СтрШаблон(НСтр("ru = 'Шаг 1 из %1: Вход в облачный сервис'"), ВсегоШаговМастера());

КонецФункции

&НаКлиенте
Функция ЗаголовокСопоставлениеПользователей()
	
	Возврат СтрШаблон(НСтр("ru = 'Шаг 3 из %1: Сопоставление пользователей'"), ВсегоШаговМастера());

КонецФункции

&НаКлиенте
Функция ЗаголовокСопоставлениеРасширений()
	
	
	Возврат СтрШаблон(НСтр("ru = 'Шаг 3 из %1: Сопоставление расширений'"), ВсегоШаговМастера());

КонецФункции

&НаКлиенте
Функция ВсегоШаговМастера()
	ВсегоШагов = 3;
	Если ПользователиИБ.Количество() > 0 Тогда
		ВсегоШагов = ВсегоШагов + 1;
	КонецЕсли;
	Если РасширенияДляВосстановления.Количество() > 0 Тогда
		ВсегоШагов = ВсегоШагов + 1;
	КонецЕсли;
	Возврат ВсегоШагов;
КонецФункции
	
&НаКлиенте
Функция ЗаголовокВыгрузкаДанных()
	
	Возврат ?(ВыполняетсяСохранениеФайла, 
		НСтр("ru = 'Выгрузка данных'"), 
		СтрШаблон(НСтр("ru = 'Шаг %1 из %1: Выгрузка данных'"), ?(ПользователиИБ.Количество() = 0, 4, 5)));

КонецФункции

#КонецОбласти 

#Область ЗаполнениеНастроекОблачногоСервиса

&НаКлиенте
Процедура ПроверитьАдресСервиса()
	
	Если ЗначениеЗаполнено(АдресСервиса) Тогда
		Элементы.Далее.Доступность = Ложь;
		ПоказатьСостояниеОжидание(Элементы.ПроверкаАдресаСервиса.Имя);
		ПараметрыЗапроса = ШаблонПараметровЗапросов();
		ПараметрыЗапроса.Вставить("Имя", ИмяКонфигурации);
		ПараметрыЗапроса.Вставить("Версия", ВерсияКонфигурации);
		НачатьЗапросПоНастройкам(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
		ПроверитьРезультатЗапроса();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьЗапросПоНастройкам(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	ОбработчикРезультата = "Подключаемый_ЗаполнитьНастройки";
	ИмяМететода = "ПолучитьИнформациюОВозможностяхПерехода";
	ПараметрыЗапроса.Вставить("МетодИнформация", "info");
	ПараметрыЗапроса.Вставить("МетодВариантыЗагрузки", 
		СтрШаблон("import-options?name=%1&version=%2", ПараметрыЗапроса.Имя, ПараметрыЗапроса.Версия));
    НачатьВыполнениеФоновогоЗаданияНаСервере(ИмяМететода, ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьНастройки(Результат, ДополнительныеПараметры) Экспорт
	
	// Если в процессе получения данных был выбран другой способ перехода, 
	// нужно отказаться от заполнения.
	Если Не СпособПерехода = СпособВыгрузка() Или ТипЗнч(Результат) <> Тип("Структура") Или Не Результат.Свойство(
		"Информация") Тогда
		Возврат;
	КонецЕсли;
	
	Отладка_КонфигурацияНеПоддерживается = (ПараметрЗапуска = "ОтладкаПереходаВОблако/КонфигурацияНеПоддерживается");
	Отладка_ОтсутствуютОбслуживающиеОрганизации = (ПараметрЗапуска = "ОтладкаПереходаВОблако/ОтсутствуютОбслуживающиеОрганизации");
	Отладка_МиграцияНеДоступна = (ПараметрЗапуска = "ОтладкаПереходаВОблако/МиграцияНеДоступна");
	Отладка_МиграцияДоступна = (ПараметрЗапуска = "ОтладкаПереходаВОблако/МиграцияДоступна");
	Отладка_ВыгрузкаНеДоступна = (ПараметрЗапуска = "ОтладкаПереходаВОблако/ВыгрузкаНеДоступна");
	Отладка_ПриглашениеДляРегистрацииНеДоступно = (ПараметрЗапуска = "ОтладкаПереходаВОблако/ПриглашениеДляРегистрацииНеДоступно");
	
	ИмяЭлементаСостояния = Элементы.ПроверкаАдресаСервиса.Имя;
	Если Результат.Информация.КодСостояния <> 200 Тогда
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
		
		Если РазделениеВключено Тогда
			ШаблонСообщения = НСтр("ru = 'Облачный сервис не найден или не поддерживает интерактивный перенос в сервис.'");			
		Иначе
			ШаблонСообщения = НСтр("ru = 'Облачный сервис не найден или не поддерживает интерактивный переход в сервис.'");			
		КонецЕсли; 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ШаблонСообщения, ,
			Элементы.АдресСервиса.Имя, Элементы.АдресСервиса.Имя);
		Возврат;
	КонецЕсли;
	
	Данные = Результат.Информация.Данные;
	
	Если Не Данные.Enabled Тогда
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
		
		Если РазделениеВключено Тогда
			ШаблонСообщения =  НСтр("ru = 'Облачный сервис не поддерживает интерактивный перенос в сервис.'");
		Иначе
			ШаблонСообщения =  НСтр("ru = 'Облачный сервис не поддерживает интерактивный переход в сервис.'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ШаблонСообщения, ,
			Элементы.АдресСервиса.Имя, Элементы.АдресСервиса.Имя);
		Возврат;
	КонецЕсли; 
	
	АдресРегистрации = Данные.url_register;
	АдресВосстановления = Данные.url_recover;
	АдресПрограммногоИнтерфейса = Данные.url_api;
	Данные.Свойство("api_version", ВерсияПрограммногоИнтерфейса);
	
	ВосстановлениеРегистрация = Новый Массив;
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Забыли логин или пароль?'")));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(" "));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Восстановить'"),,,, Данные.url_recover));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(Символы.ПС + Символы.ПС));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Не зарегистрированы в облаке?'")));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(" "));
	ВосстановлениеРегистрация.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Зарегистрироваться'"),,,,  СсылкаРегистрация()));
	Элементы.ВосстановлениеРегистрация.Заголовок = Новый ФорматированнаяСтрока(ВосстановлениеРегистрация);
	
	Данные.Свойство("register_available", ДоступенЗапросНаРегистрацию);
	Если Не Отладка_ПриглашениеДляРегистрацииНеДоступно Тогда
		Данные.Свойство("invitation_available", ДоступноПриглашениеДляРегистрации);
	КонецЕсли;
	Данные.Свойство("support_companies_available", ИспользоватьОбслуживающиеОрганизации);
	Данные.Свойство("url_adm", АдресЛичногоКабинета);
	Данные.Свойство("url_epf", АдресОбработкиНаСервере);
	
	Если ИспользоватьОбслуживающиеОрганизации И Не Отладка_ОтсутствуютОбслуживающиеОрганизации Тогда
		Данные.Свойство("support_companies_count", КоличествоОбслуживающихОрганизаций);
	КонецЕсли;
	
	ПолеКонтактыПровайдера = "provider_contacts";
	Если Данные.Свойство(ПолеКонтактыПровайдера) Тогда
		КонтактыПровайдера = Данные[ПолеКонтактыПровайдера];
		ПровайдерГород = КонтактыПровайдера.city;
		ПровайдерСайт = КонтактыПровайдера.site;
		ПровайдерТелефон = КонтактыПровайдера.phone;
		ПровайдерПочта = КонтактыПровайдера.email;
	КонецЕсли; 
	
	Если Данные.applications.Найти(ИмяКонфигурации) = Неопределено Или Отладка_КонфигурацияНеПоддерживается Тогда
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Облачный сервис не поддерживает конфигурацию ''%1'''"), СинонимКонфигурации),, 
			Элементы.АдресСервиса.Имя, Элементы.АдресСервиса.Имя);
		Элементы.Далее.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Ошибка = Ложь;
	СообщениеОбОшибке = "";
	Данные = Результат.ВариантыЗагрузки.Данные;
	Если Результат.ВариантыЗагрузки.Ошибка Тогда
		Ошибка = Истина;
		СообщениеОбОшибке = Результат.ВариантыЗагрузки.СообщениеОбОшибке;
	ИначеЕсли Данные.error Тогда 
		Ошибка = Истина;
		СообщениеОбОшибке = Данные.description;
	КонецЕсли;
	
	Если Ошибка Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке,, Элементы.АдресСервиса.Имя);
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
	Иначе
		Если Отладка_МиграцияДоступна Тогда
			МиграцияДоступна = Истина;
		ИначеЕсли Отладка_МиграцияНеДоступна Тогда
			МиграцияДоступна = Ложь;
		Иначе
			МиграцияДоступна = Данные.migration;
		КонецЕсли; 
		Если Не Отладка_ВыгрузкаНеДоступна Тогда
			ВыгрузкаДоступна = Данные.upload;
		КонецЕсли;
		МинимальнаяВерсияДляВыгрузки = Данные.upload_min_version;
		Для Каждого ПоддерживаемаяВерсия Из Данные.migration_versions Цикл
			Если ПоддерживаемыеВерсииДляМиграции.НайтиПоЗначению(ПоддерживаемаяВерсия) = Неопределено Тогда
				ПоддерживаемыеВерсииДляМиграции.Добавить(ПоддерживаемаяВерсия);
			КонецЕсли;
		КонецЦикла;
		УстановитьВидимостьПоСостояниюОбновления();
		ПоказатьСостояниеГотово(ИмяЭлементаСостояния);
		Элементы.Далее.Доступность = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаАдресаЭлектроннойПочты

&НаКлиенте
Процедура ПроверитьАдресПочты()
	
	Если ЗначениеЗаполнено(РегистрацияПочта) И ВариантРегистрации <> ВариантВводКодаАктивации() Тогда
		ПоказатьСостояниеОжидание(Элементы.ПроверкаПочты.Имя);
		ПараметрыЗапроса = ШаблонПараметровЗапросов();
		ПараметрыЗапроса.Вставить("Почта", РегистрацияПочта);
		НачатьПроверкуПочты(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
		ПроверитьРезультатЗапроса();
	Иначе
		ПоказатьСостояниеПусто("ПроверкаПочты");
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьПроверкуПочты(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	ОбработчикРезультата = "Подключаемый_ПоказатьРезультатПроверкиАдресаЭлектроннойПочты"; 
	ПараметрыЗапроса.Вставить("Метод", СтрШаблон("email-available?email=%1", 
		КодироватьСтроку(ПараметрыЗапроса.Почта, СпособКодированияСтроки.КодировкаURL)));
	
	НачатьВыполнениеФоновогоЗаданияНаСервере("ПолучитьДанные", ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьРезультатПроверкиАдресаЭлектроннойПочты(Результат, ДополнительныеПараметры) Экспорт
	
	Ошибка = Ложь;
	СообщениеОбОшибке = "";
	ИмяЭлементаСостояния = Элементы.ПроверкаПочты.Имя;
	Данные = Результат.Данные;
	Если Результат.Ошибка Тогда
		Ошибка = Истина;
		СообщениеОбОшибке = Результат.СообщениеОбОшибке;
	ИначеЕсли Данные.error Тогда 
		Ошибка = Истина;
		СообщениеОбОшибке = Данные.description;
	КонецЕсли;
	
	Если Ошибка Тогда
		Если ВариантРегистрации <> ВариантЗапросНаРегистрацию() И  Результат.КодСостояния = 409 Тогда
			СообщениеОбОшибке = СообщениеОбОшибке + Символы.ПС + СтрШаблон(
				НСтр("ru = 'Регистрировались ранее? Вернитесь на шаг назад и выберите вариант ''%1''.'"),
				Элементы.ВвестиКодАктивации.СписокВыбора[0].Представление);
		КонецЕсли; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке, , Элементы.РегистрацияПочта.Имя);
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
	Иначе	
		ПоказатьСостояниеГотово(ИмяЭлементаСостояния);
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти 

#Область ЗаполнениеДанныхАктивации

&НаКлиенте
Процедура ПроверитьКодАктивации()
	
	ИмяЭлементаСостояния = Элементы.ПроверкаКодаАктивации.Имя;
	Если ЗначениеЗаполнено(КодАктивации) Тогда
		ПоказатьСостояниеОжидание(ИмяЭлементаСостояния);
		ПараметрыЗапроса = ШаблонПараметровЗапросов();
		ПараметрыЗапроса.Вставить("КодАктивации", КодАктивации);
		НачатьЗапросПоКодуАктивации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
		ПроверитьРезультатЗапроса();
	Иначе
		ПоказатьСостояниеПусто(ИмяЭлементаСостояния);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьЗапросПоКодуАктивации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	ОбработчикРезультата = "Подключаемый_ЗаполнитьДанныеПоКодуАктивации";
	ПараметрыЗапроса.Вставить("Метод", СтрШаблон("reg-info?code=%1", 
		КодироватьСтроку(ПараметрыЗапроса.КодАктивации, СпособКодированияСтроки.КодировкаURL)));
    НачатьВыполнениеФоновогоЗаданияНаСервере("ПолучитьДанные", ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьДанныеПоКодуАктивации(Результат, ДополнительныеПараметры) Экспорт
	
	Данные = Результат.Данные;
	ИмяЭлементаСостояния = Элементы.ПроверкаКодаАктивации.Имя;
	
	Если Результат.Ошибка Тогда
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке,, 
			Элементы.КодАктивации.Имя);
	 
	ИначеЕсли Данные.Error Тогда
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
		ОбщегоНазначенияКлиент.СообщитьПользователю(Данные.description,, 
			Элементы.КодАктивации.Имя);
	Иначе
		ПоказатьСостояниеГотово(ИмяЭлементаСостояния);
		Информация = Данные.info;
		
		Если Данные.type = ТипРегистрацииПриглашение() Тогда
			ИнформацияОО = Данные.supportCompany;
			ООКод = ИнформацияОО.id;
			ООГород = ИнформацияОО.city;
			ООНаименование = ИнформацияОО.name;
			ООСайт = ИнформацияОО.site;
			ООТелефон = ИнформацияОО.phone;
			ООПочта = ИнформацияОО.email;
		Иначе
			ВариантРегистрации = ВариантЗапросНаРегистрацию();
			УстановитьСтраницуВводКодаАктивации(Элементы.Страницы.ТекущаяСтраница);
			НачатьАктивациюКодаРегистрации();
		КонецЕсли;
		
		РегистрацияИмя = Информация.name;
		РегистрацияЛогин = ?(Информация.Свойство("login"), Информация.login, Информация.email);
		РегистрацияПочта = Информация.email;
		РегистрацияТелефон = Информация.phone;
		
		ПоказатьПризнакЗаполнения(РегистрацияИмя, Элементы.ПроверкаИмени.Имя);
		ПоказатьСостояниеГотово(Элементы.ПроверкаПочты.Имя);
		ПоказатьПризнакЗаполнения(РегистрацияТелефон, Элементы.ПроверкаТелефона.Имя);
		
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти 

#Область РегистрацияВСервисе

&НаКлиенте
Процедура НачатьРегистрациюВСервисе(ТекущаяСтраница)
	
	РегистрацияЛогин = РегистрацияПочта; // Для регистраций в Мастере перехода почта назначается логином.
	
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Имя", РегистрацияИмя);
	ПараметрыЗапроса.Вставить("Почта", РегистрацияПочта);
	ПараметрыЗапроса.Вставить("Логин", РегистрацияЛогин);
	ПараметрыЗапроса.Вставить("Телефон", РегистрацияТелефон);
	ПараметрыЗапроса.Вставить("Пароль", РегистрацияПароль);
	ПараметрыЗапроса.Вставить("КодАктивации", КодАктивации);
	ПараметрыЗапроса.Вставить("ООКод", ООКод);
	ПараметрыЗапроса.Вставить("ВариантРегистрации", ВариантРегистрации);
	Если ДоступноПриглашениеДляРегистрации Тогда
		ТипРегистрации = ТипРегистрацииПриглашение();
	ИначеЕсли ЗначениеЗаполнено(КодАктивации) Тогда
		ТипРегистрации = ТипРегистрацииЗапрос();
	Иначе
		ТипРегистрации = ТипРегистрацииРучная();
	КонецЕсли; 
	
	ПараметрыЗапроса.Вставить("ТипРегистрации", ТипРегистрации);

	НачатьЗапросРегистрации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	Если ВариантРегистрации = ВариантВыборОО() Или ВариантРегистрации = ВариантВводКодаАктивации() Тогда
		ОписаниеОжидания = СтрокаКонтактовОбслуживающейОрганизации();
	ИначеЕсли ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
		ОписаниеОжидания = СтрокаКонтактовСлужбыПоддержки();
	Иначе
		ОписаниеОжидания = ?(КоличествоОбслуживающихОрганизаций = 0, 
			СтрокаКонтактовСлужбыПоддержки(), СтрокаАвтоподбораОрганизации());
	КонецЕсли;
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = НСтр("ru = 'Регистрация в облачном сервисе'");
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Выполняется регистрация...'");
	ПараметрыОжидания.ОписаниеОжидания = ОписаниеОжидания;
	ПараметрыОжидания.ОтображатьЗаголовокОписания = Истина;
	ПараметрыОжидания.ДопустимоеОжидание = 5;
	УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьЗапросРегистрации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	Данные = Новый Структура;
	Данные.Вставить("type", ПараметрыЗапроса.ТипРегистрации);
	Данные.Вставить("login", ПараметрыЗапроса.Логин);
	Данные.Вставить("name", ПараметрыЗапроса.Имя);
	Данные.Вставить("password", ПараметрыЗапроса.Пароль);
	Данные.Вставить("email",ПараметрыЗапроса.Почта);
	Данные.Вставить("phone", ПараметрыЗапроса.Телефон);
	Если ПараметрыЗапроса.ВариантРегистрации = ВариантВводКодаАктивации() Тогда
		Данные.Вставить("code", ПараметрыЗапроса.КодАктивации);
	КонецЕсли; 
	Если ПараметрыЗапроса.ВариантРегистрации = ВариантВыборОО() Тогда
		Данные.Вставить("supportCompanyId", ПараметрыЗапроса.ООКод);
	КонецЕсли;
	
	ОбработчикРезультата = "Подключаемый_ЗаполнитьФормуПослеРегистрации"; 
	ПараметрыЗапроса.Вставить("Данные", Данные);
	ПараметрыЗапроса.Вставить("Метод", "register");
	НачатьВыполнениеФоновогоЗаданияНаСервере("ОтправитьДанные", ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьФормуПослеРегистрации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Ошибка Тогда
		ПоказатьОшибкуНаСтраницеОжидания(Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли; 
	
	Данные = Результат.Данные;
	Если Данные.error Тогда
		ПоказатьОшибкуНаСтраницеОжидания(Данные.description);
	Иначе
		ПоказатьСостояниеОжидание("СостояниеКартинка");
		Логин = РегистрацияЛогин;
		Пароль = РегистрацияПароль;
		Если ВариантРегистрации = ВариантЗапросНаРегистрацию() Тогда
			УстановитьСтраницуВводКодаАктивации(Элементы.Страницы.ТекущаяСтраница);
		Иначе
			НачатьВходВСервис(Элементы.Страницы.ТекущаяСтраница);
		КонецЕсли; 
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВводПароля()
	
	Если ЗначениеЗаполнено(РегистрацияПароль) И ЗначениеЗаполнено(РегистрацияПодтверждениеПароля)
		И РегистрацияПароль = РегистрацияПодтверждениеПароля Тогда
		ПоказатьСостояниеГотово(Элементы.ПроверкаПароля.Имя);
	Иначе
		ПоказатьСостояниеПусто(Элементы.ПроверкаПароля.Имя)
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область АктивацияКодаРегистрации

&НаКлиенте
Процедура НачатьАктивациюКодаРегистрации()
	
	Если ЗначениеЗаполнено(КодАктивации) Тогда
		ПоказатьСостояниеОжидание(Элементы.ПроверкаКодАктивации.Имя);
		ПараметрыЗапроса = ШаблонПараметровЗапросов();
		ПараметрыЗапроса.Вставить("КодАктивации", КодАктивации);
		НачатьЗапросАктивации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
		ПроверитьРезультатЗапроса();
	Иначе
		ПоказатьСостояниеПусто(Элементы.ПроверкаКодАктивации.Имя);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьЗапросАктивации(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания) 
	
	Данные = Новый Структура;
	Данные.Вставить("code", ПараметрыЗапроса.КодАктивации);
	
	ОбработчикРезультата = "Подключаемый_ЗаполнитьФормуПослеАктивации"; 
	ПараметрыЗапроса.Вставить("Метод", "activation");
	ПараметрыЗапроса.Вставить("Данные", Данные);
	НачатьВыполнениеФоновогоЗаданияНаСервере("ОтправитьДанные", ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьФормуПослеАктивации (Результат, ДополнительныеПараметры) Экспорт

	Ошибка = Ложь;
	СообщениеОбОшибке = "";
	ИмяЭлементаСостояния = Элементы.ПроверкаКодАктивации.Имя;
	Данные = Результат.Данные;
	Если Результат.Ошибка Тогда
		Ошибка = Истина;
		СообщениеОбОшибке = Результат.СообщениеОбОшибке;
	ИначеЕсли Данные.error Тогда 
		Ошибка = Истина;
		СообщениеОбОшибке = Данные.description;
	КонецЕсли;
	
	Если Ошибка Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке,, Элементы.КодАктивацииЗапросНаРегистрацию.Имя);
		ПоказатьСостояниеОшибка(ИмяЭлементаСостояния);
	Иначе
		ПолеЛогин = "login";
		Если Данные.Свойство(ПолеЛогин) Тогда
			РегистрацияЛогин = Данные[ПолеЛогин];
		Иначе
			РегистрацияЛогин = РегистрацияПочта;
		КонецЕсли;
		Логин = РегистрацияЛогин;
		Пароль = РегистрацияПароль;
		ПоказатьСостояниеГотово(Элементы.ПроверкаКодАктивации.Имя);
		Если ЗначениеЗаполнено(Пароль) Тогда
			НачатьВходВСервис(Элементы.Страницы.ТекущаяСтраница);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти

#Область ВходВСервис

&НаКлиенте
Процедура НачатьВходВСервис(ТекущаяСтраница)
	
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Логин", Логин);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	ПараметрыЗапроса.Вставить("Метод", "usr/account/list");
	ПараметрыЗапроса.Вставить("ОбработчикРезультата", "Подключаемый_ЗаполнитьФормуПослеВхода");
	
	НачатьВыполнениеМетодаИнтерфейса(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = ЗаголовокВход();
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Выполняется вход...'");
	ПараметрыОжидания.ДопустимоеОжидание = 5;
	УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьФормуПослеВхода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Ошибка Тогда
		ПоказатьОшибкуНаСтраницеОжидания(Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	УстановитьКонтролыПоУмолчанию();
	
	СписокВыбора = Элементы.АбонентКод.СписокВыбора;
	Данные = Результат.Данные;
	СписокВыбора.Очистить();
	Для Каждого Элемент Из Данные.account Цикл
		Если Элемент.role = РольВладелец() Тогда
			Если Не ЗначениеЗаполнено(КодАбонента) Тогда
				КодАбонента = Элемент.id;
				АбонентНаименование = Элемент.name;
			КонецЕсли;
			СписокВыбора.Добавить(Элемент.id, Элемент.name);
		КонецЕсли; 
	КонецЦикла;
	
	УстановитьСтраницуСпособПерехода(Элементы.Страницы.ТекущаяСтраница); 
	
КонецПроцедуры
 
#КонецОбласти

#Область СопоставлениеРасширений

&НаКлиенте
Функция ПроверитьЗаполнениеРасширенийДляВосстановления()
	Отказ = Ложь;
	
	СоответствиеРасширенийНомерамСтрок = Новый Соответствие;
	
	НомерСтроки = 0;
	Для Каждого СтрокаРасширение Из РасширенияДляВосстановления Цикл
		НомерСтроки = НомерСтроки + 1;
		Если СтрокаРасширение.Восстановить И Не ЗначениеЗаполнено(СтрокаРасширение.ИспользоватьРасширение) Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Для расширения ''%1'' указана необходимость восстановления, но не выбрано восстанавливаемое расширение сервиса'"), 
				СтрокаРасширение.ИспользоватьРасширение);
			
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РасширенияДляВосстановления",
				НомерСтроки, 
				"ИспользоватьРасширение");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаРасширение.ИспользоватьРасширение) Тогда
			Продолжить;
		КонецЕсли;
		
		НомераСтрок = СоответствиеРасширенийНомерамСтрок.Получить(СтрокаРасширение.ИспользоватьРасширение);
		Если НомераСтрок = Неопределено Тогда
			НомераСтрок = Новый Массив;
			СоответствиеРасширенийНомерамСтрок.Вставить(СтрокаРасширение.ИспользоватьРасширение, НомераСтрок);
		КонецЕсли;
		НомераСтрок.Добавить(НомерСтроки);
	КонецЦикла;
	
	Для Каждого КлючЗначение Из СоответствиеРасширенийНомерамСтрок Цикл		
		НомераСтрок = КлючЗначение.Значение;
		Если НомераСтрок.Количество() = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Обнаружено дублирование расширения ''%1'' в строках № %2'"), 
			КлючЗначение.Ключ, 
			СтрСоединить(НомераСтрок, ", "));
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РасширенияДляВосстановления",
			НомераСтрок[0], 
			"ИспользоватьРасширение");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
	КонецЦикла;
	
	Возврат Не Отказ;
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборРасширения(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбработатьВыборРасширенияНаСервере(Результат, Элементы.РасширенияДляВосстановления.ТекущаяСтрока);
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборРасширенияНаСервере(ИмяРасширения, ТекущаяСтрока)
	МассивСтрок = РасширенияИзМС.НайтиСтроки(Новый Структура("Имя", ИмяРасширения));
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтрокаРасширения = МассивСтрок[0];
	ТекСтрока = РасширенияДляВосстановления[ТекущаяСтрока];
	
	Если ТекСтрока.ИзменяетСтруктуруДанных <> СтрокаРасширения.ИзменяетСтруктуруДанных Тогда
		Если ТекСтрока.ИзменяетСтруктуруДанных Тогда
			Текст = НСтр("ru = 'Расширение ''%1'' изменяет структуру данных, но выбрано расширение не изменяющее структуру данных.'");
		Иначе
			Текст = НСтр("ru = 'Расширение ''%1'' не изменяет структуру данных, но выбрано расширение изменяющее структуру данных.'");
		КонецЕсли;
		ТекстСообщения = СтрШаблон(Текст, ТекСтрока.Наименование);
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РасширенияДляВосстановления",
			ТекущаяСтрока + 1, "ИспользоватьРасширение");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКДанным);
		Возврат;
	КонецЕсли;
	
	ТекСтрока.ИспользоватьРасширениеИмя = ИмяРасширения;
	ТекСтрока.Версия = СтрокаРасширения.Версия;
	ТекСтрока.ИспользоватьРасширение = СтрокаРасширения.Наименование;
	Если Не ТекСтрока.Восстановить Тогда
		ТекСтрока.Восстановить = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьСопоставлениеРасширений(ТекущаяСтраница)
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Логин", Логин);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	ПараметрыЗапроса.Вставить("КодАбонента", КодАбонента);
	ПараметрыЗапроса.Вставить("Метод", "usr/extension/list_for_compare");
	ПараметрыЗапроса.Вставить("ОбработчикРезультата", "Подключаемый_ЗаполнитьСписокРасширений");
	
	Данные = Новый Структура;
	Данные.Вставить("account", КодАбонента);
	Данные.Вставить("sysname", "");
	Данные.Вставить("sysversion", "");
	
	ПараметрыЗапроса.Вставить("Данные", Данные);
	
	НачатьВыполнениеМетодаИнтерфейса(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = ЗаголовокСопоставлениеРасширений();
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Чтение списка расширений...'");
	ПараметрыОжидания.ДопустимоеОжидание = 5;

	УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьСписокРасширений(Результат, ДополнительныеПараметры) Экспорт	
	Если ЕстьОшибки(Результат) Тогда
		Возврат;
	КонецЕсли;
	УстановитьКонтролыПоУмолчанию();
	
	ЗаполнитьСписокРасширенийНаСервере(Результат.Данные);
	
	УстановитьСтраницуСопоставлениеРасширений(Элементы.Страницы.ТекущаяСтраница);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРасширенийНаСервере(Данные)	
	РасширенияИзМС.Очистить();
	Элементы.РасширенияДляВосстановленияВерсия.СписокВыбора.Очистить();
	Для Каждого Элемент Из Данные.extension Цикл
		НоваяСтрока = РасширенияИзМС.Добавить();
		НоваяСтрока.Наименование = Элемент.description;
		НоваяСтрока.Версия = Элемент.version;
		НоваяСтрока.Имя = Элемент.id;
		НоваяСтрока.ИзменяетСтруктуруДанных = Элемент.changes_data_structure;
	КонецЦикла;
	АдресХраненияРасширений = ПоместитьВоВременноеХранилище(РасширенияИзМС.Выгрузить(), УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасширенияИБ()
	УстановитьПривилегированныйРежим(Истина);
	ВыбиратьРасширенияИзОбласти = ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных()
		И ОбщегоНазначения.РазделениеВключено();

	Для Каждого РасширениеКонфигурации Из РасширенияКонфигурации.Получить(, ИсточникРасширенийКонфигурации.СеансАктивные) Цикл
		Если Не РасширениеКонфигурации.Активно Тогда
			Продолжить;
		КонецЕсли;
		Если ВыбиратьРасширенияИзОбласти И 
			РасширениеКонфигурации.ОбластьДействия = ОбластьДействияРасширенияКонфигурации.ИнформационнаяБаза Тогда
			Продолжить;
		КонецЕсли;
		СтрокаРасширение = РасширенияДляВосстановления.Добавить();
		СтрокаРасширение.Наименование = 
			?(ЗначениеЗаполнено(РасширениеКонфигурации.Синоним), РасширениеКонфигурации.Синоним, РасширениеКонфигурации.Имя);
		СтрокаРасширение.ИзменяетСтруктуруДанных = РасширениеКонфигурации.ИзменяетСтруктуруДанных();
		СтрокаРасширение.Восстановить = СтрокаРасширение.ИзменяетСтруктуруДанных;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СопоставлениеПользователей

&НаКлиенте
Процедура НачатьСопоставлениеПользователей(ТекущаяСтраница)
	
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Логин", Логин);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	ПараметрыЗапроса.Вставить("КодАбонента", КодАбонента);
	ПараметрыЗапроса.Вставить("Метод", "usr/account/users/list");
	ПараметрыЗапроса.Вставить("ОбработчикРезультата", "Подключаемый_ЗаполнитьСписокПользователей");
	
	Данные = Новый Структура;
	Данные.Вставить("id", КодАбонента);
	
	ПараметрыЗапроса.Вставить("Данные", Данные);
	
	НачатьВыполнениеМетодаИнтерфейса(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = ЗаголовокСопоставлениеПользователей();
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Чтение списка пользователей...'");
	ПараметрыОжидания.ДопустимоеОжидание = 5;

	УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьСписокПользователей(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибки(Результат) Тогда
		Возврат;
	КонецЕсли; 
	Данные = Результат.Данные;
	УстановитьКонтролыПоУмолчанию();
	
	ПраваПользователей.Очистить();
	Для Каждого Элемент Из Данные.user Цикл
		НоваяСтрока = ПраваПользователей.Добавить();
		НоваяСтрока.ПолноеИмя = Элемент.name;
		НоваяСтрока.Логин = Элемент.login;
		НоваяСтрока.ЭтоВладелецАбонента = (Элемент.role = РольВладелец());
	КонецЦикла;
	ПраваПользователей.Сортировать("ПолноеИмя");
	
	СопоставитьПользователей();
	ОбновитьСтатусСопоставленияПользователей();
	УстановитьСтраницуСопоставлениеПользователей(Элементы.Страницы.ТекущаяСтраница); 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователяСервиса()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Логин", Логин);
	ПараметрыФормы.Вставить("Пароль", Пароль);
	ПараметрыФормы.Вставить("КодАбонента", КодАбонента);
	ПараметрыФормы.Вставить("АдресПрограммногоИнтерфейса", АдресПрограммногоИнтерфейса);
	ПараметрыФормы.Вставить("ВерсияПрограммногоИнтерфейса", ВерсияПрограммногоИнтерфейса);
	
	ТекущиеДанные = Элементы.ПраваПользователей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И Не ЗначениеЗаполнено(ТекущиеДанные.ПолноеИмя) Тогда
		ПараметрыФормы.Вставить("ПолноеИмя", ТекущиеДанные.ПолноеИмяПользователяИБ);
		ПараметрыФормы.Вставить("Идентификатор", ТекущиеДанные.Идентификатор);
		ПараметрыФормы.Вставить("Почта", ТекущиеДанные.ЭлектроннаяПочта);
	Иначе
		ПараметрыФормы.Вставить("ПолноеИмя", "");
		ПараметрыФормы.Вставить("Идентификатор", Неопределено);
		ПараметрыФормы.Вставить("Почта", "");
	КонецЕсли; 
	
	Оповещение = Новый ОписаниеОповещения("СоздатьПользователяСервисаЗавершение", ЭтотОбъект);
	
	ИмяФормыВыбора = "Обработка.МастерПереходаВОблако.Форма.ДобавлениеПользователя";
	
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователяСервисаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		НоваяСтрока = ПраваПользователей.Добавить();
		НоваяСтрока.ПолноеИмя = Результат.ПолноеИмя;
		НоваяСтрока.Логин = Результат.Логин;
		НоваяСтрока.ЭтоВладелецАбонента = (Результат.Роль = РольВладелец());
		Если ЗначениеЗаполнено(Результат.Идентификатор) Тогда
			Поиск = ПользователиИБ.НайтиСтроки(Новый Структура("Идентификатор", Результат.Идентификатор));
			Если Поиск.Количество() > 0 Тогда
				Поиск[0].ЛогинПользователяСервиса = Результат.Логин;
				НоваяСтрока.Идентификатор = Поиск[0].Идентификатор;
				НоваяСтрока.ПолноеИмяПользователяИБ = Поиск[0].ПолноеИмя;
				НоваяСтрока.Право = ?(Результат.Роль = РольВладелец(), ПравоЗапускИАдминистрирование(), ПравоЗапуск());
				ДополнитьСписокНеСопоставленнымиПользователями();
				ОбновитьСтатусСопоставленияПользователей();
			КонецЕсли; 
		КонецЕсли; 
		Элементы.ПраваПользователей.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователейИБ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.ИдентификаторПользователяСервиса КАК ИдентификаторПользователяСервиса,
		|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
		|	Пользователи.Наименование КАК Наименование,
		|	Пользователи.Ссылка КАК Пользователь,
		|	МАКСИМУМ(ПользователиКонтактнаяИнформация.Представление) КАК ЭлектроннаяПочта
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|		ПО Пользователи.Ссылка = ПользователиКонтактнаяИнформация.Ссылка
		|		И (ПользователиКонтактнаяИнформация.Вид = &ВидАдресЭлектроннойПочты)
		|ГДЕ
		|	НЕ Пользователи.Служебный
		|СГРУППИРОВАТЬ ПО
		|	Пользователи.ИдентификаторПользователяСервиса,
		|	Пользователи.ИдентификаторПользователяИБ,
		|	Пользователи.Наименование,
		|	Пользователи.Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("ВидАдресЭлектроннойПочты", Справочники.ВидыКонтактнойИнформации.EmailПользователя);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	КешПользователей = Новый Соответствие;
	
	ПользователиИБ.Очистить();
	УстановитьПривилегированныйРежим(Истина);
	Пока Выборка.Следующий() Цикл
		ПользовательИзКеша = КешПользователей[Выборка.ИдентификаторПользователяИБ];
		
		Если ПользовательИзКеша = Ложь Тогда
			ПользовательИБ = Неопределено;
		ИначеЕсли ПользовательИзКеша = Неопределено Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Выборка.ИдентификаторПользователяИБ);
			Если ПользовательИБ = Неопределено Тогда
				КешПользователей[Выборка.ИдентификаторПользователяИБ] = Ложь;
			Иначе
				КешПользователей[Выборка.ИдентификаторПользователяИБ] = ПользовательИБ;
			КонецЕсли;
		Иначе
			ПользовательИБ = ПользовательИзКеша;
		КонецЕсли;
		
		Если ПользовательИБ = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = ПользователиИБ.Добавить();
		НоваяСтрока.Пользователь = Выборка.Пользователь;
		НоваяСтрока.Идентификатор = Выборка.Пользователь.УникальныйИдентификатор();
		НоваяСтрока.ЭлектроннаяПочта = Выборка.ЭлектроннаяПочта;
		НоваяСтрока.Логин = ПользовательИБ.Имя;
		НоваяСтрока.ПолноеИмя = ПользовательИБ.ПолноеИмя;
	КонецЦикла;
	ПользователиИБ.Сортировать("ПолноеИмя");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПользователяИБ(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ДанныеИБ = ВыбранныйЭлемент.Значение; // ДанныеФормыЭлементКоллекции 
		Для Каждого СтрокаТаблицы Из ПраваПользователей Цикл
			Если СтрокаТаблицы.Идентификатор = ДанныеИБ.Идентификатор И ЗначениеЗаполнено(СтрокаТаблицы.Логин) Тогда
				СтрокаТаблицы.Идентификатор = Неопределено;
				СтрокаТаблицы.Пользователь = Неопределено;
				СтрокаТаблицы.ПолноеИмяПользователяИБ = Неопределено;
				СтрокаТаблицы.Право = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
		ТекущиеДанные = Элементы.ПраваПользователей.ТекущиеДанные;
		ТекущиеДанные.Идентификатор = ДанныеИБ.Идентификатор;
		ТекущиеДанные.Пользователь = ДанныеИБ.Пользователь;
		ТекущиеДанные.ПолноеИмяПользователяИБ = 
			?(ЗначениеЗаполнено(ДанныеИБ.ПолноеИмя), ДанныеИБ.ПолноеИмя, ДанныеИБ.Логин);
		ДанныеИБ.ЛогинПользователяСервиса = ТекущиеДанные.Логин;
		Для Каждого ДанныеИБ Из ПользователиИБ Цикл
			Если ДанныеИБ.ЛогинПользователяСервиса = ТекущиеДанные.Логин 
				И ТекущиеДанные.Идентификатор <> ДанныеИБ.Идентификатор Тогда
				ДанныеИБ.ЛогинПользователяСервиса = "";
			КонецЕсли; 
		КонецЦикла; 
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Право) Тогда
			ТекущиеДанные.Право = ?(ТекущиеДанные.ЭтоВладелецАбонента, ПравоЗапускИАдминистрирование(), ПравоЗапуск());
		КонецЕсли;
		
	КонецЕсли;
	
	ДополнитьСписокНеСопоставленнымиПользователями();
	ОбновитьСтатусСопоставленияПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьПользователей()
	
	Для Каждого Элемент Из ПользователиИБ Цикл
		Элемент.ЛогинПользователяСервиса = Неопределено;
	КонецЦикла; 
	
	ПоискИБ = ПользователиИБ.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТекущегоПользователя));
	ПоискСервис = ПраваПользователей.НайтиСтроки(Новый Структура("Логин", Логин));
	
	Если ПоискИБ.Количество() > 0 И ПоискСервис.Количество() > 0 Тогда
		ПоискСервис[0].Идентификатор = ПоискИБ[0].Идентификатор;
		ПоискСервис[0].Право = ?(ПоискСервис[0].ЭтоВладелецАбонента, ПравоЗапускИАдминистрирование(), ПравоЗапуск());
		ПоискСервис[0].ПолноеИмяПользователяИБ = ПоискИБ[0].ПолноеИмя;
		ПоискИБ[0].ЛогинПользователяСервиса = ПоискСервис[0].Логин;
	КонецЕсли; 
	
	Для Каждого Строка Из ПраваПользователей Цикл
		Если ЗначениеЗаполнено(Строка.Идентификатор) Тогда
			Продолжить;
		КонецЕсли; 
		Поиск = ПользователиИБ.НайтиСтроки(Новый Структура("Логин", Строка.Логин));
		Если Поиск.Количество() > 0 И Не ЗначениеЗаполнено(Поиск[0].ЛогинПользователяСервиса) Тогда
			Строка.Идентификатор = Поиск[0].Идентификатор;
			Строка.Право = ?(Строка.ЭтоВладелецАбонента, ПравоЗапускИАдминистрирование(), ПравоЗапуск());
			Строка.ПолноеИмяПользователяИБ = Поиск[0].ПолноеИмя;
			Поиск[0].ЛогинПользователяСервиса = Строка.Логин;
		КонецЕсли;
		Поиск = ПользователиИБ.НайтиСтроки(Новый Структура("ПолноеИмя", Строка.ПолноеИмя));
		Если Поиск.Количество() > 0 И Не ЗначениеЗаполнено(Поиск[0].ЛогинПользователяСервиса) Тогда
			Строка.Идентификатор = Поиск[0].Идентификатор;
			Строка.Право = ?(Строка.ЭтоВладелецАбонента, ПравоЗапускИАдминистрирование(), ПравоЗапуск());
			Строка.ПолноеИмяПользователяИБ = Поиск[0].ПолноеИмя;
			Поиск[0].ЛогинПользователяСервиса = Строка.Логин;
		КонецЕсли;
	КонецЦикла;
	
	ДополнитьСписокНеСопоставленнымиПользователями();
		
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьСписокНеСопоставленнымиПользователями()
	
	Поиск = ПраваПользователей.НайтиСтроки(Новый Структура("ПолноеИмя", ""));
	Для Каждого Строка Из Поиск Цикл
		ПраваПользователей.Удалить(Строка);
	КонецЦикла; 
	Для Каждого Строка Из ПользователиИБ Цикл
		Если Не ЗначениеЗаполнено(Строка.ЛогинПользователяСервиса) Тогда
			НоваяСтрока = ПраваПользователей.Добавить();
			НоваяСтрока.Идентификатор = Строка.Идентификатор;
			НоваяСтрока.Пользователь = Строка.Пользователь;
			НоваяСтрока.ПолноеИмяПользователяИБ = Строка.ПолноеИмя;
			НоваяСтрока.ЭлектроннаяПочта = Строка.ЭлектроннаяПочта;
			НоваяСтрока.Гиперссылка = НСтр("ru = 'Добавить'");
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусСопоставленияПользователей()
	
	Сопоставлено = 0;
	Для Каждого Элемент Из ПользователиИБ Цикл
		Если ЗначениеЗаполнено(Элемент.ЛогинПользователяСервиса) Тогда
			Сопоставлено = Сопоставлено + 1;
		КонецЕсли; 	
	КонецЦикла; 
	
	Элементы.СтатусСопоставления.Заголовок = СтрШаблон(
		НСтр("ru = 'Сопоставлено пользователей: %1 из %2'"), Сопоставлено, ПользователиИБ.Количество()); 
	
КонецПроцедуры

#КонецОбласти 

#Область ПередачаДанныхВСеврис

&НаКлиенте 
Процедура НачатьПередачуДанных(ТекущаяСтраница)
	
	ИмяФайлаНаКлиенте = ПолучитьИмяФайлаВыгрузкиДанных(РежимВыгрузкиДляТехническойПоддержки);
	
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Логин", Логин);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	ПараметрыЗапроса.Вставить("КодАбонента", КодАбонента);
	ПараметрыЗапроса.Вставить("ИмяФайла", ИмяФайлаНаКлиенте);
	ПараметрыЗапроса.Вставить("ИмяФайлаВременногоХранилища", ИмяФайлаВременногоХранилища);
	ПараметрыЗапроса.Вставить("РазмерФайла", РазмерФайлаВыгрузки);

	НачатьПередачуФайла(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = ЗаголовокВыгрузкаДанных();
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Передача данных в сервис...'");
	ПараметрыОжидания.ДопустимоеОжидание = 15;
	УстановитьСтраницуОжидание(ТекущаяСтраница, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьПередачуФайла(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	ОбработчикРезультата = "Подключаемый_ПослеПередачиФайла";
	НачатьВыполнениеФоновогоЗаданияНаСервере("ПередатьФайл", ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеПередачиФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибки(Результат) Тогда
		Возврат;
	КонецЕсли; 
	
	ИдентификаторФайла = Результат.ИдентификаторФайла;
	
	СоздатьПриложениеИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПриложениеИзФайла()
	
	УстановитьКонтролыПоУмолчанию();
	
	ПараметрыЗапроса = ШаблонПараметровЗапросов();
	ПараметрыЗапроса.Вставить("Логин", Логин);
	ПараметрыЗапроса.Вставить("Пароль", Пароль);
	ПараметрыЗапроса.Вставить("КодАбонента", КодАбонента);
	ПараметрыЗапроса.Вставить("Метод", "usr/tenant/create_from_data_dump");
	ПараметрыЗапроса.Вставить("ОбработчикРезультата", "Подключаемый_ПослеСозданияПриложения");
	ПараметрыЗапроса.Вставить("Таймаут", 60);

	Данные = Новый Структура;
	Данные.Вставить("file_id", ИдентификаторФайла);
	Данные.Вставить("name", НаименованиеПриложения);
	Данные.Вставить("timezone ", ЧасовойПояс);
	
	ПользователиПриложения = Новый Массив;
	Для Каждого Строка Из ПраваПользователей Цикл
		Если ЗначениеЗаполнено(Строка.Право) Тогда
			ПользовательПриложения = Новый Структура;
			ПользовательПриложения.Вставить("login", Строка.Логин);
			ПользовательПриложения.Вставить("role", Строка.Право);
			ПользовательПриложения.Вставить("user_id", Строка(Строка.Идентификатор));
			ПользователиПриложения.Добавить(ПользовательПриложения);
		КонецЕсли; 
	КонецЦикла; 
	Данные.Вставить("users", ПользователиПриложения); 
	ПараметрыЗапроса.Вставить("Данные", Данные);
	
	НачатьВыполнениеМетодаИнтерфейса(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания);
	
	ПараметрыОжидания = ШаблонПараметровОжидания();
	ПараметрыОжидания.ЗаголовокСтраницы = ЗаголовокВыгрузкаДанных();
	ПараметрыОжидания.ОписаниеСостояния = НСтр("ru = 'Создание приложения...'");
	ПараметрыОжидания.ДопустимоеОжидание = 5;
	
	УстановитьСтраницуОжидание(Элементы.страницы.ТекущаяСтраница, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеСозданияПриложения(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибки(Результат) Тогда
		ЧастиЗаголовка = Новый Массив;
		ЧастиЗаголовка.Добавить(НСтр("ru = 'Не удалось создать приложение по причине:'"));
		ЧастиЗаголовка.Добавить(Символы.ПС);
		ЧастиЗаголовка.Добавить(Элементы.СостояниеТекст.Заголовок);
		ЧастиЗаголовка.Добавить(Символы.ПС);
		ЧастиЗаголовка.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Повторить попытку'"),,,, СсылкаПовторитьПопыткуСозданияПриложения())); 
		Элементы.СостояниеТекст.Заголовок = Новый ФорматированнаяСтрока(ЧастиЗаголовка);
		Возврат;
	КонецЕсли; 
	
	УдалитьВременныеДанныеПослеСохранения();	
	УстановитьСтраницуЗавершение(Элементы.Страницы.ТекущаяСтраница);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Процедура НачатьВыполнениеФоновогоЗаданияНаСервере(ИмяМетода, ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания)
	
	ПараметрыВыполненияОбработки = Новый Структура;
	Если ИмяМетода = "ПолучитьИнформациюОВозможностяхПерехода" Тогда
		ПараметрыВыполненияОбработки.Вставить("Адрес", ПараметрыЗапроса.АдресСервиса);
		ПараметрыВыполненияОбработки.Вставить("МетодИнформация", ПараметрыЗапроса.МетодИнформация);
		ПараметрыВыполненияОбработки.Вставить("МетодВариантыЗагрузки", ПараметрыЗапроса.МетодВариантыЗагрузки);
	ИначеЕсли ИмяМетода = "ПолучитьДанные" Тогда
		ПараметрыВыполненияОбработки.Вставить("Адрес", ПараметрыЗапроса.АдресСервиса);
		ПараметрыВыполненияОбработки.Вставить("Метод", ПараметрыЗапроса.Метод);
	ИначеЕсли ИмяМетода = "ОтправитьДанные" Тогда
		ПараметрыВыполненияОбработки.Вставить("Адрес", ПараметрыЗапроса.АдресСервиса);
		ПараметрыВыполненияОбработки.Вставить("Метод", ПараметрыЗапроса.Метод);
		ПараметрыВыполненияОбработки.Вставить("Данные", ПараметрыЗапроса.Данные);
	ИначеЕсли ИмяМетода = "ВыполнитьМетодВнешнегоИнтерфейса" Тогда
		ПараметрыВыполненияОбработки.Вставить("АдресПрограммногоИнтерфейса", ПараметрыЗапроса.АдресПрограммногоИнтерфейса);
		ПараметрыВыполненияОбработки.Вставить("ВерсияПрограммногоИнтерфейса", ПараметрыЗапроса.ВерсияПрограммногоИнтерфейса);
		ПараметрыВыполненияОбработки.Вставить("Авторизация", ПараметрыЗапроса.Авторизация);
		ПараметрыВыполненияОбработки.Вставить("Метод", ПараметрыЗапроса.Метод);
		ПараметрыВыполненияОбработки.Вставить("Данные", ПараметрыЗапроса.Данные);
	ИначеЕсли ИмяМетода = "ПередатьФайл" Тогда
		ПараметрыВыполненияОбработки = ПараметрыЗапроса;
	КонецЕсли; 
	
	ИмяОбработки = "МастерПереходаВОблако";
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИмяОбработки", ИмяОбработки);
	ПараметрыЗадания.Вставить("ИмяМетода", ИмяМетода);
	ПараметрыЗадания.Вставить("ПараметрыВыполнения", ПараметрыВыполненияОбработки);
	ПараметрыЗадания.Вставить("ЭтоВнешняяОбработка", Ложь);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ПараметрыЗапроса.Ключ);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон("МастерПереходаВОблако.%1", ИмяМетода);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0.4;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", ПараметрыЗапроса.Ключ); 
	
	ВыполняемыйМетод = "ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки";
	Результат = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗадания, ПараметрыВыполнения);
	
	АдресХранилища = Результат.АдресРезультата;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатЗапроса()
	
	Оповещение = Новый ОписаниеОповещения(ОбработчикРезультата, ЭтотОбъект);
	Результат = ПроверитьРезультатЗапросаНаСервере(ИдентификаторЗадания, АдресХранилища);
	Если Результат = Неопределено Тогда
		СчетчикОжидания = СчетчикОжидания + 1;
		Если ДопустимоеОжидание > 0 И СчетчикОжидания > ДопустимоеОжидание Тогда
			Элементы.СостояниеТекст.Заголовок = ТекстДолгийЗапрос();
		КонецЕсли; 
		ПодключитьОбработчикОжидания("ПроверитьРезультатЗапроса", 1, Истина);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьРезультатЗапросаНаСервере(ИдентификаторЗадания, АдресХранилища)
	
	ФЗ = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	Если ФЗ <> Неопределено И ФЗ.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		Возврат Неопределено;
		
	ИначеЕсли ФЗ <> Неопределено И ФЗ.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
		Возврат ПолучитьИзВременногоХранилища(АдресХранилища);
	ИначеЕсли ФЗ <> Неопределено И ФЗ.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
		ТекстОшибки = ТехнологияСервиса.ПодробныйТекстОшибки(ФЗ.ИнформацияОбОшибке);
		Сообщения = ФЗ.ПолучитьСообщенияПользователю();
		Для Каждого Сообщение Из Сообщения Цикл
			ТекстОшибки = Сообщение.Текст + Символы.ПС + ТекстОшибки;
		КонецЦикла;
		ВызватьИсключение ТекстОшибки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ЕстьОшибки(Результат)
	
	Если Результат.Ошибка Тогда
		ПоказатьОшибкуНаСтраницеОжидания(Результат.СообщениеОбОшибке);
		Возврат Истина
	КонецЕсли;
	Данные = Результат.Данные;
	Если ТипЗнч(Данные) = Тип("Структура") И Данные.Свойство("general") И Данные.general.error Тогда
		ПоказатьОшибкуНаСтраницеОжидания(Данные.general.message);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции
 
&НаСервереБезКонтекста
Процедура НачатьВыполнениеМетодаИнтерфейса(ПараметрыЗапроса, ОбработчикРезультата, АдресХранилища, ИдентификаторЗадания)
	
	ОбработчикРезультата = ПараметрыЗапроса.ОбработчикРезультата;
	Если ПараметрыЗапроса.Свойство("КодАбонента") Тогда
		Авторизация = ПараметрыАвторизации(
			ПараметрыЗапроса.Логин, ПараметрыЗапроса.Пароль, ПараметрыЗапроса.КодАбонента);
	Иначе
		Авторизация = ПараметрыАвторизации(ПараметрыЗапроса.Логин, ПараметрыЗапроса.Пароль);
	КонецЕсли; 
	ПараметрыЗапроса.Вставить("Авторизация", Авторизация);
	
	Если ПараметрыЗапроса.Свойство("Данные") Тогда
		Если ПараметрыЗапроса.Данные.Свойство("sysname") Тогда
			ПараметрыЗапроса.Данные.sysname = Метаданные.Имя; 
		КонецЕсли;
		Если ПараметрыЗапроса.Данные.Свойство("sysversion") Тогда
			ПараметрыЗапроса.Данные.sysversion = Метаданные.Версия; 
		КонецЕсли;
	Иначе		
		ПараметрыЗапроса.Вставить("Данные", Неопределено);
	КонецЕсли; 
	
	НачатьВыполнениеФоновогоЗаданияНаСервере("ВыполнитьМетодВнешнегоИнтерфейса", 
		ПараметрыЗапроса, АдресХранилища, ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыАвторизации(Логин, Пароль, КодАбонента = Неопределено)
	
	ПараметрыАвторизации = Новый Структура;
	ПараметрыАвторизации.Вставить("Логин", Логин);
	ПараметрыАвторизации.Вставить("Пароль", Пароль);
	Если Не КодАбонента = Неопределено Тогда
		ПараметрыАвторизации.Вставить("КодАбонента", КодАбонента);
	КонецЕсли; 
	
	Возврат ПараметрыАвторизации;
	
КонецФункции

&НаКлиенте
Процедура ВыборОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ООКод = Результат.Код;
		ООГород = Результат.Город;
		ООНаименование = Результат.Наименование;
		ООСайт = Результат.Сайт;
		ООТелефон = Результат.Телефон;
		ООПочта = Результат.Почта;
		
		Элементы.ОбслуживающаяОрганизация.СписокВыбора.Очистить();
		Элементы.ОбслуживающаяОрганизация.СписокВыбора.Добавить(ООКод, ООНаименование);
			
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпособаПерехода()
	
	Если СпособПерехода = СпособВыгрузка() Тогда
		Элементы.ГруппаВыгрузкаДанных.ЦветФона = ЦветВыбран();
		Элементы.ГруппаМиграция.ЦветФона = ?(МиграцияДоступна, ЦветНеВыбран(), ЦветНеДоступен());
	Иначе
		Элементы.ГруппаМиграция.ЦветФона = ЦветВыбран();
		Элементы.ГруппаВыгрузкаДанных.ЦветФона = ЦветНеВыбран();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпособаПереходаНаГлавной()
	
	Если СпособПерехода = СпособВыгрузка() Тогда
		Элементы.ВизуализацииПерехода.ТекущаяСтраница = Элементы.СтраницаОблако;
		ПроверитьАдресСервиса();
	ИначеЕсли СпособПерехода = СпособФайл() Тогда
		Элементы.ВизуализацииПерехода.ТекущаяСтраница = Элементы.СтраницаФайл;
		ПоказатьСостояниеПусто(Элементы.ПроверкаАдресаСервиса.Имя);
		Элементы.Далее.Доступность = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуНаСтраницеОжидания(СообщениеОбОшибке)
	
	ПоказатьСостояниеОшибка("СостояниеКартинка");
	Элементы.СостояниеТекст.Заголовок = Новый ФорматированнаяСтрока(СообщениеОбОшибке);
	Элементы.Далее.Доступность = Ложь;
	Элементы.Назад.Доступность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСостояниеПусто(ИмяЭлемента)
	
	Элементы[ИмяЭлемента].Картинка = Новый Картинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСостояниеОжидание(ИмяЭлемента)
	
	Элементы[ИмяЭлемента].Картинка = Элементы.КартинкаОжидание.Картинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСостояниеОшибка(ИмяЭлемента)
	
	Элементы[ИмяЭлемента].Картинка = Элементы.КартинкаОшибка.Картинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСостояниеГотово(ИмяЭлемента)
	
	Элементы[ИмяЭлемента].Картинка = Элементы.КартинкаГотово.Картинка;
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаКонтактовОбслуживающейОрганизации()
	
	СайтСсылка = ?(СтрНайти(ООСайт, "http") = 0, "http://" + ООСайт, ООСайт);
	
	ШрифтЗаголовка = Новый Шрифт(,, Истина);
	ШрифтТекста = Новый Шрифт(,,,,,,90);
	
	Контакты = Новый Массив;
	Контакты.Добавить(Новый ФорматированнаяСтрока(ООНаименование, ШрифтЗаголовка));
	Контакты.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
	Если ЗначениеЗаполнено(ООГород) Тогда
		Контакты.Добавить(Новый ФорматированнаяСтрока(ООГород, ШрифтТекста));
		Контакты.Добавить(Новый ФорматированнаяСтрока("   ", ШрифтТекста));
	КонецЕсли;
	Если ЗначениеЗаполнено(ООТелефон) Тогда
		ДобавитьКонтактТелефон(Контакты, ООТелефон, ШрифтТекста);
	КонецЕсли; 
	Контакты.Добавить(Новый ФорматированнаяСтрока(Символы.ПС, ШрифтТекста));
	Если ЗначениеЗаполнено(ООПочта) Тогда
		ДобавитьКонтактПочта(Контакты, ООПочта, ШрифтТекста);
	КонецЕсли;
	Если ЗначениеЗаполнено(ООСайт) Тогда
		Контакты.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Сайт:'") , ШрифтТекста));
		Контакты.Добавить(Новый ФорматированнаяСтрока(" ", ШрифтТекста));
		КОнтакты.Добавить(Новый ФорматированнаяСтрока(ООСайт, ШрифтТекста,,, СайтСсылка));
	КонецЕсли; 
	
	Возврат Новый ФорматированнаяСтрока(Контакты);
	
КонецФункции 

&НаКлиенте
Функция СтрокаАвтоподбораОрганизации()
	
	ШрифтЗаголовка = Новый Шрифт(,, Истина);
	ШрифтТекста = Новый Шрифт(,,,,,,90);
	
	СоставЗаголовка = Новый Массив;
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru = 'Обслуживающая организация не выбрана.'"), ШрифтЗаголовка));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru = 'Мы сами назначим обслуживающую организацию, которая будет помогать Вам при работе в сервисе.'")));
	Если ЗначениеЗаполнено(ПровайдерТелефон) Или ЗначениеЗаполнено(ПровайдерПочта) Тогда
		СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
		СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Служба поддержки:'")));
		СоставЗаголовка.Добавить(" ");
	КонецЕсли; 	
	Если ЗначениеЗаполнено(ПровайдерТелефон) Тогда
		ДобавитьКонтактТелефон(СоставЗаголовка, ПровайдерТелефон, ШрифтТекста);
	КонецЕсли; 
	Если ЗначениеЗаполнено(ПровайдерПочта) Тогда
		ДобавитьКонтактПочта(СоставЗаголовка, ПровайдерПочта, ШрифтТекста);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(СоставЗаголовка);

КонецФункции

&НаКлиенте
Функция СтрокаКонтактовСлужбыПоддержки()
	
	ШрифтЗаголовка = Новый Шрифт(,, Истина);
	ШрифтТекста = Новый Шрифт(,,,,,,90);
	СоставЗаголовка = Новый Массив;
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Служба поддержки'"), ШрифтЗаголовка));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
	Если ЗначениеЗаполнено(ПровайдерТелефон) Тогда
		ДобавитьКонтактТелефон(СоставЗаголовка, ПровайдерТелефон, ШрифтТекста);
	КонецЕсли; 
	Если ЗначениеЗаполнено(ПровайдерПочта) Тогда
		ДобавитьКонтактПочта(СоставЗаголовка, ПровайдерПочта, ШрифтТекста);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(СоставЗаголовка);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьКонтактТелефон(СоставЗаголовка, КонтактТелефон, Знач ШрифтТекста)
	
	ТелефонСсылка = "tel: " + ТолькоЦифры(КонтактТелефон);
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'тел.:'") , ШрифтТекста));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(" ", ШрифтТекста));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(КонтактТелефон + " ", ШрифтТекста,,, ТелефонСсылка));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока("   ", ШрифтТекста));

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтактПочта(СоставЗаголовка, КонтактПочта, Знач ШрифтТекста)
	
	ПочтаСсылка = "mailto: " + КонтактПочта;
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'e-mail:'") , ШрифтТекста));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(" ", ШрифтТекста));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока(КонтактПочта, ШрифтТекста,,, ПочтаСсылка));
	СоставЗаголовка.Добавить(Новый ФорматированнаяСтрока("   ", ШрифтТекста));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТолькоЦифры(Строка)

	ОбработаннаяСтрока = "";

	Для НомерСимвола = 1 По СтрДлина(Строка) Цикл
		Символ = Сред(Строка, НомерСимвола, 1);
		Если Символ >= "0" И Символ <= "9" Тогда
			ОбработаннаяСтрока = ОбработаннаяСтрока + Символ;
		КонецЕсли;
	КонецЦикла;

	Возврат ОбработаннаяСтрока;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СсылкаРегистрация()
	
	Возврат "Регистрация";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СсылкаПовторитьПопыткуСозданияПриложения()
	
	Возврат "ПовторитьПопыткуСозданияПриложения";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СсылкаПовторитьПопыткуПередачиДанных()
	
	Возврат "ПовторитьПопыткуПередачиДанных";
	
КонецФункции
 
&НаКлиенте
Процедура ПриИзмененииВариантаРегистрации()
	
	Если ВариантРегистрации <> ВариантВыборОО() Тогда
		ООКод = 0;
		ООНаименование = "";
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция ШаблонПараметровОжидания()
	
	ПараметрыОжидания = Новый Структура;
	ПараметрыОжидания.Вставить("ЗаголовокСтраницы", );
	ПараметрыОжидания.Вставить("ОписаниеОжидания", "");
	ПараметрыОжидания.Вставить("ОписаниеСостояния", "");
	ПараметрыОжидания.Вставить("ОтображатьЗаголовокОписания", Ложь);
	ПараметрыОжидания.Вставить("ДопустимоеОжидание", 0);
	
	Возврат ПараметрыОжидания;

КонецФункции

&НаКлиенте
Функция ШаблонПараметровЗапросов()
	
	Шаблон = Новый Структура;
	Шаблон.Вставить("Ключ", УникальныйИдентификатор);
	Шаблон.Вставить("АдресСервиса", АдресСервиса);
	Шаблон.Вставить("АдресПрограммногоИнтерфейса", АдресПрограммногоИнтерфейса);
	Шаблон.Вставить("ВерсияПрограммногоИнтерфейса", ВерсияПрограммногоИнтерфейса);
	
	Возврат Шаблон;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ШаблонОписанияОшибкиВыгрузкиДанных()
	
	Возврат НСтр("ru = 'При выгрузке данных произошла ошибка: %1.'") + Символы.ПС + ПодсказкаПриОшибке();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПодсказкаПриОшибке()
	
	Возврат НСтр("ru = 'При выгрузке данных произошла ошибка: %1.
				|
				|Расширенная информация для службы поддержки записана в журнал регистрации. 
				|Если причина ошибки неизвестна, рекомендуется обратиться в службу технической поддержки, 
				|предоставив для расследования информационную базу и выгрузку журнала регистрации.'");
	
КонецФункции

&НаКлиенте
Процедура ПоказатьПризнакЗаполнения(Значение, ИмяЭлементаПризнака)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ПоказатьСостояниеГотово(ИмяЭлементаПризнака);
	Иначе
		ПоказатьСостояниеПусто(ИмяЭлементаПризнака);
	КонецЕсли; 
	
КонецПроцедуры

&Наклиенте
Функция ТекстДолгийЗапрос()
	
	Возврат НСтр("ru = 'Запрос выполняется дольше обычного...'") ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеПраваПользователя(Право)
	
	Если Право = ПравоЗапуск() Тогда
		Возврат НСтр("ru = 'Запуск'");
	ИначеЕсли Право = ПравоЗапускИАдминистрирование() Тогда
		Возврат НСтр("ru = 'Запуск и администрирование'");
	КонецЕсли;

КонецФункции

&НаСервере
Процедура УстановитьВидимостьПоСостояниюОбновления()
	
	Элементы.ГруппаПараметрыТребуетсяОбновление.Видимость = Не МиграцияДоступна;
	Элементы.ГруппаВыгрузкаТребуетсяОбновление.Видимость = Не МиграцияДоступна;
	Элементы.ГруппаМиграцияТребуетсяОбновление.Видимость = Не МиграцияДоступна;
	Элементы.ПримечаниеОтступ.Видимость = МиграцияДоступна;
	Элементы.ГруппаМиграцияШапка.Доступность = МиграцияДоступна;
	Элементы.ГруппаМиграция.ЦветФона = ?(МиграцияДоступна, ЦветНеВыбран(), ЦветНеДоступен());
	Элементы.ПримечаниеТребуетсяОбновление.РасширеннаяПодсказка.Заголовок = СтрШаблон(
		НСтр("ru = 'Поддерживаемые версии: %1'"), 
		СтрСоединить(ПоддерживаемыеВерсииДляМиграции.ВыгрузитьЗначения(), "; ")); 
		
	Элементы.ГруппаВыгрузкаДанныхШапка.Доступность = ВыгрузкаДоступна;
	Если Не ВыгрузкаДоступна Тогда
		Элементы.ПолучитьФайлВыгрузки.РасширеннаяПодсказка.Заголовок = СтрШаблон(
			НСтр("ru = 'Минимальная поддерживаемая версия: %1'"), МинимальнаяВерсияДляВыгрузки);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемОповещение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РольВладелец()
	
	Возврат "owner";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПравоЗапуск()
	
	Возврат "user";

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПравоЗапускИАдминистрирование()
	
	Возврат "administrator";

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантВыборОО()
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантАвтоматическоеНазначениеОО()
	
	Возврат 1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантВводКодаАктивации()
	
	Возврат 2;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпособВыгрузка()
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпособМиграция()
	
	Возврат 1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпособФайл()
	
	Возврат 2;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантЗапросНаРегистрацию()
	
	Возврат 3;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипРегистрацииЗапрос()
	
	Возврат "Approval"; // Не локализуется.
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипРегистрацииПриглашение()
	
	Возврат "Invitation"; // Не локализуется.
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипРегистрацииРучная()
	
	Возврат "Registration"; // Не локализуется.
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЦветВыбран()
	
	// @skip-check new-color - особенность реализации.
	Возврат Новый Цвет(255, 255, 217);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЦветНеВыбран()
	
	// @skip-check new-color - особенность реализации.
	Возврат Новый Цвет(255, 255, 254);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЦветНеДоступен()
	
	// @skip-check new-color - особенность реализации.
	Возврат Новый Цвет(250, 250, 250);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЧасть(ИдентификаторСостояния)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторСостояния);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЧастиВыгрузкиЗагрузкиОбластейДанных.ИмяФайлаВременногоХранилища КАК ИмяФайлаВременногоХранилища,
	|	ЧастиВыгрузкиЗагрузкиОбластейДанных.НомерЧасти КАК НомерЧасти
	|ИЗ
	|	РегистрСведений.ЧастиВыгрузкиЗагрузкиОбластейДанных КАК ЧастиВыгрузкиЗагрузкиОбластейДанных
	|ГДЕ
	|	ЧастиВыгрузкиЗагрузкиОбластейДанных.Идентификатор = &Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЧасти";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		ВызватьИсключение НСтр("ru = 'Следующая часть выгрузки не обнаружена'");
	КонецЕсли;
	
	ИмяФайла = ФайлыБТС.ПолноеИмяФайлаВременногоХранилища(Выборка.ИмяФайлаВременногоХранилища);
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	Запись = РегистрыСведений.ЧастиВыгрузкиЗагрузкиОбластейДанных.СоздатьМенеджерЗаписи();
	Запись.Идентификатор = ИдентификаторСостояния;
	Запись.НомерЧасти = Выборка.НомерЧасти;
	Запись.Удалить();
	ФайлыБТС.УдалитьФайлВременногоХранилища(Выборка.ИмяФайлаВременногоХранилища);
	
	Возврат ДвоичныеДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция РазмерПриложенияБольшеДопустимого()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не РаботаВМоделиСервиса.РазделениеВключено() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РазмерПриложений.Размер КАК Размер
	|ИЗ
	|	РегистрСведений.РазмерПриложений КАК РазмерПриложений";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Или Выборка.Размер = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РазмерПриложения = Выборка.Размер / 1024 / 1024;
		
	Возврат РазмерПриложения > Константы.МаксимальныйРазмерПриложенияДляВыгрузкиБезРасширенияРаботыСФайлами.Значение();
	
КонецФункции

&НаКлиенте
Функция ЭтоВебКлиент()
	
	#Если ВебКлиент Тогда
		Возврат Истина;
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
