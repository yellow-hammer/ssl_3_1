
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.Дата.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.Номер.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.Комментарий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	КонецЕсли;
	
	УстановитьВалюту();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	УстановитьВалюту();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НазначениеПлатежаЗаполнитьПоШаблону(Команда)
	
	Если Модифицированность Или Объект.Ссылка.Пустая() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("НазначениеПлатежаЗаполнитьПоШаблонуВопрос", ЭтотОбъект),
			НСтр("ru = 'Для продолжения необходимо записать документ. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		НазначениеПлатежаЗаполнитьПоШаблонуПродолжение();
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВалюту()
	Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "Валюта");
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаЗаполнитьПоШаблонуВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да И Записать() Тогда
		НазначениеПлатежаЗаполнитьПоШаблонуПродолжение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаЗаполнитьПоШаблонуПродолжение()
	
	ШаблоныСообщенийКлиент.ВыбратьШаблон(
		Новый ОписаниеОповещения("НазначениеПлатежаЗаполнитьПоШаблонуЗавершение", ЭтотОбъект),
		"Произвольный",
		Объект.Ссылка,
		ВладелецШаблона());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВладелецШаблона()
	Возврат ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ._ДемоСписаниеБезналичныхДенежныхСредств");
КонецФункции

&НаКлиенте
Процедура НазначениеПлатежаЗаполнитьПоШаблонуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.НазначениеПлатежа = ТекстНазначенияПлатежа(Результат, Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстНазначенияПлатежа(Шаблон, Предмет, УникальныйИдентификатор)
	
	Результат = ШаблоныСообщений.СформироватьСообщение(Шаблон, Предмет, УникальныйИдентификатор);
	
	Возврат Результат.Текст;
	
КонецФункции

#КонецОбласти