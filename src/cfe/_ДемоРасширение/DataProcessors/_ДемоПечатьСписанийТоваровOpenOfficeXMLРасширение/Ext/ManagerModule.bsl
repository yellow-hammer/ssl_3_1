///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - настройки интеграции этого объекта:
//     * ДобавитьКомандыПечати - Булево
//     * Размещение - Массив из Строка
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	Настройки.ДобавитьКомандыПечати = Истина;
	Настройки.Размещение.Добавить(Метаданные.Документы._ДемоСписаниеТоваров);
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Команда = КомандыПечати.Добавить();
	Команда.Представление = НСтр("ru = 'Списание товаров в Open Office XML (из расширения)'");
	Команда.Идентификатор = "СписаниеТоваровOpenOfficeXML";
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "СписаниеТоваровOpenOfficeXML");
	Если ПечатнаяФорма <> Неопределено Тогда
		ОфисныеДокументы = НапечататьСписаниеТоваров(МассивОбъектов);
		ПечатнаяФорма.СинонимМакета    = НСтр("ru = 'Списание товаров (документ Microsoft Word)'");
		ПечатнаяФорма.ОфисныеДокументы = ОфисныеДокументы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПечатьДокумента_ДемоСписаниеТоваров

////////////////////////////////////////////////////////////////////////////////
// Работа с макетами офисных документов.

Функция ПолучитьДанныеДляПечатнойФормыСписаниеТоваров(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_ДемоСписаниеТоваров.Ссылка КАК Ссылка,
	|	_ДемоСписаниеТоваров.Дата КАК Дата,
	|	_ДемоСписаниеТоваров.Номер КАК Номер,
	|	ЕСТЬNULL(_ДемоСписаниеТоваров.Организация.Наименование, """") КАК Организация,
	|	_ДемоСписаниеТоваров.МестоХранения КАК МестоХранения,
	|	ЕСТЬNULL(_ДемоСписаниеТоваров.Ответственный.Наименование, """") КАК Ответственный
	|ИЗ
	|	Документ._ДемоСписаниеТоваров КАК _ДемоСписаниеТоваров
	|ГДЕ
	|	_ДемоСписаниеТоваров.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатПоШапке = Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_ДемоСписаниеТоваровТовары.Ссылка КАК Ссылка,
	|	_ДемоСписаниеТоваровТовары.Номенклатура КАК Номенклатура,
	|	_ДемоСписаниеТоваровТовары.Количество КАК Количество
	|ИЗ
	|	Документ._ДемоСписаниеТоваров.Товары КАК _ДемоСписаниеТоваровТовары
	|ГДЕ
	|	_ДемоСписаниеТоваровТовары.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатПоТоварам = Запрос.Выполнить();
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("РезультатПоШапке", РезультатПоШапке);
	ДанныеПечати.Вставить("РезультатПоТоварам", РезультатПоТоварам);
	
	Возврат ДанныеПечати;
	
КонецФункции

Функция НапечататьСписаниеТоваров(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	
	МакетДокумента = УправлениеПечатью.МакетПечатнойФормы("Обработка._ДемоПечатьСписанийТоваровOpenOfficeXMLРасширение.ПФ_DOC_СписаниеТоваров_ru");
	ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыСписаниеТоваров(МассивОбъектов);
	Шаблон = НСтр("ru = '[Организация]-[МестоХранения] Счет №[Номер] от [Дата]'");
	
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);
	Если Макет = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийКолонтитул",       "ВерхнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийЧетныйКолонтитул", "ВерхнийЧетныйКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаДокумента",          "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаДокументаЧасть2",    "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаДокументаЧасть3",    "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицы",            "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы",           "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ПодвалДокумента",         "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийКолонтитул",        "НижнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийЧетныйКолонтитул",  "НижнийЧетныйКолонтитул");
	
	// Данные для печати преобразуются в массив структур
	ДанныеПоШапке = ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеДляПечати.РезультатПоШапке.Выгрузить());
	Товары = ДанныеДляПечати.РезультатПоТоварам.Выгрузить();
	
	Для Каждого ДанныеПечати Из ДанныеПоШапке Цикл
		
		// Данные печати дополняются новыми полями, которые не возможно получить в запросе
		ТоварыПоСсылке = Товары.НайтиСтроки(Новый Структура("Ссылка", ДанныеПечати.Ссылка));
		ДанныеПечатиТовары = Новый Массив;
		СуммаИтого = 0;
		КоличествоСтрокТовары = 0;
		Для Каждого Товар Из ТоварыПоСсылке Цикл
			КоличествоСтрокТовары = КоличествоСтрокТовары + 1;
			Сумма = 100;
			СуммаИтого = СуммаИтого + Сумма;
			СтрокаТаблицыТовары = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Товар);
			СтрокаТаблицыТовары.Вставить("Сумма",       Сумма);
			СтрокаТаблицыТовары.Вставить("НомерСтроки", КоличествоСтрокТовары);
			ДанныеПечатиТовары.Добавить(СтрокаТаблицыТовары);
		КонецЦикла;
		
		СуммаИтогоПрописью = ЧислоПрописью(СуммаИтого, "ДП = Истина","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2" );
		ДанныеПечати.Дата = Формат(ДанныеПечати.Дата, "ДЛФ=D");
		ДанныеПечати.Ответственный = "";
		// Локализация
		ДанныеПечати.Ответственный = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ДанныеПечати.Ответственный);
		// Конец Локализация
	
		ДанныеПечати.Вставить("СуммаИтого",            СуммаИтого);
		ДанныеПечати.Вставить("СуммаИтогоПрописью",    СуммаИтогоПрописью);
		ДанныеПечати.Вставить("КоличествоСтрокТовары", КоличествоСтрокТовары);
		ДанныеПечати.Вставить("ДатаФормирования", Формат(ТекущаяДатаСеанса(), "ДЛФ=D"));
		
		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
		Если ПечатнаяФорма = Неопределено Тогда
			УправлениеПечатью.ОчиститьСсылки(Макет);
			Возврат ОфисныеДокументы;
		КонецЕсли;
		
		// Вывод верхних колонтитулов
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ВерхнийКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Для четных страниц свои колонтитулы
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ВерхнийЧетныйКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Вывод верхней части документа - обычная область с параметрами.
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаДокумента"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаДокументаЧасть2"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаДокументаЧасть3"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		Если ДанныеПечатиТовары.Количество() > 0 Тогда
			// Вывод коллекции данных из информационной базы в виде таблицы.
			Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаТаблицы"]);
			УправлениеПечатью.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			
			Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["СтрокаТаблицы"]);
			УправлениеПечатью.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеПечатиТовары);
		КонецЕсли;
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ПодвалДокумента"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Вывод нижних колонтитулов
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["НижнийКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Для четных страниц свои колонтитулы
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["НижнийЧетныйКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
	
		УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
		
		ЗначенияРеквизитовДокумента = РеквизитыДокумента();
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитовДокумента, ДанныеПечати);
		ЗначенияРеквизитовДокумента.Дата = Формат(ЗначенияРеквизитовДокумента.Дата, "ДЛФ=D");
		ЗначенияРеквизитовДокумента.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗначенияРеквизитовДокумента.Номер);
		ИмяДокумента = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ЗначенияРеквизитовДокумента);
		
		ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, ИмяДокумента);
		
	КонецЦикла;
	
	УправлениеПечатью.ОчиститьСсылки(Макет);
	
	Возврат ОфисныеДокументы;
	
КонецФункции

Функция РеквизитыДокумента()
	РеквизитыДокумента = Новый Структура;
	РеквизитыДокумента.Вставить("Организация");
	РеквизитыДокумента.Вставить("МестоХранения");
	РеквизитыДокумента.Вставить("Номер");
	РеквизитыДокумента.Вставить("Дата");
	РеквизитыДокумента.Вставить("Ссылка");
	
	Возврат РеквизитыДокумента;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
