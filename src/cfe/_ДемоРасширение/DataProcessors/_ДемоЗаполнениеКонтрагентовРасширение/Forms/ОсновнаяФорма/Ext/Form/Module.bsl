///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗаполнитьКонтрагенты(УникальныйИдентификатор, МассивКонтрагентов, ОписаниеКоманды)
	ИмяПроцедуры = "Обработки._ДемоЗаполнениеКонтрагентовРасширение.ВыполнитьКомандуВФоне";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПараметрКоманды", МассивКонтрагентов);
	ПараметрыПроцедуры.Вставить("ОписаниеКоманды", ОписаниеКоманды);
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Демо: Заполнение контрагентов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, НастройкиЗапуска);
КонецФункции

&НаКлиенте
Процедура ЗаполнитьВсеЗавершение(Операция, ДополнительныеПараметры) Экспорт
	Если Операция = Неопределено Тогда
		Возврат; // Задание отменено.
	КонецЕсли;
	Если Операция.Статус <> "Выполнено" Тогда
		ВызватьИсключение Операция.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Контрагенты успешно заполнены'"),,, БиблиотекаКартинок.Успешно32);
	ОповеститьОбИзменении(Тип("СправочникСсылка._ДемоКонтрагенты"));
	Если ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения") И ВладелецФормы.ИмяФормы = "Справочник._ДемоКонтрагенты.Форма.ФормаЭлемента" Тогда
		ВладелецФормы.Прочитать();
	КонецЕсли;
	Если Открыта() Тогда
		Закрыть(Истина);
	КонецЕсли;
КонецПроцедуры

// Обработчик клиентской команды дополнительной обработки "ЗаполнитьПолноеНаименование".
// См. Обработки._ДемоЗаполнениеКонтрагентовРасширение.ДобавитьКомандыЗаполнения.
//
// Параметры:
//   НеИспользуется      - Произвольный     - неиспользуемый параметр.
//   ПараметрыВыполнения - Структура        - следующие параметры:
//     * Форма           - ФормаКлиентскогоПриложения - форма, в которой выполняется команда.
//     * ОписаниеКоманды - Структура        - свойства структуры соответствуют таблице
//                          см. ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения.КомандыЗаполнения.
//
&НаКлиенте
Процедура Подключаемый_ЗаполнитьПолноеНаименование(НеИспользуется, ПараметрыВыполнения) Экспорт
	Объект = ПараметрыВыполнения.Форма.Объект;
	НовоеЗначение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Команда: %1;
		|Дата изменения: %2.'"),
		ПараметрыВыполнения.ОписаниеКоманды.Представление,
		Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=yyyy-MM-dd"));
	
	Если Не ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Если Объект.НаименованиеПолное = НовоеЗначение Тогда
			ПоказатьОповещениеПользователя(, , НСтр("ru = 'Полное наименование уже заполнено.'"), БиблиотекаКартинок.ДиалогИнформация);
			Возврат;
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Объект ""%1"" не обработан: реквизит ""НаименованиеПолное"" уже заполнен.'"),
				Строка(Объект.Наименование));
		КонецЕсли;
	КонецЕсли;
	
	Объект.НаименованиеПолное = НовоеЗначение;
	
	ПоказатьОповещениеПользователя(, , НСтр("ru = 'Полное наименование успешно заполнено.'"), БиблиотекаКартинок.Успешно32);
	
	ПараметрыВыполнения.Форма.Модифицированность = Истина;
КонецПроцедуры

// Обработчик клиентской команды дополнительной обработки "ЗаполнитьВсе".
// См. Обработки._ДемоЗаполнениеКонтрагентовРасширение.ДобавитьКомандыЗаполнения.
//
// Параметры:
//   Контрагенты         - Массив из СправочникСсылка._ДемоКонтрагенты - которые требуется заполнить.
//   ПараметрыВыполнения - Структура        - следующие параметры:
//     * Форма           - ФормаКлиентскогоПриложения - форма, в которой выполняется команда.
//     * ОписаниеКоманды - Структура        - свойства структуры соответствуют таблице
//                          см. ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения.КомандыЗаполнения.
//
&НаКлиенте
Процедура Подключаемый_ЗаполнитьВсе(Контрагенты, ПараметрыВыполнения) Экспорт
	ДлительнаяОперация = ЗаполнитьКонтрагенты(УникальныйИдентификатор, Контрагенты, ПараметрыВыполнения.ОписаниеКоманды);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Идет комплексное заполнение...'");
	
	Обработчик = Новый ОписаниеОповещения("ЗаполнитьВсеЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
КонецПроцедуры

#КонецОбласти
