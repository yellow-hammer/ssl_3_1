///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - настройки интеграции этого объекта:
//     * ДобавитьКомандыЗаполнения - Булево
//     * Размещение - Массив из Строка
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	Настройки.ДобавитьКомандыЗаполнения = Истина;
	Настройки.Размещение.Добавить(Метаданные.Справочники._ДемоКонтрагенты);
КонецПроцедуры

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - См. ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения.КомандыЗаполнения
//   Параметры - См. ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения.Параметры
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	Команда = КомандыЗаполнения.Добавить();
	Команда.Представление = НСтр("ru = 'Заполнить полное наименование (из расширения, на клиенте без записи объекта)'");
	Команда.Идентификатор = "ЗаполнитьПолноеНаименование";
	Команда.Порядок       = 10;
	Команда.МножественныйВыбор = Ложь;
	Команда.ВидимостьВФормах   = "ФормаЭлемента";
	Команда.РежимЗаписи        = "НеЗаписывать";
	Команда.ИмяФормы           = "Форма";
	Команда.Обработчик         = "Подключаемый_ЗаполнитьПолноеНаименование";
	
	Команда = КомандыЗаполнения.Добавить();
	Команда.Представление = НСтр("ru = 'Заполнить ИНН (из расширения, на сервере без записи объекта)'");
	Команда.Идентификатор = "ЗаполнитьИНН";
	Команда.Порядок       = 11;
	Команда.МножественныйВыбор = Ложь;
	Команда.ВидимостьВФормах   = "ФормаЭлемента";
	Команда.РежимЗаписи        = "НеЗаписывать";
	Команда.Обработчик         = "ЗаполнитьИНН";
	
	Команда = КомандыЗаполнения.Добавить();
	Команда.Представление = НСтр("ru = 'Добавить префикс к Наименованию (из расширения, открытие формы)...'");
	Команда.Идентификатор = "ДобавитьПрефиксКНаименованию";
	Команда.Порядок       = 12;
	Команда.ИмяФормы      = "Форма.ДополнительнаяФорма";
	
	Команда = КомандыЗаполнения.Добавить();
	Команда.Представление = НСтр("ru = 'Комплексная очистка (из расширения, на сервере)'");
	Команда.Идентификатор = "ОчиститьВсе";
	Команда.Порядок       = 13;
	Команда.Обработчик    = "ВыполнитьКоманду";
	
	Команда = КомандыЗаполнения.Добавить();
	Команда.Представление = НСтр("ru = 'Комплексное заполнение (из расширения, на клиенте)'");
	Команда.Идентификатор = "ЗаполнитьВсе";
	Команда.Порядок       = 14;
	Команда.ИмяФормы      = "Форма";
	Команда.Обработчик    = "Подключаемый_ЗаполнитьВсе";
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик команды.
//
// Параметры:
//   Контрагент - СправочникСсылка._ДемоКонтрагенты
//   ПараметрыВыполнения - см. ПодключаемыеКомандыКлиентСервер.ПараметрыВыполненияКоманды
//
Процедура ЗаполнитьИНН(Контрагент, ПараметрыВыполнения) Экспорт
	Генератор = Новый ГенераторСлучайныхЧисел;
	
	ПараметрыВыполнения.Форма.Объект.ИНН = Формат(Генератор.СлучайноеЧисло(1, 999999999), "ЧЦ=12; ЧДЦ=0; ЧВН=; ЧГ=");
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = НСтр("ru = 'Поле ""ИНН"" заполнено.'");
	Сообщение.ИдентификаторНазначения = ПараметрыВыполнения.Форма.УникальныйИдентификатор;
	Сообщение.Сообщить();
	
	ПараметрыВыполнения.Форма.Модифицированность = Истина;
КонецПроцедуры

// Обработчик команды.
//
// Параметры:
//   МассивКонтрагентов - Массив из СправочникСсылка._ДемоКонтрагенты
//   ПараметрыВыполнения - см. ПодключаемыеКомандыКлиентСервер.ПараметрыВыполненияКоманды
//
Процедура ВыполнитьКоманду(МассивКонтрагентов, ПараметрыВыполнения) Экспорт
	
	Команда = ПараметрыВыполнения.ОписаниеКоманды;
	
	ЗаполнятьНаименование = (Команда.Идентификатор = "ЗаполнитьПолноеНаименование" Или Команда.Идентификатор = "ЗаполнитьВсе");
	ДобавлятьПрефикс = (Команда.Идентификатор = "ДобавитьПрефиксКНаименованию" Или Команда.Идентификатор = "ЗаполнитьВсе");
	ОчищатьВсе = (Команда.Идентификатор = "ОчиститьВсе");
	
	МассивОшибок = Новый Массив;
	
	// Заполнение объектов
	Для Каждого Контрагент Из МассивКонтрагентов Цикл
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник._ДемоКонтрагенты");
		ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", Контрагент);
		
		НачатьТранзакцию();
		Попытка
			БлокировкаДанных.Заблокировать();
			ОбъектНазначения = Контрагент.ПолучитьОбъект();
			
			Если ЗаполнятьНаименование И Не ОбъектНазначения.ЭтоГруппа Тогда
				Если Не ПустаяСтрока(ОбъектНазначения.НаименованиеПолное) Тогда
					МассивОшибок.Добавить(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Объект ""%1"" не обработан: реквизит ""НаименованиеПолное"" уже заполнен.'"),
							Строка(ОбъектНазначения)));
				Иначе
					ОбъектНазначения.НаименованиеПолное = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Команда: %1
						           |Время изменения: %2'"),
						Команда.Представление,
						Строка(ТекущаяДатаСеанса()));
				КонецЕсли;
			КонецЕсли;
			
			Если ДобавлятьПрефикс Тогда
				ОбъектНазначения.Наименование = Префикс() + ОбъектНазначения.Наименование;
			КонецЕсли;
			
			Если ОчищатьВсе Тогда
				ОбъектНазначения.Наименование = СтрЗаменить(ОбъектНазначения.Наименование, Префикс(), "");
				Если Не ОбъектНазначения.ЭтоГруппа Тогда
					ОбъектНазначения.НаименованиеПолное = "";
					ОбъектНазначения.ИНН = "";
				КонецЕсли;
			КонецЕсли;
			
			ОбъектНазначения.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
	// Вывод результата
	Всего = МассивКонтрагентов.Количество();
	Ошибок = МассивОшибок.Количество();
	Заполнено = Всего - Ошибок;
	
	Если Всего = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не выбраны объекты для изменения'");
	КонецЕсли;
	
	Если Всего = 1 Тогда
		Если Ошибок > 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = МассивОшибок[0];
			Сообщение.Сообщить();
		Иначе
			Если Команда.Идентификатор = "ЗаполнитьВсе" Тогда
				ЗаголовокОповещения = НСтр("ru = 'Наименование и префикс заполнены'");
			ИначеЕсли ЗаполнятьНаименование Тогда
				ЗаголовокОповещения = НСтр("ru = 'Полное наименование заполнено'");
			ИначеЕсли ДобавлятьПрефикс Тогда
				ЗаголовокОповещения = НСтр("ru = 'Добавлен префикс к краткому наименованию'");
			Иначе
				ЗаголовокОповещения = НСтр("ru = 'Наименование и префикс очищены'");
			КонецЕсли;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ЗаголовокОповещения;
			Сообщение.Сообщить();
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Если Ошибок = 0 Тогда
		ТекстОповещения = НСтр("ru = 'Обработано'")
			+ " "
			+ СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Всего,
				НСтр("ru = 'объект,объекта,объектов'"));
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОповещения;
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Кратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Всего объектов: %1
		           |Успешно заполнено: %2
		           |Ошибок: %3'"),
		Формат(Всего,     "ЧН=0; ЧГ=0"),
		Формат(Заполнено, "ЧН=0; ЧГ=0"), 
		Формат(Ошибок,    "ЧН=0; ЧГ=0"));
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Кратко;
	Сообщение.Сообщить();
	
	Подробно = "";
	Для Каждого ТекстОшибки Из МассивОшибок Цикл
		Подробно = Подробно + "---" + Символы.ПС + Символы.ПС + ТекстОшибки + Символы.ПС + Символы.ПС;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Обработка заполнения справочника Демо: Контрагенты'", Метаданные.ОсновнойЯзык.КодЯзыка), 
		УровеньЖурналаРегистрации.Предупреждение, Метаданные.Справочники._ДемоКонтрагенты,,
		Подробно);
	
КонецПроцедуры

Функция Префикс()
	
	Возврат НСтр("ru = 'ПР'") + " ";
	
КонецФункции

// Обработчик команды.
//
// Параметры:
//   ПараметрыПроцедуры - Структура:
//    * ОписаниеКоманды - Структура - состав свойств совпадает с колонками таблицы значений параметра Команды процедуры
///                                  ПодключаемыеКомандыПереопределяемый.ПриОпределенииКомандПодключенныхКОбъекту.
//                                   Ключевые свойства:
//      ** Идентификатор  - Строка - идентификатор команды.
//      ** Представление  - Строка - представление команды в форме.
//      ** Имя            - Строка - имя команды в форме.
//      ** ДополнительныеПараметры - Структура - дополнительные свойства, состав которых определяется видом 
//                                   конкретной команды.
//    * ПараметрКоманды - Массив из СправочникСсылка._ДемоКонтрагенты
//   АдресХранилища - Строка - адрес временного хранилища
//
Процедура ВыполнитьКомандуВФоне(ПараметрыПроцедуры, АдресХранилища) Экспорт
	// Имитация длительной операции
	НачатьТранзакцию();
	Попытка
		ДатаЗавершения = ТекущаяДатаСеанса() + 5;
		Пока ТекущаяДатаСеанса() <= ДатаЗавершения Цикл
			// Имитация длительной операции
		КонецЦикла;
		ВыполнитьКоманду(ПараметрыПроцедуры.ПараметрКоманды, ПараметрыПроцедуры);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;	
КонецПроцедуры

#КонецОбласти

#КонецЕсли