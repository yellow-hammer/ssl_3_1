///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СделатьРабочимЗавершение", ЭтотОбъект, ПараметрКоманды);
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Отметить сегмент %1 как основной?
		|Основной сегмент подсвечивается жирным шрифтом.'"), Строка(ПараметрКоманды));
	ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СделатьРабочимЗавершение(Результат, Сегмент) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьРабочийСегмент(Сегмент);
	Оповестить("Запись__ДемоСегментыПартнеровРасширение", Новый Структура, Сегмент);
	ОбновитьПовторноИспользуемыеЗначения();
	СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРабочийСегмент(Сегмент)
	
	Справочники._ДемоСегментыПартнеровРасширение.УстановитьОсновнойСегмент(Сегмент);
	
КонецПроцедуры

#КонецОбласти
