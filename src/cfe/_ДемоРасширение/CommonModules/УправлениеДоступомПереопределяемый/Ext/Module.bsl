///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

&После("ПриЗаполненииСписковСОграничениемДоступа")
Процедура _ДемоРасширениеПриЗаполненииСписковСОграничениемДоступа(Списки)
	
	Списки.Вставить(Метаданные.РегистрыСведений._ДемоЗаведующиеМестамиХранения, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений._ДемоСегментыПартнеровРасширение, Истина);
	
КонецПроцедуры

&После("ПриЗаполненииПоставляемыхПрофилейГруппДоступа")
Процедура _ДемоПриЗаполненииПоставляемыхПрофилейГруппДоступа(ОписанияПрофилей, ПараметрыОбновления)
	
	_ДемоЗаполнитьПрофильОтветственныйЗаЗаведующихМестамиХранения(ОписанияПрофилей);
	
КонецПроцедуры

Процедура _ДемоЗаполнитьПрофильОтветственныйЗаЗаведующихМестамиХранения(Знач ОписанияПрофилей)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя           = "_ДемоОтветственныйЗаЗаведующихМестамиХранения";
	ОписаниеПрофиля.Родитель      = "ДополнительныеПрофили";
	ОписаниеПрофиля.Идентификатор = "d7370d7f-13d6-11e7-b658-005056c00008";
	ОписаниеПрофиля.Наименование  =
		НСтр("ru = 'Ответственный за заведующих местами хранения (дополнительно из расширения)'",
			ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Назначется пользователям, которые отвечают за места хранения (склады, точки отгрузки и др.).'");
	
	// Основные возможности профиля.
	ОписаниеПрофиля.Роли.Добавить("Подсистема_ДемоРасширение");
	ОписаниеПрофиля.Роли.Добавить("_ДемоДобавлениеИзменениеЗаведующихМестамиХраненияРасширение");
	
	// Виды ограничения доступа профиля.
	ОписаниеПрофиля.ВидыДоступа.Добавить("_ДемоМестаХранения");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

#КонецОбласти
