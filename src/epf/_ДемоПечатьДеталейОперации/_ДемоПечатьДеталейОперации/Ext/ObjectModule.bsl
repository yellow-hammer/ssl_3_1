///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке
//
Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("3.1.11.2");
	ПараметрыРегистрации.Информация = НСтр("ru = 'Обработка формирования печатной формы документа ""Демо: Детали операции"". Используется для демонстрации возможностей работы подсистемы ""Дополнительные отчеты и обработки"" с расширениями.'");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия = "3.1.11.2";
	ПараметрыРегистрации.Назначение.Добавить("Документ._ДемоОперация");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Детали операции (внешняя печатная форма)'");
	Команда.Идентификатор = "ДеталиОперации";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;
	Команда.Модификатор = "ПечатьMXL";
	
	Возврат ПараметрыРегистрации;
КонецФункции

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ДеталиОперации");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьДеталейОперации(МассивОбъектов, ОбъектыПечати, "Операция");
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Детали операции (внешняя печатная форма)'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ._ДемоОперация.ПФ_MXL_ДеталиОперации";
	КонецЕсли;
	
КонецПроцедуры

// Процедура печати документа.
//
// Параметры:
//  МассивОбъектов - Массив - объекты, по которым требуется сформировать печатную форму;
//  ОбъектыПечати  - СписокЗначений - разметка табличных документов по печатаемым объектам:
//   * Значение      - ЛюбаяСсылка - печатаемый объект;
//   * Представление - Строка - имя области, соответствующее объекту.
//  ИмяМакета      - Строка - "Счет" или "Заказ".
//  ВыводитьПлатежныеРеквизиты - Булево - если Истина, выводит шапку с платежными реквизитами в счете.
//  КодЯзыка - Строка - язык, на котором требуется сформировать печатную форму.
//                      Состоит из кода языка по ISO 639-1 и, опционально, кода страны по ISO 3166-1, разделенных
//                      символом подчеркивания. Примеры: "en", "en_US", "en_GB", "ru", "ru_RU".
//                      Значение по умолчанию - язык конфигурации.
//
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма.
//
Функция ПечатьДеталейОперации(МассивОбъектов, ОбъектыПечати, ИмяМакета = "Счет", ВыводитьПлатежныеРеквизиты = Истина, КодЯзыка = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	_ДемоОперация.Ссылка КАК Ссылка,
	|	_ДемоОперация.Номер КАК Номер,
	|	_ДемоОперация.Дата КАК Дата,
	|	_ДемоОперация.Комментарий КАК Комментарий
	|ИЗ
	|	Документ._ДемоОперация КАК _ДемоОперация
	|ГДЕ
	|	_ДемоОперация.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ДемоОперация.Ссылка КАК Ссылка,
	|	_ДемоРегистрНакопленияРасширение.НомерСтроки КАК НомерСтроки,
	|	_ДемоРегистрНакопленияРасширение.ВидДвижения КАК ВидДвижения,
	|	_ДемоНоменклатура.НаименованиеДляПечати КАК НаименованиеДляПечати,
	|	_ДемоНоменклатура.Наименование КАК Номенклатура,
	|	_ДемоРегистрНакопленияРасширение.Сумма КАК Сумма
	|ИЗ
	|	Документ._ДемоОперация КАК _ДемоОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления._ДемоРегистрНакопленияРасширение КАК _ДемоРегистрНакопленияРасширение
	|		ПО _ДемоОперация.Ссылка = _ДемоРегистрНакопленияРасширение.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник._ДемоНоменклатура КАК _ДемоНоменклатура
	|		ПО (_ДемоРегистрНакопленияРасширение.Номенклатура = _ДемоНоменклатура.Ссылка)
	|ГДЕ
	|	_ДемоОперация.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ДемоОперация.Ссылка КАК Ссылка,
	|	_ДемоРегистрБухгалтерииРасширение.НомерСтроки КАК НомерСтроки,
	|	_ДемоНоменклатура.НаименованиеДляПечати КАК НаименованиеДляПечати,
	|	_ДемоНоменклатура.Наименование КАК Номенклатура,
	|	_ДемоРегистрБухгалтерииРасширение.СчетДт КАК СчетДт,
	|	_ДемоРегистрБухгалтерииРасширение.СчетКт КАК СчетКт,
	|	_ДемоРегистрБухгалтерииРасширение.Сумма КАК Сумма
	|ИЗ
	|	Документ._ДемоОперация КАК _ДемоОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии._ДемоРегистрБухгалтерииРасширение КАК _ДемоРегистрБухгалтерииРасширение
	|		ПО _ДемоОперация.Ссылка = _ДемоРегистрБухгалтерииРасширение.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник._ДемоНоменклатура КАК _ДемоНоменклатура
	|		ПО (_ДемоРегистрБухгалтерииРасширение.Номенклатура = _ДемоНоменклатура.Ссылка)
	|ГДЕ
	|	_ДемоОперация.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ДемоОперация.Ссылка КАК Ссылка,
	|	_ДемоРегистрРасчетаРасширение.НомерСтроки КАК НомерСтроки,
	|	_ДемоРегистрРасчетаРасширение.ВидРасчета КАК ВидРасчета,
	|	_ДемоНоменклатура.НаименованиеДляПечати КАК НаименованиеДляПечати,
	|	_ДемоНоменклатура.Наименование КАК Номенклатура,
	|	_ДемоРегистрРасчетаРасширение.Амортизация КАК Амортизация,
	|	_ДемоРегистрРасчетаРасширение.Сторно КАК Сторно
	|ИЗ
	|	Документ._ДемоОперация КАК _ДемоОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета._ДемоРегистрРасчетаРасширение КАК _ДемоРегистрРасчетаРасширение
	|		ПО _ДемоОперация.Ссылка = _ДемоРегистрРасчетаРасширение.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник._ДемоНоменклатура КАК _ДемоНоменклатура
	|		ПО (_ДемоРегистрРасчетаРасширение.Номенклатура = _ДемоНоменклатура.Ссылка)
	|ГДЕ
	|	_ДемоОперация.Ссылка В(&МассивОбъектов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "Операция_ДеталиОперации";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ._ДемоОперация.ПФ_MXL_ДеталиОперации", КодЯзыка);
	
	Шапка = Результат[0].Выбрать();
	
	Пока Шапка.Следующий() Цикл
		
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		
		ДанныеПечати.Вставить("Дата", Формат(Шапка.Дата, "Л=" + КодЯзыка + "; ДЛФ=DD"));
		ДанныеПечати.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер));
		
		ТекстЗаголовка = Документы._ДемоОперация.СформироватьЗаголовокДокумента(
			Шапка, ИмяМакета = НСтр("ru = 'Демо: Операция'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		
		СоответствиеРезультата = Новый Соответствие();
		ИтераторРегистра = 1;
		Пока ИтераторРегистра <= Результат.ВГраница() Цикл
			ТаблицаДвижений = Результат[ИтераторРегистра].Выгрузить();
			Номенклатура = ТаблицаДвижений.ВыгрузитьКолонку("Номенклатура");
			Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
				Если ЗначениеЗаполнено(СтрокаТаблицы.НаименованиеДляПечати) Тогда
					СтрокаТаблицы.Номенклатура = СтрокаТаблицы.НаименованиеДляПечати;
				КонецЕсли;
			КонецЦикла;
			СоответствиеРезультата.Вставить(ИтераторРегистра, ТаблицаДвижений);
			ИтераторРегистра = ИтераторРегистра + 1;
		КонецЦикла;
		
		ДанныеПечати.Вставить("Комментарий", Шапка.Комментарий);
		
		МассивОбластейМакета = Новый Массив;
		
		МассивОбластейМакета.Добавить("ЗаголовокОперации");
		
		Если ЗначениеЗаполнено(Шапка.Комментарий) Тогда
			МассивОбластейМакета.Добавить("Комментарий");
		КонецЕсли;
		
		МассивОбластейМакета.Добавить("ШапкаТаблицыРегистрНакопления");
		МассивОбластейМакета.Добавить("СтрокаРегистрНакопления");
		МассивОбластейМакета.Добавить("ИтогоРегистрНакопления");
		
		МассивОбластейМакета.Добавить("ШапкаТаблицыРегистрБухгалтерии");
		МассивОбластейМакета.Добавить("СтрокаРегистрБухгалтерии");
		МассивОбластейМакета.Добавить("ИтогоРегистрБухгалтерии");
		
		МассивОбластейМакета.Добавить("ШапкаТаблицыРегистрРасчета");
		МассивОбластейМакета.Добавить("СтрокаРегистрРасчета");
		МассивОбластейМакета.Добавить("ИтогоРегистрРасчета");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если Найти(ИмяОбласти, "Строка") = 0 Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			Иначе
				ИндексРезультата = 0;
				Если ИмяОбласти = "СтрокаРегистрНакопления" Тогда
					ИндексРезультата = 1;
				ИначеЕсли ИмяОбласти = "СтрокаРегистрБухгалтерии" Тогда
					ИндексРезультата = 2;
				ИначеЕсли ИмяОбласти = "СтрокаРегистрРасчета" Тогда
					ИндексРезультата = 3;
				КонецЕсли;
				ТаблицаДвижений = СоответствиеРезультата[ИндексРезультата]; 
				Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли